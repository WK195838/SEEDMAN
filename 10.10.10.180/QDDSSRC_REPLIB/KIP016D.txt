     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : KIP016D                                    *
     A*    PROGRAM NAME : VIP銷售明細查詢                          *
     A*    AUTHOR       : DEREK                                      *
     A*    CREATE DATE  : 2015/06/05   需求單號: 1040502           *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :專櫃進銷存系統                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*                                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A*           #LIN           3S 0H
     A*           #COL           3S 0H
     A                                  1  2'KIP016'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'VIP銷售明細查詢'
     A                                      DSPATR(RI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'連絡電話:'
     A            DBA11     R        B  4 13REFFLD(VP02 W1VPPF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  4 30'分機:'
     A            DBA12     R        B  4 38REFFLD(VP021 W1VPPF)
     A            DVP03         32O  O  4 45
     A                                  5  1'銷售日期:'
     A            DBA01S         6  0B  5 13EDTWRD('  /  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 22'-'
     A            DBA01E         6  0B  5 24EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  5 33'(MM/DD/YY)'
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =結束'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            3Y 0H
     A            DBA01          6  0O  9  2EDTWRD('  /  /  ')
     A            BA02      R        O  9 11REFFLD(BA02 W2BAPF)
     A            BB03      R        O  9 18REFFLD(BB03 W2BBPF)
     A            MA03      R        O  9 28REFFLD(MA03 MTMAPF)
     A            DBB04          5  0O  9 49EDTCDE(4)
     A            DBB07     R        O  9 55REFFLD(BB07 W2BBPF)
     A                                      EDTCDE(4)
     A            DBB05          5  0O  9 73EDTCDE(4)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0026)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A*****                                 ROLLDOWN(26)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A**96                                  SFLMSGID(CPD6A69 *LIBL/PTMF)
     A**97                                  SFLMSGID(CPD6A66 *LIBL/PTMF)
     A            DRRN           3  0H      SFLRCDNBR
     A                                  1  2'KIP016'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'VIP銷售明細查詢'
     A                                      DSPATR(RI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'連絡電話:'
     A            DBA11     R        O  4 13REFFLD(VP02 W1VPPF)
     A                                  4 30'分機:'
     A            DBA12     R        O  4 38REFFLD(VP021 W1VPPF)
     A            DVP03         32O  O  4 45
     A                                  5  1'銷售日期:'
     A            DBA01S         6  0O  5 13EDTWRD('  /  /  ')
     A                                  5 22'-'
     A            DBA01E         6  0O  5 24EDTWRD('  /  /  ')
     A                                  5 33'(MM/DD/YY)'
     A                                  6  1'累積消費總額:'
     A            DBA04T    R        O  6 17REFFLD(BA04 W2BAPF)
     A                                      EDTCDE(J)
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  2'日期  專櫃  產品代號 -
     A                                      產品名稱      銷售數量   -
     A                                      銷售金額 搭贈數量'
     A                                      DSPATR(UL)
     A***         SIB051        11A  H
     A*N60                                  DSPATR(HI)
     A**60                                  DSPATR(PC)
     A**60                                  DSPATR(RI)
     A***         SIB052         5A  H
     A**                                    DSPATR(CS)
     A*N60                                  DSPATR(HI)
     A**60                                  DSPATR(PC)
     A**60                                  DSPATR(RI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =結束'
     A                                    +2'PF12 =回前畫面'
     A                                    +2'PA1 =下一頁'
     A                                    +2'PA2 =上一頁'
