     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIP018D                                    *
     A*    AUTHOR       : DEREK                                      *
     A*    CREATE DATE  : 105/12/08                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :單瓶獎金設定                              *
     A*    SYSTEM       :茂世進銷存                                *
     A*    REMARK       :單品獎金設定                              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            SEC01H    R        H      REFFLD(EC01 W3ECPF)
     A            SEC02     R        H      REFFLD(EC02 W3ECPF)
     A            DOPT1          1A  B  9  3VALUES(' ' '2' '3' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SEC01          4  0O  9  7EDTWRD('  /  ')
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(MSG1006 *LIBL/R1MF)
     A                                  1  2'KIP018'
     A                                  1 26MSGCON(030 MSG9999 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'單瓶獎金設定'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'2=修改'
     A                                      COLOR(BLU)
     A                                  5 26'3=拷貝'
     A                                      COLOR(BLU)
     A                                  5 35'4=刪除'
     A                                      COLOR(BLU)
     A                                  5 44'5=查詢'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  7  3'A   年月 '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A            DBGN1          4  0B  8  7EDTWRD('  /  ')
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F6 =新增'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 MSG9996 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DNEW           1A  H
     A            DOPT2          1A  B  9  3VALUES(' ' '1' '2' '4')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A            SED02     R        B  9  6REFFLD(ED02 W3EDPF)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            SMA03     R        O  9 16REFFLD(MA03 MTMAPF)
     A            SED03     R        B  9 37REFFLD(ED03 W3EDPF)
     A                                      EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0012)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A  33                                  CF04(04)
     A  33                                  CF05(05)
     A N33                                  CF07(07)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(MSG1006 *LIBL/R1MF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'KIP018'
     A                                  1 26MSGCON(030 MSG9999 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'單瓶獎金設定'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  4 17'1=新增'
     A                                      COLOR(BLU)
     A                                  4 26'2=修改'
     A                                      COLOR(BLU)
     A                                  4 35'4=刪除'
     A                                      COLOR(BLU)
     A                                  6  1'年月:'
     A                                      COLOR(BLU)
     A            DEC01          4  0B    +1EDTWRD('  /  ')
     A                                      COLOR(BLU)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  3'A 產品代號／說明            -
     A                                        單瓶獎金 '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  33                                +2'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  33                                +2'F5 =參考資料'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A  35                             23 10'F7 =下載檔案'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 MSG9996 *LIBL/R1MF)
     A                                      DSPATR(HI)
