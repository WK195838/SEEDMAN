     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    DSPF NAME    : KIR030D                                    *
     A*    AUTHOR       : DEREK                                      *
     A*    CREATE DATE  : 2016/12/15                                 *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : KIR030                                     *
     A*    SYSTEM       :茂世進銷存                                *
     A*    SUBSYSTEM    :專櫃進銷存系統                            *
     A*    REMARK       :單店獎金表                                *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN  #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'KIR030'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 36'單店獎金表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 26'獎金年月:'
     A                                      COLOR(BLU)
     A            DEE01          4  0B  6 38
     A                                      EDTWRD('  /  ')
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  6 46'(MM/YY)'
     A                                      COLOR(BLU)
     A                                  7 26'專櫃代號:'
     A                                      COLOR(BLU)
     A            DEE02S    R        B  7 38REFFLD(EE02 W3EEPF)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  7 44'-'
     A            DEE02E    R        B  7 46REFFLD(EE02 W3EEPF)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 16 26'執行環境:'
     A                                      COLOR(BLU)
     A            $PENV          1A  B 16 38VALUES('0' '1' '2')
     A                                 16 43'(0-查詢 1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機:'
     A                                      DSPATR(HI)
     A            $PRTCD         2  0B 18 38DSPATR(HI)
     A                                      EDTCDE(3)
     A  27                             21 27'請輸入挑選區間'
     A                                      DSPATR(RI)
     A  28                             21 27'報表處理中，請稍候'
     A                                      DSPATR(BL)
     A  96        ERR1          63A  O 24  2MSGID(UPT 5008 PTMF)
     A                                      DSPATR(HI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                      COLOR(BLU)
     A                                 23 20'PF04 =欄位查詢'
     A                                      COLOR(BLU)
     A                                 24 67'【泛太資訊】'
