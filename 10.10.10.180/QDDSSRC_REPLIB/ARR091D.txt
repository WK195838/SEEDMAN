     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : ARR091D                                    *
     A*    AUTHOR       : DEREK                                      *
     A*    CREATE DATE  : 107/07/30                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR091                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :應收報表                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARR091'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 35'應收報表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 26'公司別:'
     A            DAD10S    R        B  6 38REFFLD(AD10)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  6 41'-'
     A            DAD10E    R        B  6 43REFFLD(AD10)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  7 26'客戶代號:'
     A                                      DSPATR(HI)
     A            DAD02S    R        B  7 38REFFLD(AD02)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N67                                  DSPATR(HI)
     A                                  7 44'-'
     A            DAD02E    R        B  7 47REFFLD(AD02)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A N68                                  DSPATR(HI)
     A                                  8 26'發票日期:'
     A            DAD11          6Y 0B  8 38EDTWRD('  /  /  ')
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                  8 48'止'
     A                                  8 53'(MM/DD/YY)'
     A                                  9 26'(選取此日期前之應收帳款，+
     A                                      並在此日期後沖帳的資料)'
     A                                      DSPATR(HI)
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 42'(1-即時   2-批次)'
     A*****                            17 26'列印份數:'
     A*****       $CPY           3Y 0B 17 38EDTCDE(3)
     A*****                                 CHECK(RB)
     A*****                            18 26'列表機　:'
     A*****                                 DSPATR(HI)
     A*****       $PRTID         2Y 0B 18 38CHECK(RB)
     A*****                                 DSPATR(HI)
     A*****                                 EDTCDE(3)
     A*****                            20 27'***請準備１３２行報表紙***'
     A*****                                 DSPATR(HI)
     A                                 20 27'***產生Ｅｘｃｅｌ檔案***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR9          50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
