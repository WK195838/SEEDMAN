     A*%%TS  SD  19950426  155703  LILY        REL-V2R3M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : SOR200D                                    *
     A*    PROGRAM NAME :寄賣客戶出貨報表(SCREEN)                  *
     A*    AUTHOR       : CLEMENT                                    *
     A*    CREATE DATE  : 112/07/19                                  *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :銷售訂單系統                              *
     A*   需求單號:1120457                                         *
     A*00B需求單號: 1130122                                        *
     A*00B 113/03/22 DEREK增加挑選的條件:產品代號起迄          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR200'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'寄賣客戶出貨報表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  7 26'客戶代號  :'
     A                                      DSPATR(HI)
     A            DWF01S         5A  B  7 40
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(UL)
     A                                  7 47'~'
     A            DWF01E         5A  B  7 49
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(UL)
     A                                  7 63'(空白 : 全部)'
     A                                  9 26'日期範圍  :'
     A            DSC08S         6  0B  9 40EDTWRD('  /  /  ')
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                  9 49'~'
     A            DSC08E         6  0B  9 51EDTWRD('  /  /  ')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                  9 63'(MM/DD/YY)'
     A                                 11 26'日期選項  :'
     A            DATOPT         1A  B 11 40
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 11 42'(1.送貨日期2.確認日期)'
 00B+A                                 13 26'產品代號  :'
 00B+A            DSF03S         9A  B 13 40
 00B+A  66                                  DSPATR(RI)
 00B+A  66                                  DSPATR(PC)
 00B+A                                 13 50'~'
 00B+A            DSF03E         9A  B 13 52
 00B+A  67                                  DSPATR(RI)
 00B+A  67                                  DSPATR(PC)
     A                                 17 26'執行環境:'
     A            $EVR           1Y 0B 17 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 44'(1-即時 2-批次)'
     A                                 21 27'***產生Ｅｘｃｅｌ檔案***'
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'PF3 =回主畫面'
     A                                 23 28'PF4 =欄位查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR8          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
