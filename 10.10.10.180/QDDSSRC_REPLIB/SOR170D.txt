     A*%%TS  SD  19950819  085731  D910074     REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOR170D                                    *
     A*    AUTHOR       : CAROL                                      *
     A*    CREATE DATE  : 108/10/18                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR170                                     *
     A*    SYSTEM       :茂世進銷存系統                            *
     A*    SUBSYSTEM    :銷售系統                                  *
     A*    REMARK       :寄賣客戶產品異動報表                      *
     /*=============================================================*/
     /*  CODE       AUTHOR.     DATE    DESCRIPTION
     /*-------------------------------------------------------------
     /*=============================================================*/
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR170'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'寄賣客戶產品異動報表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A*
     A                                  6 22'客戶代號:'
     A                                      DSPATR(HI)
     A            DSE04     R        B  6 34REFFLD(SC04)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                  8 22'分店代號:'
     A                                      DSPATR(HI)
     A            DSE05     R        B  8 34REFFLD(SC05)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 12 22'送／退貨日期:'
     A            DSE24S         6  0B 12 38
     A                                      EDTWRD('  /  /  ')
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 12 47'-'
     A            DSE24E         6  0B 12 49
     A                                      EDTWRD('  /  /  ')
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                    +2'(MM/DD/YY)'
     A                                 17 22'執行環境:'
     A            $EVR           1Y 0B 17 34VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                    +2'(1-即時   2-批次)'
     A                                 20 27'***產生EXCEL檔案***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          63A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          63A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR9          63A  O 24  2MSGID(UPT 0130 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
