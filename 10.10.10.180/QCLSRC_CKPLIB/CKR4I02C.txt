     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    PROGRAM ID   : CKR4I0                                    */
     /*    PROGRAM NAME :列印票據作業－變動                       */
     /*    AUTHOR       : A1162                                     */
     /*    CREATE DATE  : 84/02/08                                  */
     /*    UPDATE DATE  :                                           */
     /*    SYSTEM       :                                           */
     /***************************************************************/
             PGM

             DCL        VAR(&DHF02)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&DHF16)    TYPE(*CHAR) LEN(6)
             DCL        VAR(&DHF06F)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DHF06T)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DHF71F)   TYPE(*CHAR) LEN(4)
             DCL        VAR(&DHF71T)   TYPE(*CHAR) LEN(4)
             DCL        VAR(&DHF76)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&DTYPE)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&DTEST)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&DHI15)    TYPE(*CHAR) LEN(1)

             DCL        VAR(&$PENV)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTCD)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6401I)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6403I)   TYPE(*DEC)  LEN(3 0)

             CHGVAR     VAR(&DHF02)    VALUE(%SST(*LDA 501 2))
             CHGVAR     VAR(&DHF16)    VALUE(%SST(*LDA 505 6))
             CHGVAR     VAR(&DHF06F)   VALUE(%SST(*LDA 511 8))
             CHGVAR     VAR(&DHF06T)   VALUE(%SST(*LDA 519 8))
             CHGVAR     VAR(&DHF71F)   VALUE(%SST(*LDA 527 4))
             CHGVAR     VAR(&DHF71T)   VALUE(%SST(*LDA 531 4))
             CHGVAR     VAR(&DHF76)    VALUE(%SST(*LDA 535 1))
             CHGVAR     VAR(&DTYPE)    VALUE(%SST(*LDA 536 1))
             CHGVAR     VAR(&DTEST)    VALUE(%SST(*LDA 537 1))
             CHGVAR     VAR(&DHI15)    VALUE(%SST(*LDA 538 1))

     /*****  CPYF       --> CKR4I0F                             *****/
             CPYF       FROMFILE(CKR4I0F) TOFILE(QTEMP/CKR4I0F) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/CKR4I0F))
             OVRDBF     FILE(CKR4I0F) TOFILE(QTEMP/CKR4I0F) SHARE(*YES)

             OVRDBF     FILE(CKHFPF) SHARE(*YES)

           /* DTEST='N'--->非試印 */
             IF         COND(&DTEST *EQ 'N') THEN(DO)

           /* DTYPE='1'--->首印，列印日期(HF73)=0 */
             IF         COND(&DTYPE *EQ '1') THEN(DO)
             OPNQRYF    FILE((CKHFPF)) OPTION(*ALL) QRYSLT(' HF02 +
                          *EQ ("' *CAT &DHF02 *CAT '") & WHF16 *EQ +
                          ("' *CAT &DHF16 *CAT '") & WHF06 *EQ +
                          %RANGE("' *CAT &DHF06F *CAT '" "' *CAT +
                          &DHF06T *CAT '") & WHF71 *EQ %RANGE("' +
                          *CAT &DHF71F *CAT '" "' *CAT &DHF71T *CAT +
                          '") & HF76 *EQ ("' *CAT &DHF76 *CAT '") & +
                          HF03 *EQ " " & HF73 *EQ 0 ') +
                          KEYFLD((HF02) (HF06) (HF71)) +
                          MAPFLD((WHF16 '%SST(HF16 9 6)' *CHAR 6) +
                          (WHF06 HF06 *CHAR 8) (WHF71 HF71 *CHAR 4))
                          ENDDO
             ELSE (DO)
           /* DTYPE='2'--->重印，列印日期(HF73)<>0 */
             OPNQRYF    FILE((CKHFPF)) OPTION(*ALL) QRYSLT(' HF02 +
                          *EQ ("' *CAT &DHF02 *CAT '") & WHF16 *EQ +
                          ("' *CAT &DHF16 *CAT '") & WHF06 *EQ +
                          %RANGE("' *CAT &DHF06F *CAT '" "' *CAT +
                          &DHF06T *CAT '") & WHF71 *EQ %RANGE("' +
                          *CAT &DHF71F *CAT '" "' *CAT &DHF71T *CAT +
                          '") & HF76 *EQ ("' *CAT &DHF76 *CAT '") & +
                          HF03 *EQ " " & HF73 *NE 0 ') +
                          KEYFLD((HF02) (HF06) (HF71)) +
                          MAPFLD((WHF16 '%SST(HF16 9 6)' *CHAR 6) +
                          (WHF06 HF06 *CHAR 8) (WHF71 HF71 *CHAR 4))
                          ENDDO
             ENDDO

             IF         COND(&DHI15 *EQ 'A') THEN(DO)
             OVRPRTF    FILE(CKR4I0P) PAGESIZE(20 132) CPI(10) +
                          OVRFLW(20)
             ENDDO
             ELSE       CMD(DO)
             OVRPRTF    FILE(CKR4I0P) PAGESIZE(20 198) CPI(15) +
                          OVRFLW(20)
             ENDDO

             CALL       CKR4I02
             CALL       CKR4I03

             IF         COND(&DTEST *EQ 'N') THEN(DO)
             CLOF       OPNID(CKHFPF)
             ENDDO

             DLTOVR     FILE(*ALL)

             RTVDTAARA  DTAARA(*LDA (219 1))  RTNVAR(&$PENV)
             RTVDTAARA  DTAARA(*LDA (125 3))  RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (217 2)) RTNVAR(&$PRTCD)
             CHGVAR     VAR(&P6401I) VALUE('CKR4I0P')
             CHGVAR     VAR(&P6403I) VALUE(&$CPY)
             CALL       PGM(P64) PARM(&P6401I &$PENV &P6403I &$PRTCD)

             ENDPGM
