     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI122D                                    *
     A*    PROGRAM NAME :退休金提撥異動全頁查詢
     A*    AUTHOR       : A1386 SHARON                               *
     A*    CREATE DATE  :
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :勞退新制作業                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYH5PF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(HI)
     A            H502      R        O  8  4REFFLD(H502)
     A            EM03      R        O  8 13REFFLD(EM03 PYEMPF)
     A            H503      R        O  8 28REFFLD(H503)
     A                                      EDTWRD('    /  /  ')
     A            H504      R        O  8 39REFFLD(H504)
     A            H504N          8A  O    +1
     A            H506      R        O  8 52REFFLD(H506)
     A                                      EDTCDE(1)
     A            H505      R        O  8 65REFFLD(H505)
     A                                      EDTCDE(1)
     A            H507      R        O  8 74REFFLD(H507)
     A                                      EDTCDE(1)
     A                                  9 39'異動項目:'
     A                                      COLOR(PNK)
     A            H514      R        O    +1REFFLD(H514)
     A                                      COLOR(PNK)
     A            H514N         14A  O    +1
     A                                      COLOR(PNK)
     A*-------------------------------------------------------------***
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI122.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 28'退休金提撥異動全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DH501     R        O  4 13REFFLD(H501)
     A  60                                  DSPATR(RI)
     A            CM02          22O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'    員工編號姓名    '
     A                                      DSPATR(UL)
     A                                    +2'異動日期'
     A                                      DSPATR(UL)
     A                                    +1' 異動別 '
     A                                      DSPATR(UL)
     A                                    +1'月提繳工資'
     A                                      DSPATR(UL)
     A                                    +1'企提率'
     A                                      DSPATR(UL)
     A                                    +1'自提率'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 36'PA01=下一頁　'
     A                                 23 53'PA02=上一頁　'
     A                                 24 67'【泛太資訊】'
