     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR207D                                    *
     A*    PROGRAM NAME :遲到資料明細表列印                        *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 86/02/11                                   *
     A*    UPDATE DATE  : 90/05/09 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYR207D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 33'遲到資料明細表'
     A                                      DSPATR(RI)
     A                                  5 28'公司編號　　　:'
     A            DLA01     R        B  5 46REFFLD(LA01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 28'部門編號　　起:'
     A                                      DSPATR(HI)
     A            DEM041         7A  B  7 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM042         7A  B  8 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 28'員工編號　　起:'
     A                                      DSPATR(HI)
     A            DLA021    R        B 10 46REFFLD(LA02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 11 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DLA022    R        B 11 46REFFLD(LA02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 13 28'考勤日期　　起:'
     A            DLA031    R   -2   B 13 46REFFLD(LA03)
     A                                      EDTWRD('0  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 14 28'　　　　　　迄:'
     A            DLA032    R   -2   B 14 46REFFLD(LA03)
     A                                      EDTWRD('0  /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14RANGE('0' '2')
     A                                      CHECK(RB)
     A                                 17 17'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A            $PRTCD         2A  B 19 14CHECK(RB)
     A  27                             21 29'***請準備８０格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF04=欄位查詢'
     A                                 24 67'【泛太資訊】'
