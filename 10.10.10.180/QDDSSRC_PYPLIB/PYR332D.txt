     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR332D                                    *
     A*    PROGRAM NAME :薪資單列印                                *
     A*    AUTHOR       : A1386 SHARON                               *
     A*    CREATE DATE  : 2004/12/31                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A*-------------------------------------------------------------***
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<PYR332D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 35'薪資單列印'
     A                                      DSPATR(RI)
     A*
     A                                  4 28'公司編號　　　:'
     A            DEM01     R        B  4 46REFFLD(EM01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5 28'部門編號　　起:'
     A                                      DSPATR(HI)
     A            DEM041         7A  B  5 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM042         7A  B  6 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 28'員工編號　　起:'
     A                                      DSPATR(HI)
     A            DEM021    R        B  7 46REFFLD(EM02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM022    R        B  8 46REFFLD(EM02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 28'發薪年月　　　:'
     A            DYM            5Y 0B  9 46
     A                                      EDTWRD('0   /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 10 28'期　　別　　　:'
     A            DPH04     R        B 10 46REFFLD(PH04)
     A                                      VALUES('1' '2')
     A                                 10 49'( 1-下期 2-上期)'
     A*
     A                                 11 28'發薪日期　　　:'
     A            DYMD           7  0B 11 46EDTWRD('0   /  /  ')
     A*勞退增加部份
     A                                 12 28'提撥扣款日　　:'
     A            DYMD1          7  0B 12 46EDTWRD('0   /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 13 28'勞保局入帳日　:'
     A            DYMD2          7  0B 13 46EDTWRD('0   /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 14 28'文字說明:'
     A            REMARK        40O  B 14 40
     A
     A*
     A*
     A                                 18  2'執行環境:'
     A            $PENV          1A  B 18 14VALUES('0' '1' '2')
     A                                 18 18'(0-查詢 1-即時 2-批次)'
     A                                 19  2'列印份數:'
     A            $CPY           3Y 0B 19 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 14
     A*
     A  27                             21 27'*** 請準備薪資單預印紙 ***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
