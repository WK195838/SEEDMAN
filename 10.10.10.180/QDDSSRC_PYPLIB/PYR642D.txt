     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR642D                                    *
     A*    PROGRAM NAME :健保調整投保申報資料列印                  *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/03/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :員工保險維護子系統                        *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A*-------------------------------------------------------------***
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<PYR642D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A                                  2 31'健保保額調低明細表'
     A                                      DSPATR(RI)
     A*
     A                                  5 28'公　　司:'
     A            DEM01     R        B  5 40REFFLD(EM01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A*******************************************************
     A*                                 7 28'部　　門:'
     A*                                     DSPATR(HI)
     A*           DEM04S         7   B  7 40
     A* 61                                  DSPATR(PC)
     A* 61                                  DSPATR(RI)
     A*                                 7 46'-'
     A*           DEM04E         7   B  7 48
     A* 62                                  DSPATR(PC)
     A* 62                                  DSPATR(RI)
     A*******************************************************
     A                                  7 28'員工編號:'
     A                                      DSPATR(HI)
     A            DEM02S    R        B  7 40REFFLD(EM02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  7 49'-'
     A            DEM02E    R        B  7 51REFFLD(EM02)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 28'調整年月:'
     A            DYM            4Y 0B  9 40EDTWRD('0  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 11 28'排序方式:'
     A            DOPT           1A  B 11 40VALUES('1' '2')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 11 42'(1-依調整後金額)'
     A                                 12 42'(2-依講師身份証號)'
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'印表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 27'***請準備好１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
