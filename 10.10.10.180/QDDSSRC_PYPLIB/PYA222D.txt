     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA222D                                    *
     A*    PROGRAM NAME :員工住宿資料建立                          *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 91/07/12                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*------------------------------------------------------------***
     A          R DSPD1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA222.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工住宿資料建立'
     A                                      DSPATR(RI)
     A*
     A                                  5  1'公司編號:'
     A            DLI01     R        B  5 13REFFLD(LI01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  6  1'員工編號:'
     A                                      DSPATR(HI)
     A            DLI02     R        B  6 13REFFLD(LI02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8  1'申請類別:'
     A            DLI03     R        B  8 13REFFLD(LI03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                    +1'(1.住宿2.退宿)        '
     A*                                   +1'(1.住宿2.退宿3.復宿)'
     A                                  7  1'生效日期:'
     A            DLI04     R   -1   B  7 13REFFLD(LI04)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                      EDTWRD('0   /  /  ')
     A*
     A                                  9  1'作業選擇:'
     A            DOPT           1Y 0B  9 13VALUES(1 2 4 5)
     A                                  9 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                 10  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA222.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工住宿資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  7 28'公　　司:'
     A            DLI01     R        O  7 40REFFLD(LI01)
     A            DLI01N        14O  O  7 49
     A                                  8 28'員　　工:'
     A            DLI02     R        O  8 40REFFLD(LI02)
     A            EM03      R        O  8 49
     A                                  9 28'申請類別:'
     A            DLI03     R        O  9 40REFFLD(LI03)
     A            DLI03N         6O  O    +1
     A                                 10 28'生效日期:'
     A            DLI04     R   -1   O 10 40REFFLD(LI04)
     A                                      EDTWRD('0   /  /  ')
     A**911211                         12 28'住宿日期:'
     A            DLI05     R   -1   H      REFFLD(LI05)
     A**                                    EDTWRD('0   /  /  ')
     A**64                                  DSPATR(PC)
     A**64                                  DSPATR(RI)
     A**31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A**                               14 28'退宿日期:'
     A            DLI06     R   -1   H      REFFLD(LI06)
     A**                                    EDTWRD('0   /  /  ')
     A**65                                  DSPATR(PC)
     A**65                                  DSPATR(RI)
     A**33                                  DSPATR(UL)
     A*N33                                  DSPATR(PR)
     A*
     A                                 12 26'月租費金額:'
     A            DLI09     R        B 12 40REFFLD(LI09)
     A                                      EDTCDE(1)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  98        ERR           70A  O 22  2DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 19'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
