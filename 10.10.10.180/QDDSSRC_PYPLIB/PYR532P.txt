     A****************************************************************
     A*    PRTF NAME    : PYR532P                                    *
     A*    PROGRAM NAME :員工考績資料表　　　　                    *
     A*    AUTHOR       : TOM                                        *
     A*    CREATE DATE  : 20000915                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : MIRS                                       *
     A*    REMARK       :在職人員資料列印                          *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1
     A                                     1
     A                                      '員工考績資料表'
     A                                      SPACEB(001)
     A                                     1
     A                                      '部門代號:'
     A                                      SPACEB(001)
     A            DEM041         7        13
     A                                    +1
     A                                      '-'
     A            DEM042         7        +1
     A                                    77
     A                                      '日期:'
     A            DDATE          6  0     85
     A                                      EDTWRD('0  /  /  ')
     A                                    94
     A                                      '頁次:'
     A                                   102
     A                                      PAGNBR
     A                                     1
     A                                      '員工編號:'
     A                                      SPACEB(001)
     A            DRV021    R             13
     A                                      REFFLD(RV02)
     A                                    22
     A                                      '-'
     A            DRV022    R             24
     A                                      REFFLD(RV02)
     A                                    +4
     A                                      '考績年度:'
     A            DRV03     R   -1        +1
     A                                      REFFLD(RV03)
     A                                      EDTCDE(1)
     A                                    77
     A                                      '時間:'
     A                                    85
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    95
     A                                      '<PYR532P>'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ========'
     A                                      SPACEB(001)
     A                                     1
     A                                      '部門名稱'
     A                                      SPACEB(001)
     A                                   +12
     A                                      '員工姓名'
     A                                    46
     A                                      '組別'
     A                                    53
     A                                      '成績'
     A                                    60
     A                                      '考核結果'
     A                                    71
     A                                      '產假'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ========'
     A                                      SPACEB(001)
     A          R PD1
     A                                      SPACEB(001)
     A            RVD03          7A        1
     A            DP02      R              8REFFLD(DP02 PYDPPF)
     A            RVD04          8A       24
     A            EM03          14A       32
     A            RVA08          2A       48
     A            RVAMT          3S 0     54EDTCDE(4)
     A            RVD05          1A       65
     A            RVD06          1A       73
     A          R PT5
     A                                      SPACEB(001)
     A                                     2'人數總計---->'
     A            CNT            4S 0     +1EDTCDE(4)
     A                                    +1'人'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ========'
     A                                     1
     A                                      'PYR532P--->報表結束'
     A                                      SPACEB(001)
     A*
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
