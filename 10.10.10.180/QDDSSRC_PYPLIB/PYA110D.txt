     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA110D                                    *
     A*    PROGRAM NAME :全民健保資料建立                          *
     A*    AUTHOR       : A1413 CLOUD                                *
     A*    CREATE DATE  : 95/05/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA110.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'全民健保資料建立'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DE801     R        B  4 13REFFLD(E801 PYE8PF)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DE802     R        B  5 13REFFLD(E802 PYE8PF)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'健保生效日:'
     A            DE803     R   -2   B  6 15REFFLD(E803 PYE8PF)
     A                                      EDTWRD('0  /  /  ')
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A            DE803A    R        H      REFFLD(E803 PYE8PF)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 23 46'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            3Y 0H
     A*           DSEL           1A  B 11  3
     A* 61                                  DSPATR(PC)
     A* 61                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*                                     TEXT('OPT')
     A            DE206     R        O 11  7REFFLD(E206)
     A* 61                                  DSPATR(PC)
     A* 61                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                      TEXT('眷屬姓名')
     A            DE207     R        O 11 23REFFLD(E207)
     A* 62                                  DSPATR(PC)
     A* 62                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                      TEXT('身份証號')
     A            DE208     R   -2   O 11 35REFFLD(E208)
     A                                      EDTWRD('0  /  /  ')
     A* 63                                  DSPATR(PC)
     A* 63                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                      TEXT('出生日期')
     A            DE209     R        O 11 49REFFLD(E209)
     A* 64                                  DSPATR(PC)
     A* 64                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                      TEXT('稱謂代碼')
     A            DE211     R        O 11 56REFFLD(E211)
     A* 65                                  DSPATR(PC)
     A* 65                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                      TEXT('補助代碼')
     A            DE205     R        O 11 63REFFLD(E205)
     A*                                     VALUES(' ' 'Y' 'N')
     A* 60                                  DSPATR(PC)
     A* 60                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                      TEXT('轉出停保')
     A            DE203     R   -2   O 11 68REFFLD(E203)
     A                                      EDTWRD('0  /  /  ')
     A                                      TEXT('生效日期')
     A* 66                                  DSPATR(PC)
     A* 66                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                 12  7'說明：'
     A            DE212     R        O 12 16REFFLD(E212)
     A                                      COLOR(PNK)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0005)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF13(13)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A                                      SFLDROP(CF02)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA110.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'全民健保資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4  2'公司編號:'
     A            DE801     R        O  4 14REFFLD(E801 PYE8PF)
     A            CM02      R        O  4 17
     A                                  5  2'員工編號:'
     A            DE802     R        O  5 14REFFLD(E802 PYE8PF)
     A*           DE802A        10A  H
     A            EM03      R        O  5 25
     A                                  6  2'生效日期:'
     A            DE803     R   -2   O  6 14REFFLD(E803 PYE8PF)
     A                                      EDTWRD('0  /  /  ')
     A*           DE803A    R        H      REFFLD(E803 PYE8PF)
     A                                  4 40'異動類別:'
     A            DE804     R        B  4 52REFFLD(E804 PYE8PF)
     A                                      VALUES(' ' '1' '2' '3')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                  4 57'1投保2調整3轉出'
     A                                      DSPATR(HI)
     A                                  5 40'投保金額:'
     A            DE805     R        B  5 52REFFLD(E805 PYE8PF)
     A                                      EDTCDE(1)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                  6 40'補助碼  :'
     A                                      DSPATR(HI)
     A            DE808     R        B  6 52REFFLD(E808 PYE8PF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A*           DE808A    R        H      REFFLD(E808 PYE8PF)
     A                                  6 56'投保人數:'
     A            DE806     R        O  6 68REFFLD(E806 PYE8PF)
     A                                      EDTCDE(1)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  2'眷屬資料:'
     A*                                10  2'OPT'
     A                                 10  9'眷屬姓名'
     A                                      DSPATR(UL)
     A                                 10 23'身份証號'
     A                                      DSPATR(UL)
     A                                 10 34'出生日期'
     A                                      DSPATR(UL)
     A                                 10 46'稱謂'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 53'補助'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 60'轉出'
     A                                      DSPATR(UL)
     A                                 10 68'生效日'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位說明'
     A                                 23 38'PF12 =回前畫面'
     A                                 23 56'PF13 =資料確認'
     A                                 24 67'【泛太資訊】'
     A                                 24  2'PF2 =補充說明'
     A                                 24 20'PF05 =參考資料'
     A                                 24 38'PA1 =下一頁　'
     A                                 24 56'PA2 =上一頁　'
