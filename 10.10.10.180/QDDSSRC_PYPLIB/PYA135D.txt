     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA131D                                    *
     A*    PROGRAM NAME :勞／健保調整試算作業
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/03/26                                   *
     A*    UPDATE DATE  : 91/07/01 A1336 KLAUS                       *
     A*    SYSTEM       :員工保險維護子系統                        *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF13(13)
     A                                  1  2'<PYA135D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'勞／健保調整試算作業'
     A                                      DSPATR(RI)
     A*
     A                                  6 28'公司編號:'
     A            DEM01     R        B  6 40REFFLD(EM01)
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A*
     A                                  8 28'發放年月自:'
     A            DYM1      R   -1   B  8 44REFFLD(PW03)
     A                                      EDTWRD('   /  ')
     A  61                                  DSPATR(PC RI)
     A*
     A                                  9 28'        至:'
     A            DYM2      R   -1   B  9 44REFFLD(PW03)
     A                                      EDTWRD('   /  ')
     A  63                                  DSPATR(PC RI)
     A*
     A                                 11 28'生效日期:'
     A            DYMD           7  0B    +1
     A                                      EDTCDE(Y)
     A  62                                  DSPATR(PC RI)
     A*
     A                                 13 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A  97                             15 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  96                             15 28'試算作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11' '
     A                                      DSPATR(RI)
     A                                 16 66' '
     A                                      DSPATR(RI)
     A                                 17 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
