     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYH103P                                    *
     A*    PROGRAM NAME :勞退新制員工資料提撥明細表                *
     A*    AUTHOR       : A1415                                      *
     A*    CREATE DATE  : 94/03/01                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A          R PH1
     A*%%***********************************************************************
     A                                      SKIPB(002)
     A            CM03          34A        1
     A                                      SPACEA(001)
     A                                     1
     A                                      '員工退休金提撥明細表'
     A                                      SPACEA(002)
     A                                   100
     A                                      '日期:'
     A            DDATE          6  0    107
     A                                      EDTWRD('0  /  /  ')
     A                                   116
     A                                      '頁次:'
     A                                   123
     A                                      PAGNBR
     A                                      SPACEA(001)
     A                                     1
     A                                      '提撥月份:'
     A            DYMM           4S 0O    13
     A                                      EDTWRD('0  /  ')
     A                                   100
     A                                      '時間:'
     A                                   107
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   116
     A                                      '<PYH103P>'
     A                                      SPACEA(002)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =========================='
     A                                      SPACEA(001)
     A                                     1
     A                                      '部　　門'
     A                                      SPACEB(001)
     A                                    23
     A                                      '員　　工'
     A                                    47
     A                                      '月提繳工資'
     A                                    +2
     A                                      '企提率'
     A                                    +2
     A                                      '企業提撥金額'
     A                                    +2
     A                                      '自提率'
     A                                    +2
     A                                      '員工提繳金額'
     A                                    +1
     A                                      '離職碼'
     A*                                   +1
     A*                                     '計薪天數'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =========================='
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*
     A          R PD1
     A*%%***********************************************************************
     A                                      SPACEA(002)
     A            EM04           7         2
     A            DP02          16         9
     A            H602           8A       26
     A            EM03          14A       34
     A            H409           6  0     51
     A                                      EDTCDE(1)
     A            H408           3  2     63
     A                                      EDTCDE(1)
     A            H606           6  0     75
     A                                      EDTCDE(1)
     A            H407           3  2     89
     A                                      EDTCDE(1)
     A            H605           6  0    101
     A                                      EDTCDE(1)
     A            EM11           1A      115
     A*           PW07           2  0    125EDTCDE(1)
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A          R PT1
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------------'
     A                                      SPACEA(001)
     A                                     2
     A                                      '小計'
     A                                      SPACEB(001)
     A            @H602          9  0     24
     A                                      EDTCDE(1)
     A                                    35
     A                                      '人'
     A            @H606          9  0     71
     A                                      EDTCDE(1)
     A            @H605          9  0     97
     A                                      EDTCDE(1)
     A*%%***********************************************************************
     A          R PT2
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =========================='
     A                                      SPACEA(002)
     A                                     2
     A                                      '合計'
     A            @@H602         9  0     24
     A                                      EDTCDE(1)
     A                                    35
     A                                      ' 人 '
     A            @@H606         9  0     71
     A                                      EDTCDE(1)
     A            @@H605         9  0     97
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========================='
     A                                     1
     A                                      '　　　'
     A                                      SPACEB(001)
     A                                     1
     A                                      '總經理'
     A                                    30
     A                                      '廠　長'
     A                                    60
     A                                      '會計'
     A                                    90
     A                                      '覆核'
     A                                   122
     A                                      '製　表'
     A*-------------------------------------------------------------***
     A*%%***********************************************************************
     A          R PE9
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
     A*%%***********************************************************************
