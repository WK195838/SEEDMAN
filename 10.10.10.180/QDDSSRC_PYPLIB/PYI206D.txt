     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI206D                                    *
     A*    PROGRAM NAME :排班資料查詢    　                      *
     A*    AUTHOR       : A1344 OD                                   *
     A*    CREATE DATE  : 91/11/04                                   *
     A*    UPDATE DATE  :
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*--------------------------------------------------------------**
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            T202      R        O  8  5
     A            DEM03     R        O  8 15REFFLD(EM03)
     A            T206      R        O  8 31
     A            T206N         16   O  8 37
     A            CT203     R   -1   O  8 56REFFLD(T203)
     A                                      EDTWRD('   /  ')
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI206D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'排班資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DT201     R        O  4 13REFFLD(LA01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DT201N        14O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'員工編號'
     A                                      DSPATR(UL)
     A                                  6 15'員工姓名    '
     A                                      DSPATR(UL)
     A                                  6 30'部　　           門'
     A                                      DSPATR(UL)
     A                                  6 54'排班年月'
     A                                      DSPATR(UL)
     A            DT202     R        B  7  5REFFLD(T202)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DT206     R        B  7 31REFFLD(T206)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DT203     R   -1   B  7 56REFFLD(T203)
     A                                      EDTWRD('   /  ')
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 36'PA01=下一頁'
     A                                 23 50'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
