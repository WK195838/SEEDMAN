     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI201D                                    *
     A*    PROGRAM NAME :加班資料查詢　　                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 86/01/20                                   *
     A*    UPDATE DATE  : 91/06/17 A1336 KLAUS                       *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            OV02      R        O  8  7
     A            EM03      R        O  8 17
     A            OV03D          7  0O  8 33EDTWRD('   /  /  ')
     A            OV07      R        O  8 45EDTCDE(4)
     A            OV08      R        O  8 54EDTCDE(4)
     A            OV10      R        O  8 63EDTCDE(4)
     A            OV17      R        O  8 72EDTCDE(4)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI201D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'加班資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DOV01     R        O  4 13REFFLD(OV01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DOV01N        14O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5'員工編號'
     A                                      DSPATR(UL)
     A                                  6 17'  員工姓名  '
     A                                      DSPATR(UL)
     A                                  6 33'加班日期'
     A                                      DSPATR(UL)
     A                                  5 45'平常'
     A                                  6 45'加班'
     A                                      DSPATR(UL)
     A                                  5 54'超時'
     A                                  6 54'加班'
     A                                      DSPATR(UL)
     A                                  5 63'週日'
     A                                  6 63'加班'
     A                                      DSPATR(UL)
     A                                  5 72'週日'
     A                                  6 72'超時'
     A                                      DSPATR(UL)
     A            DOV02     R        B  7  7DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      REFFLD(OV02)
     A            DOV03     R   -1   B  7 33DSPATR(CS HI)
     A                                      EDTWRD('   /  /  ')
     A                                      REFFLD(OV03)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA01=下一頁'
     A                                 23 56'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
