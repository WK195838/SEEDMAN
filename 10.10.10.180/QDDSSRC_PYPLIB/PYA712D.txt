     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA712D                                    *
     A*    PROGRAM NAME :各類所得課稅資料維護－多筆                *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/08/06                                   *
     A*    UPDATE DATE  : 90/08/23 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA712.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'各類所得課稅資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DTB11     R        B  4 13REFFLD(TB11 PYTBPF)
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            CM02      R        O  4 17REFFLD(CM02 SYCMPF)
     A                                  5  1'所得年度:'
     A            DTB01     R   -1   B  5 13REFFLD(TB01 PYTBPF)
     A                                      EDTCDE(4)
     A  61                                  DSPATR(PC RI)
     A                                  6  1'身份證號:'
     A            DTB05     R        B  6 13REFFLD(TB05 PYTBPF)
     A  62                                  DSPATR(PC RI)
     A                                  6 35'員工編號:'
     A            DTB12     R        B  6 47REFFLD(TB12 PYTBPF)
     A  63                                  DSPATR(PC RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            3Y 0H
     A            DTB03     R        H      REFFLD(TB03 PYTBPF)
     A            DTB96     R   -2   H      REFFLD(TB96 PYTBPF)
     A*
     A            DYN            1   H
     A            DWAY           1   H
     A            DCUNT          2Y 0O 14  2EDTCDE(4)
     A            DSEL           1A  B 14  5VALUES('D' ' ')
     A  76                                  DSPATR(PC RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A*
     A            DTB18     R   -1   B 14  8REFFLD(TB18 PYTBPF)
     A                                      EDTWRD('   /  /  ')
     A  68                                  DSPATR(PC RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DTB04     R        B 14 20REFFLD(TB04 PYTBPF)
     A  69                                  DSPATR(PC RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DTB04N        16O  O 14 23COLOR(PNK)
     A            DTB09     R        B 14 41REFFLD(TB09 PYTBPF)
     A  73                                  DSPATR(PC RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DTB19     R        B 14 55REFFLD(TB19 PYTBPF)
     A  72                                  DSPATR(PC RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DTB19N    R        O    +1REFFLD(DP02 PYDPPF)
     A                                      COLOR(PNK)
     A            DTB15     R   -1   B 15 20REFFLD(TB15 PYTBPF)
     A                                      EDTWRD('0   /  ')
     A  74                                  DSPATR(PC RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                    +1'~'
     A            DTB16     R   -1   B    +1REFFLD(TB16 PYTBPF)
     A                                      EDTWRD('0   /  ')
     A  75                                  DSPATR(PC RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DTB07     R        B 15 38REFFLD(TB07 PYTBPF)
     A                                      EDTCDE(K)
     A  70                                  DSPATR(PC RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DTB08     R        B 15 52REFFLD(TB08 PYTBPF)
     A                                      EDTCDE(K)
     A  71                                  DSPATR(PC RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            TOTAL     R        O 15 66REFFLD(TB08 PYTBPF)
     A                                      EDTCDE(K)
     A                                      COLOR(PNK)
     A*
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0003)
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      ROLLUP(25)
     A***************************           SFLDROP(CF02)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA712.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'各類所得課稅資料維護'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4  2'公司編號:'
     A            DTB11     R        O    +1REFFLD(TB11 PYTBPF)
     A            CM02      R        O  4 17REFFLD(CM02 SYCMPF)
     A                                  4 37'所得年度:'
     A            DTB01     R   -1   O    +1REFFLD(TB01 PYTBPF)
     A                                  5  2'身份證號:'
     A            DTB05     R        O    +1REFFLD(TB05 PYTBPF)
     A  77                                  DSPATR(RI PC)
     A                                  5 37'員工編號:'
     A            DTB12     R        O  5 49REFFLD(TB12 PYTBPF)
     A*
     A                                  6  2'所得姓名:'
     A            DTB13     R        B  6 14REFFLD(TB13 PYTBPF)
     A  66                                  DSPATR(RI PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7  2'所得地址:'
     A            DTB14     R        B  7 14REFFLD(TB14 PYTBPF)
     A  67                                  DSPATR(RI PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                  8  2'證號別　:'
     A            DTB06     R        B  8 14REFFLD(TB06 PYTBPF)
     A  64                                  DSPATR(RI PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 17'( " "-身份證號 1-統一編號 3-+
     A                                      外籍無地址 4-外籍有地址)'
     A                                  9  2'證號錯誤:'
     A            DTB10     R        B  9 14REFFLD(TB10 PYTBPF)
     A  65                                  DSPATR(RI PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 17'( " "-無誤　　 A-錯誤　　 B-+
     A                                      無從查對)'
     A*
     A                                 10  1'==================================-
     A                                      ===================================-
     A                                      =========='
     A*
     A                                 11  1'刪除'
     A                                    +1'給付日期'
     A                                 11 19'格式代號'
     A                                      DSPATR(HI)
     A                                 11 40'特定編號'
     A                                      DSPATR(HI)
     A                                 11 54'作帳單位'
     A                                      DSPATR(HI)
     A*
     A                                 12  4'D'
     A                                 12 40'給付總額'
     A                                 12 54'扣繳稅額'
     A                                 12 68'給付淨額'
     A                                 12 19'所得年月'
     A*
     A                                 13  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                 20  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                 21  2'特定編號：租賃所得（５１）為房屋+
     A                                      稅籍編號．執行業務所得（９Ａ）為+
     A                                      業別代號．'
     A                                      COLOR(PNK)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位說明'
     A                                 23 38'PF12 =回前畫面'
     A                                 23 56'PF13 =資料確認'
     A                                 24 67'【泛太資訊】'
     A                                 24  2'PA01 =下一頁　'
     A                                 24 20'PA02 =上一頁　'
