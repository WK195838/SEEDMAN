     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI905D                                    *
     A*    PROGRAM NAME :週班設定全頁查詢                          *
     A*    AUTHOR       : A1268  VNAESSA                             *
     A*    CREATE DATE  : 98/09/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 　　　  　                             *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1   B  8  2DSPATR(CS PC HI)
     A                                      VALUES('/' ' ')
     A            XH02      R        O  8  5
     A            XH03      R        O  8 20
     A            XH04      R        O  8 26
     A            XH05      R        O  8 32
     A            XH06      R        O  8 38
     A            XH07      R        O  8 44
     A            XH08      R        O  8 50
     A            XH09      R        O  8 56
     A            XH10      R        O  8 62
     A            DXH11     R   -2   O  8 70REFFLD(XH11)
     A                                      EDTWRD('  /  /  ')
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*                                     CSRLOC(#LIN #COL)
     A                                      OVERLAY
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0013)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI905>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A                                  2 32'週班設定全頁查詢'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司編號:'
     A            DXH01     R        O  4 13REFFLD(XG01)
     A            DXH01N        14      4 16
     A                                  5  1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ---------'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'工作區　 '                   +
     A                                      DSPATR(UL)
     A                                  6 17'週班別週日週一　週二+
     A                                      　週三週四週五週六'
     A                                      DSPATR(UL)
     A                                  6 69'生效日期'
     A                                      DSPATR(UL)
     A            DDH02     R        B  7  5REFFLD(XH02)
     A                                      DSPATR(CS PC HI)
     A            DDH03     R        B  7 20REFFLD(XH03)
     A                                      DSPATR(CS PC HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 29'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
