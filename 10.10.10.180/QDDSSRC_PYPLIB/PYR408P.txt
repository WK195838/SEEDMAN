     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR408P                                    *
     A*    PROGRAM NAME :特別休假代金簽呈表                        *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  : 90/04/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                          *
     A****************************************************************
     A                                      REF(PYRF)
     A*------------------------------------------------------------***
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R             54
     A                                      SPACEB(001)
     A*
     A                                    54'特別休假代金簽呈表：'
     A                                      SPACEB(001)
     A            PMD1           4  0     +1EDTWRD('0  /  ')
     A                                    +1'-'
     A            PMD2           4  0     +1EDTWRD('0  /  ')
     A                                    +1'入社者'
     A*
     A                                     1
     A                                      '部門編號 :'
     A                                      SPACEB(002)
     A            DEM04S         7        13
     A                                    +1
     A                                      '-'
     A            DEM04E         7        +1
     A                                   104
     A                                      '日期:'
     A            DDATE          6  0    112
     A                                      EDTWRD('0  /  /  ')
     A                                   121
     A                                      '頁次:'
     A                                   129
     A                                      PAGNBR
     A                                     1
     A                                      '員工編號 :'
     A                                      SPACEB(001)
     A            DEM02S         8        +1
     A                                    +1
     A                                      '-'
     A            DEM02E         8        +1
     A                                    +5
     A                                      '特休年度:'
     A            PYEAR          2  0     +1
     A                                    +5
     A                                      '處理年月:'
     A            PYM            4  0     +1
     A                                      EDTWRD('0  /  ')
     A                                   104
     A                                      '時間:'
     A                                   112
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   121
     A                                      '<PYR407P>'
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============================'
     A                                      SPACEB(001)
     A                                     1
     A                                      '編號'
     A                                      SPACEB(001)
     A                                     7
     A                                      '部門'
     A                                    30
     A                                      '員工'
     A                                    52
     A                                      '到職日期'
     A                                    62
     A                                      '可休日數'
     A                                    72
     A                                      '已休日數'
     A                                    82
     A                                      '可折日數'
     A                                    92
     A                                      '折發金額'
     A                                   103
     A                                      '備                      註'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============================'
     A                                      SPACEB(001)
     A*------------------------------------------------------------***
     A          R PD1
     A                                      SPACEB(001)
     A            PRRN           3  0      2
     A            EM041     R              6
     A            EM042     R              7
     A            EM043     R              8
     A            EM044     R              9
     A            PDP02     R             13
     A                                      REFFLD(DP02)
     A            H202      R             29
     A                                      REFFLD(H202 PY2HPF)
     A            EM03      R             37
     A            EM13      R             52EDTWRD('    /  /  ')
     A            PH208     R    4S 1     66
     A                                      REFFLD(H208 PY2HPF)
     A                                      EDTCDE(J)
     A            PH209     R    4S 1     76
     A                                      REFFLD(H209 PY2HPF)
     A                                      EDTCDE(J)
     A            PH205     R    4S 1     86
     A                                      REFFLD(H205 PY2HPF)
     A                                      EDTCDE(J)
     A            H206      R             92
     A                                      REFFLD(H206 PY2HPF)
     A                                      EDTCDE(J)
     A            MEMO          31       102
     A*
     A*-------------------------------------------------------------***
     A          R PT1
     A                                      SPACEB(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============================'
     A                                      SPACEB(001)
     A            TT11           5  0     27
     A                                      EDTCDE(J)
     A                                      SPACEB(001)
     A                                    13
     A                                      '公司合計:'
     A                                    35
     A                                      '人'
     A            TT12           8  0     91
     A                                      EDTCDE(J)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============================'
     A                                      SPACEB(001)
     A*-------------------------------------------------------------***
     A          R PE1
     A                                      SPACEB(001)
     A                                     1'總經理'
     A                                      SPACEB(001)
     A                                    30
     A                                      '管理部'
     A                                    60
     A                                      '人事課'
     A                                    90
     A                                      '承辦人'
     A*-------------------------------------------------------------***
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
