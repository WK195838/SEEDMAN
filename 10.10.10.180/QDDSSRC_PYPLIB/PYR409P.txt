     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR409P                                    *
     A*    PROGRAM NAME :特別休假代金計算表　　　　                *
     A*    AUTHOR       : A1328 JEFFREY                              *
     A*    CREATE DATE  : 90/04/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                          *
     A****************************************************************
     A                                      REF(PYRF)
     A*------------------------------------------------------------***
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R             71
     A                                      SPACEB(001)
     A*
     A                                    75
     A                                      '特別休假代金計算表'
     A                                      SPACEB(001)
     A*
     A                                     1
     A                                      '部門編號 :'
     A                                      SPACEB(002)
     A            DEM04S         7        13
     A                                    +1
     A                                      '-'
     A            DEM04E         7        +1
     A                                   148
     A                                      '日期:'
     A            DDATE          6  0    156
     A                                      EDTWRD('0  /  /  ')
     A                                   165
     A                                      '頁次:'
     A                                   172
     A                                      PAGNBR
     A                                     1
     A                                      '員工編號 :'
     A                                      SPACEB(001)
     A            DEM02S         8        +1
     A                                    +1
     A                                      '-'
     A            DEM02E         8        +1
     A                                    +7
     A                                      '特休年度:'
     A            PYEAR          2  0     +1
     A                                    +7
     A                                      '處理年月:'
     A            PYM            4  0     +1
     A                                      EDTWRD('0  /  ')
     A            PMD1           4  0     99
     A                                      EDTWRD('0  /  ')
     A                                   105
     A                                      '-'
     A            PMD2           4  0    107
     A                                      EDTWRD('0  /  ')
     A                                   113
     A                                      '入社者'
     A                                   148
     A                                      '時間:'
     A                                   156
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   165
     A                                      '<PYR409P>'
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =='
     A                                      SPACEB(001)
     A                                   103
     A                                      '固定'
     A                                      SPACEB(001)
     A                                   110
     A                                      '可折發            |'
     A                                   163
     A                                      '可折日數上限'
     A                                     1
     A                                      '部門'
     A                                      SPACEB(001)
     A                                    22
     A                                      '員工'
     A                                    45
     A                                      '(本　　俸+職務加給+主管加給'
     A                                    78
     A                                      '+午餐津貼'
     A                                    89
     A                                      '+交通津貼) /天數X當年假＝'
     A                                   120
     A                                      '發放代金|'
     A                                   135
     A                                      '入社日　　年　資  已休日'
     A                                   163
     A                                      '新增假Ｘ比率'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =='
     A                                      SPACEB(001)
     A*-------------------------------------------------------------***
     A          R PD1
     A                                      SPACEB(001)
     A            EM041     R              1
     A            EM042     R              2
     A            EM043     R              3
     A            EM044     R              4
     A            PDP02     R              8
     A                                      REFFLD(DP02)
     A            H202      R             24
     A                                      REFFLD(H202 PY2HPF)
     A            EM03      R             32
     A                                      REFFLD(EM03)
     A            H211      R             46
     A                                      EDTCDE(J)
     A                                      REFFLD(H211 PY2HPF)
     A            H212      R             57
     A                                      EDTCDE(J)
     A                                      REFFLD(H212 PY2HPF)
     A            H214      R             68
     A                                      EDTCDE(J)
     A                                      REFFLD(H214 PY2HPF)
     A            H215      R             79
     A                                      EDTCDE(J)
     A                                      REFFLD(H215 PY2HPF)
     A            H213      R             90
     A                                      EDTCDE(J)
     A                                      REFFLD(H213 PY2HPF)
     A                                   105
     A                                      '30'
     A            PH205          4  1    112
     A                                      EDTCDE(J)
     A            H206      R            120
     A                                      EDTCDE(J)
     A                                      REFFLD(H206 PY2HPF)
     A                                   130
     A                                      '|'
     A            PEM13     R            132
     A                                      REFFLD(EM13)
     A                                      EDTWRD('    /  /  ')
     A            H216Y          2  0    143
     A                                      EDTCDE(1)
     A                                   145
     A                                      '年'
     A            H216M          2  0    149
     A                                      EDTCDE(1)
     A                                   151
     A                                      '月'
     A            PH209          4  1    157
     A                                      EDTCDE(J)
     A            PH208          4  1    165
     A                                      EDTCDE(J)
     A                                   173
     A                                      '1/4'
     A*
     A*-------------------------------------------------------------***
     A          R PT1
     A                                      SPACEB(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =='
     A                                      SPACEB(001)
     A            TT11           5  0     24
     A                                      EDTCDE(J)
     A                                      SPACEB(001)
     A                                    12
     A                                      '公司合計:'
     A                                    31
     A                                      '人'
     A            TT12           8  0    119
     A                                      EDTCDE(J)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =='
     A                                      SPACEB(001)
     A*-------------------------------------------------------------***
     A          R PE1
     A                                      SPACEB(001)
     A                                     1
     A                                      '總經理'
     A                                      SPACEB(001)
     A                                    40
     A                                      '管理部'
     A                                    80
     A                                      '人事課'
     A                                   120
     A                                      '承辦人'
     A*-------------------------------------------------------------***
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
