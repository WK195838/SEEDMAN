     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYA711D                                    *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 89/02/23                                   *
     A*    UPDATE DATE  : 89/09/04 A1274 EVA                         *
     A*    PROGRAM NAME : PYA711                                     *
     A*    SYSTEM       : MIRS                                       *
     A*    SUBSYSTEM    : PY                                         *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYTBPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A                                      CA03(03)
     A*------------------------------------------------------------***
     A          R DSPC1
     A                                  1  2'<PYA711D>'
     A                                  1 24'＜＜其他所得稅課稅資料維護＞＞'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2  2'<PYA711.1>'
     A                                  4 29'公司編碼:'
     A            DTB11     R        B  4 41REFFLD(TB11)
     A  60                                  DSPATR(RI PC)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  6 29'所得年月:'
     A            DTB16     R   -2   B  6 41REFFLD(TB16)
     A  61                                  DSPATR(RI PC)
     A                                      EDTWRD('0  /  ')
     A                                  8 29'格式代號:'
     A            DTB04     R        B  8 41REFFLD(TB04)
     A  62                                  DSPATR(RI PC)
     A                                 10 29'身份証號:'
     A            DTB05     R        B 10 41REFFLD(TB05)
     A  63                                  DSPATR(RI PC)
     A                                 12 29'作業選擇:'
     A            DOPT           1A  B 12 41
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 12 46'1.新增'
     A                                 13 46'2.修改'
     A                                 14 46'4.刪除'
     A                                 15 46'5.查詢'
     A                                 18  3'◎格式代號:'
     A                                 18 18'50-薪資　'
     A                                 18 30'51-租賃　　　　　'
     A                                 18 50'5A,5B,5C,5D,5E-其他利息'
     A                                 19 18'53-權利金'
     A                                 19 30'54-盈餘　　　　　'
     A                                 19 50'9A-執行業務報酬　'
     A                                 20 18'9B-稿費　'
     A                                 20 30'91-競技及機會中獎'
     A                                 20 50'92-其他所得　　　'
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 24 67'【泛太資訊】'
     A*-----------------------------------------------------------*
     A          R DSPC2
     A                                      CA12(12)
     A                                  1  2'<PYA711D>'
     A                                  1 24'＜＜其他所得稅課稅資料維護＞＞'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2  2'<PYA711.2>'
     A                                  4  2'公司編碼:'
     A            DTB11     R        O  4 14REFFLD(TB11)
     A            CM03          34A  O  4 18
     A                                  5  2'所得年月:'
     A            DTB16     R   -2   O  5 14REFFLD(TB16)
     A                                      EDTWRD('0  /  ')
     A                                  6  2'格式代號:'
     A            DTB04     R        O  6 14REFFLD(TB04)
     A            A2TB04        16A  O  6 18
     A                                  7  2'身份証號:'
     A            DTB05     R        O  7 14REFFLD(TB05)
     A                                  9  2'給付總額:'
     A            TB07      R        B  9 14EDTCDE(J)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'扣繳稅額:'
     A            TB08      R        B 10 14EDTCDE(J)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  2'證號別　:'
     A            TB06      R        B 12 14
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 17'( " "-身份證號 1-統一編號 3-+
     A                                      外籍無地址 4-外籍有地址)'
     A                                 13  2'證號錯誤:'
     A            TB10      R        B 13 14
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 17'( " "-無誤　　 A-錯誤　　 B-+
     A                                      無從查對)'
     A                                 14  2'員工編號:'
     A            TB12      R        B 14 14
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  2'特定編號:'
     A            TB09      R        B 15 14
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 27'(註)'
     A                                 16  2'所得姓名:'
     A            TB13      R        B 16 14
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17  2'所得地址:'
     A            TB14      R        B 17 14
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18  2'所得月份:'
     A            TB15D     R   -2   B 18 14EDTWRD('0  /  ')
     A                                      REFFLD(TB15)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 20'-'
     A            TB16D     R   -2   O 18 22EDTWRD('0  /  ')
     A                                      REFFLD(TB16)
     A* 71                                  DSPATR(RI)
     A* 71                                  DSPATR(PC)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                  4 54'媒體申報:'
     A            TB02      R        O  4 66
     A                                  4 71'-'
     A            TB03      R        O  4 73
     A                                 20  4'註：租賃所得為房屋稅籍編號，執行+
     A                                      業務所得為業別，其它所得為所得者+
     A                                      代號'
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
