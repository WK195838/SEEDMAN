     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR802P                                    *
     A*    PROGRAM NAME :業務獎金資料列印                          *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 96/08/01                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1
     A                                      SPACEA(001)
     A                                     1
     A                                      '業績獎金明細表'
     A                                      SPACEA(002)
     A                                    51
     A                                      '日期:'
     A            DDATE          6  0     59
     A                                      EDTWRD('0  /  /  ')
     A                                    68
     A                                      '頁次:'
     A                                    76
     A                                      PAGNBR
     A                                      SPACEA(002)
     A                                     1
     A                                      '發放日期:'
     A            PPH96          6S 0     12
     A                                      EDTWRD('0  /  /  ')
     A                                    25
     A                                      '公司:'
     A            DOS01          2A       32
     A                                    51
     A                                      '時間:'
     A                                    59
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    69
     A                                      '<PYR802P>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A                                     1
     A                                      '員工編號'
     A                                    17
     A                                      '員工姓名'
     A                                    39
     A                                      '業績獎金'
     A                                    52
     A                                      '獎金稅額'
     A                                    65
     A                                      '實際金額'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A          R PD1
     A            POS02     R              2
     A                                      REFFLD(OS02 PYOSPF)
     A            PEM03     R             17REFFLD(EM03 PYEMPF)
     A            POS06     R             40
     A                                      REFFLD(OS06 PYOSPF)
     A                                      EDTCDE(1)
     A            TEMP      R             53
     A                                      REFFLD(OS06 PYOSPF)
     A                                      EDTCDE(1)
     A            POS07     R             66
     A                                      REFFLD(OS06 PYOSPF)
     A                                      EDTCDE(1)
     A                                      SPACEA(002)
     A          R PT1
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEA(001)
     A                                    19
     A                                      '小計'
     A            PTO01          7  0     40
     A                                      EDTCDE(1)
     A            PTO02          7  0     53
     A                                      EDTCDE(1)
     A            PTO03          7  0     66
     A                                      EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A                                     1
     A                                      'PYR802P--->報表結束'
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
