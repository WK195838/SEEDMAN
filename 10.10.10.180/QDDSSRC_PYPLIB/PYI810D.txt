     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYBSPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            SOPT           1A  B  8  2DSPATR(HI)
     A N30                                  DSPATR(UL PC)
     A  30                                  DSPATR(PR)
     A            SBS01     R        O  8  6REFFLD(BS01)
     A            SBS02     R        O  8 12REFFLD(BS02)
     A            SEM03     R   -4   O  8 21REFFLD(EM03 PYEMPF)
     A            SBS03     R   -2   O  8 34REFFLD(BS03)
     A                                      EDTWRD('0  /  ')
     A            SBS05     R        O  8 45REFFLD(BS05)
     A            SBS06     R   -2   O  8 52REFFLD(BS06)
     A                                      EDTWRD('0  /  ')
     A            SBS07     R   -2   O  8 63REFFLD(BS07)
     A                                      EDTWRD('0  /  ')
     A            SBS04     R        O  8 74REFFLD(BS04)
     A*----------------------------------------------------------------
     A**                                    CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI810D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'薪資遞延發放資料查詢'
     A                                      DSPATR(RI)
     A                                  5  2'S (=作業) : /-挑選'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'公司'
     A                                      DSPATR(UL)
     A                                  6 11'員工編號'
     A                                      DSPATR(UL)
     A                                  6 32'補發年月'
     A                                      DSPATR(UL)
     A                                  6 43'期別'
     A                                      DSPATR(UL)
     A                                  6 50'起始年月'
     A                                      DSPATR(UL)
     A                                  6 61'截止年月'
     A                                      DSPATR(UL)
     A                                  6 72'狀態'
     A                                      DSPATR(UL)
     A            KBS01     R        B  7  6REFFLD(BS01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            KBS02     R        B  7 12REFFLD(BS02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A                                 23 36'PA1 =下一頁　'
     A                                 23 53'PA2 =上一頁　'
