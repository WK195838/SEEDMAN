     A*%%TS  SD  20060319  191535  PPSPGMRCU   REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA903D                                    *
     A*    PROGRAM NAME :勞健保及勞退保額調整維護                  *
     A*    AUTHOR       : A1445 HONDA                                *
     A*    CREATE DATE  : 94/12/13                                   *
     A*    UPDATE DATE  : 95/03/23                                   *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYLOPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19970222  142213  ESPGMR      REL-V3R1M0  5763-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA903.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 27'勞健保及勞退保額調整維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DLO01S         2A  B    +1
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                    +1'-'
     A            DLO01E         2A  B    +1
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A            DLO02S         8A  B    +1
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                    +1'-'
     A            DLO02E         8A  B    +1
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  6  1'年　　月:'
     A            DLO04          4  0B    +1
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                      EDTWRD('0  /  ')
     A                                  7  1'保額變動:'
     A            DLO13          1A  B    +1
     A                                    +1'(Y/N/A)'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A*                                23 20'PF04=欄位查詢'
     A*                                23 38'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A          R DSPD3
     A*%%TS  SD  20060319  191535  PPSPGMRCU   REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA903.3>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 27'勞健保及勞退保額調整維護'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4  1'公司編號:'
     A            LO01           2A  O  4 13
     A            CM03      R        O  4 24REFFLD(CM03 SYCMPF)
     A                                  5  1'部門編號:'
     A            LO05           7A  O  5 13
     A            DP08      R        O  5 24REFFLD(DP08 PYDPPF)
     A                                  6  1'年　　月:'
     A            DLO04          4Y 0O  6 13EDTWRD('0  /  ')
     A                                  7  1'員工編號:'
     A            LO02           8A  O  7 13TEXT('員工編號')
     A            LO04          14A  O  7 24TEXT('員工姓名')
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 13  1'舊勞保金額:'
     A            LO07      R        O 13 15REFFLD(LO07)
     A                                      EDTCDE(1)
     A                                 14  1'新勞保金額:'
     A            DLO08          7Y 0B 14 15EDTCDE(1)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  1'舊健保金額:'
     A            LO09      R        O 11 15REFFLD(LO09)
     A                                      EDTCDE(1)
     A                                 12  1'新健保金額:'
     A            DLO10          7Y 0B 12 15EDTCDE(1)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  1'舊勞退金額:'
     A            LO11      R        O  9 15REFFLD(LO11)
     A                                      EDTCDE(1)
     A                                 10  1'新勞退金額:'
     A            DLO12          6Y 0B 10 17EDTCDE(1)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17  1'平均薪資:'
     A            DLO15          7Y 0O 17 15EDTCDE(1)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A* 54                                  SFLNXTCHG
     A            RRN            3Y 0H
     A            LO08      R        H      REFFLD(LO08)
     A                                      TEXT('新勞保')
     A            LO10      R        H      REFFLD(LO10)
     A                                      TEXT('新健保')
     A            LO05      R        H      REFFLD(LO05)
     A                                      TEXT('部門')
     A            LO15      R        H      REFFLD(LO15)
     A                                      TEXT('平均金額')
     A            OPT            1A  B  9  2
     A  69                                  DSPATR(RI PC)
     A            LO01      R        O    +2REFFLD(LO01)
     A  30                                  COLOR(PNK)
     A            LO02      R        O    +3REFFLD(LO02)
     A  30                                  COLOR(PNK)
     A            LO04      R        O    +1REFFLD(LO04)
     A  30                                  COLOR(PNK)
     A            LO07      R        O    +1REFFLD(LO07)
     A  30                                  COLOR(PNK)
     A                                      EDTCDE(1)
     A            LO09      R        O    +1REFFLD(LO09)
     A  30                                  COLOR(PNK)
     A                                      EDTCDE(1)
     A            LO11      R        O    +1REFFLD(LO11)
     A  30                                  COLOR(PNK)
     A                                      EDTCDE(1)
     A            LO12      R        O    +1REFFLD(LO12)
     A  30                                  COLOR(PNK)
     A                                      EDTCDE(1)
     A            LO13      R        O    +2REFFLD(LO13)
     A  30                                  COLOR(PNK)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF23(23)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                      ROLLUP(25)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA903.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 27'勞健保及勞退保額調整維護'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DLO01S         2A  O    +1
     A                                    +1'-'
     A            DLO01E         2A  O    +1
     A                                  5  1'員工編號:'
     A            DLO02S         8A  O    +1
     A                                    +1'-'
     A            DLO02E         8A  O    +1
     A                                  6  1'年　　月:'
     A            DLO04          4  0O    +1
     A                                      EDTWRD('0  /  ')
     A                                  7  1'保額變動:'
     A            DLO13          1A  O    +1
     A                                    +1'(Y/N/A)'
     A                                  8  1'　公司　員工編號　　員工姓名　 +
     A                                      　舊勞保　　舊健保　+
     A                                      　舊勞退　新勞退變更'
     A                                      DSPATR(UL)
     A
     A          R DSPC2
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PA01 =下一頁'
     A                                 23 36'PA02 =上一頁'
     A                                 23 52'PA23 =資料確認'
     A                                 24  2'PF12 =回上一層'
     A                                 24 67'【泛太資訊】'
