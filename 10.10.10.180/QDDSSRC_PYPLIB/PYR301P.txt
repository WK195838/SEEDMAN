     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM ID   : PYR301P                                    *
     A*    PROGRAM NAME :薪資基本資料明細表                      *
     A*    AUTHOR       : A1084  CHARLES                             *
     A*    CREATE DATE  : 81/02/14                                   *
     A*    UPDATE DATE  : 90/04/18 A1274  EVA                        *
     A*    SYSTEM       :人事薪資系統                              *
     A*    REMARK       :薪資基本資料列印                          *
     A****************************************************************
     A                                      REF(PYRF)
     A*------------------------------------------------------------***
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1
     A                                   136
     A                                      '日期:'
     A            DDATE          6  0    144
     A                                      EDTWRD('0  /  /  ')
     A                                   153
     A                                      '頁次:'
     A                                   161
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     1
     A                                      '薪資基本資料明細表'
     A                                      SPACEB(001)
     A                                   136
     A                                      '時間:'
     A                                   144
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   154
     A                                      '<PYR301P>'
     A*
     A                                     1
     A                                      '部門代號:'
     A                                      SPACEB(002)
     A            DEM041         7        13
     A                                    +1
     A                                      '-'
     A            DEM042         7        +1
     A                                    +1
     A                                      '員工編號:'
     A            DEM021    R             +1
     A                                      REFFLD(EM02)
     A                                    +1
     A                                      '-'
     A            DEM022    R             +1
     A                                      REFFLD(EM02)
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======================='
     A                                      SPACEB(001)
     A*
     A                                     1'部　門'
     A                                      SPACEB(001)
     A                                    22'員　工'
     A                                    46'職稱'
     A                                    62'職等職級'
     A                                    75'基本底薪'
     A            SS021     R             90REFFLD(SS02)
     A            SS022     R            105REFFLD(SS02)
     A            SS023     R            120REFFLD(SS02)
     A            SS024     R            135REFFLD(SS02)
     A                                   152'合計金額'
     A                                   164'存入銀行'
     A                                   176'存入帳號'
     A                                   190'離職碼'
     A*------------------------------------------------------------***
     A          R PH11
     A                                      SPACEB(001)
     A            SS021     R             90REFFLD(SS02)
     A            SS022     R            105REFFLD(SS02)
     A            SS023     R            120REFFLD(SS02)
     A            SS024     R            135REFFLD(SS02)
     A*------------------------------------------------------------***
     A          R PH12
     A                                      SPACEB(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======================='
     A*------------------------------------------------------------***
     A          R PD1
     A                                      SPACEB(001)
     A            EM041     R              1
     A            EM042     R              2
     A            EM043     R              3
     A            EM044     R              4
     A            DP02      R              6REFFLD(DP02 PYDPPF)
     A            PY02      R             23
     A            EM03      R             31
     A            JT02      R             46
     A            EM06      R             64
     A            EM07      R             68
     A*           PY03      R             75EDTCDE(J)
     A            DPY03          7  0     75EDTCDE(J)
     A            PD041     R             90EDTCDE(J)
     A                                      REFFLD(PD04)
     A            PD042     R            105EDTCDE(J)
     A                                      REFFLD(PD04)
     A            PD043     R            120EDTCDE(J)
     A                                      REFFLD(PD04)
     A            PD044     R            135EDTCDE(J)
     A                                      REFFLD(PD04)
     A            PALL      R            152EDTCDE(J)
     A                                      REFFLD(PD04)
     A            PBN02         10       164
     A            PY06      R            176
     A            EM11      R            194
     A*------------------------------------------------------------***
     A          R PD2
     A                                      SPACEB(001)
     A            PD041     R             90EDTCDE(J)
     A                                      REFFLD(PD04)
     A            PD042     R            105EDTCDE(J)
     A                                      REFFLD(PD04)
     A            PD043     R            120EDTCDE(J)
     A                                      REFFLD(PD04)
     A            PD044     R            135EDTCDE(J)
     A                                      REFFLD(PD04)
     A*------------------------------------------------------------***
     A          R PT5
     A                                      SPACEA(001)
     A N35                                 1' '
     A                                      SPACEB(002)
     A N35                                 1'公司合計==>'
     A N35        PT51           5  0     +1EDTCDE(1)
     A N35                                +0'人'
     A N35        PT52           9  0     73EDTCDE(J)
     A            T5041         10  0     86EDTCDE(J)
     A            T5042         10  0    101EDTCDE(J)
     A            T5043         10  0    116EDTCDE(J)
     A            T5044         10  0    131EDTCDE(J)
     A N35        T5045         10  0    148EDTCDE(J)
     A*------------------------------------------------------------***
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======================='
     A*
     A                                     1
     A                                      'PYR301P--->報表結束'
     A                                      SPACEB(001)
     A*------------------------------------------------------------***
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
     A*------------------------------------------------------------***
     A*------------------------------------------------------------***
     A*------------------------------------------------------------***
     A          R PT1
     A                                      SPACEA(001)
     A N31                                 1
     A                                      ' '
     A                                      SPACEB(002)
     A N31                                 1
     A                                      '單位小計==>'
     A N31        PT11           5  0     +1
     A                                      EDTCDE(1)
     A N31                                +0
     A                                      '人'
     A N31        PT12           9  0     55
     A                                      EDTCDE(1)
     A            T1041         10  0     68
     A                                      EDTCDE(1)
     A            T1042         10  0     83
     A                                      EDTCDE(1)
     A            T1043         10  0     98
     A                                      EDTCDE(1)
     A            T1044         10  0    113
     A                                      EDTCDE(1)
     A*------------------------------------------------------------***
     A          R PT2
     A                                      SPACEA(001)
     A N32                                 1
     A                                      '部門小計==>'
     A N32        PT21           5  0     +1
     A                                      EDTCDE(1)
     A N32                                +0
     A                                      '人'
     A N32        PT22           9  0     55
     A                                      EDTCDE(1)
     A            T2041         10  0     68
     A                                      EDTCDE(1)
     A            T2042         10  0     83
     A                                      EDTCDE(1)
     A            T2043         10  0     98
     A                                      EDTCDE(1)
     A            T2044         10  0    113
     A                                      EDTCDE(1)
     A*------------------------------------------------------------***
     A          R PT3
     A                                      SPACEA(001)
     A N33                                 1
     A                                      '部門中計==>'
     A N33        PT31           5  0     +1
     A                                      EDTCDE(1)
     A N33                                +0
     A                                      '人'
     A N33        PT32           9  0     55
     A                                      EDTCDE(1)
     A            T3041         10  0     68
     A                                      EDTCDE(1)
     A            T3042         10  0     83
     A                                      EDTCDE(1)
     A            T3043         10  0     98
     A                                      EDTCDE(1)
     A            T3044         10  0    113
     A                                      EDTCDE(1)
     A*------------------------------------------------------------***
     A          R PT4
     A                                      SPACEA(001)
     A N34                                 1
     A                                      '部門合計==>'
     A N34        PT41           5  0     +1
     A                                      EDTCDE(1)
     A N34                                +0
     A                                      '人'
     A N34        PT42           9  0     55
     A                                      EDTCDE(1)
     A            T4041         10  0     68
     A                                      EDTCDE(1)
     A            T4042         10  0     83
     A                                      EDTCDE(1)
     A            T4043         10  0     98
     A                                      EDTCDE(1)
     A            T4044         10  0    113
     A                                      EDTCDE(1)
