     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR113D                                    *
     A*    PROGRAM NAME :離職人數統計表                          *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 1997/01/14                                 *
     A*    UPDATE DATE  : 1999/10/04 A1274 EVA                       *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYR113D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 33'離職人數統計表'
     A                                      DSPATR(RI)
     A                                  5 30'公司編號　　:'
     A            DEM01     R        B  5 46REFFLD(EM01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 30'部門編號　起:'
     A                                      DSPATR(HI)
     A            DEM041         7A  B  7 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 30'　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM042         7A  B  8 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 30'離職年月　　:'
     A            DYM            4Y 0B 10 46
     A                                      EDTWRD('0  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A  27                             21 27'***請準備好８０格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
