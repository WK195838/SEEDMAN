     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYR403D                                    *
     A*    PROGRAM NAME :特別休假對照表                            *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 1997/01/17                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<PYR403D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 33'特別休假對照表'
     A                                      DSPATR(RI)
     A*
     A                                  4 28'公司編號　　　:'
     A            DLT01     R        B  4 46REFFLD(LT01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 28'年　　度　　　:'
     A            DLT03     R   -1   B  7 46REFFLD(LT03)
     A                                      EDTWRD('0   ')
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  9 28'部門編號　　起:'
     A                                      DSPATR(HI)
     A            DEM04S         7   B  9 46
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 10 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM04E         7   B 10 46
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 12 28'員工編號　　起:'
     A                                      DSPATR(HI)
     A            DLT02S    R        B 12 46REFFLD(LT02)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 13 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DLT02E    R        B 13 46REFFLD(LT02)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 27'*** 請準備８０格報表紙 ***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
