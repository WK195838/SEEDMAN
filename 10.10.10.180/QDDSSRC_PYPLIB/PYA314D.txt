     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PYA314D                                    *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  : 88/07/19                                   *
     A*    UPDATE DATE  :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03 '回主畫面')
     A                                      CF04(04)
     A                                      CF13(13)
     A                                      CF15(15)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA314D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 29'其它薪資批次資料建立'
     A                                      DSPATR(RI)
     A*
     A                                  5  3'公司編號:'
     A            DWD01     R        B  5 15REFFLD(WD01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  6  3'薪資年月:'
     A            DWD03     R   -1   B  6 15REFFLD(WD03)
     A                                      EDTWRD('0   /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                  7  3'期　　別:'
     A            DWD04     R        B  7 15REFFLD(WD04)
     A                                      VALUES('1' '2' '3')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  7 17'1.下期'
     A                                    +2'2.上期'
     A                                    +2'3.外勞'
     A                                  8  3'薪資項目:'
     A                                      DSPATR(HI)
     A            DWD05     R        B  8 15REFFLD(WD05)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            SS02      R        O  8 19
     A****************************************************************
     A*                                 8  3'員工編號:'
     A            DWD02     R        H      REFFLD(WD02)
     A* 63                                  DSPATR(PC)
     A* 63                                  DSPATR(RI)
     A            EM033         12   H
     A            EM032         20   H
     A****************************************************************
     A                                  8  3'部　　門:'
     A            DDS04          5   B  8 15
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A*
     A                                  9  3'處理方式:'
     A            DOPT           1Y 0B  9 15EDTCDE(4)
     A                                      VALUES(1 2 3)
     A                                  9 17'1.新增不取代'
     A                                 10 17'2.新增且取代'
     A                                 11 17'3.新增並累加'
     A*
     A                                 15  1'<<扣款或所得方式>>'
     A                                 17  5'每一在職員工底薪　除以'
     A            D01            3  1B 17 30EDTCDE(2)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 17 35'再乘以'
     A            D02            3  1B 17 44EDTCDE(2)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 17 49'或固定金額：'
     A            D03            6  0B 17 64EDTCDE(2)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A*                                17  7'註：底薪即加班費計薪基礎'
     A  97                             20 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(RI)
     A  96                             20 23'　　　作業處理中！！　　'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =查詢'
     A                                 23 38'PF13 =處理開始'
     A*                                23 38'PF15 =強迫處理'
     A                                 24 67'【泛太資訊】'
