     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA310D                                    *
     A*    PROGRAM NAME :員工晉升試算作業  　　                  *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 91/09/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF13(13)
     A                                  1  2'<PYA310.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工晉升試算作業'
     A                                      DSPATR(RI)
     A*
     A                                  5 28'公司編號  :'
     A            DT801     R        B  5 42REFFLD(T801)
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A*
     A                                  6 28'員工編號  :'
     A            DT802     R        B  6 42REFFLD(T802)
     A  61                                  DSPATR(PC RI)
     A*
     A                                  7 28'生效日期  :'
     A            DT804     R   -1  0B  7 42REFFLD(T804)
     A                                      EDTWRD('0   /  /  ')
     A  62                                  DSPATR(PC RI)
     A*
     A                                  8 28'晉升職等數:'
     A            DTT1           2  0B  8 42
     A                                      EDTCDE(4)
     A  63                                  DSPATR(PC RI)
     A                                  8 46'等'
     A*
     A                                  9 28'　　或級數:'
     A            DTT2           3  0B  9 42
     A                                      EDTCDE(3)
     A  64                                  DSPATR(PC RI)
     A                                  9 46'級'
     A                                 10 28'晉升原因  :'
     A            DSH05         14O  B 10 42
     A                                 12 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 13 11' '
     A                                      DSPATR(RI)
     A                                 13 66' '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A  97                             14 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  96                             14 30'試算作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
