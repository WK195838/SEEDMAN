     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PYA321D                                    *
     A*    AUTHOR       : JADI                                       *
     A*    CREATE DATE  : 98/12/14                                   *
     A*    UPDATE DATE  :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYAIPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03 '回主畫面')
     A                                      CF18(18)
     A                                  1  2'<PYA321.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 27'公司年度調薪(AI)公式設定 '
     A                                      DSPATR(RI)
     A*
     A                                  4  3'公司編號:'
     A            DAI01     R        B  4 16REFFLD(AI01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A*N32                                  DSPATR(PR)
     A                                  5  3'調薪年度:'
     A            DAI02     R   -2   B  5 16REFFLD(AI02)
     A                                      EDTWRD('  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  3'調薪月份:'
     A            DAI03     R        B  6 16REFFLD(AI03)
     A                                      EDTWRD('  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  3'調薪別:'
     A            DAI04     R        O  7 16REFFLD(AI04)
     A                                      DFTVAL('AI')
     A                                  8  3'作業選擇:'
     A            DOPT           1Y 0B  8 16VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  8 18'( 1.新增'
     A                                    +1'2.修改'
     A                                    +1'4.刪除'
     A                                    +1'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                  1  2'<PYA321.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 27'公司年度調薪(AI)公式設定 '
     A                                      DSPATR(RI)
     A*
     A                                  4  3'公司編號:'
     A            DAI01     R        O  4 16REFFLD(AI01)
     A                                  5  3'調薪年度:'
     A            DAI02     R   -2   O  5 16REFFLD(AI02)
     A                                      EDTWRD('  ')
     A                                  6  3'調薪月份:'
     A            DAI03     R        O  6 16REFFLD(AI03)
     A                                      EDTWRD('  ')
     A                                  7  3'調薪別:'
     A            DAI04     R        O  7 16REFFLD(AI04)
     A                                      DFTVAL('AI')
     A                                  8  3'作業選擇:'
     A            DOPT           1Y 0O  8 16
     A                                      EDTCDE(4)
     A                                  8 18'( 1.新增'
     A                                    +1'2.修改'
     A                                    +1'4.刪除'
     A                                    +1'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                 10  3'基準日期:'
     A            DAI05          6Y 0B 10 16EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 12  1'<<調薪公式>>'
     A                                 14  5'調薪比例＝'
     A            DAI06     R        B 14 20VALUES('A' 'B' 'C')
     A                                      REFFLD(AI06)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 14 23'*'
     A            DAI07     R        B 14 25REFFLD(AI07)
     A                                      EDTWRD('  .  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 14 31'% +'
     A            DAI08     R        B 14 35REFFLD(AI08)
     A                                      EDTWRD('   ,   ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 15  7'A:本薪  B:(職等上限＋下限)/2'
     A                                    +2' C:職等基本調薪額'
     A                                 17  5'尾數處理至'
     A            DAI09     R        B 17 18REFFLD(AI09) VALUES(0 1 2 3)
     A                                      DFTVAL('0')
     A                                    +2'(0:個位數 1:十位數 2:百位數'
     A                                    +1'3:千位數)'
     A                                 18 13'採'
     A            DAI10     R        B 18 18REFFLD(AI10) VALUES('H' 'P' 'M')
     A                                      DFTVAL('H')
     A                                    +3'(H:四捨五入 P:無條件進位'
     A                                    +2'M:無條件捨去)'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F12=回前畫面'
     A                                 23 44'F13=試算及印表'
     A                                 24 67'【泛太資訊】'
