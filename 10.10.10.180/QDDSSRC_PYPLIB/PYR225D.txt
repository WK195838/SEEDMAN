     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR205D                                    *
     A*    PROGRAM NAME :加班請假記錄表列印                        *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 2001/10/18                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A*------------------------------------------------------------***
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<PYR225D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'個人加班請假記錄表'
     A                                      DSPATR(RI)
     A*
     A                                  4 28'公司編號　　　:'
     A            DEM01     R        B  4 46REFFLD(EM01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  6 28'部門編號　　起:'
     A                                      DSPATR(HI)
     A            DEM041         7   B  6 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM042         7   B  7 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 28'員工編號　　起:'
     A                                      DSPATR(HI)
     A            DEM021    R        B  9 46REFFLD(EM02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 10 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM022    R        B 10 46REFFLD(EM02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 12 28'起迄日期　　起:'
     A            DOV031    R   -2   B 12 46REFFLD(OV03)
     A                                      EDTWRD('0  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 13 28'　　　　　　迄:'
     A            DOV032    R   -2   B 13 46REFFLD(OV03)
     A                                      EDTWRD('0  /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A*
     A*                                15 28'考勤年度　　　:'
     A*           DYEAR          2  0B 15 46
     A*                                     EDTWRD('0  ')
     A* 66                                  DSPATR(PC)
     A* 66                                  DSPATR(RI)
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 27'***請準備１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF03 =回主畫面'
     A                                 23 28'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
