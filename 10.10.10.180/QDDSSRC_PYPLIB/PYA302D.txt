     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYA302D                                    *
     A*    AUTHOR       : A1070  CHOIE                               *
     A*    CREATE DATE  : 79/06/01                                   *
     A*    UPDATE DATE  : 790629                                     *
     A*    PROGRAM NAME : PYA301                                     *
     A*    SYSTEM       : MIRS                                       *
     A*    SUBSYSTEM    : PY                                         *
     A*    REMARK       :薪資基本資料建立                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA302.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工調薪資料建立'
     A                                      DSPATR(RI)
     A*
     A                                  4  1'公司編號:'
     A            DSH01     R        B  4 13REFFLD(SH01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DSH02     R        B  5 13REFFLD(SH02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'生效日期:'
     A            DSH03     R   -1   B  6 13REFFLD(SH03)
     A                                      EDTWRD('0   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                  7 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            2S 0O 11  7
     A            DNEW           1   H
     A            DSD04     R        B 11 16REFFLD(SD04)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            SS02      R        O 11 24
     A            SS06      R        O 11 44
     A            DSD05     R        O 11 52REFFLD(SD05)
     A                                      EDTCDE(2)
     A            DSD06     R        B 11 65REFFLD(SD06)
     A                                      CHECK(RB)
     A                                      EDTCDE(2)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0008)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA302.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工調薪資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4  2'公　　司:'
     A            DSH01     R        O  4 14REFFLD(SH01)
     A            DSH01N        14O  O  4 23
     A                                  4 43'部　　門:'
     A            DEM04          7A  O  4 55
     A            DP02      R        O  4 63REFFLD(DP02 PYDPPF)
     A                                  5  2'員　　工:'
     A            DSH02     R        O  5 14REFFLD(SH02)
     A            EM03      R        O  5 23
     A                                  5 43'職　　稱:'
     A            EM05      R        O  5 55
     A            JT02      R        O  5 63
     A                                  6  2'調薪原因:'
     A                                      DSPATR(HI)
     A            DSH04     R        B  6 14REFFLD(SH04)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DSH05     R        O  6 23REFFLD(SH05)
     A                                  6 43'生效日期:'
     A            DSH03     R   -1   O  6 55REFFLD(SH03)
     A                                      EDTWRD('0   /  /  ')
     A                                  7  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  8  5'序'
     A                                  8 12'薪資代碼'
     A                                      DSPATR(HI)
     A                                  8 23'薪資名稱'
     A                                  8 41'薪資別'
     A                                  8 54'調薪前'
     A                                  8 67'調薪後'
     A                                  9  5'----'
     A                                  9 13'--------'
     A                                  9 24'--------------'
     A                                  9 42'------'
     A                                  9 52'---------'
     A                                  9 65'---------'
     A                                 10 16'10'
     A                                 10 24'底薪'
     A                                 10 44'A'
     A            DSH07     R        O 10 52REFFLD(SH07)
     A                                      EDTCDE(1)
     A            DSH08     R        B 10 65REFFLD(SH08)
     A                                      EDTCDE(1)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A*
     A          R DSPC1                     OVERLAY
     A                                 19  2'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =================='
     A                                 20  2'薪資別= A-固定金額B-薪資比率+
     A                                       C-變動金額'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                    +2'PF04=欄位查詢'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PF13=確認'
     A                                 24  2'PA1=下一頁'
     A                                    +2'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
