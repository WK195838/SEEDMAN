     A*%%TS  SD  20000817  170138  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA121D                                    *
     A*    PROGRAM NAME :員工工作內容資料輸入                      *
     A*    AUTHOR       : A1266 HANK                                 *
     A*    CREATE DATE  : 89/08/17                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA121.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'員工工作內容資料輸入'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DD201     R        B  4 13REFFLD(D201)
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DD202     R        B  5 13REFFLD(D202)
     A  61                                  DSPATR(PC RI)
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 16'( 1.新增 2.修改 4.刪除 5.+
     A                                      查詢)'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF04=欄位查詢'
     A***                              23 20'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*-------------------------------------------------------------***
     A          R DSPD2
     A*%%TS  SD  20000817  170138  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF12(12)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA121.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'員工工作內容資料輸入'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72
     A                                  4  1'公司編號:'
     A            DD201     R        O  4 13REFFLD(D201)
     A            DD201N        14O  O  4 22
     A                                  5  1'員工編號:'
     A            DD202     R        O  5 13REFFLD(D202)
     A            DD202N    R        O  5 22REFFLD(EM03)
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  1'備註說明:'
     A            DD203     R        B  8 12REFFLD(D203)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD204     R        B  9 12REFFLD(D204)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD205     R        B 10 12REFFLD(D205)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD206     R        B 11 12REFFLD(D206)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD207     R        B 12 12REFFLD(D207)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD208     R        B 13 12REFFLD(D208)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD209     R        B 14 12REFFLD(D209)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD210     R        B 15 12REFFLD(D210)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD211     R        B 16 12REFFLD(D211)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD212     R        B 17 12REFFLD(D212)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD213     R        B 18 12REFFLD(D213)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD214     R        B 19 12REFFLD(D214)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD215     R        B 20 12REFFLD(D215)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DD216     R        B 21 12REFFLD(D216)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
