     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA228D                                    *
     A*    PROGRAM NAME :年休補發基礎薪資項目維護                  *
     A*    AUTHOR       : A1386 SHARON                               *
     A*    CREATE DATE  : 94/02/23                                   *
     A*    UPDATE DATE  :
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA228.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 28'年休補發基礎薪資項目維護'
     A                                      DSPATR(RI)
     A                                  5  1'  公　司:'
     A*                                     DSPATR(HI)
     A            DPD401    R        B  5 13REFFLD(PD401 PYPD4PF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  6  1'  職務別:'
     A                                      DSPATR(HI)
     A            DPD402    R        B  6 13REFFLD(PD402 PYPD4PF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            3Y 0H
     A            DPD403    R        B  9 15REFFLD(PD403 PYPD4PF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  61                                  DSPATR(PC RI)
     A            DSS02     R        B  9 21REFFLD(SS02 PYSSPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  62                                  DSPATR(PC RI)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0012)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA228.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 28'獎金計算基礎薪資項目維護'
     A                                      DSPATR(RI)
     A                                  5  1'  公　司:'
     A            DPD401    R        O  5 13REFFLD(PD401 PYPD4PF)
     A            CM02      R        O  5 16REFFLD(CM02 SYCMPF)
     A                                  6  1'  職務別:'
     A            DPD402    R        O  6 13REFFLD(PD402 PYPD4PF)
     A            DJT02     R        O  6 18REFFLD(JT02 PYJTPF)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  8  1'薪資項目:'
     A                                  8 13'項目'
     A                                      DSPATR(HI)
     A                                  8 21'名稱'
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PA01 =下一頁'
     A                                 23 36'PA02 =上一頁'
     A                                 23 52'PA13 =資料確認'
     A                                 24  2'PF12 =回上一層'
     A                                 24 67'【泛太資訊】'
