     A*%%TS  SD  20051118  141548  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI409D                                    *
     A*    PROGRAM NAME :稅所得登錄作業維護                        *
     A*    AUTHOR       : A1269                                      *
     A*    CREATE DATE  : 88/01/26                                   *
     A*    UPDATE DATE  : 89/09/05                                   *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A*%%TS  SD  20051116  162121  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #FLD          10A  H
     A            #REC          10A  H
     A            DOPT           1Y 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYI409>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'年度稅所得登錄作業查詢'
     A                                      DSPATR(RI)
     A                                  2 64'時間:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'公司編號:'
     A            DPH01     R        B  4 13REFFLD(EM01)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DPH02     R        B  5 13REFFLD(EM02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            EM03      R        O  5 22REFFLD(EM03)
     A                                  5 45'年度:'
     A            DYY            2Y 0B  5 53EDTWRD('0  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF04=欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A*%%TS  SD  20051118  141548  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A            DNEW           1A  H
     A            RRN            4Y 0H
     A            HPH04          1A  H
     A            PB04           2A  H
     A            DPH03          4Y 0O  9  3EDTWRD('0  /  ')
     A  88                                  DSPATR(ND)
     A            TOT            8A  O  9 10
     A            DPH17          9Y 0O  9 19EDTCDE(1)
     A            DPH17A         9Y 0O  9 32EDTCDE(1)
     A            DHD06          9Y 0O  9 47EDTCDE(1)
     A            DHD06A         9Y 0O  9 60EDTCDE(1)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  20051118  141548  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0999)
     A                                      SFLPAG(0014)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A N53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A            DOPT           1Y 0H
     A                                  1  2'<PYI409>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'年度稅所得登錄作業查詢'
     A                                      DSPATR(RI)
     A                                  2 64'時間:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'公司編號:'
     A            DPH01     R        O  4 13REFFLD(EM01)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DPH02     R        O  5 13REFFLD(EM02)
     A            EM03      R        O  5 22REFFLD(EM03)
     A                                  5 45'年度:'
     A            DYY            2Y 0O  5 53EDTWRD('0  ')
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7 19'*---------薪資收入----*'
     A                                      COLOR(PNK)
     A                                  7 46'*--------所得稅--------*'
     A                                      COLOR(PNK)
     A                                  8  1'薪資年月'
     A                                      COLOR(PNK)
     A                                  8 25'固定'
     A                                      COLOR(PNK)
     A                                  8 36'非固定'
     A                                      COLOR(PNK)
     A                                  8 53'固定'
     A                                      COLOR(PNK)
     A                                  8 64'非固定'
     A                                      COLOR(PNK)
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                      OVERLAY
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24  2'PF03=回主畫面'
     A                                 24 19'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
