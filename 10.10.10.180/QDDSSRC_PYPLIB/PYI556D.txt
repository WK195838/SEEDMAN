     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI556D                                    *
     A*    PROGRAM NAME :考績項目代碼檔維護                        *
     A*    AUTHOR       : A1386 SHARON                               *
     A*    CREATE DATE  : 93/02/19                                   *
     A*    UPDATE DATE  : 93/02/19 A1386 SHARON                      *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(PYFLIBYK/PYRV2PF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            RVB02     R        O  8  7
     A            RVB03     R        O  8 12
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI556D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'考績項目代碼檔查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DVB01     R        O  4 13REFFLD(RVB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DVB01N        14O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5'代碼'
     A                                      DSPATR(UL)
     A                                  6 12'  考績名稱  '
     A                                      DSPATR(UL)
     A            DVB02     R        B  7  6DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      REFFLD(RVB02)
     A            DVB03     R        B  7 13DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      REFFLD(RVB03)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA01=下一頁'
     A                                 23 56'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
