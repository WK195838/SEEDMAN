     A****************************************************************/
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   */
     A*                                                              */
     A*    PROGRAM-ID   : PYR321D                                    */
     A*    PROGRAM NAME :所得扣繳稅額繳款書－工作地點              */
     A*    AUTHOR       : A1274 EVA                                  */
     A*    CREATE DATE  :                                            */
     A*    UPDATE DATE  :                                            */
     A*    SYSTEM       :人事薪資系統                              */
     A****************************************************************/
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------**
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYR321D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'所得扣繳稅額繳款書'
     A                                      DSPATR(RI)
     A                                  3 31' 　（工作地點）'
     A*
     A                                  5 32'公　　司:'
     A            DPH01     R        B  5 44REFFLD(PH01)
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A*
     A                                  7 32'員工編號:'
     A                                      DSPATR(HI)
     A            DPH021    R        B  7 44REFFLD(PH02 PYPHPF)
     A  62                                  DSPATR(PC RI)
     A            DPH022    R        B  8 44REFFLD(PH02 PYPHPF)
     A  63                                  DSPATR(PC RI)
     A*
     A                                 10 32'所得年月:'
     A            DYM            5Y 0B 10 44EDTWRD('0   /  ')
     A  61                                  DSPATR(PC RI)
     A*
     A                                 13 30'退休金提撥:'
     A            DSEL           1A  B 13 44VALUES('Y' 'N')
     A  64                                  DSPATR(PC RI)
     A                                 13 48' Y.含提撥退休金員工'
     A                                 14 48' N.不含提撥退休金員工'
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                      CHECK(RB)
     A                                 17 17'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A            $PRTCD         2A  B 19 14CHECK(RB)
     A  27                             21 30'請準備８０格報表紙'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
