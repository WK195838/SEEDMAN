     A*%%***********************************************************************
     A*%%TS  RD  19991011  140759  PPSPGMR     REL-V3R2M0  5763-PW1
     A*%%FI+10660100000000000000000000000000000000000000000000000000
     A*%%FI       0000000000000000000000000000000000000000000000000
     A*%%***********************************************************************
     A****************************************************************
     A*    PRTF NAME    : PYR1251P                                   *
     A*    PROGRAM NAME : PYR125                                     *
     A*    AUTHOR       : TOM T.                                     *
     A*    CREATE DATE  : 89/09/01                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : MIRS                                       *
     A*    REMARK       :異動人員資料列印                          *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A            CM03      R              1
     A                                    82
     A                                      '日期:'
     A            DDATE          6  0     90
     A                                      EDTWRD('0  /  /  ')
     A                                    99
     A                                      '頁次:'
     A                                   106
     A                                      PAGNBR
     A                                     1
     A                                      '人事異動明細表－依異動別'
     A                                      SPACEB(001)
     A                                    82
     A                                      '時間:'
     A                                    90
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   100
     A                                      '<PYR125>'
     A                                     1
     A                                      '異動別:'
     A                                      SPACEB(002)
     A            PH11          12O       11
     A                                     1
     A                                      '日　期:'
     A                                      SPACEB(001)
     A            DTX041    R   -2        11
     A                                      REFFLD(TX04)
     A                                      EDTWRD('0  /  /  ')
     A                                    20
     A                                      '-'
     A            DTX042    R   -2        22
     A                                      REFFLD(TX04)
     A                                      EDTWRD('0  /  /  ')
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========'
     A                                      SPACEB(001)
     A                                     1
     A                                      '部門名稱'
     A                                      SPACEB(001)
     A                                    24
     A                                      '員工姓名'
     A                                    47
     A                                      '生效日期'
     A                                    59
     A                                      '調整後金額'
     A                                    72
     A                                      '留職天數'
     A                                    83
     A                                      '離職原因'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========'
     A                                      SPACEB(001)
     A          R PD1
     A                                      SPACEA(001)
     A  31        EM041     R              1
     A  31        EM042     R              2
     A  31        EM043     R              3
     A  31        EM044     R              4
     A  31        DP02      R              8REFFLD(DP02 PYDPPF)
     A  32        TX02      R             25
     A  32        EM03      R             +1
     A            PTX04     R   -2        48
     A                                      REFFLD(TX04)
     A                                      EDTWRD('0  /  /  ')
     A            TX05      R             61
     A                                      EDTCDE(2)
     A            TX07      R             75
     A                                      EDTCDE(2)
     A            QJ02      R             82
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========'
     A                                     1
     A                                      'PYR125P--->報表結束'
     A                                      SPACEB(001)
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
