     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA704D                                    *
     A*    PROGRAM NAME :所得稅扣繳申報核定                        *
     A*    AUTHOR       : A1274  EVA                                 *
     A*    CREATE DATE  : 89/03/14                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*------------------------------------------------------------****
     A          R DSPC1
     A                                      CA03(03)
     A                                      CA07(07)
     A                                      CF13(13)
     A                                  1  2'<PYA704D>'
     A                                  1 26'＜＜所得稅扣繳申報核定＞＞'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A                                  4 29'媒體單位:'
     A            DTD02     R        B  4 41REFFLD(TD02 PYTDPF)
     A  60                                  DSPATR(PC RI)
     A                                  5 29'公司代號:'
     A            DTD11     R        B  5 41REFFLD(TD11 PYTDPF)
     A  63                                  DSPATR(PC RI)
     A                                  6 29'年　　度:'
     A            DTD01     R   -2   B  6 41REFFLD(TD01 PYTDPF)
     A                                      EDTWRD('0  ')
     A  61                                  DSPATR(PC RI)
     A                                  7 29'處理年月:'
     A            DTD16     R   -2   B  7 41REFFLD(TD16 PYTDPF)
     A                                      EDTWRD('0  /  ')
     A  62                                  DSPATR(PC RI)
     A                                  8 29'作業選擇:'
     A            DSEL           1   B  8 41VALUES('1' '2')
     A                                  8 43' 1-核定 2-申報'
     A                                 10 29'重新核定:'
     A            DOPT           1   B 10 41
     A                                      VALUES('Y' 'N')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 43'(Y/N)'
     A*
     A                                 12 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 13 11' '
     A                                      DSPATR(RI)
     A                                 13 66' '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A  97                             14 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  96                             14 30'申報資料處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A  98                             14 23'流水號:'
     A  98        DFA04F    R        O 14 33REFFLD(FA04)
     A  98                             14 41'-'
     A  98        DFA04T    R        O 14 43REFFLD(FA04)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A  98                             15 23'總件數:'
     A  98        DFA04N    R        O 15 33REFFLD(FA04)
     A                                      EDTCDE(3)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11' '
     A                                      DSPATR(RI)
     A                                 16 66' '
     A                                      DSPATR(RI)
     A                                 17 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*                                19  2'磁帶機　:'
     A*           $TDEV         10   B 19 14
     A*                                21 28'***請準備好磁帶***'
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF07 =檔案下載'
     A                                    +2'PF13 =處理開始'
     A            DMARK         14A  O 24 66MSGID(UMI 0001 CEMF)
     A                                      DSPATR(HI)
     A  96        ERR7          70A  O 22  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR2          70A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR3          70A  O 22  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR4          70A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR5          70A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR6          70A  O 22  2MSGID(UMI 0560 CEMF)
     A                                      DSPATR(HI)
