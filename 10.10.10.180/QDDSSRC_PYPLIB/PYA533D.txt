     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA533D                                    *
     A*    PROGRAM NAME :勞保投保級數設定　　　　                  *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 89/08/21                                   *
     A*    UPDATE DATE  : 90/06/22 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統　                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            2Y 0H
     A            DL101     R        B  8 26REFFLD(L101 PYL1PF)
     A                                      EDTCDE(2)
     A  60                                  DSPATR(PC RI)
     A            DL102     R        B  8 41REFFLD(L102 PYL1PF)
     A                                      EDTCDE(2)
     A  61                                  DSPATR(PC RI)
     A            DL103     R        B  8 61REFFLD(L103 PYL1PF)
     A                                      EDTCDE(2)
     A  62                                  DSPATR(PC RI)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0015)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA533D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'勞工保險級數設定'
     A                                      DSPATR(RI)
     A                                  4  2'投保費率:'
     A            DL201     R        B  4 14REFFLD(L201 PYL2PF)
     A                                      EDTCDE(1)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                  5  2'就保費率:'
     A            DL206     R        B  5 14REFFLD(L206 PYL2PF)
     A                                      EDTCDE(1)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                  4 24'員工負擔:'
     A            DL202     R        B  4 36REFFLD(L202 PYL2PF)
     A                                      EDTCDE(1)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                  4 50'勞保墊償:'
     A            DL203     R        B  4 62REFFLD(L203 PYL2PF)
     A                                      EDTCDE(1)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                  5 24'公司負擔:'
     A            DL204     R        B  5 36REFFLD(L204 PYL2PF)
     A                                      EDTCDE(1)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                  5 50'勞保職災:'
     A            DL205     R        B  5 62REFFLD(L205 PYL2PF)
     A                                      EDTCDE(1)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  2'投保分級'
     A                                  7 24'級數'
     A                                      COLOR(PNK)
     A                                  7 39'月投保金額'
     A                                      COLOR(PNK)
     A                                  7 59'日投保金額'
     A                                      COLOR(PNK)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PA01 =下一頁　'
     A                                 23 37'PA02 =上一頁　'
     A                                 24 67'【泛太資訊】'
