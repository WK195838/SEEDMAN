     A*%%TS  SD  20040114  155859  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA559                                     *
     A*    PROGRAM NAME :年終獎金月數維護
     A*    AUTHOR       : A1379  MAX                                 *
     A*    CREATE DATE  : 93.01.02                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :基本資料系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PTRF)
     A                                      PRINT
     A          R DSPD1
     A*                                     RTNCSRLOC(&#REC &#FLD)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA559.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'年終獎金月數維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司:'
     A            DBM01          2A  B  4 10
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            CM02          14A  O  4 13
     A                                  5  2'職稱:'
     A                                      DSPATR(HI)
     A            DBM02          3A  B  5 10
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A            DBM02C        14A  O  5 14
     A                                  5 29'年度:'
     A            DBM03          3Y 0B  5 37
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                  6  2'等級:'
     A            DBM04          1A  B  6 10
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  2'性別:'
     A            DBM06          1A  B  7 10
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  7 12'(M.男生 F.女生)'
     A                                  8  2'作業選擇:'
     A            DOPT           1Y 0B  8 14VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8 16'( 1.新增'
     A                                  8 27'2.修改'
     A                                  8 36'4.刪除'
     A                                  8 45'5.查詢)'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  20040114  150548  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                  1  2'<PYA559.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'年終獎金月數維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司:'
     A            DBM01          2A  O  4 10
     A            CM02          14A  O  4 13
     A                                  5  2'職稱:'
     A            DBM02          3A  O  5 10
     A            DBM02C        14A  O  5 14
     A                                  5 29'年度:'
     A            DBM03          3Y 0O  5 37
     A                                  6  2'等級:'
     A            DBM04          1A  O  6 14
     A                                  7  2'性別:'
     A            DBM06          1A  O  7 10
     A                                  7 12'(M.男生 F.女生)'
     A                                  8  2'作業選擇:'
     A            DOPT           1Y 0O  8 14EDTCDE(4)
     A                                  8 16'( 1.新增'
     A                                  8 27'2.修改'
     A                                  8 36'4.刪除'
     A                                  8 45'5.查詢)'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 10  2'獎金月數:'
     A            DBM05          8  6B 10 14EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 28'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
