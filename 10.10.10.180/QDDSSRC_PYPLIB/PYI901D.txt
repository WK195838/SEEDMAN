     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI901D                                    *
     A*    PROGRAM NAME : 工作區全頁查詢                           *
     A*    AUTHOR       : A1268  VNAESSA                              *
     A*    CREATE DATE  : 98/09/16                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 　　　　　　                             *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1   B  8  2DSPATR(CS PC HI)
     A                                      VALUES('/' ' ')
     A            XA02      R        O  8  7
     A            XA03      R        O  8 21
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*                                     CSRLOC(#LIN #COL)
     A                                      OVERLAY
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0013)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI901>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A                                  2 34'工作區全頁查詢'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司編號:'
     A            DXA01     R        O  4 13REFFLD(XA01)
     A            DXA01N        14      4 16
     A                                  5  1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ---------'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  6'工作區代碼'
     A                                      DSPATR(UL)
     A                                  6 21'工作區說明                  '
     A                                      DSPATR(UL)
     A            DXA02     R        B  7  7REFFLD(XA02)
     A                                      DSPATR(CS PC HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 29'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
