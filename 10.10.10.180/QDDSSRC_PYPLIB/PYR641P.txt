     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR641P                                    *
     A*    PROGRAM NAME :勞保調整投保申報表　                      *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/03/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :員工保險維護子系統                        *
     A****************************************************************
     A                                      REF(PYR641WF)
     A*------------------------------------------------------------***
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R             24REFFLD(CM03 SYCMPF)
     A            HYM            4  0     25EDTWRD('0  /  ')
     A                                      SPACEB(001)
     A                                    31'勞保調整投保申報表  '
     A            PTYPE         10O       +1
     A                                     1'列印日期: '
     A                                      SPACEB(002)
     A            DYMD           6  0     +1EDTWRD('0  /  /  ')
     A                                    60'<PYR641P>'
     A                                    70'頁次: '
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ============='
     A                                      SPACEB(001)
     A*
     A                                     1'被保險人'
     A                                      SPACEB(001)
     A                                    42'∼　保　額　∼'
     A*
     A                                     1'姓　　名'
     A                                      SPACEB(001)
     A                                    17'身份証號'
     A                                    30'出生日期'
     A                                    42'調整前　調整後'
     A                                    60'備　註'
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ============='
     A                                      SPACEB(001)
     A*
     A*------------------------------------------------------------***
     A          R PD1
     A                                      SPACEB(001)
     A            PEM03     R              1REFFLD(WF01)
     A            PEM31     R   10        17REFFLD(WF02)
     A            PEM33          6  0     30EDTWRD('0  /  /  ')
     A            P1308B    R             40EDTCDE(1)
     A                                      REFFLD(WF04)
     A            PW130B    R             49EDTCDE(1)
     A                                      REFFLD(WF05)
     A*
     A                                     1''
     A                                      SPACEB(001)
     A*------------------------------------------------------------***
     A          R PT1
     A                                      SPACEB(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ============='
     A                                     1
     A                                      '合　計　：'
     A                                      SPACEB(001)
     A            TT01           6  0     14
     A                                      EDTCDE(1)
     A                                    21
     A                                      '名'
     A                                    27
     A                                      '負責人　：'
     A            DNAME1        14        40
     A                                    55
     A                                      '經辦人　：'
     A            DNAME2        14        68
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ============='
     A                                      SPACEB(001)
     A*------------------------------------------------------------***
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
