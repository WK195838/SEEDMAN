     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI204D                                    *
     A*    PROGRAM NAME :遲到資料全頁查詢　　                      *
     A*    AUTHOR       : A1355 ERIC                                 *
     A*    CREATE DATE  : 91/11/04                                   *
     A*    UPDATE DATE  :
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*--------------------------------------------------------------**
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            LA02      R        O  8  5
     A            DEM03     R        O  8 15REFFLD(EM03)
     A            LA07D     R        O  8 31REFFLD(LA07)
     A            LA03D          7Y 0O  8 41EDTCDE(Y)
     A            LA04      R       0O  8 59EDTCDE(4)
     A            LA05      R       0O  8 71EDTCDE(4)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI204D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'遲到資料全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DLA01     R        O  4 13REFFLD(LA01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DLA01N        14O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'員工編號'
     A                                      DSPATR(UL)
     A                                  6 15'員工姓名    '
     A                                      DSPATR(UL)
     A                                  6 30'部　　門'
     A                                      DSPATR(UL)
     A                                  6 41'  遲到日'
     A                                      DSPATR(UL)
     A                                  6 52'遲到次數'
     A                                      DSPATR(UL)
     A                                  6 64'早退次數'
     A                                      DSPATR(UL)
     A            DLA02     R        B  7  5REFFLD(LA02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DLA07     R        B  7 31REFFLD(LA07)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DLA03          7Y 0B  7 41
     A                                      EDTCDE(Y)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 36'PA01=下一頁'
     A                                 23 50'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
