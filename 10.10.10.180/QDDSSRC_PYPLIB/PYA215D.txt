     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA215D                                    *
     A*    PROGRAM NAME :刷卡異常資料維護                          *
     A*    AUTHOR       : A1184                                      *
     A*    CREATE DATE  : 85/03/23                                   *
     A*    UPDATE DATE  : 88/12/20 A1274 EVA                         *
     A*    SYSTEM       : PY                                         *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA215.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 63'日期:'
     A            DDATE          6Y 0O  1 71EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'刷卡異常資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DT602     R        B  4 13REFFLD(T602)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DT603     R        B  5 13REFFLD(T603)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'刷卡日期:'
     A            DT601     R   -1   B  6 13REFFLD(T601)
     A                                      EDTWRD('   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'異常狀態:'
     A            DT604     R        B  7 13REFFLD(T604)
     A                                      VALUES('@' '*' 'N' 'E' '#' 'L')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  8 17'( 1.新增'
     A                                  8 28'2.修改'
     A                                  8 37'4.刪除'
     A                                  8 46'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                 18  1'異常狀態碼:'
     A                                 19 15'無上班刷卡資料: *   無排班表: -
     A                                      N   上班異常: @'
     A                                 20 15'無下班刷卡資料: #   遲　　到: -
     A                                      L   下班異常: E'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF04=欄位查詢'
     A                                 23 36'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA215.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 63'日期:'
     A            DDATE          6Y 0O  1 71EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'刷卡異常資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DT602     R        O  4 13REFFLD(T602)
     A            DT602N    R        O  4 22REFFLD(CM02)
     A                                  5  1'員工編號:'
     A            DT603     R        O  5 13REFFLD(T603)
     A            EM03D     R        O  5 22REFFLD(EM03)
     A                                  6  1'刷卡日期:'
     A            DT601     R   -1   O  6 13REFFLD(T601)
     A                                      EDTWRD('   /  /  ')
     A                                  7  1'異常狀態:'
     A            DT604     R        O  7 13REFFLD(T604)
     A            DT604D        16A  O  7 20
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0O  8 13EDTCDE(4)
     A                                  8 17'( 1.新增'
     A                                  8 28'2.修改'
     A                                  8 37'4.刪除'
     A                                  8 46'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 11 17'上班時間'
     A                                 11 30'下班時間'
     A                                 11 47'系統處理:'
     A            DT608     R        O 11 59REFFLD(T608)
     A            DT608D        12A  O 11 62
     A                                 13  7'應刷:'
     A            DT102     R        O 13 19REFFLD(T102)
     A                                      EDTCDE(3)
     A                                 13 22':'
     A            DT103     R        O 13 24REFFLD(T103)
     A                                      EDTCDE(3)
     A            DT112     R        O 13 32REFFLD(T112)
     A                                      EDTCDE(3)
     A                                 13 35':'
     A            DT113     R        O 13 37REFFLD(T113)
     A                                      EDTCDE(3)
     A                                 13 47'異常時間:'
     A            DT605     R        B 13 59REFFLD(T605)
     A                                      EDTCDE(1)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 13 62'時'
     A            DT606     R        B 13 67REFFLD(T606)
     A                                      EDTCDE(1)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 13 70'分'
     A                                 15  7'實際:'
     A            T503A     R        O 15 19REFFLD(T503)
     A                                      EDTCDE(3)
     A                                 15 22':'
     A            T504A     R        O 15 24REFFLD(T504)
     A                                      EDTCDE(3)
     A            T503B     R        O 15 32REFFLD(T503)
     A                                      EDTCDE(3)
     A                                 15 35':'
     A            T504B     R        O 15 37REFFLD(T504)
     A                                      EDTCDE(3)
     A                                 15 47'更正狀態:'
     A                                      DSPATR(HI)
     A            DT607     R        B 15 59REFFLD(T607)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            DT607D        12A  O 15 62
     A                                 18  2'更正狀態碼:'
     A                                 19 16'忘刷卡: F   簽到: T   遲　到-
     A                                      : L'
     A                                 19 56'上班異常: @'
     A                                 20 16'不處理: D   公出: C  '
     A                                 20 56'下班異常: E'
     A                                 21 16'PS.可輸入假別代碼'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF03=回主畫面'
     A                                 23 27'PF04=參考資料'
     A                                 23 44'PF05=參考資料'
     A                                 23 61'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
