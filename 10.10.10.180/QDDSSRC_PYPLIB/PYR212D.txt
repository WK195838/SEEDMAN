     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR212D                                    *
     A*    PROGRAM NAME :排班資料報表列印                          *
     A*    AUTHOR       : A1184                                      *
     A*    CREATE DATE  : 85/04/27                                   *
     A*    UPDATE DATE  : 89/08/25 A1274 EVA                         *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A*                                     CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<PYR212>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'排班資料報表列印'
     A                                      DSPATR(RI)
     A*
     A                                  6 30'公司編號　　:'
     A            DT201     R        B  6 46REFFLD(T201)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  8 30'部門編號　起:'
     A                                      DSPATR(HI)
     A            DEM04F         7   B  8 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 30'　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM04T         7   B  9 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 11 30'員工編號　起:'
     A                                      DSPATR(HI)
     A            DT202F    R        B 11 46REFFLD(T202)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 12 30'　　　　　迄:'
     A                                      DSPATR(HI)
     A            DT202T    R        B 12 46REFFLD(T202)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 14 30'排班月份　起:'
     A            DT203F    R   -1   B 14 46EDTWRD('   /  ')
     A                                      REFFLD(T203)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 15 30'　　　　　迄:'
     A            DT203T    R   -1   B 15 46EDTWRD('   /  ')
     A                                      REFFLD(T203)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1   B 17 14VALUES('0' '1' '2')
     A                                 17 17'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 19 14
     A  27                             21 30'請準備１３２格報表紙'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF03 =回主畫面'
     A                                 23 28'PF04 =欄位查詢'
     A*                                23 46'PF14 =報表列印'
     A                                 24 67'【泛太資訊】'
