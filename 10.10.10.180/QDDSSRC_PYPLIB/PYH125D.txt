     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYH125D                                    *
     A*    PROGRAM NAME :員工退休金提撥明細表                      *
     A*    AUTHOR       : A1415 REENE                                *
     A*    CREATE DATE  : 94/03/01                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYH125D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'員工退休金提撥明細表'
     A                                      DSPATR(RI)
     A                                  5 28'公司編號      :'
     A            DH601          2A  B  5 46
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A*                                     SPACEA(1)
     A                                  7 28'部  門      :'
     A                                      DSPATR(HI)
     A            DEM04F         7A  B  7 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 53'~'
     A            DEM04T         7A  B  7 56
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A*
     A                                 12 28'員工類別      :'
     A            DPT            1A  B 12 46VALUES('1' '2' ' ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 12 50'(1.在職員工2.離職員工)'
     A                                  9 28'員工編號　　起:'
     A                                      DSPATR(HI)
     A            DH602F         8   B  9 46
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 10 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DH602T         8   B 10 46
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 14 28'提撥日期　　  :'
     A            DYM            6Y 0B 14 46EDTWRD('0   /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 18  2'執行環境:'
     A            $PENV          1A  B 18 14VALUES('0' '1' '2')
     A                                 18 18'(0-查詢 1-即時 2-批次)'
     A                                 19  2'列印份數:'
     A            $CPY           3Y 0B 19 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 14
     A  27                             21 27'***請準備１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
