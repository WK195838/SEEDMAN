     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR222D                                    *
     A*    PROGRAM NAME :員工出勤簡表　　　　　　                  *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 1999/07/29                                 *
     A*    UPDATE DATE  : 1999/10/04 A1274 EVA                       *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A*------------------------------------------------------------***
     A          R DSPD1                     RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYR222D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72
     A                                      EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 33'員工出勤簡表'
     A                                      DSPATR(RI)
     A*
     A                                  5 28'公司編號　　　:'
     A            DOV01     R        B  5 46REFFLD(ED01)
     A  61                                  DSPATR(PC RI)
     A                                  7 28'部門編號　　起:'
     A                                      DSPATR(HI)
     A            DEM041         7   B  7 46
     A  62                                  DSPATR(PC RI)
     A                                  8 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM042         7   B  8 46
     A  62                                  DSPATR(PC RI)
     A                                 10 28'員工編號　　起:'
     A                                      DSPATR(HI)
     A            DOV021    R        B 10 46REFFLD(ED02)
     A  63                                  DSPATR(PC RI)
     A                                 11 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DOV022    R        B 11 46REFFLD(ED02)
     A  63                                  DSPATR(PC RI)
     A                                 13 28'出勤月份　　起:'
     A            DOV031         4  0B 13 46
     A                                      EDTCDE(Y)
     A  64                                  DSPATR(PC RI)
     A                                 14 28'　　　　　　迄:'
     A            DOV032         4  0B 14 46
     A                                      EDTCDE(Y)
     A  65                                  DSPATR(PC RI)
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 27'*** 請準備８０格報表紙 ***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
