     A*%%TS  SD  19980403  095911  ADMR        REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR618D                                    *
     A*    PROGRAM NAME :勞保投保資料明細表　　                    *
     A*    AUTHOR       : A1413 CLOUD                                *
     A*    CREATE DATE  : 2006/05/04                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYR618D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'勞保投保資料明細表'
     A                                      DSPATR(RI)
     A                                  5 28'公司編號      :'
     A            DEM01     R        B  5 46REFFLD(EM01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 28'部門編號　　起:'
     A                                      DSPATR(HI)
     A            DEM04S         7A  B  7 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM04E         7A  B  8 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 28'員工編號　　起:'
     A                                      DSPATR(HI)
     A            DEM02S    R        B 10 46REFFLD(EM02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 11 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM02E    R        B 11 46REFFLD(EM02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 13 28'呈現退保　　  :'
     A            DOPT           1A  B 13 46VALUES('Y' 'N')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 13 48'( Y.呈現 N.不呈現)'
     A                                 14 28'歷史資料　　  :'
     A            DSTORY         1A  B 14 46VALUES('Y' 'N')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 14 48'( Y.呈現 N.不呈現)'
     A                                 15 28'排列順序　　  :'
     A            DSEQ           1A  B 15 46VALUES('1' '2' '3')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 15 48'( 1. , 2. , 3.)'
     A*
     A                                 18  2'執行環境:'
     A            $PENV          1A  B 18 14VALUES('0' '1' '2')
     A                                 18 18'(0-查詢 1-即時 2-批次)'
     A                                 19  2'列印份數:'
     A            $CPY           3Y 0B 19 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 14
     A  27                             21 27'***請準備８０格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
