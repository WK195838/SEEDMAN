     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA904D                                    *
     A*    PROGRAM NAME :勞健保及勞退保額調整報表                  *
     A*    AUTHOR       : A1445 HONDA                                *
     A*    CREATE DATE  : 2005/12/15                                 *
     A*    UPDATE DATE  : 2005/12/15                                 *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*                                     REF(*LIBL/PYRF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A* 28                                  FRCDTA
     A* 28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<PYA904D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 27'勞健保及勞退保額調整報表'
     A                                      DSPATR(RI)
     A*
     A                                  4 28'公司編號　　起:'
     A*                                     DSPATR(HI)
     A            DLO011         2A  B  4 46
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5 28'　　　　　　迄:'
     A*                                     DSPATR(HI)
     A            DLO012         2A  B  5 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 28'部門編號　　起:'
     A*                                     DSPATR(HI)
     A            DLO051         7A  B  7 46
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8 28'　　　　　　迄:'
     A*                                     DSPATR(HI)
     A            DLO052         7A  B  8 46
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 10 28'員工編號　　起:'
     A*                                     DSPATR(HI)
     A            DLO021         8A  B 10 46
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 11 28'　　　　　　迄:'
     A*                                     DSPATR(HI)
     A            DLO022         8A  B 11 46
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 13 28'生效年月　　　:'
     A            DYM            4Y 0B 13 46
     A                                      EDTWRD('0  /  ')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 15 28'變動別(Y/N/A) :'
     A            DLO13          1A  B 15 46
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 27'***請準備１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
