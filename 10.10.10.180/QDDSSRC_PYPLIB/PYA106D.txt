     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA106D                                    *
     A*    PROGRAM NAME :休假基本資料建立                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 86/02/21                                   *
     A*    UPDATE DATE  : 88/11/18 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA106.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'休假基本資料建立'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DLT01     R        B  4 13REFFLD(LT01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DLT02     R        B  5 13REFFLD(LT02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            EM03      R        O  5 23REFFLD(EM03)
     A                                  6  1'年　　度:'
     A            DLT03     R   -2   B  6 13REFFLD(LT03)
     A                                      EDTWRD('0  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 16'( 1.新增 2.修改 4.刪除 5.+
     A                                      查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF04=欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  19980709  170809  PPSPGMR40   REL-V3R1M0  5763-PW1
     A                                      CA03(03)
     A                                      CF12(12)
     A                                  1  2'<PYA106.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'休假基本資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72
     A                                  4  1'公司編號:'
     A            DLT01     R        O  4 13REFFLD(LT01)
     A            DLT01N        14O  O  4 16
     A                                  5  1'員工編號:'
     A            DLT02     R        O  5 13REFFLD(LT02)
     A            EM03      R        O  5 23REFFLD(EM03)
     A                                  6  1'年　　度:'
     A            DLT03     R   -2   O  6 13REFFLD(LT03)
     A                                      EDTWRD('0  ')
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0O  7 13EDTCDE(4)
     A                                  7 16'( 1.新增 2.修改 4.刪除 5.+
     A                                      查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 10  1'<<年　　假>>'
     A                                 10 16'年假天數:'
     A            DLT10     R        B 10 28REFFLD(LT10)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 33'天'
     A                                 10 47'己休天數:'
     A            DLT11     R        B 10 59REFFLD(LT11)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 64'天'
     A            DLT12     R        B 10 69REFFLD(LT12)
     A                                      EDTCDE(4)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 72'時'
     A                                 12 16'去年轉休:'
     A            DLT13     R        B 12 28REFFLD(LT13)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 33'天'
     A            DLT14     R        B 12 38REFFLD(LT14)
     A                                      EDTCDE(M)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 41'時'
     A                                 12 47'未休天數:'
     A            LTT1      R        O 12 59REFFLD(LT13)
     A                                      EDTCDE(M)
     A                                 12 64'天'
     A            LTT2      R        O 12 69REFFLD(LT14)
     A                                      EDTCDE(M)
     A                                 12 72'時'
     A                                 14 16'明年預借:'
     A            DLT15     R        B 14 28REFFLD(LT15)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 33'天'
     A            DLT16     R        B 14 38REFFLD(LT16)
     A                                      EDTCDE(M)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 41'時'
     A                                 16  1'<<月　　休>>'
     A                                 16 16'月休天數:'
     A            DLT20     R        B 16 28REFFLD(LT20)
     A                                      EDTCDE(4)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                 16 33'天'
     A                                 16 47'己休天數:'
     A            DLT21     R        B 16 59REFFLD(LT21)
     A                                      EDTCDE(4)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                 16 64'天'
     A            DLT22     R        B 16 69REFFLD(LT22)
     A                                      EDTCDE(4)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                 16 72'時'
     A                                 18 16'去年轉休:'
     A            DLT23     R        B 18 28REFFLD(LT23)
     A                                      EDTCDE(M)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                 18 33'天'
     A            DLT24     R        B 18 38REFFLD(LT24)
     A                                      EDTCDE(M)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                 18 41'時'
     A                                 18 47'未休天數:'
     A            LTT3      R        O 18 59REFFLD(LT23)
     A                                      EDTCDE(M)
     A                                 18 64'天'
     A            LTT4      R        O 18 69REFFLD(LT24)
     A                                      EDTCDE(M)
     A                                 18 72'時'
     A                                 20 16'明年預借:'
     A            DLT25     R        B 20 28REFFLD(LT25)
     A                                      EDTCDE(M)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                 20 33'天'
     A            DLT26     R        B 20 38REFFLD(LT26)
     A                                      EDTCDE(M)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 20 41'時'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            2S 0H
     A            WFUN           9O  O 10 16
     A            DLU05     R        B 10 29REFFLD(LU05)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DLU06     R        B 10 41REFFLD(LU06)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            LUT1      R        O 10 53REFFLD(LU06)
     A                                      EDTCDE(M)
     A            TOTAL     R        O 10 65REFFLD(LU06)
     A                                      EDTCDE(M)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19980521  221256  PPSPGMR40   REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A                                  1  2'<PYA106.3>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'休假基本資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72
     A                                  4  1'公司編號:'
     A            DLT01     R        O  4 13REFFLD(LT01)
     A            DLT01N        14O  O  4 16
     A*
     A                                  5  1'員工編號:'
     A            DLT02     R        O  5 13REFFLD(LT02)
     A            EM03      R        O  5 23REFFLD(EM03)
     A*
     A                                  6  1'年　　度:'
     A            DLT03     R   -2   O  6 13REFFLD(LT03)
     A*
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0O  7 13EDTCDE(4)
     A                                  7 16'( 1.新增 2.修改 4.刪除 5.+
     A                                      查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  1'<<加班補休>>'
     A                                  9 27'補休時數'
     A                                  9 39'已休時數'
     A                                  9 51'未休時數'
     A                                  9 63'累計時數'
     A*----------------------------------------------------------------
     A          R DSPC1
     A*%%TS  SD  19970224  132751  ESPGMR      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
