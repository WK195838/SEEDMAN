     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI906D                                    *
     A*    PROGRAM NAME :排班作業全頁查詢                          *
     A*    AUTHOR       : A1268  VNAESSA                             *
     A*    CREATE DATE  : 98/09/29                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 　　　  　                             *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1   B  8  2DSPATR(CS PC HI)
     A                                      VALUES('/' ' ')
     A            XI06      R        O  8  7
     A            XI02      R        O  8 22
     A            XI03      R        O  8 31
     A            DXI04     R   -2   O  8 39REFFLD(XI04)
     A                                      EDTWRD('  /  /  ')
     A            DXI05     R   -2   O  8 52REFFLD(XI05)
     A                                      EDTWRD('  /  /  ')
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*                                     CSRLOC(#LIN #COL)
     A                                      OVERLAY
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0013)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI906>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A                                  2 35'排班作業全頁查詢'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司編號:'
     A            DXI01     R        O  4 13REFFLD(XI01)
     A            DXI01N        14      4 16
     A                                  5  1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ---------'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  6'工作區　 '
     A                                      DSPATR(UL)
     A                                  6 19'工作組'
     A                                      DSPATR(UL)
     A                                  6 28'週班別'
     A                                      DSPATR(UL)
     A                                  6 38'起始日期'
     A                                      DSPATR(UL)
     A                                  6 51'截止日期'
     A                                      DSPATR(UL)
     A            DDI06     R        B  7  7REFFLD(XI06)
     A                                      DSPATR(CS PC HI)
     A            DDI02     R        B  7 22REFFLD(XI02)
     A                                      DSPATR(CS PC HI)
     A            DDI03     R        B  7 31REFFLD(XI03)
     A                                      DSPATR(CS PC HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 29'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
