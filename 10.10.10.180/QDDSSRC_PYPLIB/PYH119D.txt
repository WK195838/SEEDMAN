     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYH119D                                    *
     A*    PROGRAM NAME :月提繳工資分級設定　　　                  *
     A*    AUTHOR       : A1415 REENE                                *
     A*    CREATE DATE  : 94/02/17                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統　                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            2Y 0H
     A            DEL            1A  B  7 14VALUES('D' ' ')
     A            DH701          2  0B  7 18
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            MH701          2  0H
     A            DH702          6  0B  7 30
     A                                      EDTCDE(2)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DH703          6  0B  7 48
     A                                      EDTCDE(2)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DH704          6  0B    +8
     A                                      EDTCDE(2)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0031)
     A                                      SFLPAG(0015)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYH119D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'月提繳工資分級表設定'
     A                                      DSPATR(RI)
     A                                  4  2'企業提撥比率下限:'
     A            DHB02          5  2B    +1EDTCDE(1)
     A                                    +1'%'
     A                                  4 32'員工自提比率上限:'
     A            DHB03          5  2B    +1EDTCDE(1)
     A                                    +1'%'
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  6  2'投保分級'
     A                                  6 14'D'
     A                                      DSPATR(HI)
     A                                  6 17'級數'
     A                                      COLOR(PNK)
     A                                  6 29'上限'
     A                                      COLOR(PNK)
     A                                  6 47'下限'
     A                                      COLOR(PNK)
     A                                    +8'月提繳工資'
     A                                      COLOR(PNK)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PA01 =下一頁　'
     A                                 23 37'PA02 =上一頁　'
     A                                 24 67'【泛太資訊】'
