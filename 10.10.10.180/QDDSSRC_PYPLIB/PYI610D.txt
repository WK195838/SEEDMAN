     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI610D                                    *
     A*    PROGRAM NAME :綜合福利金發放作業全頁查詢                *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/04/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :福委會管理系統                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(HI)
     A            GD02      R        O  8  4REFFLD(GD02 PYGDPF)
     A            GD021     R        O  8 13REFFLD(EM03 PYEMPF)
     A            GD07N         10A  O  8 28
     A            GD08      R        O  8 39REFFLD(GD08 PYGDPF)
     A                                      EDTCDE(1)
     A            DGD06     R   -1   O  8 50REFFLD(GD06 PYGDPF)
     A                                      EDTWRD('0   /  /  ')
     A            DGD09     R   -1   O  8 64REFFLD(GD09 PYGDPF)
     A                                      EDTWRD('0   /  ')
     A*-------------------------------------------------------------***
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI610.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 25'綜合福利金發放作業全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DGD01     R        O  4 13REFFLD(GD01 PYGDPF)
     A  60                                  DSPATR(RI)
     A            DGD01N        22O  O  4 16
     A                                  5  1'福利年度:'
     A            DGD04     R   -1   O  5 13REFFLD(GD04 PYGDPF)
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'    員工編號姓名     '
     A                                      DSPATR(UL)
     A                                  6 28'申請項目'
     A                                      DSPATR(UL)
     A                                    +1'核發金額'
     A                                      DSPATR(UL)
     A                                    +1'核發日期'
     A                                      DSPATR(UL)
     A                                    +1'申請年月'
     A                                      DSPATR(UL)
     A            KGD02     R        B  7  4REFFLD(GD02 PYGDPF)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 36'PA01=下一頁　'
     A                                 23 53'PA02=上一頁　'
     A                                 24 67'【泛太資訊】'
