     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA308D                                    *
     A*    PROGRAM NAME :每月薪資資料調整                          *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 85/08/27                                   *
     A*    UPDATE DATE  : 89/09/04 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA308.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'每月薪資資料調整'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DPW01     R        B  4 13REFFLD(PW01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DPW02     R        B  5 13REFFLD(PW02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'年　　月:'
     A            DPW03     R   -2   B  6 13REFFLD(PW03)
     A                                      EDTWRD('0  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'期　　別:'
     A            DPW04     R        B  7 13REFFLD(PW04)
     A                                      VALUES('1' '2' '3')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  7 16'(1. 5日 2. 20日-
     A                                       3.年終獎金)'
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  8 16'( 1.新增 2.修改 4.刪除 5.+
     A                                      查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 21'PF04 =欄位查詢'
     A                                 23 40'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*------------------------------------------------------------***
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            2S 0O  9  4
     A            DWD05     R        B  9  9REFFLD(WD05)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            SS02      R        O  9 12
     A            DWD06     R        B  9 28REFFLD(WD06)
     A                                      CHECK(RB)
     A                                      EDTCDE(K)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*------------------------------------------------------------***
     A          R SFLCR                     SFLCTL(SFLSR)
     A  *DS3                                SFLLIN(0008)
     A                                      SFLSIZ(0048)
     A                                      SFLPAG(0024)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA308.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'每月薪資資料調整'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72
     A                                  4  1'公司編號:'
     A            DPW01     R        O  4 13REFFLD(PW01)
     A            CM02      R        O  4 22REFFLD(CM02)
     A                                  4 38'年　　月:'
     A            DPW03     R   -2   O  4 50REFFLD(PW03)
     A                                      EDTWRD('0  /  ')
     A                                  5 56'實發金額:'
     A            DCC            7  0O  5 69
     A                                      EDTCDE(K)
     A                                  5  1'員工編號:'
     A            DPW02     R        O  5 13REFFLD(PW02)
     A            EM03      R        O  5 22REFFLD(EM03)
     A                                  5 38'期　　別:'
     A            DPW04     R        O  5 50REFFLD(PW04)
     A                                  6  1'==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                  7  2'序號'
     A                                  7 12'薪資名稱'
     A                                  7 29'金　額'
     A                                  7 44'序號'
     A                                  7 54'薪資名稱'
     A                                  7 71'金　額'
     A                                  8  3'----  -----------------'
     A                                  8 28'----------'
     A                                  8 45'----  -----------------'
     A                                  8 70'----------'
     A*------------------------------------------------------------***
     A          R DSPC1
     A                                      OVERLAY
     A                                 21  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 23 36'PA1=下一頁'
     A                                 23 50'PA2=上一頁'
     A  97                             23 64'PF05=重算所得稅'
     A                                 24 61'【泛太資訊】'
     A*------------------------------------------------------------***
     A          R DSPD2
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF13(13)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA308.3>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'每月薪資資料調整'
     A                                      DSPATR(RI)
     A                                  4  2'公司編號:'
     A            DPW01     R        O  4 14REFFLD(PW01)
     A                                  5  2'員工編號:'
     A            DPW02     R        O  5 14REFFLD(PW02)
     A                                  6  2'年　　月:'
     A            DPW03     R   -2   O  6 14REFFLD(PW03)
     A                                      EDTWRD('0  /  ')
     A                                  7  2'期　　別:'
     A            DPW04     R        O  7 14REFFLD(PW04)
     A                                  7 17'(1. 5日 2. 20日-
     A                                       3.年終獎金)'
     A                                  9  2'底　　薪:'
     A            DPW05     R        O  9 14REFFLD(PW05)
     A                                      EDTCDE(J)
     A                                  9 32'計薪天數:'
     A            DPW07     R        O  9 44REFFLD(PW07)
     A                                      EDTCDE(J)
     A* 62                                  DSPATR(PC)
     A* 62                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*                                 9 52'扣薪時數:'
     A*           DPW14     R        O  9 64REFFLD(PW14)
     A*                                     EDTCDE(J)
     A* 63                                  DSPATR(PC)
     A* 63                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*                                10  2'加班時數:'
     A*                                10 14'免稅-'
     A*           DPW32     R        O 10 22REFFLD(PW14)
     A*                                     EDTCDE(J)
     A* 64                                  DSPATR(PC)
     A* 64                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*                                10 36'應稅-'
     A*           DPW31     R        O 10 44REFFLD(PW14)
     A*                                     EDTCDE(J)
     A* 65                                  DSPATR(PC)
     A* 65                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*                                10 56'值班-'
     A*           DPW33     R        O 10 64REFFLD(PW14)
     A*                                     EDTCDE(J)
     A* 66                                  DSPATR(PC)
     A* 66                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                 12  2'應付金額:免稅-'
     A            DPW16     R        O 12 21REFFLD(PW16)
     A                                      EDTCDE(J)
     A                                 12 32'固定應稅-'
     A            DPW17     R        O 12 44REFFLD(PW17)
     A                                      EDTCDE(J)
     A                                 12 55'非固定應稅-'
     A            DPW27     R        O 12 69REFFLD(PW17)
     A                                      EDTCDE(J)
     A                                 13  2'應扣金額:免稅-'
     A            DPW18     R        O 13 21REFFLD(PW18)
     A                                      EDTCDE(J)
     A                                 13 32'固定應稅-'
     A            DPW28     R        O 13 44REFFLD(PW17)
     A                                      EDTCDE(J)
     A                                 13 55'非固定應稅-'
     A            DPW29     R        O 13 69REFFLD(PW17)
     A                                      EDTCDE(J)
     A                                 14  2'上期借資:'
     A            DPW30     R        O 14 14REFFLD(PW30)
     A                                      EDTCDE(J)
     A                                 15  2'勞保費　:'
     A            DPW19     R        O 15 14REFFLD(PW19)
     A                                      EDTCDE(J)
     A* 74                                  DSPATR(PC)
     A* 74                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                 15 32'天數-'
     A            DPW22     R        O 15 40REFFLD(PW22)
     A                                      EDTCDE(J)
     A* 75                                  DSPATR(PC)
     A* 75                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                 16  2'健保費　:'
     A            DPW24     R        O 16 14REFFLD(PW24)
     A                                      EDTCDE(J)
     A* 76                                  DSPATR(PC)
     A* 76                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                 16 32'人數-'
     A            DPW26     R        O 16 40REFFLD(PW26)
     A                                      EDTCDE(1)
     A* 77                                  DSPATR(PC)
     A* 77                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                 18  2'所得稅率:'
     A            DPW15     R        O 18 14REFFLD(PW15)
     A                                      EDTCDE(1)
     A                                 18 57'離職碼　:'
     A            DPW20     R        O 18 69REFFLD(PW20)
     A*                                     VALUES(' ' 'Y' '*' 'R' 'S')
     A* 79                                  DSPATR(PC RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*
     A                                 20  2'發放說明:'
     A            DPW34     R        O 20 14REFFLD(PW34)
     A* 80                                  DSPATR(PC RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 23 38'PF13=確認'
     A                                 24 67'【泛太資訊】'
