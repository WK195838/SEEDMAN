     A*%%TS  SD  20020821  143130  PPSPGMREL   REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA505D                                    *
     A*    PROGRAM NAME :核薪標準等級設定　                        *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 86/01/03                                   *
     A*    UPDATE DATE  : 91/06/14 A1336 KLAUS
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA505.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'核薪標準等級設定'
     A                                      DSPATR(RI)
     A                                  5  1'職　　等:'
     A            DPS01     R        B  5 13REFFLD(PS01 PYPSPF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A*%%TS  SD  20020821  143130  PPSPGMREL   REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN            3Y 0H
     A            DPS04          3  0B 10 22EDTCDE(J)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DPS03     R        B 10 32REFFLD(PS03 PYPSPF)
     A                                      EDTCDE(J)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DPS12     R        O 10 44REFFLD(PS02 PYPSPF)
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0012)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA505D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'核薪標準等級設定'
     A                                      DSPATR(RI)
     A                                  4  1'職　　等  :'
     A            DPS01     R        O  4 15REFFLD(PS01 PYPSPF)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  6  1'職等金額  :'
     A            DPS02     R        B  6 15REFFLD(PS02 PYPSPF)
     A                                      EDTCDE(1)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                  6 32'績效獎金:'
     A            DPS10     R        B  6 45REFFLD(PS10 PYPSPF)
     A                                      EDTCDE(1)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                  7  1'每職級金額:'
     A            DPS09     R        B  7 15REFFLD(PS09 PYPSPF)
     A                                      EDTCDE(1)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  9  1'核薪分級  :'
     A                                  9 20'級數'
     A                                  9 32'職級金額'
     A                                  9 44'本　　薪'
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PA01 =下一頁'
     A                                 23 36'PA02 =上一頁'
     A                                 23 52'PA13 =資料確認'
     A                                 24  2'PF12 =回上一層'
     A                                 24 67'【泛太資訊】'
