     A****************************************************************
     A*    PRTF NAME    : PYR307P                                    *
     A*    PROGRAM NAME : PYR307                                     *
     A*    AUTHOR       : A1490 TERESA                               *
     A*    CREATE DATE  : 96/05/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : MIRS                                       *
     A*    REMARK       :薪資資料列印                              *
     A****************************************************************
     A                                      REF(PYEMPF)
     A*---------------------------------------------------------------*
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1REFFLD(CM03 SYCMPF)
     A                                      CHRSIZ(2)
     A                                      SPACEA(001)
     A                                     1
     A                                      '公司負擔成本統計表'
     A                                      CHRSIZ(2)
     A                                      SPACEA(001)
     A                                   176
     A                                      '列印日期:'
     A            PDATE          8  0    188
     A                                      EDTWRD('0    /  /  ')
     A                                      SPACEA(001)
     A                                     1
     A                                      '統計年月:'
     A            CDYMYY         4  0     +2
     A                                    +1
     A                                      '年'
     A            CDYMMM         2  0     +1
     A                                    +1
     A                                      '月'
     A                                   176
     A                                      '頁  次:'
     A                                   188
     A                                      PAGNBR
     A                                      SPACEA(001)
     A            LINE1        198A        1
     A                                      SPACEA(001)
     A                                     1'部門'
     A                                    17
     A                                      '代號  /      姓名'
     A                                    45'到職日'
     A                                    60'離職日'
     A                                    72'健保金額'
     A                                    83'公司負擔健保'
     A                                    98'勞保金額'
     A                                   109'公司負擔勞保'
     A                                   124'勞退提繳工資'
     A                                   139'企業提繳'
     A                                      SPACEA(001)
     A            LINE2        198A        1
     A                                      SPACEA(001)
     A*---------------------------------------------------------------*
     A          R PD1
     A  61        DP02      R              1REFFLD(DP02 PYDPPF)
     A            PEM02     R             17
     A                                      REFFLD(EM03)
     A            PEM03     R   -4        +1
     A                                      REFFLD(EM03)
     A            PEM13     R             43EDTWRD('    /  /  ')
     A                                      REFFLD(EM13)
     A            PEM19     R             58EDTWRD('    /  /  ')
     A                                      REFFLD(EM19)
     A            PDW04          6  0     74EDTCDE(2)
     A            PDW05          6  0     87EDTCDE(2)
     A            PDW06          6  0    100EDTCDE(2)
     A            PDW07          6  0    113EDTCDE(2)
     A            PDW08          6  0    128EDTCDE(2)
     A            PDW09          6  0    141EDTCDE(2)
     A                                      SPACEA(001)
     A            LINE3        198A        1
     A                                      SPACEA(001)
     A*---------------------------------------------------------------*
     A          R PT1
     A                                     1'部門小計'
     A            PTO01          7  0     72EDTCDE(2)
     A            PTO02          7  0     85EDTCDE(2)
     A            PTO03          7  0     98EDTCDE(2)
     A            PTO04          7  0    111EDTCDE(2)
     A            PTO05          7  0    126EDTCDE(2)
     A            PTO06          7  0    139EDTCDE(2)
     A                                      SPACEA(001)
     A            LINE4        198A        1
     A                                      SPACEA(001)
     A*---------------------------------------------------------------*
     A          R PT2
     A                                     1
     A                                      '總  計'
     A            PTO11          8  0     71EDTCDE(2)
     A            PTO12          8  0     84EDTCDE(2)
     A            PTO13          8  0     97EDTCDE(2)
     A            PTO14          8  0    110EDTCDE(2)
     A            PTO15          8  0    125EDTCDE(2)
     A            PTO16          8  0    138EDTCDE(2)
     A                                      SPACEA(001)
     A            LINE5        198A        1
     A                                      SPACEA(001)
     A*---------------------------------------------------------------*
     A          R PE1
     A                                     1
     A                                      '備  註'
     A                                    74
     A                                      '總經理'
     A                                    96
     A                                      '副總經理'
     A                                   120
     A                                      '會計'
     A                                   140
     A                                      '人事'
     A                                   160
     A                                      '出納'
     A                                   180
     A                                      '製表'
