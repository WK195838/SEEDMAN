     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    DSPF NAME    : PYA410D                                    *
     A*    PROGRAM NAME :褔利獎金資料核定作業                      *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/02/26                                   *
     A*    UPDATE DATE  : 90/05/09 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*------------------------------------------------------------***
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF13(13)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYA410D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'福利獎金資料核定作業'
     A                                      DSPATR(RI)
     A*
     A                                  5 25'福委會　:'
     A            DPB01     R        B  5 39REFFLD(PB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 25'獎金年度:'
     A            DPB03     R   -1   B  7 39REFFLD(PB03)
     A                                      EDTWRD('0   ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  9 25'獎金類別:'
     A                                      DSPATR(HI)
     A            DPB04     R        B  9 39REFFLD(PB04)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            SS02      R        O  9 42
     A                                 11 25'發放日期:'
     A            DPB05          7  0B 11 39
     A                                      EDTWRD('0   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A*
     A                                 15 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 16 11' '
     A                                      DSPATR(RI)
     A                                 16 66' '
     A                                      DSPATR(RI)
     A                                 17 11' '
     A                                      DSPATR(RI)
     A  97                             17 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  96                             17 30'核定作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 17 66' '
     A                                      DSPATR(RI)
     A                                 18 11' '
     A                                      DSPATR(RI)
     A                                 18 66' '
     A                                      DSPATR(RI)
     A                                 19 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
