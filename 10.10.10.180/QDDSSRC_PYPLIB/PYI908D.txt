     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI908D                                    *
     A*    PROGRAM NAME :飛機勤務設定　                            *
     A*    AUTHOR       : B1038  SHARON                              *
     A*    CREATE DATE  : 98/11/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 　　　　　　                             *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1   B  8  2DSPATR(CS PC HI)
     A                                      VALUES('/' ' ')
     A            PXJ02     R   -2   O  8  5REFFLD(XJ02)
     A                                      EDTWRD('  /  /  ')
     A                                  8 15'週日:'
     A            XJ03      R           8 23
     A                                  9 15'週一:'
     A            XJ04      R           9 23
     A                                 10 15'週二:'
     A            XJ05      R          10 23
     A                                 11 15'週三:'
     A            XJ06      R          11 23
     A                                 12 15'週四:'
     A            XJ07      R          12 23
     A                                 13 15'週五:'
     A            XJ08      R          13 23
     A                                 14 15'週六:'
     A            XJ09      R          14 23
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*                                     CSRLOC(#LIN #COL)
     A                                      OVERLAY
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0002)
     A                                      CA03(03)
     A                                      CF02(02)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI908>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A                                  2 32'飛機勤務全頁查詢'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司編號:'
     A            DXJ01     R        O  4 13REFFLD(XJ01)
     A            DXJ01N        14      4 16
     A                                  5  1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ---------'
     A                                  6  2'S'
     A                                  6  4'生效日期'
     A                                  6 15'勤務說明'
     A            DXJ02     R   -2   B  7  5REFFLD(XJ02)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(CS PC HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F2 =明細資料'
     A                                 23 46'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
