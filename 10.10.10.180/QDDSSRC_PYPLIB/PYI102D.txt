     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI102D                                    *
     A*    PROGRAM NAME :人事調動資料查詢                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 86/02/11                                   *
     A*    UPDATE DATE  : 89/08/25 A1274 EVA                         *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            TR02      R        O  8  5
     A            EM03      R        O  9 14
     A            DTR04     R   -1   O  8 26REFFLD(TR04)
     A                                      EDTWRD('   /  /  ')
     A            TR03      R        O  8 37
     A            TR03N          6O  O  8 39
     A            TR05N     R        O  8 47REFFLD(DP02)
     A            TR06N     R        O  8 65REFFLD(DP02)
     A            TR07N     R        O  9 47REFFLD(JT02)
     A            TR08N     R        O  9 65REFFLD(JT02)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0070)
     A                                      SFLPAG(0007)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI102D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'人事調動資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DTR01     R        O  4 13REFFLD(OV01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DTR01N        14O  O  4 16
     A                                  5 49'部　　門'
     A                                  5 67'部　　門'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5'     員工編號      生效日期+
     A                                      調動類別'
     A                                      DSPATR(UL)
     A                                  6 47'  職　　稱  調  職　　稱  '
     A                                      DSPATR(UL)
     A            DTR02     R        B  7  5REFFLD(TR02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            KTR04     R   -1   B  7 26REFFLD(TR04)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      EDTWRD('   /  /  ')
     A            DTR03     R        B  7 37REFFLD(TR03)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA01=下一頁'
     A                                 23 56'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
