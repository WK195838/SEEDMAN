     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR711D                                    *
     A*    PROGRAM NAME :各類所得扣繳資料表－明細　　　            *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/08/24                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A*------------------------------------------------------------***
     A          R DSPD1                     RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYR711D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 28'各類所得扣繳資料表－明細'
     A                                      DSPATR(RI)
     A*
     A                                  5 28'公司編號　　　:'
     A            DEM01     R        B  5 46REFFLD(ED01)
     A  61                                  DSPATR(PC RI)
     A*
     A                                  7 28'作帳單位　　起:'
     A                                      DSPATR(HI)
     A            DEM041         7   B  7 46
     A  62                                  DSPATR(PC RI)
     A                                  8 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM042         7   B  8 46
     A  62                                  DSPATR(PC RI)
     A*
     A                                 10 28'身份證號　　起:'
     A            DTB051    R        B 10 46REFFLD(TB05)
     A  63                                  DSPATR(PC RI)
     A                                 11 28'　　　　　　迄:'
     A            DTB052    R        B 11 46REFFLD(TB05)
     A  63                                  DSPATR(PC RI)
     A*
     A                                 13 28'所得類別　　起:'
     A                                      DSPATR(HI)
     A            DTB041    R        B 13 46REFFLD(TB04 PYTBPF)
     A  65                                  DSPATR(PC RI)
     A                                 14 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DTB042    R        B 14 46REFFLD(TB04 PYTBPF)
     A  65                                  DSPATR(PC RI)
     A*
     A                                 16 28'所得年度　　　:'
     A            DYEAR          3  0B 16 46EDTCDE(4)
     A  64                                  DSPATR(PC RI)
     A*
     A                                 18  2'執行環境:'
     A            $PENV          1A  B 18 14VALUES('0' '1' '2')
     A                                 18 18'(0-查詢 1-即時 2-批次)'
     A                                 19  2'列印份數:'
     A            $CPY           3Y 0B 19 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 14
     A*
     A  27                             21 26'*** 請準備198格報表紙 ***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
