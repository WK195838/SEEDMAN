     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA521D                                    *
     A*    PROGRAM NAME :薪資彙總項目會計科目維護                  *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 99/02/01                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*---------------------------------------------------------------*
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA521.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            $DDATE         6Y 0O  1 71EDTWRD('  /  /  ')
     A*
     A            $USERN        10A  O  2  2
     A                                  2 33'薪資彙總項目會計科目維護'
     A                                      DSPATR(RI)
     A*
     A                                  4  1'公司編號:'
     A            DBX01     R        B  4 13REFFLD(BX01 PYBXPF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A*
     A                                  5  1'薪資彙總項目:'
     A            DBX02     R        B  5 17REFFLD(BX02 PYBXPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DSS02     R        O  5 21REFFLD(SS02 PYSSPF)
     A*
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 16'( 1.新增2.修改4.刪除5.查詢-
     A                                      )'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*-----------------------------------------------------------------*
     A          R DSPD2
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A*
     A                                  1  2'<PYA521.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            $DDATE         6Y 0O  1 71EDTWRD('  /  /  ')
     A*
     A            $USERN        10A  O  2  2
     A                                  2 33'薪資彙總項目會計科目維護'
     A                                      DSPATR(RI)
     A*
     A                                  4  1'公司編號:'
     A            DBX01     R        O  4 13REFFLD(BX01 PYBXPF)
     A*
     A                                  5  1'薪資彙總項目:'
     A            DBX02     R        O  5 17REFFLD(BX02 PYBXPF)
     A            DSS02     R        O  5 21REFFLD(SS02 PYSSPF)
     A*
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0O  6 13EDTCDE(4)
     A                                  6 16'( 1.新增2.修改4.刪除5.查詢-
     A                                      )'
     A*
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A                                  8  1'會計科目代號:'
     A            DBX03     R        B  8 17REFFLD(BX03 PYBXPF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A*
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A*
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF04=欄位查詢'
     A                                 23 36'PF05=參考資料'
     A                                 23 53'PF12=回前畫面'
     A*
     A                                 24 67'【泛太資訊】'
