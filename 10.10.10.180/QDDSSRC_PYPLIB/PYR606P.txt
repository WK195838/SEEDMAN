     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR606P                                    *
     A*    PROGRAM NAME :綜合福利金餘額查詢表　                  *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/04/18                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                          *
     A****************************************************************
     A                                      REF(PYGHJF01)
     A*------------------------------------------------------------***
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R             34
     A                                      REFFLD(CM03 PYRF)
     A*
     A                                    37
     A                                      '綜合福利金餘額查詢表'
     A                                      SPACEB(001)
     A*
     A                                     1
     A                                      '部門編號:'
     A                                      SPACEB(001)
     A            DEM041         5        12
     A                                    18
     A                                      '-'
     A            DEM042         5        20
     A                                    67
     A                                      '日期: '
     A            $DATE          6  0     76
     A                                      EDTWRD('  /  /  ')
     A                                    85
     A                                      '頁次: '
     A                                    93
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*
     A                                     1
     A                                      '員工編號:'
     A                                      SPACEB(001)
     A            DEM021    R             12
     A                                      REFFLD(GH02)
     A                                    21
     A                                      '-'
     A            DEM022    R             23
     A                                      REFFLD(GH02)
     A                                    36
     A                                      '福利年度:'
     A            DYEAR          3  0     48
     A                                    52
     A                                      '年'
     A                                    67
     A                                      '時間:'
     A                                    76
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    85
     A                                      '<PYR606P>'
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========================== '
     A                                      SPACEB(001)
     A*
     A                                     1
     A                                      '部　門'
     A                                      SPACEB(001)
     A                                    24
     A                                      '員　工'
     A                                    47
     A                                      '年度福利金額'
     A                                    62
     A                                      '個人申請總額'
     A                                    77
     A                                      '福利金餘額'
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========================== '
     A                                      SPACEB(001)
     A*------------------------------------------------------------***
     A          R PD1
     A                                      SPACEB(001)
     A  31        EM041     R              2
     A  31        EM042     R              3
     A  31        EM043     R              4
     A  31        EM044     R              5
     A  31        DP02      R              7
     A                                      REFFLD(DP02 PYDPPF)
     A            GH02      R             25
     A            EM03      R             33
     A            GH04      R             51EDTCDE(J)
     A            PPAY      R             66REFFLD(GH04 PYGHPF)
     A                                      EDTCDE(J)
     A            PRST      R             79REFFLD(GH04 PYGHPF)
     A                                      EDTCDE(J)
     A*------------------------------------------------------------***
     A          R PT1
     A                                      SPACEB(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ----------------------------'
     A                                     1
     A                                      '部門小計:'
     A                                      SPACEB(001)
     A            TT01           4  0     31
     A                                      EDTCDE(K)
     A                                    37
     A                                      '人'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========================== '
     A                                      SPACEB(001)
     A*------------------------------------------------------------***
     A          R PT2
     A                                     1
     A                                      '公司總計:'
     A                                      SPACEB(001)
     A            TT00           4  0     31
     A                                      EDTCDE(K)
     A                                    37
     A                                      '人'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========================== '
     A                                      SPACEB(001)
     A                                     1'PYR606P--->報表結束'
     A                                      SPACEB(001)
     A*------------------------------------------------------------***
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
