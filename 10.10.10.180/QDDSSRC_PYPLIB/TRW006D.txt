     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : TRW006D                                    *
     A*    PROGRAM NAME :健保補助視窗查詢                          *
     A*    AUTHOR       : A1491 EASON                                *
     A*    CREATE DATE  : 2008.04.01                                 *
     A*    UPDATE DATE  :
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYE9PF)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R DSPD0                     ASSUME
     A                                  1  2' '
     A*----------------------------------------------------------------
     A          R SFLSR3                    SFL
     A            RRN3           3Y 0H
     A            SEL            1A  B  6  4DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            E901      R        O  6  7REFFLD(E901 PYE9PF)
     A            E902      R        O    +1REFFLD(E902 PYE9PF)
     A            E903      R        O    +1REFFLD(E903 PYE9PF)
     A                                      EDTCDE(N)
     A            DE904     R  -10   O    +1REFFLD(E904 PYE9PF)
     A*----------------------------------------------------------------
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0006)
     A                                      WINDOW(3 11 14 55)
     A                                      CA12(12)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      USRRSTDSP
     A            DRRN3          3S 0H      SFLRCDNBR
     A                                  1 15'健保補助查詢'
     A                                      DSPATR(HI)
     A                                  1  2' <TRW006>  '
     A                                  4  4'S'
     A                                  4  7'A'
     A                                      DSPATR(UL)
     A                                  4 10'%'
     A                                      DSPATR(UL)
     A                                  4 13'金額'
     A                                      DSPATR(UL)
     A                                  4 20'備註'
     A                                      DSPATR(UL)
     A            KE901     R        B  5  7REFFLD(E901 PYE9PF)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD1                     WINDOW(SFLCR3)
     A                                 13  2'執行'
     A                                    +2'PF12=回前畫面'
