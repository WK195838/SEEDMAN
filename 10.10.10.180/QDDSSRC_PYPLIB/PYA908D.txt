     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA908D                                    *
     A*    PROGRAM NAME :健保調整暨薪資明細報表列印                *
     A*    AUTHOR       : A1445 HONDA                                *
     A*    CREATE DATE  : 95/03/18                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*                                     REF(*LIBL/PYLPPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                      CF13(13)
     A                                  1  2'<PYA908.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 26'健保調整暨薪資明細報表列印'
     A                                      DSPATR(RI)
     A*---------------------------------------------------------------
     A                                  7 28'公司編號:'
     A            DLP01          2A  B  7 40
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DCM03         44O  O    +1
     A*-------------------------------------------------------------
     A                                  9 28'健保調整年月:'
     A            DLP03          4Y 0B  9 44EDTWRD('0  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A*-------------------------------------------------------------
     A                                 11 28'列印方式Y/N :'
     A            DELYN          1A  B 11 44VALUES(' ' 'Y' 'N')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 11 46'( Y列印健保調低人員)'
     A                                 12 46'( N列印所有人員薪資)'
     A*-------------------------------------------------------------
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 27'***請準備１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  98        ERMSG         70A  O 22  2
     A                                      DSPATR(HI)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
