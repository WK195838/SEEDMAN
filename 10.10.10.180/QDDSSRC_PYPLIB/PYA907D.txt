     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA907D                                    *
     A*    PROGRAM NAME :勞保健保勞退三合一批次調薪檔製作          *
     A*    AUTHOR       : A1445 HONDA                                *
     A*    CREATE DATE  : 95/03/16                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*                                     REF(*LIBL/PYLOPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF13(13)
     A                                  1  2'<PYA907.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 24'勞保健保勞退三合一批次調薪檔製作'
     A                                      DSPATR(RI)
     A*---------------------------------------------------------------
     A                                  7 28'公司編號:'
     A            DLO01          2A  B  7 40
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DCM03         44O  O    +1
     A*-------------------------------------------------------------
     A                                  9 28'製作年月:'
     A            DLO03          4Y 0B  9 40EDTWRD('0  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A*-------------------------------------------------------------
     A                                 14 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11' '
     A                                      DSPATR(RI)
     A  97                             16 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  96                             16 28'試算作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 16 66' '
     A                                      DSPATR(RI)
     A                                 17 11' '
     A                                      DSPATR(RI)
     A                                 17 66' '
     A                                      DSPATR(RI)
     A                                 18 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  98        ERMSG         70A  O 22  2
     A                                      DSPATR(HI)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
