     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI517D                                    *
     A*    PROGRAM NAME :公司基本資料查詢                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 85/12/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  6  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            CM01      R        O  6  9
     A            CM02      R        O  6 17
     A            CM06      R        O  6 33
     A            CM04      R        O  6 51
     A            CM13      R        O  6 66
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0160)
     A                                      SFLPAG(0016)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI517D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'公司基本資料查詢'
     A                                      DSPATR(RI)
     A                                  4  2'S'
     A                                      DSPATR(UL)
     A                                  4  5'公司編號'
     A                                      DSPATR(UL)
     A                                  4 17'  公司簡稱  '
     A                                      DSPATR(UL)
     A                                  4 33'   公司電話  '
     A                                      DSPATR(UL)
     A                                  4 50'統一編號'
     A                                      DSPATR(UL)
     A                                  4 62'薪資組別'
     A                                      DSPATR(UL)
     A            DCM01     R        B  5  9REFFLD(CM01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 23 36'PA1=下一頁'
     A                                 23 50'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
