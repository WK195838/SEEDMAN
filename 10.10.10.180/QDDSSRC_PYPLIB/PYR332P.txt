     A****************************************************************
     A*    PRTF NAME    : PYR332P                                    *
     A*    PROGRAM NAME : PYR3322                                    *
     A*    AUTHOR       : A1386  SHARON                              *
     A*    CREATE DATE  : 93/12/31 A1386 SHARON                      *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A*    REMARK       :薪資資料列印                              *
     A****************************************************************
     A                                      REF(PYRF)
     A*------------------------------------------------------------***
     A          R PH1
     A                                      SKIPB(003)
     A            CM03      R             22REFFLD(CM03 SYCMPF)
     A                                    58'頁次:'
     A                                    68PAGNBR
     A                                      SPACEA(002)
     A*
     A            PH03Y1         3  0     25
     A                                      EDTWRD('0   ')
     A                                    +0
     A                                      '年'
     A            PH03M1         2  0     +0
     A                                      EDTCDE(1)
     A                                    +0
     A                                      '月'
     A                                    +1'薪資明細'
     A                                    56'發薪日期:'
     A            DYMD           7  0     +1EDTWRD('0   /  /  ')
     A                                      SPACEA(002)
     A*
     A                                     2'部門:'
     A            DP02      R             +1REFFLD(DP02 PYDPPF)
     A                                    29'員工姓名:'
     A            EM03X         14        +1
     A                                    56'員工編號:'
     A            PH02      R             +1
     A                                      SPACEA(001)
     A*
     A*94/08/03                            1''
     A*                                     SPACEA(001)
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A                                     2
     A                                      '薪資項目'
     A                                    17
     A                                      '金　　額'
     A                                    29
     A                                      '薪資項目'
     A                                    44
     A                                      '金　　額'
     A                                    56
     A                                      '薪資項目'
     A                                    71
     A                                      '金　　額'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEA(001)
     A            ARR1      R              2
     A                                      REFFLD(SS02)
     A            AMT1           7  0     17
     A                                      EDTCDE(K)
     A            ARR2      R             29
     A                                      REFFLD(SS02)
     A            AMT2           7  0     44
     A                                      EDTCDE(K)
     A            ARR3      R             56
     A                                      REFFLD(SS02)
     A            AMT3           7  0     71
     A                                      EDTCDE(K)
     A*----------------------------------------------------------------
     A          R PE1
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A  25                                 2
     A                                      '調薪生效日'
     A                                      SPACEB(001)
     A  25        SH03W     R   -1        17
     A                                      REFFLD(SH03)
     A                                      EDTWRD('0   /  /  ')
     A  25                                29
     A                                      '調薪前金額 '
     A  25        SH071     R             44
     A                                      REFFLD(SH07)
     A                                      EDTCDE(1)
     A  25                                56
     A                                      '調薪後金額 '
     A  25        SH081     R             71
     A                                      REFFLD(SH08)
     A                                      EDTCDE(1)
     A*
     A                                     2'薪資加項'
     A                                      SPACEB(001)
     A            PADD      R             17
     A                                      REFFLD(PH17)
     A                                      EDTCDE(J)
     A                                    29'計薪天數'
     A            PH07      R            + 1REFFLD(PH07)
     A                                      EDTCDE(1)
     A                                    47'平日加班'
     A            PD07           4  1    + 1EDTCDE(1)
     A                                   + 1'超時加班'
     A            PD08           4  1    + 1EDTCDE(1)
     A************************************************************
     A                                    80
     A                                      ' '
     A                                     2'薪資減項'
     A                                      SPACEB(001)
     A            PSUB      R             17
     A                                      REFFLD(PH16)
     A                                      EDTCDE(J)
Y0818A*
Y0818A                                    47'假日加班'
Y0818A            PD10           4  1    + 1EDTCDE(1)
Y0818A                                   + 1'國定假日'
Y0818A            PD17           4  1    + 1EDTCDE(1)
Y0818A*
     A                                    29'請假時數:'
Y0818A                                      SPACEB(001)
     A            PD21      R            + 1REFFLD(LC02)
     A            PD31           4  1    + 1
     A                                      EDTCDE(1)
     A            PD22      R            + 1REFFLD(LC02)
     A            PD32           4  1    + 1
     A                                      EDTCDE(1)
     A            PD23      R            + 1REFFLD(LC02)
     A            PD33           4  1    + 1
     A                                      EDTCDE(1)
     A************************************************************
     A*
     A                                     2''
     A                                      SPACEB(001)
     A            PD24      R             41REFFLD(LC02)
     A            PD34           4  1    + 1
     A                                      EDTCDE(1)
     A            PD25      R            + 1REFFLD(LC02)
     A            PD35           4  1    + 1
     A                                      EDTCDE(1)
     A            PD26      R            + 1REFFLD(LC02)
     A            PD36           4  1    + 1
     A                                      EDTCDE(1)
     A************************************************************
     A                                    80
     A                                      ' '
     A                                     1
     A                                      '*實發金額*'
     A                                      SPACEB(001)
     A            PHTOT     R             17
     A                                      REFFLD(PH16)
     A                                      EDTCDE(J)
Y0818A*                                   47'加班一假'
Y0818A*           PD10           4  1    + 1EDTCDE(1)
Y0818A*                                  + 1'加班國假'
Y0818A*           PD17           4  1    + 1EDTCDE(1)
     A************************************************************
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
     A*勞退新增部份--------------------------------------------------
     A          R PZ1
     A*94/08/03                             SPACEB(001)
     A                                     1
     A                                      '企提金額'
     A            BMT1           5  0    + 1
     A                                      EDTCDE(1)
     A                                   + 1
     A                                      '自提金額'
     A            BMT2           5  0    + 1
     A                                      EDTCDE(1)
     A                                   + 1'企提累計'
     A            BMT3           8  0    + 1
     A                                      EDTCDE(1)
     A                                   + 1'自提累計'
     A            BMT4           8  0    + 1
     A                                      EDTCDE(1)
     A                                      SPACEA(001)
     A                                     1
     A                                      '提撥扣款日'
     A            DYMD1          7  0    + 1
     A                                      EDTWRD('0  /  /  ')
     A                                   + 1'勞保局入帳日'
     A            DYMD2          7  0    + 1
     A                                      EDTWRD('0  /  /  ')
     A                                   + 1'說明'
     A            REMARK        40O      + 1
