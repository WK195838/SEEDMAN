     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA220P                                    *
     A*    PROGRAM NAME :差勤／加班申請單　　　　　                *
     A*    AUTHOR       : A1266 HANK                                 *
     A*    CREATE DATE  : 89/09/13                                   *
     A*    UPDATE DATE  : 90/03/20 A1274 EVA                         *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      REF(PYRF)
     A*------------------------------------------------------------****
     A          R PH1
     A                                  2 32'差勤／加班申請單'
     A                                  4  2'公　　司:'
     A            DMV01N    R        O  4 14REFFLD(CM02 SYCMPF)
     A                                  4 60'申請日期:'
     A            DMV11     R   -1   O  4 72REFFLD(MV11)
     A                                      EDTWRD('   /  /  ')
     A                                  5  2'部　　門:'
     A            DEM04N        14A  O  5 14
     A                                  5 60'申請時間:'
     A            DMV12     R        O  5 72REFFLD(MV12)
     A                                      EDTWRD('  :  :  ')
     A                                  6  2'員　　工:'
     A            DMV02     R        O  6 15REFFLD(MV02)
     A            EM03      R        O  6 24
     A                                  6 60'入社日期:'
     A            DEM13     R   -1   O  6 72REFFLD(EM13)
     A                                      EDTWRD('   /  /  ')
     A                                  9 26'申請項目  :'
     A            DMV10     R        O  9 40REFFLD(MV10)
     A            DMV10N        10A  O  9 42
     A                                 11 26'假　　別  :'
     A            DMV04     R        O 11 40REFFLD(MV04)
     A            LC02      R        O 11 42
     A                                 13 26'期間（起）:'
     A            DMV03     R   -1   O 13 40REFFLD(MV03)
     A                                      EDTWRD('   /  /  ')
     A            DMV06     R        O 13 50REFFLD(MV06)
     A                                      EDTWRD('0  :  ')
     A                                 14 26'　　（迄）:'
     A            DMV05     R   -1   O 14 40REFFLD(MV05)
     A                                      EDTWRD('   /  /  ')
     A            DMV07     R        O 14 50REFFLD(MV07)
     A                                      EDTWRD('0  :  ')
     A                                 16 26'天　　數  :'
     A            DMV09     R        O 16 40REFFLD(MV09)
     A                                      EDTCDE(4)
     A                                 18 26'時　　數  :'
     A            DMV08     R        O 18 40REFFLD(MV08)
     A                                      EDTCDE(4)
     A  25                             20  2
     A                                      '*注意:此員工本年度尚未產生年假+
     A                                      資料!'
     A                                 21  2
     A                                      '部主管:'
     A                                 21 34
     A                                      '單位主管:'
     A                                 21 67
     A                                      '申請人:'
     A*------------------------------------------------------------****
     A          R PH2
     A                                  2 32'差勤／加班申請單'
     A                                  4  2'公　　司:'
     A            DMV01N    R        O  4 14REFFLD(CM02 SYCMPF)
     A                                  4 60'申請日期:'
     A            DMV11     R   -1   O  4 72REFFLD(MV11)
     A                                      EDTWRD('   /  /  ')
     A                                  5  2'部　　門:'
     A            DEM04N        14A  O  5 14
     A                                  5 60'申請時間:'
     A            DMV12     R        O  5 72REFFLD(MV12)
     A                                      EDTWRD('  :  :  ')
     A                                  6  2'員　　工:'
     A            DMV02     R        O  6 15REFFLD(MV02)
     A            EM03      R        O  6 24
     A                                  6 60'入社日期:'
     A            DEM13     R   -1   O  6 72REFFLD(EM13)
     A                                      EDTWRD('   /  /  ')
     A                                  8  2'申請項目:'
     A            DMV10     R        O  8 15REFFLD(MV10)
     A            DMV10N        10A  O  8 17
     A*
     A                                  9  2'加班日期:'
     A            DMV03     R   -1   O  9 15REFFLD(MV03)
     A                                      EDTWRD('   /  /  ')
     A*
     A                                 12  2'<<津貼次數>>'
     A                                 13 15'平日誤餐費:'
     A            DOV42     R        O 13 30REFFLD(OV42)
     A                                      EDTCDE(4)
     A                                    +1'次'
     A                                 13 47'假日誤餐費:'
     A            DOV40     R        O 13 62REFFLD(OV40)
     A                                      EDTCDE(4)
     A                                    +1'次'
     A*
     A                                 16  2'<<計薪加班>>'
     A                                 17 15'平日加班:'
     A            DOV21     R        O 17 27REFFLD(OV21)
     A                                      EDTCDE(4)
     A                                    +1'小時'
     A*
     A                                 18 15'超時加班:'
     A            DOV22     R        O 18 27REFFLD(OV22)
     A                                      EDTCDE(4)
     A                                    +1'小時'
     A*
     A                                 19 15'假日加班:'
     A            DOV24     R        O 19 27REFFLD(OV24)
     A                                      EDTCDE(4)
     A                                    +1'小時'
     A*
     A                                 21  2'部主管:'
     A                                 21 34'單位主管:'
     A                                 21 67'申請人:'
