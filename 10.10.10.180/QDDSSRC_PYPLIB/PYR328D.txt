     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYR328D                                    *
     A*    PROGRAM NAME :薪資月份加總表列印                        *
     A*    AUTHOR       : A1491 EASON                                *
     A*    CREATE DATE  : 2007/08/23                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYPTPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYR328D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'薪資月份加總列印'
     A                                      DSPATR(RI)
     A                                  3 36'依公司別'
     A                                  7 27'公司編號:'
     A*                                     DSPATR(HI)
     A            SPT01     R        B  7 39REFFLD(PT01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 42'~'
     A            EPT01     R        B  7 44REFFLD(PT01)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  9 27'計算年月:'
     A            DOPT           1A  B  9 39VALUES('1' '2')
     A                                  9 41'(1)計薪年月(2)給付年月'
     A  38                             11 27'計薪年月:'
     A*                                     DSPATR(HI)
     A  38        SPT03     R   -2   B 11 39REFFLD(PT03)
     A                                      EDTWRD('0  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A  39                             11 27'給付年月:'
     A*                                     DSPATR(HI)
     A  39        SPT04     R   -2   B 11 39REFFLD(PT04)
     A                                      EDTWRD('0  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 19  2'執行環境:'
     A            $PENV          1A  B 19 14VALUES('0' '1' '2')
     A                                 19 18'(0-查詢 1-即時 2-批次)'
     A                                 20  2'列印份數:'
     A            $CPY           3Y 0B 20 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 21  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 21 14
     A  27                             21 27'***請準備１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 23 42'F14 =確定列印'
     A                                 24 67'【泛太資訊】'
