     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYHI132D                                   *
     A*    PROGRAM NAME :月提繳工資調整資料全頁查詢
     A*    AUTHOR       : MAX PENG                                   *
     A*    CREATE DATE  : 94/09/15
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :勞退新制子系統
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYA131WF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(HI)
     A            W1302     R        O  8  4
     A                                      COLOR(PNK)
     A            EM03      R        O  8 13REFFLD(EM03 PYEMPF)
     A                                      COLOR(PNK)
     A            W1304     R        O  8 28EDTCDE(1)
     A                                      COLOR(PNK)
     A            W1308     R        O  8 39EDTCDE(1)
     A                                      COLOR(PNK)
     A                                  9 26'調整前資料'
     A            W1308B    R        O  9 39EDTCDE(1)
     A*-------------------------------------------------------------***
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYHI132.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 28'月提繳工資調整全頁查詢  '
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DEM01     R        O  4 13REFFLD(W1301)
     A  60                                  DSPATR(RI)
     A            DEM01N        22O  O  4 16
     A                                    +2'調整年月:'
     A            DW1303    R   -1   O    +1REFFLD(W1303S)
     A                                      EDTWRD('0  /  ')
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'    員工編號姓名    '
     A                                      DSPATR(UL)
     A                                    +1'平均收入'
     A                                      DSPATR(UL)
     A                                    +1'月提繳工資'
     A                                      DSPATR(UL)
     A            DW1302    R        B  7  4REFFLD(W1302)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 36'PA01=下一頁　'
     A                                 23 53'PA02=上一頁　'
     A                                 24 67'【泛太資訊】'
