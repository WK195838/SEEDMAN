     A*%%TS  SD  20000925  150033  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA116D                                    *
     A*    PROGRAM NAME :團體保險資料建立                          *
     A*    AUTHOR       : A1266 HANK                                 *
     A*    CREATE DATE  : 89/09/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  20000925  134455  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA116.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'團體保險資料建立'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DX101     R        B  4 13REFFLD(X101)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DX102     R        B  5 13REFFLD(X102)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'生效日期:'
     A            DX103     R   -1   B  6 13REFFLD(X103)
     A                                      EDTWRD('   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'異動類別:'
     A            DX104     R        B  7 13REFFLD(X104)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                      VALUES(' ' '1' '2' '3' '4' '9')
     A                                  7 16'(1.投保2.調整3.停保4.復保9-
     A                                      .退保)'
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  8 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 23 46'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A*%%TS  SD  20000925  140321  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN            3Y 0O 11  2EDTCDE(Z)
     A            DX205     R        B 11  9REFFLD(X205)
     A                                      VALUES(' ' 'Y')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DX206     R        B 11 13REFFLD(X206)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DX207     R        B 11 28REFFLD(X207)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DX208     R   -1   B 11 40REFFLD(X208)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DX210     R        B 11 49REFFLD(X210)
     A                                      VALUES(' ' 'Y')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DX209     R        B 11 52REFFLD(X209)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DX209N        12A  O 11 54
     A            DX211     R        B 11 67REFFLD(X211)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  20000925  140321  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA116.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'團體保險資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4  2'公司編號:'
     A            DX101     R        O  4 14REFFLD(X101)
     A            CM02      R        O  4 17
     A                                  4 39'生效日期:'
     A            DX103     R   -1   O  4 51REFFLD(X103)
     A                                      EDTWRD('   /  /  ')
     A                                  4 62'類別:'
     A            DX104     R        O  4 70REFFLD(X104)
     A            DDTYPE         6O  O  4 72
     A                                  5  2'員工編號:'
     A            DX102     R        O  5 14REFFLD(X102)
     A            EM03      R        O  5 23
     A                                  5 39'受益人　:'
     A            DX108     R        B  5 51REFFLD(X108)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                  6  2'應繳保費總額:'
     A            DX105     R        O  6 18REFFLD(X105)
     A                                      EDTCDE(1)
     A                                  6 39'投保人數:'
     A            DX106     R        O  6 51REFFLD(X106)
     A                                      EDTCDE(1)
     A                                  6 62'停保:'
     A            DX107     R        B  6 70REFFLD(X107)
     A                                      VALUES(' ' 'Y')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                  6 72'(Y/ )'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  2'眷屬資料:'
     A                                  9 46'民前'
     A                                 10  2'序停保'
     A                                      DSPATR(UL)
     A                                 10 13'  眷屬姓名  '
     A                                      DSPATR(UL)
     A                                 10 28'身份證號'
     A                                      DSPATR(UL)
     A                                 10 39'出生日期Y'
     A                                      DSPATR(UL)
     A                                 10 52'  眷屬稱謂  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 67'   受益人   '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 19'PF4 =欄位說明'
     A                                 23 53'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A                                 24  2'PA1 =下一頁　'
     A                                 24 19'PA2 =上一頁　'
     A                                 23 36'PF5 =參考資料'
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A*%%TS  SD  20000925  145651  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A  55                                  SFLNXTCHG
     A            RRN1           3Y 0H
     A            DNEW1          1A  H
     A            DDEL1          1A  B 11  3VALUES(' ' 'D')
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DX304     R        B 11  7REFFLD(X304)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A*           DX305     R        B 11 22REFFLD(X305)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A* 72                                  DSPATR(PC)
     A* 72                                  DSPATR(RI)
     A            DX905          8Y 0O 11 32EDTCDE(1)
     A            DX910          8Y 0O 11 44EDTCDE(1)
     A                                 12  6'/'
     A            DX909     R        O    +1REFFLD(X909)
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  20000925  145651  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0005)
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A***                                   SFLDROP(CF02)
     A  56                                  SFLDSP
     A  57                                  SFLDSPCTL
     A  58                                  SFLCLR
     A  59                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYA116.3>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'團體保險資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4  2'公司編號:'
     A            DX101     R        O  4 14REFFLD(X101)
     A            CM02      R        O  4 17
     A                                  4 39'生效日期:'
     A            DX103     R   -1   O  4 51REFFLD(X103)
     A                                      EDTWRD('   /  /  ')
     A                                  4 62'類別:'
     A            DX104     R        O  4 70REFFLD(X104)
     A            DDTYPE         6O  O  4 72
     A                                  5  2'員工編號:'
     A            DX102     R        O  5 14REFFLD(X102)
     A            EM03      R        O  5 23
     A                                  5 39'受益人　:'
     A            DX108     R        O  5 51REFFLD(X108)
     A                                  6  2'應繳保費總額:'
     A            DX105     R        O  6 18REFFLD(X105)
     A                                      EDTCDE(1)
     A                                  6 39'投保人數:'
     A            DX106     R        O  6 51REFFLD(X106)
     A                                      EDTCDE(1)
     A                                  6 62'停保:'
     A            DX107     R        O  6 70REFFLD(X107)
     A                                  6 72'(Y/ )'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  2'保險計劃:'
     A                                 10  3'D'
     A                                      DSPATR(UL)
     A                                 10  5'保險/說明'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*                                10 20'計劃代號'
     A*                                     DSPATR(UL)
     A*                                     DSPATR(HI)
     A                                 10 32'投保金額'
     A                                      DSPATR(UL)
     A                                 10 44'應繳保費'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC2
     A*%%TS  SD  20000925  150033  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 19'PF4 =欄位說明'
     A                                 23 53'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A                                 24  2'PA1 =下一頁　'
     A                                 24 19'PA2 =上一頁　'
     A                                 23 36'PF5 =參考資料'
     A**                               23 53'PF2 =明細資料'
