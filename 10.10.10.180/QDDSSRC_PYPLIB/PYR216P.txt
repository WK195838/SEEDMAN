     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYR216P                                    *
     A*    PROGRAM NAME :工時表                                    *
     A*    AUTHOR       : A1386 SHARON                               *
     A*    CREATE DATE  : 2004/12/30                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1                       SKIPB(2)
     A            CM03      R              1
     A                                      SPACEA(1)
     A                                     1'工時表'
     A                                      SPACEA(2)
     A                                     1'工作月份:'
     A            DT203     R   -1        13REFFLD(T203)
     A                                      EDTWRD('   /  ')
     A                                    80'日期:'
     A            DDATE          6  0     88EDTWRD('0  /  /  ')
     A                                    97'頁次:'
     A                                   105PAGNBR
     A                                      SPACEA(1)
     A                                     1'部門代號:'
     A            DEM041         7        +1
     A                                    +1'-'
     A            DEM042         7        +1
     A                                    80'時間:'
     A                                    88TIME
     A                                      EDTWRD('  :  :  ')
     A                                    97'<PYR216P>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =========='
     A                                      SPACEA(1)
     A                                     1'部門名稱'
     A                                    26'員工姓名'
     A                                    45'正常工時'
     A                                    57'請假工時'
     A                                    67'加班工時'
     A                                    77'實際工時'
     A                                    88'薪  資'
     A                                    99'伙  食'
     A                                   110'加  班'
     A                                   121'平均成本'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =========='
     A*
     A          R PD1                       SPACEB(1)
     A  31        EM041     R              1
     A  31        EM042     R              2
     A  31        EM043     R              3
     A  31        EM044     R              4
     A  31        DP02      R              8REFFLD(DP02 PYDPPF)
     A            EM02      R             25
     A            EM03      R             34
     A            PD11           4  1     48EDTCDE(3)
     A            PD12           4  1     59EDTCDE(3)
     A            PD13           4  1     69EDTCDE(3)
     A            PD14           4  1     79EDTCDE(3)
     A            PD15           7  0     89EDTCDE(1)
     A            PD151          7  0    100EDTCDE(1)
     A            PD152          7  0    111EDTCDE(1)
     A            PD16           7  0    122EDTCDE(1)
     A*
     A          R PT1F                      SPACEB(2)
     A                                    27'部門小計(女):'
     A            PT11F          7  1     45EDTCDE(3)
     A            PT12F          7  1     56EDTCDE(3)
     A            PT13F          7  1     66EDTCDE(3)
     A            PT14F          7  1     76EDTCDE(3)
     A            PT15F          9  0     87EDTCDE(1)
     A            PT15F1         9  0     98EDTCDE(1)
     A            PT15F2         9  0    109EDTCDE(1)
     A            PT16F          9  0    120EDTCDE(1)
     A                                      SPACEA(1)
     A          R PT1M                      SPACEB(1)
     A                                    27'部門小計(男):'
     A            PT11M          7  1     45EDTCDE(3)
     A            PT12M          7  1     56EDTCDE(3)
     A            PT13M          7  1     66EDTCDE(3)
     A            PT14M          7  1     76EDTCDE(3)
     A            PT15M          9  0     87EDTCDE(1)
     A            PT15M1         9  0     98EDTCDE(1)
     A            PT15M2         9  0    109EDTCDE(1)
     A            PT16M          9  0    120EDTCDE(1)
     A                                      SPACEA(1)
     A          R PT1                       SPACEB(1)
     A                                    33'部門小計:'
     A            PT11           7  1     45EDTCDE(3)
     A            PT12           7  1     56EDTCDE(3)
     A            PT13           7  1     66EDTCDE(3)
     A            PT14           7  1     76EDTCDE(3)
     A            PT15           9  0     87EDTCDE(1)
     A            PT151          9  0     98EDTCDE(1)
     A            PT152          9  0    109EDTCDE(1)
     A            PT16           9  0    120EDTCDE(1)
     A                                      SPACEA(1)
     A*
     A          R PT2F                      SPACEB(2)
     A                                    27'部門總計(女):'
     A            PT21F          7  1     47EDTCDE(3)
     A            PT22F          7  1     56EDTCDE(3)
     A            PT23F          7  1     66EDTCDE(3)
     A            PT24F          7  1     76EDTCDE(3)
     A            PT25F          9  0     87EDTCDE(1)
     A            PT25F1         9  0     98EDTCDE(1)
     A            PT25F2         9  0    109EDTCDE(1)
     A            PT26F          9  0    120EDTCDE(1)
     A                                      SPACEA(1)
     A          R PT2M                      SPACEB(2)
     A                                    27'部門總計(男):'
     A            PT21M          7  1     47EDTCDE(3)
     A            PT22M          7  1     56EDTCDE(3)
     A            PT23M          7  1     66EDTCDE(3)
     A            PT24M          7  1     76EDTCDE(3)
     A            PT25M          9  0     87EDTCDE(1)
     A            PT25M1         9  0     98EDTCDE(1)
     A            PT25M2         9  0    109EDTCDE(1)
     A            PT26M          9  0    120EDTCDE(1)
     A                                      SPACEA(1)
     A*
     A          R PT2                       SPACEB(1)
     A                                    33'部門總計:'
     A            PT21           7  1     47EDTCDE(3)
     A            PT22           7  1     56EDTCDE(3)
     A            PT23           7  1     66EDTCDE(3)
     A            PT24           7  1     76EDTCDE(3)
     A            PT25           9  0     87EDTCDE(1)
     A            PT251          9  0     98EDTCDE(1)
     A            PT252          9  0    109EDTCDE(1)
     A            PT26           9  0    120EDTCDE(1)
     A                                      SPACEA(1)
     A*
     A          R PT3
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =========='
     A                                      SPACEA(1)
     A                                     1'PYR216P--->報表結束'
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
