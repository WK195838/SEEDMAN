     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA905D                                    *
     A*    PROGRAM NAME :週班設定作業                              *
     A*    AUTHOR       : A1268 VANESSA                              *
     A*    CREATE DATE  : 98/09/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*                                     OVERLAY
     A          R DSPD1
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA905.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'週班設定作業'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DXH01     R        B  4 13REFFLD(XH01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DXH01N        14A  O  4 17
     A                                  5  1'工作區　:'
     A                                      DSPATR(HI)
     A            DXH02     R        B  5 13REFFLD(XH02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DXH02N        30A  O  5 25
     A                                  6  1'週班別　:'
     A            DXH03     R        B  6 13REFFLD(XH03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 16'( 1.新增2.修改4.刪除5.查詢-
     A                                      )'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 28'F4=欄位查詢'
     A                                 23 44'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A          R DSPD2
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA905.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'週班設定作業'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DXH01     R        O  4 13REFFLD(XH01)
     A            DXH01N        14A  O  4 17
     A                                  5  1'工作區　:'
     A            DXH02     R        O  5 13REFFLD(XH02)
     A            DXH02N        30A  O  5 25
     A                                  6  1'週班別　:'
     A            DXH03     R        O  6 13REFFLD(XH03)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0O  7 13EDTCDE(4)
     A                                  7 16'( 1.新增2.修改4.刪除5.查詢-
     A                                      )'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  1'週日班別:'
     A                                      DSPATR(HI)
     A            DXH04     R        B  9 13REFFLD(XH04)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DXF41     R        O  9 16REFFLD(XF04)
     A                                      EDTWRD('0 :  ')
     A                                  9 22'~'
     A            DXF42     R        O  9 24REFFLD(XF05)
     A                                      EDTWRD('0 :  ')
     A            DXF43     R        O  9 30REFFLD(XF03)
     A                                 10  1'週一班別:'
     A                                      DSPATR(HI)
     A            DXH05     R        B 10 13REFFLD(XH05)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DXF51     R        O 10 16REFFLD(XF04)
     A                                      EDTWRD('0 :  ')
     A                                 10 22'~'
     A            DXF52     R        O 10 24REFFLD(XF05)
     A                                      EDTWRD('0 :  ')
     A            DXF53     R        O 10 30REFFLD(XF03)
     A                                 11  1'週二班別:'
     A                                      DSPATR(HI)
     A            DXH06     R        B 11 13REFFLD(XH06)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DXF61     R        O 11 16REFFLD(XF04)
     A                                      EDTWRD('0 :  ')
     A                                 11 22'~'
     A            DXF62     R        O 11 24REFFLD(XF05)
     A                                      EDTWRD('0 :  ')
     A            DXF63     R        O 11 30REFFLD(XF03)
     A                                 12  1'週三班別:'
     A                                      DSPATR(HI)
     A            DXH07     R        B 12 13REFFLD(XH07)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DXF71     R        O 12 16REFFLD(XF04)
     A                                      EDTWRD('0 :  ')
     A                                 12 22'~'
     A            DXF72     R        O 12 24REFFLD(XF05)
     A                                      EDTWRD('0 :  ')
     A            DXF73     R        O 12 30REFFLD(XF03)
     A                                 13  1'週四班別:'
     A                                      DSPATR(HI)
     A            DXH08     R        B 13 13REFFLD(XH08)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DXF81     R        O 13 16REFFLD(XF04)
     A                                      EDTWRD('0 :  ')
     A                                 13 22'~'
     A            DXF82     R        O 13 24REFFLD(XF05)
     A                                      EDTWRD('0 :  ')
     A            DXF83     R        O 13 30REFFLD(XF03)
     A                                 14  1'週五班別:'
     A                                      DSPATR(HI)
     A            DXH09     R        B 14 13REFFLD(XH09)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DXF91     R        O 14 16REFFLD(XF04)
     A                                      EDTWRD('0 :  ')
     A                                 14 22'~'
     A            DXF92     R        O 14 24REFFLD(XF05)
     A                                      EDTWRD('0 :  ')
     A            DXF93     R        O 14 30REFFLD(XF03)
     A                                 15  1'週六班別:'
     A                                      DSPATR(HI)
     A            DXH10     R        B 15 13REFFLD(XH10)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DXF11     R        O 15 16REFFLD(XF04)
     A                                      EDTWRD('0 :  ')
     A                                 15 22'~'
     A            DXF12     R        O 15 24REFFLD(XF05)
     A                                      EDTWRD('0 :  ')
     A            DXF13     R        O 15 30REFFLD(XF03)
     A                                 16  1'生效日期:'
     A            DXH11     R   -2   B 16 13REFFLD(XH11)
     A                                      EDTWRD('  /  /  ')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 28'F4=欄位查詢'
     A                                 23 45'F5 =參考資料'
     A                                 23 63'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
