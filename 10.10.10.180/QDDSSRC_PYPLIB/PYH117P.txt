     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYH115P                                    *
     A*    PROGRAM NAME :不適用退休金制度員工查詢列印            *
     A*    AUTHOR       : MAX CHI                                    *
     A*    CREATE DATE  : 94/03/16                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A          R PH1
     A                                      SKIPB(002)
     A            CM03          34A        1
     A                                      SPACEA(001)
     A                                     1
     A                                      '不適用退休金制度員工查詢列印'
     A                                      SPACEA(002)
     A                                     1
     A                                      ' 部門編號  :'
     A            DEM04F         7A       15
     A                                    22
     A                                      '~'
     A            DEM04T         7A       24
     A                                    80
     A                                      '日期:'
     A            DDATE          6  0     87
     A                                      EDTWRD('0  /  /  ')
     A                                    96
     A                                      '頁次:'
     A                                   103
     A                                      PAGNBR
     A                                      SPACEA(001)
     A                                     1' 員工編號 :'
     A            DH402F         8A       15
     A                                    25
     A                                      '~'
     A            DH402T         8A       28
     A                                    80
     A                                      '時間:'
     A                                    87
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    96
     A                                      '<PYH117P>'
     A                                      SPACEA(002)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ====='
     A                                      SPACEA(001)
     A                                     1
     A                                      '部　　門'
     A                                    23
     A                                      '員  工'
     A                                    47
     A                                      '備註'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ====='
     A                                      SPACEA(001)
     A*
     A          R PD1
     A                                      SPACEA(001)
     A            EM04           7         2
     A            DP02          16         9
     A            H402           8A       26
     A            EM03          14A       34
     A            TXT           50A       51
     A*
     A          R PT1
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----'
     A                                      SPACEA(001)
     A                                     2
     A                                      ' 小計 '
     A            @H402          9  0     24
     A                                      EDTCDE(1)
     A                                    35'名'
     A*%%**************************************************************
     A          R PT2
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ====='
     A                                      SPACEA(001)
     A                                     2
     A                                      ' 合計 '
     A            @@H402         9  0     24
     A                                      EDTCDE(1)
     A                                    35'名'
     A*%%**************************************************************
     A*
     A          R PE9
     A                                      SPACEA(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
