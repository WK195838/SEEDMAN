     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA124D                                    *
     A*    PROGRAM NAME :員工基本資料異動                          *
     A*    AUTHOR       : A1266 HANK                                 *
     A*    CREATE DATE  : 89/08/17                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF18(18)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA124.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'員工基本資料異動'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DEM01     R        B  4 13REFFLD(EM01)
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A            DEM02     R        B  5 13REFFLD(EM02)
     A  61                                  DSPATR(PC RI)
     A                                  6  1'身份證號:'
     A            DEM31     R        B  6 13REFFLD(EM31)
     A  62                                  DSPATR(PC RI)
     A            DEM311    R        B  6 24REFFLD(EM311)
     A  63                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(2 5)
     A                                      EDTCDE(4)
     A                                  7 16'( 2.修改5.查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 20 50'前次處理員工編號:'
     A            WEM02     R        O 20 70REFFLD(EM02)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*-------------------------------------------------------------***
     A          R DSPD2
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA124.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'員工基本資料異動'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72
     A                                  3 36'基本資料'
     A*
     A                                  4  2'員工編號:'
     A            DEM02     R        O  4 15REFFLD(EM02)
     A                                  4 64'離職碼　:'
     A            DEM11     R        O  4 77REFFLD(EM11)
     A*
     A                                  6  2'中文姓名:'
     A            DEM03     R        B  6 14REFFLD(EM03)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  60                                  DSPATR(PC RI)
     A*
     A                                  7  2'英文姓名:'
     A            DEM031    R        B  7 14REFFLD(EM031)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  66                                  DSPATR(PC RI)
     A*
     A                                  8  2'血　　型:'
     A            DEM30     R        B  8 14REFFLD(EM30)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 37'國　　籍:'
     A            DEM321    R        B  8 49REFFLD(EM321)
     A                                      VALUES('L' 'F')
     A                                      DSPATR(MDT)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  65                                  DSPATR(PC RI)
     A                                  8 52'( L-本國 F-外國)'
     A*
     A                                 10  2'身份証號:'
     A            DEM31     R        B 10 14REFFLD(EM31)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  61                                  DSPATR(PC RI)
     A            DEM311    R        B 10 25REFFLD(EM311)
     A                                      DSPATR(MDT)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      VALUES(' ' '1' '2')
     A                                  9 37'性　　別:'
     A            DEM32     R        B  9 49REFFLD(EM32)
     A                                      VALUES('F' 'M')
     A                                      DSPATR(MDT)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 52'( F-女性 M-男性)'
     A*
     A                                  9  2'出生日期:'
     A            DEM33     R   -1   B  9 14REFFLD(EM33)
     A                                      EDTWRD('   /  /  ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  62                                  DSPATR(PC RI)
     A                                 10 37'婚姻狀況:'
     A            DEM34     R        B 10 49REFFLD(EM34)
     A                                      VALUES('M' 'S')
     A                                      DSPATR(MDT)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 52'( M-已婚 S-未婚)'
     A*
     A                                 11  2'撫養人數:'
     A            DEM35     R        B 11 14REFFLD(EM35)
     A                                      EDTCDE(3)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'(含配偶)'
     A                                 11 37'兵　　役:'
     A            DEM36     R        B 11 49REFFLD(EM36)
     A                                      VALUES('1' '2' '3')
     A                                      DSPATR(MDT)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 52'( 1-未役 2-已役 3-免役)'
     A*
     A                                 12  2'籍　　貫:'
     A            DEM37     R        B 12 14REFFLD(EM37)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 13  2'戶籍地址:'
     A            DEM39     R        B 13 14REFFLD(EM39)
     A  63                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  2'　　區碼:'
     A                                      DSPATR(HI)
     A            DM391          5   B 14 14
     A  63                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 14 37'戶籍電話    :'
     A            DEM41     R        B 14 53REFFLD(EM41)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 15  2'通訊地址:'
     A            DEM38     R        B 15 14REFFLD(EM38)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16  2'　　區碼:'
     A                                      DSPATR(HI)
     A            DM381          5   B 16 14
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 16 37'聯絡處電話  :'
     A            DEM114    R        B 16 53REFFLD(EM114)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 17  2'通訊電話:'
     A            DEM40     R        B 17 14REFFLD(EM40)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 17 37'通訊電話２  :'
     A            DEM401    R        B 17 53REFFLD(EM401)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 18  2'連絡人　:'
     A            DEM42     R        B 18 14REFFLD(EM42)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 37'連絡人電話１:'
     A            DEM45     R        B 18 53REFFLD(EM45)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 19 37'連絡人電話２:'
     A            DEM451    R        B 19 53REFFLD(EM451)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 20 37'連絡人電話３:'
     A            DEM452    R        B 20 53REFFLD(EM452)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A*                                20  2'保證人　:'
     A*           DEM431    R        B 20 14REFFLD(EM43)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*                                20 35'保證人電話:'
     A*           DEM461    R        B 20 49REFFLD(EM46)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*
     A*                                21  2'介紹人　:'
     A*           DEM44     R        B 21 14REFFLD(EM44)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*                                21 35'介紹人電話:'
     A*           DEM47     R        B 21 49REFFLD(EM47)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF04=欄位查詢'
     A                                 23 38'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*--------------------------------------------------------------**
     A          R DSPD3                     RTNCSRLOC(&#REC &#FLD)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA124.3>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'員工基本資料異動'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72
     A                                  3 36'履歷資料'
     A                                  4  2'員　　工:'
     A            DEM02     R        O  4 14REFFLD(EM02)
     A            DEM03     R        O  4 23REFFLD(EM03)
     A                                  4 64'離職碼　:'
     A            DEM11     R        O  4 77REFFLD(EM11)
     A                                  5  2'學歷代碼:'
     A                                      DSPATR(HI)
     A            DEM93     R        B  5 14REFFLD(EM93)
     A                                      VALUES('A' '1' '2' '3' '4' '5' '6' -
     A                                      '7' '8' '9' ' ')
     A  60                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      DSPATR(MDT)
     A            EM93N          6   O  5 16
     A                                  6 75'(Y/N)'
     A*
     A                                  7  2'學歷'
     A                                  7  9'學校名稱'
     A                                      DSPATR(HI)
     A                                  7 32'科系'
     A                                  7 53'入校年月'
     A                                  7 64'離校年月'
     A                                  7 75'畢'
     A*
     A                                  8  4'1.'
     A            DEM60     R        B  8  9REFFLD(EM60)
     A  61                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM61     R        B  8 32REFFLD(EM61)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM62     R   -1   B  8 56REFFLD(EM62)
     A                                      EDTWRD('   /  ')
     A  61                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM63     R   -1   B  8 67REFFLD(EM63)
     A                                      EDTWRD('   /  ')
     A  61                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM64     R        B  8 76REFFLD(EM64)
     A                                      VALUES('Y' 'N' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                  9  4'2.'
     A            DEM65     R        B  9  9REFFLD(EM65)
     A  62                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM66     R        B  9 32REFFLD(EM66)
     A  62                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM67     R   -1   B  9 56REFFLD(EM67)
     A                                      EDTWRD('   /  ')
     A  62                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM68     R   -1   B  9 67REFFLD(EM68)
     A                                      EDTWRD('   /  ')
     A  62                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM69     R        B  9 76REFFLD(EM69)
     A                                      VALUES('Y' 'N' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 10  4'3.'
     A            DEM70     R        B 10  9REFFLD(EM70)
     A  63                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM71     R        B 10 32REFFLD(EM71)
     A  63                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM72     R   -1   B 10 56REFFLD(EM72)
     A                                      EDTWRD('   /  ')
     A  63                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM73     R   -1   B 10 67REFFLD(EM73)
     A                                      EDTWRD('   /  ')
     A  63                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM74     R        B 10 76REFFLD(EM74)
     A                                      VALUES('Y' 'N' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 12  2'經歷'
     A                                 12  9'機關名稱'
     A                                 12 32'職務'
     A                                 12 53'任職年月 離職年月薪資'
     A                                 13  4'1.'
     A            DEM75     R        B 13  9REFFLD(EM75)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM76     R        B 13 32REFFLD(EM76)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM77     R   -1   B 13 56REFFLD(EM77)
     A                                      EDTWRD('   /  ')
     A  64                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM78     R   -1   B 13 66REFFLD(EM78)
     A                                      EDTWRD('   /  ')
     A  64                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM115    R        B 13 72REFFLD(EM115)
     A                                      EDTCDE(2)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  4'2.'
     A            DEM79     R        B 14  9REFFLD(EM79)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM80     R        B 14 32REFFLD(EM80)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM81     R   -1   B 14 56REFFLD(EM81)
     A                                      EDTWRD('   /  ')
     A  65                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM82     R   -1   B 14 66REFFLD(EM82)
     A                                      EDTWRD('   /  ')
     A  65                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM116    R        B 14 72REFFLD(EM116)
     A                                      EDTCDE(2)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  4'3.'
     A            DEM83     R        B 15  9REFFLD(EM83)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM84     R        B 15 32REFFLD(EM84)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM85     R   -1   B 15 56REFFLD(EM85)
     A                                      EDTWRD('   /  ')
     A  66                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM86     R   -1   B 15 66REFFLD(EM86)
     A                                      EDTWRD('   /  ')
     A  66                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM117    R        B 15 72REFFLD(EM117)
     A                                      EDTCDE(2)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 17  2'專長'
     A                                 17  9'個人專長'
     A                                      DSPATR(HI)
     A                                 17 32'訓練'
     A                                 17 53'語文能力'
     A                                      DSPATR(HI)
     A*
     A                                 18  4'1.'
     A            DEM87A         3   B 18  9
     A  67                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM87B         3   B    +1
     A  67                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM87C         3   B    +1
     A  67                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM87D         3   B    +1
     A  67                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM87E         3   B    +1
     A  67                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM118    R        B 18 32REFFLD(EM118)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM88A         3   B 18 56
     A  70                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM88B         3   B    +1
     A  70                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM88C         3   B    +1
     A  70                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM88D         3   B    +1
     A  70                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM88E         3   B    +1
     A  70                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 19  4'2.'
     A            DEM89A         3   B 19  9
     A  68                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM89B         3   B    +1
     A  68                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM89C         3   B    +1
     A  68                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM89D         3   B    +1
     A  68                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM89E         3   B    +1
     A  68                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM119    R        B 19 32REFFLD(EM119)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM90A         3   B 19 56
     A  71                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM90B         3   B    +1
     A  71                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM90C         3   B    +1
     A  71                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM90D         3   B    +1
     A  71                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM90E         3   B    +1
     A  71                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 20  4'3.'
     A            DEM91A         3   B 20  9
     A  69                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM91B         3   B    +1
     A  69                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM91C         3   B    +1
     A  69                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM91D         3   B    +1
     A  69                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM91E         3   B    +1
     A  69                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM120    R        B 20 32REFFLD(EM120)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM92A         3   B 20 56
     A  72                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM92B         3   B    +1
     A  72                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM92C         3   B    +1
     A  72                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM92D         3   B    +1
     A  72                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DEM92E         3   B    +1
     A  72                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF04=欄位查詢'
     A                                 23 38'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
