     A*--------------------------------------------------------------*
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      OVERLAY
     A                                      CA03(03 'RETURN')
     A                                      CF18(18 'FULL PAGE INQ.')
     A                                      CF04(04 'PROMPT')
     A                                      CSRLOC(#LIN       #COL)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA531.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'考績等級分數設定'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A                                      DSPATR(HI)
     A            DRR01     R        B  4 13REFFLD(RR01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            CM02      R        O  4 16REFFLD(CM02)
     A                                  5  1'考績項目:'
     A            DRR02     R        B  5 13REFFLD(RR02)
     A                                      VALUES('A' 'B')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 15'( A-調薪 B-獎金)'
     A                                  6  1'考績等級:'
     A            DRR03     R        B  6 13REFFLD(RR03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                  7 15'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A***                              23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                      OVERLAY
     A                                      CA03(03 'RETURN')
     A                                      CA12(12 'PREVIOUSE')
     A                                  1  2'<PYA531.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'考績等級分數設定'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DRR01     R        O  4 13REFFLD(RR01)
     A            CM02      R        O  4 16REFFLD(CM02)
     A                                  5  1'考績項目:'
     A            DRR02     R        O  5 13REFFLD(RR02)
     A            RR02N          6O  O    +1
     A                                  6  1'考績等級:'
     A            DRR03     R        O  6 13REFFLD(RR03)
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  1'分數上限:'
     A            DRR06     R        B  9 13REFFLD(RR06)
     A                                      EDTCDE(1)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  1'分數下限:'
     A            DRR07     R        B 11 13REFFLD(RR07)
     A                                      EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  1'考績比率:'
     A            DRR04     R        B 13 13REFFLD(RR04)
     A                                      EDTCDE(1)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 20'%'
     A                                 15  1'金　　額:'
     A            DRR05     R        B 15 13REFFLD(RR05)
     A                                      EDTCDE(J)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 20'元'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
