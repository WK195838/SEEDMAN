     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA610D                                    *
     A*    PROGRAM NAME :綜合福利金發放作業                        *
     A*    AUTHOR       : A1327 WILLIAMS                             *
     A*    CREATE DATE  : 89/11/28                                   *
     A*    UPDATE DATE  : 90/04/22 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA610.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'綜合福利金發放作業'
     A                                      DSPATR(RI)
     A                                  4  1'福委會  :'
     A            DGD01     R        B  4 13REFFLD(GD01)
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DGD02     R        B  5 13REFFLD(GD02)
     A  61                                  DSPATR(PC RI)
     A                                  6  1'申請日期:'
     A            DGD06     R   -1   B    +1REFFLD(GD06)
     A                                      EDTWRD('0   /  /  ')
     A  62                                  DSPATR(PC RI)
     A                                  7  1'申請項目:'
     A                                      DSPATR(HI)
     A            DGD07     R        B  7 13REFFLD(GD07)
     A  64                                  DSPATR(PC RI)
     A            DGD07N    R        O  7 22REFFLD(#Y03 PT#YPF)
     A                                  8  1'福利年度:'
     A            DGD04     R   -1   B  8 13REFFLD(GD04)
     A                                      EDTCDE(4)
     A  63                                  DSPATR(PC RI)
     A                                  9  1'作業選擇:'
     A            DOPT           1Y 0B  9 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  9 19'( 1.新增 2.修改 4.刪除 5.+
     A                                      查詢)'
     A                                 10  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                    +3'PF04=欄位查詢'
     A                                    +3'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*-------------------------------------------------------------***
     A          R DSPD2
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF04(04)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA610.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'綜合福利金發放作業'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72
     A                                  4  1'福委會　:'
     A            DGD01     R        O  4 13REFFLD(GD01)
     A            DGD01N    R        O  4 23REFFLD(CM02 SYCMPF)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DGD02     R        O  5 13REFFLD(GD02)
     A            DGD02N    R        O  5 23REFFLD(EM03 PYEMPF)
     A                                  6  1'申請日期:'
     A            DGD06     R   -1   O  6 13REFFLD(GD06)
     A                                      EDTWRD('   /  /  ')
     A                                  7  1'申請項目:'
     A                                      DSPATR(HI)
     A            DGD07     R        O  7 13REFFLD(GD07)
     A            DGD07N    R        O  7 23REFFLD(#Y03 PT#YPF)
     A                                  8  1'福利年度:'
     A            DGD04     R   -1   O  8 13REFFLD(GD04)
     A                                      EDTCDE(4)
     A                                  8 22'(中國年)'
     A                                  8 48'剩餘金額:'
     A            DMNY      R        O  8 60REFFLD(GD08)
     A                                      EDTCDE(J)
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 11  1'工作地點:'
     A            DGD03     R        O 11 13REFFLD(GD03)
     A            DGD03N    R        O 11 23REFFLD(DP02 PYDPPF)
     A                                 13  1'申請金額:'
     A            DGD08     R        B 13 13REFFLD(GD08)
     A                                      EDTCDE(J)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  65                                  DSPATR(PC RI)
     A                                 15  1'申請年月:'
     A            DGD05     R   -1   B 15 13REFFLD(GD05)
     A                                      EDTWRD('   /  ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  66                                  DSPATR(PC)
     A                                 17  1'備　　註:'
     A            DGD10     R        B 17 13REFFLD(GD10)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  67                                  DSPATR(PC)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF04=欄位查詢'
     A                                 23 38'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
