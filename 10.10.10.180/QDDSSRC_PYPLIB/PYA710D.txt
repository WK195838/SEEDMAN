     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA710.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'銀行不轉存資料登錄'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DBC01     R        B  4 13REFFLD(BC01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DBC02     R        B  5 13REFFLD(BC02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'所得類別:'
     A            DBC03     R        B  6 13REFFLD(BC03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      VALUES(' ' '1' '2' '5')
     A                                  6 16'(1. 5日薪資 2. 20日薪資-
     A                                      5.獎金)-
     A                                      '
     A                                  7  1'存入日期:'
     A            DYMD           6Y 0B  7 13
     A                                      EDTWRD('0  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A*                                     EDTCDE(4)
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  8 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF04=欄位查詢'
     A                                 23 36'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC2
     A*%%TS  SD  19980525  232842  PPSPGMR40   REL-V3R1M0  5763-PW1
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA710.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'銀行不轉存資料登錄'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  5 25'公司編號:'
     A            DBC01     R        O  5 37REFFLD(BC01)
     A            CM02      R        O  5 46
     A                                  6 25'員工編號:'
     A            DBC02     R        O  6 37REFFLD(BC02)
     A            EM03      R        O  6 46
     A                                  7 25'所得類別:'
     A            DBC03     R        O  7 37REFFLD(BC03)
     A            BC03N         10   O  7 46
     A                                  8 25'存入日期:'
     A            DYMD           6Y 0O  8 37EDTWRD('0  /  /  ')
     A                                 10 25'支票號碼:'
     A            DBC06     R        B 10 37REFFLD(BC06)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF05=參考資料'
     A                                 23 36'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
