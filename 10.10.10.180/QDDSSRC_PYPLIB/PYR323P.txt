     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR3232                                    *
     A*    PROGRAM NAME :保費明細表－標準                          *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/01/15                                   *
     A*    UPDATE DATE  : 90/02/13 A1274                             *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      REF(PYRF)
     A*-------------------------------------------------------------***
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1
     A            DPH03          5S  O     1EDTWRD('0   /  ')
     A                                      SPACEB(001)
     A                                     7'保費明細表－標準'
     A                                   149'日期:'
     A            DDATE          7  0    156EDTCDE(Y)
     A                                   168'頁次:'
     A                                   175PAGNBR
     A                                     1'部門編號:'
     A                                      SPACEB(002)
     A            DEM04F         7        13
     A                                    +1'∼'
     A            DEM04T         7        +1
     A                                    31'員工編號:'
     A            DEM02F    R             43
     A                                      REFFLD(EM02)
     A                                    52
     A                                      '∼'
     A            DEM02T    R             57
     A                                      REFFLD(EM02)
     A                                   149
     A                                      '時間:'
     A                                   156
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   168
     A                                      '<PYR323P>'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEB(001)
     A                                     1'部門別　'
     A                                      SPACEB(001)
     A                                    21'員　　工'
     A                                    47'健保投保額'
     A                                    60'公司負擔金額'
     A                                    75'員工負擔金額'
     A                                    92'勞保投保額'
     A                                   106'公司負擔金額'
     A                                   122'員工負擔金額'
     A                                   138'團保負擔金額'
     A                                   154'勞保職災金額'
     A                                   170'勞保墊償金額'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEB(001)
     A*
     A*-------------------------------------------------------------***
     A          R PD1
     A                                      SPACEB(001)
     A  31        DEM04          7         1
     A  31        DP02      R              8REFFLD(DP02 PYDPPF)
     A            EM02      R             +1
     A            EM03      R             +1
     A            E105      R             49EDTCDE(1)
     A            E105C     R             63REFFLD(HD06)
     A                                      EDTCDE(1)
     A            E105P     R             77REFFLD(HD06)
     A                                      EDTCDE(1)
     A            TX05      R        O    94EDTCDE(1)
     A            EM23C          7S 0O   110EDTCDE(J)
     A            EM23P          7S 0O   126EDTCDE(J)
     A            EM292     R        O   145EDTCDE(1)
     A            FLD01          6S 0O   160EDTCDE(1)
     A            FLD02          6S 0O   176EDTCDE(1)
     A*-------------------------------------------------------------***
     A          R PT1
     A                                      SPACEB(001)
     A                                     1'部門小計:'
     A            PT11           3  0     13EDTCDE(3)
     A                                    +1'人'
     A                                    +2'健保:'
     A            PT1A           3  0     +1EDTCDE(3)
     A                                    +1'人'
     A                                    +2'勞保:'
     A            PT1B           3  0     +1EDTCDE(3)
     A                                    +1'人'
     A            PT12           7S 0O    63EDTCDE(J)
     A            PT13           7S 0O    77EDTCDE(J)
     A            PT14           7S 0O   110EDTCDE(J)
     A            PT15           7S 0O   126EDTCDE(J)
     A            PT16           6S 0O   144EDTCDE(J)
     A            PT17           6S 0O   160EDTCDE(J)
     A            PT18           6S 0O   176EDTCDE(J)
     A*
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                      SPACEB(001)
     A*-------------------------------------------------------------***
     A          R PT2
     A                                      SPACEB(001)
     A                                     1'合　　計:'
     A            PT21           4  0     12EDTCDE(3)
     A                                    +1'人'
     A                                    +2'健保:'
     A            PT2A           3  0     +1EDTCDE(3)
     A                                    +1'人'
     A                                    +2'勞保:'
     A            PT2B           3  0     +1EDTCDE(3)
     A                                    +1'人'
     A            PT22           7S 0O    63EDTCDE(J)
     A            PT23           7S 0O    77EDTCDE(J)
     A            PT24           7S 0O   110EDTCDE(J)
     A            PT25           7S 0O   126EDTCDE(J)
     A            PT26           6S 0O   144EDTCDE(J)
     A            PT27           6S 0O   160EDTCDE(J)
     A            PT28           6S 0O   176EDTCDE(J)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEB(001)
     A*-------------------------------------------------------------***
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
