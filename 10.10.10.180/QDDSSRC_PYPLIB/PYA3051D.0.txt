     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA3051.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'其他薪資資料建立－個人'
     A                                      DSPATR(RI)
     A*
     A                                  4  1'公司編號:'
     A            DOS01     R        B  4 13REFFLD(OS01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DOS02     R        B  5 13REFFLD(OS02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'年　　月:'
     A            DOS03     R   -2   B  6 13REFFLD(OS03)
     A                                      EDTWRD('0  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'期　　別:'
     A            DOS04     R        B  7 13REFFLD(OS04)
     A                                      VALUES('1' '2' '3' ' ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  7 16'(1. 5日薪資 2. 20日薪資)'
     A                                  8  1'作業選擇:'
     A            DOPT           1A  B  8 13VALUES('1' '2' '4' '5')
     A                                  8 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*============================================================*
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            2S 0O  9  4
     A            DOS05     R        O  9  9REFFLD(OS05)
     A            SS02      R        O  9 12
     A            DOS06     R        B  9 30REFFLD(OS06)
     A                                      CHECK(RB)
     A                                      EDTCDE(K)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31 32                               DSPATR(UL)
     A N31
     AON32                                  DSPATR(PR)
     A*           DOS07     R        B  9 46REFFLD(OS07)
     A*                                     CHECK(RB)
     A*                                     EDTCDE(K)
     A* 62                                  DSPATR(PC)
     A* 62                                  DSPATR(RI)
     A* 31N32                               DSPATR(UL)
     A*N31
     A* 32                                  DSPATR(PR)
     A            DOS08     R        H      REFFLD(OS08)
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0012)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A                                  1  2'<PYA3051.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'其他薪資資料建立－個人'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4  2'公　　司:'
     A            DOS01     R        O  4 14REFFLD(OS01)
     A            CM02      R        O  4 23
     A                                  5  2'員　　工:'
     A            DOS02     R        O  5 14REFFLD(OS02)
     A            EM03      R        O  5 23
     A                                  4 60'年　　月:'
     A            DOS03     R   -2   O  4 72REFFLD(OS03)
     A                                      EDTWRD('0  /  ')
     A                                  5 60'期　　別:'
     A            DOS04N         6O  O  5 72
     A                                  6  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  7  2'序號'
     A                                  7 12'薪資名稱'
     A                                  7 31'金　額'
     A*                                 7 45'次　數'
     A                                  8  3'----  -----------------'
     A                                  8 30'----------'
     A*                                 8 44'----------'
     A          R DSPC2                     OVERLAY
     A                                 21  2'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =================='
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PF13=確認'
     A                                 24 67'【泛太資訊】'
