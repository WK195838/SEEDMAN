     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI405D                                    *
     A*    PROGRAM NAME :公司獎金調整全頁查詢　　　                *
     A*    AUTHOR       : A1346 WESTS                                *
     A*    CREATE DATE  : 2002/11/04                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            PB02      R        O  8  5
     A            DEM03     R        O  8 14REFFLD(EM03)
     A            CPB03     R   -1   O  8 30REFFLD(PB03)
     A                                      EDTCDE(4)
     A            PB04      R        O  8 35
     A            PB04N         10O  O  8 38
     A            PB06      R        O  8 49EDTCDE(N)
     A            PB08      R        O  8 60EDTCDE(N)
     A            PB12      R        O  8 71EDTCDE(N)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0300)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI405D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'公司獎金調整全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DPB01     R        O  4 13REFFLD(RV01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DPB01N        14O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'員工編號　姓名　　　 '
     A                                      DSPATR(UL)
     A                                  6 28'年度'
     A                                      DSPATR(UL)
     A                                  6 35'  種類     '
     A                                      DSPATR(UL)
     A                                  6 49'發放金額'
     A                                      DSPATR(UL)
     A                                  6 60'扣繳稅額'
     A                                      DSPATR(UL)
     A                                  6 71'實領金額'
     A                                      DSPATR(UL)
     A            DPB02     R        B  7  5REFFLD(PB02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DPB03     R   -1   B  7 30REFFLD(PB03)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            DPB04     R        B  7 35REFFLD(PB04)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA01=下一頁'
     A                                 23 56'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
