     A                                      REF(PYRF)
     A          R PH1                       SKIPB(2)
     A            CM03      R              1
     A                                      SPACEA(1)
     A                                     1'退休金累積金額明細表'
     A                                      SPACEA(2)
     A                                     1
     A                                      ' 部門編號  :'
     A            DEM04F         7A       15
     A                                   + 1
     A                                      '~'
     A            DEM04T         7A      + 1
     A                                    72'日期:'
     A            DDATE          6  0     80EDTWRD('0  /  /  ')
     A                                    89'頁次:'
     A                                    97PAGNBR
     A                                      SPACEA(1)
     A                                     1' 員工編號  :'
     A            DH402F         8A       15
     A                                    25
     A                                      '~'
     A            DH402T         8A       28
     A                                    72'時間:'
     A                                    80TIME
     A                                      EDTWRD('  :  :  ')
     A                                    90'<PYH111P>'
     A                                      SPACEA(2)
     A*                                    1'月份:'
     A*           DYM            8  0     +1EDTWRD('0   /  /  ')
     A*                                     SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     1'部門'
     A                                     8'員工編號'
     A                                    21'員工姓名'
     A                                    37'到職日'
     A                                    49'提撥累積年資'
     A                                    68'自提累積金額'
     A                                    82'企提累積金額'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A          R PD1
     A                                      SPACEA(1)
     A            EM04           7         2
     A            H402           8        10
     A            EM03          14A       23
     A            EM13X          6  0     37
     A                                      EDTWRD('0  /  /  ')
     A            DAYS           5  2     56
     A                                      EDTCDE(1)
     A            H412           9  0     70EDTCDE(1)
     A*                                     SPACEA(1)
     A            H413           9  0     84EDTCDE(1)
     A*                                     SPACEA(1)
     A*
     A          R PT1                       SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------'
     A                                      SPACEA(1)
     A                                     1'小計:'
     A            PNUM           3  0     +4EDTCDE(1)
     A                                    +2'人'
     A          R PT2
     A                                      SPACEA(1)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========='
     A                                      SPACEA(001)
     A                                     1
     A                                      '合計:'
     A            SNUM           4  0     +2
     A                                      EDTCDE(1)
     A                                    +2'人'
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
