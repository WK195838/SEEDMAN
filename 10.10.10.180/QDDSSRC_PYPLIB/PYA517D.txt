     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA517D                                    *
     A*    PROGRAM NAME :公司基本資料建立                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 85/12/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03 'RETURN')
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA517.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'公司基本資料設定'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DCM01     R        B  4 13REFFLD(CM01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'作業選擇:'
     A            DOPT           1Y 0B  5 13VALUES(1 2 4 5)
     A                                  5 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03 'RETURN')
     A                                      CA12(12 'PREVIOUSE')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA517.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'公司基本資料設定'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  5  1'公司編號:'
     A            DCM01          2A  O  5 13
     A                                  7  1'公司簡稱:'
     A            DCM02     R        B  7 13REFFLD(CM02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  1'公司全名:'
     A            DCM03     R        B  8 13REFFLD(CM03)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  1'統一編號:'
     A            DCM04     R        B  9 13REFFLD(CM04)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  1'稅籍編號:'
     A            DCM05     R        B 10 13REFFLD(CM05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  1'公司電話:'
     A            DCM06     R        B 12 13REFFLD(CM06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  1'工廠電話:'
     A            DCM07     R        B 13 13REFFLD(CM07)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  1'公司地址:'
     A            DCM08     R        B 14 13REFFLD(CM08)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  1'工廠地址:'
     A            DCM09     R        B 15 13REFFLD(CM09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17  1'薪資組別:'
     A            DCM13     R        B 17 13REFFLD(CM13)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19  1'所得扣繳義務人名　　:'
     A            DCM10     R        B 19 25REFFLD(CM10)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 20  1'所得扣繳媒體單位代號:'
     A            DCM11     R        B 20 25REFFLD(CM11)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 21  1'所得扣繳機徵機關    :'
     A            DCM12     R        B 21 25REFFLD(CM12)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
