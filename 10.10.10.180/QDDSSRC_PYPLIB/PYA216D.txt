     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA216D                                    *
     A*    PROGRAM NAME :刷卡資料異常確認批次處理                  *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 86/01/20                                   *
     A*    UPDATE DATE  : 88/10/05 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF13(13)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A                                  1  2'<PYA216.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A                                  2 32'刷卡異常確認處理'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  6 30'公司編號:'
     A            DT602     R        B  6 42REFFLD(T602)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  8 30'刷卡日期:'
     A            DT601     R   -1   B  8 42REFFLD(T601)
     A                                      EDTCDE(Y)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8 55'日前（含）'
     A                                 12 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 13 11' '
     A                                      DSPATR(RI)
     A                                 13 66' '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A  97                             14 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  28                             14 28'刷卡資料確認作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 18 24'最近確認刷卡日期:'
     A            T601D     R   -1   O 18 45EDTCDE(Y)
     A                                      REFFLD(T601)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'F03 =回主畫面'
     A                                 23 20'F13 =處理開始'
     A                                 24 67'【泛太資訊】'
