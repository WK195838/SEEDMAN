     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA337D                                    *
     A*    PROGRAM NAME :眷屬健保費資料維護                        *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 91/12/18                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A*911218                               CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA337.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'眷屬健保費資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DE501     R        B  4 13REFFLD(E501)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DE502     R        B  5 13REFFLD(E502)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'年　　月:'
     A            DE515     R   -1   B  6 13REFFLD(E515)
     A                                      EDTWRD('0   /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(2 5)
     A                                      EDTCDE(4)
     A                                  7 16'(2.修改 5.查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 21'PF04 =欄位查詢'
     A                                 23 40'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*------------------------------------------------------------***
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            2S 0H
     A            DE504     R        O  9  5REFFLD(E504)
     A            DE506     R        O  9 11REFFLD(E506)
     A            DE514     R        B  9 28REFFLD(E514)
     A                                      CHECK(RB)
     A                                      EDTCDE(K)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*------------------------------------------------------------***
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0012)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF13(13)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA337.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'眷屬健保費資料維護'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72
     A                                  4  1'公司編號:'
     A            DE501     R        O  4 13REFFLD(E501)
     A            E501N         14O  O  4 22
     A                                  5  1'員工編號:'
     A            DE502     R        O  5 13REFFLD(E502)
     A            EM03      R        O  5 22REFFLD(EM03)
     A                                  5 42'員工保費:'
     A            DE514A    R        O  5 54REFFLD(E514)
     A                                      EDTCDE(K)
     A                                  6  1'發放年月:'
     A            DE515     R   -1   O  6 13REFFLD(E515)
     A                                      EDTWRD('0   /  ')
     A                                  6 42'總保費  :'
     A            DTOT           7  0O  6 54
     A                                      EDTCDE(K)
     A                                  7  1'==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                  8  3'序號'
     A                                  8 11'眷屬姓名'
     A                                  8 30'保費  '
     A*------------------------------------------------------------***
     A          R DSPC1
     A                                      OVERLAY
     A                                 21  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 23 38'PF13=資料確認'
     A                                 23 55'PA1=下一頁'
     A                                 23 69'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
     A*------------------------------------------------------------***
