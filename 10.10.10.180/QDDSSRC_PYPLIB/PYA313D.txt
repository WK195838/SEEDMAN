     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*    PROGRAM-ID   : PYA313D                                    *
     A*    PROGRAM NAME : COST CENTER薪資月彙總檔維護              *
     A*    AUTHOR       : A1276 VINCENT                              *
     A*    CREATE DATE  : 88/02/02                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYSMPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #FLD          10A  H
     A            #REC          10A  H
     A                                  1  2'<PYA313>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 27' COST CENTER新資月彙總維護'
     A                                      DSPATR(RI)
     A                                  2 64'時間:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'公司編號：'
     A            DSM01     R        B  4 16REFFLD(SM01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'COST CENTER :'
     A            DSM021    R        B  5 16REFFLD(SM021)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DSM022    R        B  5 19REFFLD(SM022)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DSM023    R        B  5 24REFFLD(SM023)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6  1'年度月份:'
     A            DSM03          6Y 0B  6 16
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                      EDTWRD('    /  ')
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 16VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 19'( 1.新增'
     A                                  7 30'2.修改'
     A                                  7 39'4.刪除'
     A                                  7 48'5.查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF04=欄位查詢'
     A                                 24 67'【泛太資訊】'
     A                                  5 14':'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            DNEW           1A  H
     A            RRN            3Y 0H
     A            DDEL           1A  B 10  2
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DSM04     R        B 10 13REFFLD(SM04)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DNAME          6A  O 10 34
     A            DSM05     R        B 10 59REFFLD(SM05)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            HSM04     R        H      REFFLD(SM04)
     A            HSM05     R        H      REFFLD(SM05)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0012)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYA313>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 27' COST CENTER新資月彙總維護'
     A                                      DSPATR(RI)
     A                                  2 64'時間:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'公司編號：'
     A            DSM01     R        O  4 16REFFLD(SM01)
     A                                  5  2'COST CENTER :'
     A            DSM021    R        O  5 16REFFLD(SM021)
     A            DSM022    R        O  5 19REFFLD(SM022)
     A            DSM023    R        O  5 24REFFLD(SM023)
     A                                  6  1'年度月份:'
     A            DSM03          6Y 0O  6 16EDTWRD('    /  ')
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0O  7 16EDTCDE(4)
     A                                  7 19'( 1.新增'
     A                                  7 30'2.修改'
     A                                  7 39'4.刪除'
     A                                  7 48'5.查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  2'D'
     A                                  9  9'薪資項目'
     A                                  9 34'名稱'
     A                                  9 59'彙總金額'
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                      OVERLAY
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
