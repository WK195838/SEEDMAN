     A*%%TS  SD  19970505  103827  ESPGMR      REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI321D                                    *
     A*    PROGRAM NAME :年度調薪（AI）公式設定查詢              *
     A*    AUTHOR       : A1269 JADI                                 *
     A*    CREATE DATE  : 87/12/14                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19970505  103332  ESPGMR      REL-V3R1M0  5763-PW1
     A            RRN            3Y 0H
     A            DOPT           1A  B  6  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            AI01      R        O  6 05
     A            AI02      R        O  6 13
     A            AI03      R        O  6 25
     A            AI04      R        O  6 33
     A            AI05      R        O  6 43
     A            AI06      R        O  6 53
     A            AI07      R        O  6 61EDTCDE(2)
     A            AI08      R        O  6 71
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19970505  103827  ESPGMR      REL-V3R1M0  5763-PW1
     A* *DS3                                SFLLIN(0013)
     A                                      SFLSIZ(0064)
     A                                      SFLPAG(0016)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI321D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 25'年度調薪（AI）公式設定'
     A                                      DSPATR(RI)
     A                                  3 25'年度調薪（AI）公式查詢'
     A                                      DSPATR(RI)
     A                                  4  2'S'
     A                                      DSPATR(UL)
     A                                  4  4'公司'
     A                                      DSPATR(UL)
     A                                  4 12'生效年度'
     A                                      DSPATR(UL)
     A                                  4 24'月份'
     A                                      DSPATR(UL)
     A                                  4 32'調薪別'
     A                                      DSPATR(UL)
     A                                  4 42'基準日'
     A                                      DSPATR(UL)
     A                                  4 52'基礎'
     A                                      DSPATR(UL)
     A                                  4 60'百分比'
     A                                      DSPATR(UL)
     A                                  4 70'調薪額'
     A                                      DSPATR(UL)
     A            DAI01     R        B  5  5REFFLD(AI01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 23 36'PA1=下一頁'
     A                                 23 50'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
