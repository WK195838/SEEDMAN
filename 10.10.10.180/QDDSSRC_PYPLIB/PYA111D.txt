     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA111D                                    *
     A*    PROGRAM NAME :全民健保眷屬資料建立                      *
     A*    AUTHOR       : A1413 CLOUD                                *
     A*    CREATE DATE  : 95/08/30                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYE5PF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA111.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'全民健保眷屬資料建立'
     A                                      DSPATR(RI)
     A                                  4  1'公司別  :'
     A            DE501     R        B  4 13REFFLD(E501)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DCM02     R        O  4 16REFFLD(CM02 SYCMPF)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DE502     R    8A  B  5 13REFFLD(E502)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DEM03     R        O  5 22REFFLD(EM03 PYEMPF)
     A                                  6  1'眷屬證號:'
     A            DE507     R        B  6 13REFFLD(E507)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                  7  1'眷屬健保生效日:'
     A            DE512          6Y 0B  7 19EDTWRD('0  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                    +2'(YY/MM/DD) => 西元'
     A                                      COLOR(PNK)
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  8 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 23 36'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A*
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                  1  2'<PYA111.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'全民健保眷屬資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4  1'公司別  :'
     A            DE501     R        O  4 13REFFLD(E501)
     A                                  5  1'員工編號:'
     A            DE502     R    8A  O  5 13REFFLD(E502)
     A            DEM03     R        O  5 22REFFLD(EM03 PYEMPF)
     A                                  6  1'眷屬證號:'
     A            DE507     R        O  6 13REFFLD(E507)
     A                                  7  1'眷屬健保生效日:'
     A            DE512          6  0O  7 19EDTWRD('0  /  /  ')
     A* 67                                  DSPATR(PC)
     A* 67                                  DSPATR(RI)
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0O  8 13
     A                                  8 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 10  1'眷屬姓名:'
     A            DE506     R        B 10 13REFFLD(E506)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(PR)
     A                                 10 28'出生日期:'
     A            DE508          6  0B 10 40EDTWRD('0  /  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(PR)
     A                                 10 50'稱謂:'
     A                                      DSPATR(HI)
     A            DE509     R        B 10 58REFFLD(E509)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N62                                  DSPATR(HI)
     A N34                                  DSPATR(UL)
     A  34                                  DSPATR(PR)
     A            DE509N        14O  O 10 60
     A                                 12  1'異動類別:'
     A            DE513     R        B 12 13REFFLD(E513)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 12 16'1.投保2.調整3.轉出'
     A*                                12  1'公司別  :'
     A*           DE501     R        B 12 13REFFLD(E501)
     A* 66                                  DSPATR(PC)
     A* 66                                  DSPATR(RI)
     A*N31                                  DSPATR(UL)
     A* 31                                  DSPATR(PR)
     A*           DCM02     R        O 12 16REFFLD(CM02 SYCMPF)
     A*                                12 35'員工編號:'
     A*                                     DSPATR(HI)
     A*           DE502     R        B 12 47REFFLD(E502)
     A* 64                                  DSPATR(PC)
     A* 64                                  DSPATR(RI)
     A*N64                                  DSPATR(HI)
     A*N31                                  DSPATR(UL)
     A* 31                                  DSPATR(PR)
     A*           DEM03     R        O 12 58REFFLD(EM03 PYEMPF)
     A                                 14  1'補助:'
     A                                      DSPATR(HI)
     A            DE511     R        B 14  9REFFLD(E511)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DE511N        32O  O 14 11
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位說明'
     A                                    +2'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
