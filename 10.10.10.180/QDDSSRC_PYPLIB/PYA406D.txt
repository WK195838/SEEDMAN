     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA406D                                    *
     A*    PROGRAM NAME :兼職人員薪資試算作業　　　　　            *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 89/03/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CA03(03 '回主畫面')
     A                                      CF13(13)
     A                                  1  2'<PYA406D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'兼職人員薪資試算作業'
     A                                      DSPATR(RI)
     A*
     A                                  4 23'公　　司:'
     A            DPB01     R        B  4 36REFFLD(PB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            CM03      R        O  4 40REFFLD(CM03 SYCMPF)
     A*
     A                                  6 23'部　　門:'
     A            DEPT1          5   B  6 36
     A  66                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  6 42'-'
     A            DEPT2          5   B  6 44
     A  67                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A*
     A                                  8 23'薪資類別:'
     A            DPB04          2   B  8 36
     A  62                                  DSPATR(PC RI)
     A            SS02      R        O  8 40REFFLD(SS02 PYSSPF)
     A                                 10 23'發放日期:'
     A            DYMD           7Y 0B 10 36EDTCDE(Y)
     A  63                                  DSPATR(PC RI)
     A*
     A                                 12 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 13 11' '
     A                                      DSPATR(RI)
     A                                 13 66' '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A  97                             14 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  28                             14 28'薪資資料處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
