     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR220                                     *
     A*    PROGRAM NAME :刷卡資料異常報表列印                      *
     A*    AUTHOR       : A1184                                      *
     A*    CREATE DATE  : 85/03/06                                   *
     A*    UPDATE DATE  : 89/08/25 A1274 EVA                         *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYR211>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'刷卡資料異常報表列印'
     A                                      DSPATR(RI)
     A                                  5 30'公司編號　　:'
     A            DT602     R        B  5 46REFFLD(T602)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 30'部門編號　起:'
     A                                      DSPATR(HI)
     A            DEM04F         7A  B  7 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 30'　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM04T         7A  B  8 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 30'員工編號　起:'
     A                                      DSPATR(HI)
     A            DT603F    R        B 10 46REFFLD(T603)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 11 30'　　　　　迄:'
     A                                      DSPATR(HI)
     A            DT603T    R        B 11 46REFFLD(T603)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 13 30'刷卡日期　起:'
     A            DT601F    R   -1   B 13 46REFFLD(T601)
     A                                      EDTWRD('   /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 14 30'　　　　　迄:'
     A            DT601T    R   -1   B 14 46REFFLD(T601)
     A                                      EDTWRD('   /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 17'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A  27                             21 30'請準備１３２格報表紙'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF03 =回主畫面'
     A                                 23 28'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
