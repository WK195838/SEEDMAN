     A****************************************************************
     A*    PRTF NAME    : PYR106P                                    *
     A*    PROGRAM NAME : PYR106                                     *
     A*    AUTHOR       : A1084  DERLERN                             *
     A*    CREATE DATE  : 79/06/19                                   *
     A*    UPDATE DATE  : 89/08/25 A1274 EVA                         *
     A*    SYSTEM       : MIRS                                       *
     A*    REMARK       :新進人員資料列印                          *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1                       SKIPB(002)
     A            CM03      R              1
     A                                    77'日期:'
     A            DDATE          6  0     85
     A                                      EDTWRD('0  /  /  ')
     A                                    94'頁次:'
     A                                   101PAGNBR
     A                                      SPACEA(001)
     A                                     1'新進人員明細表          '
     A                                    77'時間:'
     A                                    85TIME
     A                                      EDTWRD('  :  :  ')
     A                                    95'<PYR106P>'
     A                                      SPACEA(002)
     A                                     1'部門代號:'
     A            DEM041         7        13
     A                                    +1'-'
     A            DEM042         7        +1
     A                                      SPACEA(001)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ='
     A                                      SPACEA(001)
     A                                     1'部門編號'
     A                                    26'員工編號'
     A                                    49'性別'
     A                                    56'到職日期'
     A                                    70'試用期滿日'
     A                                    84'臨時卡號'
     A                                      SPACEA(001)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ='
     A          R PD1                       SPACEB(001)
     A            EM041     R              2
     A            EM042     R              3
     A            EM043     R              4
     A            EM044     R              5
     A            DP02      R             +1REFFLD(DP02 PYDPPF)
     A            EM02      R             +1
     A            EM03      R             +1
     A            EM32      R             52
     A            PEM13     R   -2        57REFFLD(EM13)
     A                                      EDTWRD('0  /  /  ')
     A            PEM20     R   -2        71REFFLD(EM20)
     A                                      EDTWRD('0  /  /  ')
     A            T401      R             85
     A*
     A          R PT1                       SPACEB(001)
     A                                     1
     A                                      '部門人數小計:'
     A            PT11           5  0     +1
     A                                      EDTCDE(1)
     A                                    +1
     A                                      '人'
     A*
     A          R PT2                       SPACEB(001)
     A                                     1
     A                                      '部門人數中計:'
     A            PT21           5  0     +1
     A                                      EDTCDE(1)
     A                                    +1
     A                                      '人'
     A*
     A          R PT3                       SPACEB(001)
     A                                     1
     A                                      '部門人數合計:'
     A            PT31           5  0     +1
     A                                      EDTCDE(1)
     A                                    +1
     A                                      '人'
     A*
     A          R PT4                       SPACEB(001)
     A                                     1'部門人數總計:'
     A            PT41           5  0     17
     A                                      EDTCDE(1)
     A                                    +1
     A                                      '人'
     A                                      SPACEA(001)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ='
     A                                      SPACEA(001)
     A                                     1'PYR106P--->報表結束'
     A          R PE9                       SPACEB(001)
     A                                    16'無符合列印範圍之資料'
