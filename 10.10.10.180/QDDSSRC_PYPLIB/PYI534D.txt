     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI534D                                    *
     A*    PROGRAM NAME :團保費率全頁查詢　　                      *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 89/08/21                                   *
     A*    UPDATE DATE  : 91/06/21 A1336 KLAUS                       *
     A*    SYSTEM       :人事薪資系統　　                        *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            DX901     R        O  8  6REFFLD(X901 PYX9PF)
     A**          DX902     R        O  8 12REFFLD(X902 PYX9PF)
     A            DX903     R        O  8 20REFFLD(X903 PYX9PF)
     A                                      EDTCDE(3)
     A            DX904     R        O  8 31REFFLD(X904 PYX9PF)
     A                                      EDTCDE(3)
     A            DX905          9Y 0O  8 40EDTCDE(1)
     A            DX910          9Y 0O  8 52EDTCDE(1)
     A            DX906     R        O  8 67REFFLD(X906 PYX9PF)
     A                                      EDTCDE(3)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0012)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI534D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DATE           7Y 0O  1 72EDTWRD('   /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'團保費率全頁查詢'
     A                                      DSPATR(RI)
     A                                  5  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'保險'
     A                                      DSPATR(UL)
     A                                  6 18'薪資倍數'
     A                                      DSPATR(UL)
     A                                  6 29'費率(%) '
     A                                      DSPATR(UL)
     A                                  6 40' 投保金額'
     A                                      DSPATR(UL)
     A                                    +1' 應繳保費'
     A                                      DSPATR(UL)
     A                                  6 64'折扣費率(%)'
     A                                      DSPATR(UL)
     A            KX901     R        B  7  6REFFLD(X901)
     A                                      DSPATR(HI)
     A                                      DSPATR(CS)
     A**          KX902     R        B  7 12REFFLD(X902)
     A**                                    DSPATR(HI)
     A**                                    DSPATR(CS)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  1'執行  F3 =回主畫面   F12=回+
     A                                      前畫面   PA1=下一頁   PA2=上+
     A                                      一頁'
     A                                 24 67'【泛太資訊】'
