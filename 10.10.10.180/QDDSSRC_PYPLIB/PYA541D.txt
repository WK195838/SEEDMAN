     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA541D                                    *
     A*    PROGRAM NAME :各縣市健保額補助維護　　                  *
     A*    AUTHOR       : A1277 KARL                                 *
     A*    CREATE DATE  : 91/07/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統　                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*===============================================================
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4Y 0H
     A            DE701     R        B  6 15REFFLD(E701 PYE7PF)
     A*                                     EDTCDE(2)
     A  60                                  DSPATR(PC RI)
     A            DE702     R        B  6 30REFFLD(E702 PYE7PF)
     A*                                     EDTCDE(2)
     A  61                                  DSPATR(PC RI)
     A            DE703     R        B  6 58REFFLD(E703 PYE7PF)
     A                                      EDTCDE(2)
     A  62                                  DSPATR(PC RI)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0015)
     A                                      CSRLOC(#LIN     #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA541D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 62'日期:'
     A            DDATE          6Y 0O  1 71EDTWRD('0  /  /  ')
     A*
     A            $USERN        10A  O  2  2
     A                                  2 30'各縣市健保額補助維護'
     A                                      DSPATR(RI)
     A*                                 2 72TIME
     A*                                     EDTWRD('  :  :  ')
     A*
     A                                  4  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A                                  5 11'　郵遞區號  '
     A                                      DSPATR(UL)
     A*
     A                                  5 28' 地　　　　　　　　區 '
     A                                      DSPATR(UL)
     A                                      COLOR(PNK)
     A*
     A                                  5 55' 補助金額　'
     A                                      DSPATR(UL)
     A                                      COLOR(PNK)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PA01 =下一頁　'
     A                                 23 37'PA02 =上一頁　'
     A                                 24 67'【泛太資訊】'
