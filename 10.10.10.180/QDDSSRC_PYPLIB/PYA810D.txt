     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*                                                              *
     A*    PROGRAM-ID   : PYA810D                                    *
     A*    PROGRAM NAME :薪資遞延發放資料維護　　                  *
     A*    AUTHOR       : A1490 TERESA                               *
     A*    CREATE DATE  : 96/05/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYBSPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA810.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'薪資遞延發放資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DBS01     R        B  4 13REFFLD(BS01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DCM02     R   -6   O    +1REFFLD(CM02 SYCMPF)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DBS02     R        B  5 13REFFLD(BS02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DEM03     R   -4   O    +1REFFLD(EM03 PYEMPF)
     A                                  6  1'起始年月:'
     A            DBS06     R   -2   B  6 13REFFLD(BS06)
     A                                      EDTWRD('0  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                  7 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*-------------------------------------------------------------***
     A          R DSPD2
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA810.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'薪資遞延發放資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DBS01     R        O  4 13REFFLD(BS01)
     A            DCM02     R   -6   O    +1REFFLD(CM02 SYCMPF)
     A                                  5  1'員工編號:'
     A            DBS02     R        O  5 13REFFLD(BS02)
     A            DEM03     R   -4   O    +1REFFLD(EM03 PYEMPF)
     A                                  6  1'起始年月:'
     A            DBS06     R   -2   O  6 13REFFLD(BS06)
     A                                      EDTWRD('0  /  ')
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0O  7 13
     A                                  7 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  1'截止年月:'
     A            DBS07     R   -2   B  9 13REFFLD(BS07)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A                                      EDTWRD('0  /  ')
     A                                 10  1'補發年月:'
     A            DBS03     R   -2   B 10 13REFFLD(BS03)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A                                      EDTWRD('0  /  ')
     A*                                10  1'補發日  :'
     A*           DBS04     R        B 10 13REFFLD(BS04)
     A* 64                                  DSPATR(PC)
     A* 64                                  DSPATR(RI)
     A* 34                                  DSPATR(UL)
     A*N34                                  DSPATR(PR)
     A*                                     EDTCDE(3)
     A                                 11  1'期  別:'
     A            DBS05     R        B 11 13REFFLD(BS05)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A                                    +1'(1.下期2.上期)'
     A*                                11  1'起始年月:'
     A*           DBS06     R   -2   B 11 13REFFLD(BS06)
     A* 66                                  DSPATR(PC)
     A* 66                                  DSPATR(RI)
     A* 33                                  DSPATR(UL)
     A*N33                                  DSPATR(PR)
     A*                                     EDTWRD('0  /  ')
     A                                 12  1'備  註:'
     A            DBS08     R        B 12 13REFFLD(BS08)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 23 38'PF18 =查看歷史資料'
     A                                 24 67'【泛太資訊】'
