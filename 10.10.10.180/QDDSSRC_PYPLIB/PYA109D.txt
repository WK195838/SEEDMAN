     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA109D                                    *
     A*    PROGRAM NAME :人事調動資料建立  *YH USE ONLY (說明)   *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 2001/06/07                                 *
     A*    UPDATE DATE  : 2001/08/09 A1274 EVA                       *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*------------------------------------------------------------***
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYA109.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'人事調動資料建立'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DTR01     R        B  4 13REFFLD(TR01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DTR02     R        B  5 13REFFLD(TR02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'生效日期:'
     A            DTR04     R   -2   B  6 13REFFLD(TR04)
     A                                      EDTWRD('0  /  /  ')
     A                                      CHECK(RB)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'調動類別:'
     A                                      DSPATR(HI)
     A            DTR03          1A  B  7 13
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A            TR03N         14O  O  7 16
     A                                  8  1'作業選擇:'
     A            DOPT           1A  B  8 13VALUES('1' '4' '5')
     A                                  8 16'(1.新增4.刪除5.查詢)-
     A                                      '
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                      CHGINPDFT(UL)
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A*                                     CF06(06)
     A                                      CA12(12)
     A                                      CF13(13)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA109.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'人事調動資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A            TR03N         14O  O  3 34DSPATR(HI)
     A                                  4  1'公　　司　:'
     A            DTR01     R        O  4 15REFFLD(TR01)
     A            DTR01N    R        O  4 24REFFLD(TR01N)
     A                                  5  1'員　　工　:'
     A            DTR02     R        O  5 15REFFLD(TR02)
     A            EM03      R        O  5 24
     A                                  5 40'生效日期　:'
     A            DTR04     R   -2   O  5 54REFFLD(TR04)
     A                                      EDTWRD('0  /  /  ')
     A*
     A                                  7  1'公司（自）:'
     A            DTR011    R        O  7 15REFFLD(TR011)
     A  60                                  DSPATR(RI)
     A            CM021     R        O  7 24
     A                                      REFFLD(CM02)
     A                                  8  1'　　（至）:'
     A            DTR012    R        B  8 15REFFLD(TR012)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            CM022     R        O  8 24
     A                                      REFFLD(CM02)
     A*
     A                                  7 40'部門（自）:'
     A            DTR05          7A  O  7 54
     A  62                                  DSPATR(RI)
     A            DDP021    R        O    +1REFFLD(DP02)
     A                                  8 40'　　（至）:'
     A                                      DSPATR(HI)
     A            DTR06          7A  B  8 54
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A            DDP022    R        O    +1REFFLD(DP02)
     A*
     A                                 10  1'職稱（自）:'
     A            DTR07     R        O 10 15REFFLD(TR07)
     A  64                                  DSPATR(RI)
     A            DJT021    R        O 10 24REFFLD(JT02)
     A                                 11  1'　　（至）:'
     A                                      DSPATR(HI)
     A            DTR08     R        B 11 15REFFLD(TR08)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A            DJT022    R        O 11 24REFFLD(JT02)
     A*
     A                                 10 40'職等（自）:'
     A            DTR09     R        O 10 54REFFLD(TR09)
     A  66                                  DSPATR(RI)
     A                                 11 40'　　（至）:'
     A                                      DSPATR(HI)
     A            DTR10     R        B 11 54REFFLD(TR10)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A*
     A                                 13  1'副職稱（自）:'
     A            DTR19     R        O 13 17REFFLD(TR19)
     A  68                                  DSPATR(RI)
     A            DJT023    R        O 13 24REFFLD(JT02)
     A                                 14  1'  　　（至）:'
     A                                      DSPATR(HI)
     A            DTR20     R        B 14 17REFFLD(TR20)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A            DJT024    R        O 14 24REFFLD(JT02)
     A*
     A                                 13 40'職級（自）:'
     A            DTR15     R        O 13 54REFFLD(TR15)
     A                                      EDTCDE(L)
     A  70                                  DSPATR(RI)
     A                                 14 40'　　（至）:'
     A                                      DSPATR(HI)
     A            DTR16     R        B 14 54REFFLD(TR16)
     A                                      EDTCDE(L)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A*
     A                                 16  1'工作類別（自）:'
     A            DTR221    R        O 16 19REFFLD(TR221)
     A  72                                  DSPATR(RI)
     A            TR221N        10   O 16 21
     A                                 17  1'　　　　（至）:'
     A                                      DSPATR(HI)
     A            DTR222    R        B 17 19REFFLD(TR222)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A            TR222N        10   O 17 21
     A*
     A                                 16 40'生產獎金註記（自）:'
     A            DTR231    R        O 16 62REFFLD(TR231)
     A  74                                  DSPATR(RI)
     A            TR231N    R        O 16 64REFFLD(TR231N)
     A                                 17 40'　　　　　　（至）:'
     A                                      DSPATR(HI)
     A            DTR232    R        B 17 62REFFLD(TR232)
     A                                      VALUES('A' 'B' 'C' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A            TR232N    R        O 17 64REFFLD(TR232N)
     A*
     A*                                19  1'班　　別（自）:'
     A*           DTR13     R        O 19 19REFFLD(TR13)
     A* 76                                  DSPATR(RI)
     A*                                19 21'( 1-常日2-輪班3-日班4-中班-
     A*                                     5-夜班)'
     A*                                20  1'　　　　（至）:'
     A*           DTR14     R        B 20 19REFFLD(TR14)
     A*                                     VALUES('1' '2' '3' '4' '5')
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A* 77                                  DSPATR(PC)
     A* 77                                  DSPATR(RI)
     A*
     A                                 19  1'成本中心（自）:'
     A            DTR21S         5A  O 19 19
     A  78                                  DSPATR(RI)
     A            CC021     R        O 19 25REFFLD(CC02)
     A                                 20  1'    　　（至）:'
     A                                      DSPATR(HI)
     A            DTR21E         5A  B 20 19
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A            CC022     R        O 20 25REFFLD(CC02)
     A*
     A                                 19 40'工作地點（自）:'
     A            DTR131         5A  O 19 58
     A  80                                  DSPATR(RI)
     A            TR131N    R        O 19 64REFFLD(TR131N)
     A                                 20 40'  　　（至）:'
     A                                      DSPATR(HI)
     A            DTR132         5A  B 20 58
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
     A            TR132N    R        O 20 64REFFLD(TR132N)
     A*------------------------------------------------------------
     A*
     A**                               14  3'薪制（自）:'
     A**          DTR11     R        O 14 17REFFLD(TR11)
     A**66                                  DSPATR(RI)
     A**                               14 20'( 1-上期半薪 2-上期全薪 3-上+
     A**                                    期不發薪)'
     A**                               15  3'　　（至）:'
     A**          DTR12     R        B 15 17REFFLD(TR12)
     A**                                    VALUES('1' '2' '3')
     A**31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A**67                                  DSPATR(PC)
     A**67                                  DSPATR(RI)
     A*
     A**                               20  3'工作（至）:'
     A**          DTR17         62O  B 20 17
     A**31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A**          DTR18         62O  B 21 17
     A**31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF05 =參考資料'
     A                                 23 56'PF12 =回前畫面'
     A*                                24  2'PF06 =員工調薪資料建立'
     A                                 24 38'PF13 =資料確認'
     A                                 24 67'【泛太資訊】'
