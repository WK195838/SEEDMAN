     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA505D                                    *
     A*    PROGRAM NAME :核薪標準等級設定　                        *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 86/01/03                                   *
     A*    UPDATE DATE  : 91/07/25 A1336                             *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1   B  7  4DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            PS01      R        O  7  9REFFLD(PS01)
     A            PS04      R        O  7 16REFFLD(PS04)
     A                                      EDTCDE(L)
     A            TOTAL          6  0O  7 23
     A                                      EDTCDE(2)
     A*
     A          R SFLCR                     SFLCTL(SFLSR)
     A  *DS3                                SFLLIN(0004)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0030)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI505.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'核薪標準等級設定'
     A                                      DSPATR(RI)
     A                                  5  4'B'
     A                                      DSPATR(UL)
     A                                  5  7'職等'
     A                                      DSPATR(UL)
     A                                  5 14'職級'
     A                                      DSPATR(UL)
     A                                  5 21'    金額'
     A                                      DSPATR(UL)
     A                                  5 34'B'
     A                                      DSPATR(UL)
     A                                  5 37'職等'
     A                                      DSPATR(UL)
     A                                  5 44'職級'
     A                                      DSPATR(UL)
     A                                  5 51'    金額'
     A                                      DSPATR(UL)
     A            DPS01     R        B  6  9REFFLD(PS01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DPS04     R        B  6 16REFFLD(PS04)
     A                                      EDTCDE(L)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 23 36'PA1=下一頁'
     A                                 23 50'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
