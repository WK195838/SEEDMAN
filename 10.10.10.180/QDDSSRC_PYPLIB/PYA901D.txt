     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA901D                                    *
     A*    PROGRAM NAME :工作區設定　                              *
     A*    AUTHOR       : A1268 VANESSA                              *
     A*    CREATE DATE  : 98/09/15                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A*                                     OVERLAY
     A                                      CA03(03)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA901.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 35'工作區設定'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號  :'
     A            DXA01     R        B  4 15REFFLD(XA01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DXA01N        14A  O  4 18
     A                                  5  1'工作區代碼:'
     A            DXA02     R        B  5 15REFFLD(XA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'作業選擇  :'
     A            DOPT           1Y 0B  6 15VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 17'( 1.新增2.修改4.刪除5.查詢-
     A                                      )'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 29'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A          R DSPD2
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA901.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 35'工作區設定'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號  :'
     A            DXA01     R        O  4 15REFFLD(XA01)
     A            DXA01N        14      4 18
     A                                  5  1'工作區代碼:'
     A            DXA02     R        O  5 15REFFLD(XA02)
     A                                  6  1'作業選擇  :'
     A            DOPT           1Y 0O  6 15EDTCDE(4)
     A                                  6 17'( 1.新增2.修改4.刪除5.查詢-
     A                                      )'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  1'工作區說明:'
     A            DXA03     R        B  8 15REFFLD(XA03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 29'F5 =參考資料'
     A                                 23 48'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
