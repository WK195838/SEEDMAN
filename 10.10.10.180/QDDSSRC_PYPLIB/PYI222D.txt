     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI222D                                    *
     A*    PROGRAM NAME :員工住宿資料全頁查詢                      *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 89/08/22                                   *
     A*    UPDATE DATE  : 91/11/04 A1346 WEST                        *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*--------------------------------------------------------------**
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            LI02      R        O  8  5
     A            DEM03     R        O  8 15REFFLD(EM03)
     A            LI03      R        O  8 31
     A            LI03N          6   O  8 33
     A            LI04D     R   -1   O  8 41REFFLD(LI04)
     A                                      EDTWRD('   /  /  ')
     A            LI09      R        O  8 56EDTCDE(1)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI222D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工住宿資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DLI01     R        O  4 13REFFLD(TX01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DLI01N        14O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'員工編號'
     A                                      DSPATR(UL)
     A                                  6 15'員工姓名    '
     A                                      DSPATR(UL)
     A                                  6 30'申請類別'
     A                                      DSPATR(UL)
     A                                  6 41'生效日期'
     A                                      DSPATR(UL)
     A                                  6 54'月租費金額'
     A                                      DSPATR(UL)
     A            DLI02     R        B  7  5REFFLD(TR02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*911211     DLI03     R        B  7 31REFFLD(LI03)
     A*911211                               DSPATR(CS)
     A*911211                               DSPATR(HI)
     A            DLI04     R   -1   B  7 41DSPATR(CS HI)
     A                                      REFFLD(LI04)
     A                                      EDTWRD('   /  /  ')
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 36'PA01=下一頁'
     A                                 23 50'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
