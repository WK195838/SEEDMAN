     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYH104P                                    *
     A*    PROGRAM NAME :員工退休金申報明細表                    *
     A*    AUTHOR       : MAX CHI                                    *
     A*    CREATE DATE  : 94/03/17                                   *
     A*    UPDATE DATE  : 94/04/18  JIMMY WANG                       *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A          R PH1
     A                                      SKIPB(002)
     A            CM03          34A        1
     A                                      SPACEA(001)
     A                                     1
     A                                      '員工退休金申報明細表'
     A                                      SPACEA(002)
     A                                     1
     A                                      '提撥類別:'
     A            MSG1          10        13
     A                                    75
     A                                      '日期:'
     A            DDATE          6  0     82
     A                                      EDTWRD('0  /  /  ')
     A                                    91
     A                                      '頁次:'
     A                                    98
     A                                      PAGNBR
     A                                      SPACEA(001)
     A                                     1
     A                                      '調整年月:'
     A            DYMX           4  0O    14
     A                                      EDTWRD('0  /  ')
     A                                    75
     A                                      '時間:'
     A                                    82
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    91
     A                                      '<PYH104P>'
     A                                      SPACEA(002)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEA(001)
     A                                     1
     A                                      '提撥者'
     A                                    81
     A                                      '~'
     A                                    +1
     A                                      '金額'
     A                                    +1
     A                                      '~'
     A                                      SPACEA(001)
     A                                     1
     A                                      '部　　門'
     A                                    23
     A                                      '員　　工'
     A                                    49
     A                                      '身份証號'
     A                                    +4
     A                                      '出生日期'
     A                                    +5
     A                                      '調整前'
     A                                    +3
     A                                      '調整後'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEA(001)
     A          R PD1
     A                                      SPACEA(001)
     A            EM04           7         2
     A            DP02          16       + 1
     A            EM02           8A       24
     A            EM03          14O      + 1
     A            EM31          10A       49
     A            EM33X          6  0     63
     A                                      EDTWRD('0  /  /  ')
     A            H87            6  0     78EDTCDE(1)
     A            H57            6  0     89EDTCDE(1)
     A**********************************************
     A          R PT1
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------------------------------'
     A                                      SPACEA(001)
     A                                     1
     A                                      '合計'
     A            @CP            9  0     10
     A                                      EDTCDE(1)
     A                                    25
     A                                      '名'
     A                                    35
     A                                      '調整前總金額:'
     A            @H4            9  0     52
     A                                      EDTCDE(1)
     A                                    65
     A                                      '元'
     A                                    75
     A                                      '調整後總金額:'
     A            @H5            9  0     93
     A                                      EDTCDE(1)
     A                                   106
     A                                      '元'
     A*%%***********************************************************************
     A          R PE9
     A                                      SPACEA(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
     A*
