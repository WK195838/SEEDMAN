     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA518D                                    *
     A*    PROGRAM NAME :所得扣繳媒體申報資料設定                  *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 85/12/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03 'RETURN')
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA518.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 28'所得扣繳媒體申報資料設定'
     A                                      DSPATR(RI)
     A                                  4  1'媒體單位:'
     A            DFA01     R        B  4 13REFFLD(FA01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  1'縣市代號:'
     A            DFA02     R        B  5 13REFFLD(FA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'總分處  :'
     A            DFA03     R        B  6 13REFFLD(FA03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 4 5)
     A                                  7 16'(1.新增4.刪除5.查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03 'RETURN')
     A                                      CA12(12 'PREVIOUSE')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA518.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 28'所得扣繳媒體申報資料設定'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  5 30'媒體單位:'
     A            DFA01     R        O  5 42REFFLD(FA01)
     A                                  7 30'縣市代號:'
     A            DFA02     R        O  7 42REFFLD(FA02)
     A                                  9 30'總分處　:'
     A            DFA03     R        O  9 42REFFLD(FA03)
     A                                 11 30'流水號　:'
     A            DFA04     R        O 11 42REFFLD(FA04)
     A                                      EDTCDE(3)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
