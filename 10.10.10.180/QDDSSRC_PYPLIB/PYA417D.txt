     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR402D                                    *
     A*    PROGRAM NAME :公司獎金資料核定作業                      *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  :                                            *
     A*    UPDATE DATE  : 2001/05/16 KLAUS                           *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*------------------------------------------------------------***
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF13(13)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYA417D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'公司獎金取消核定作業'
     A                                      DSPATR(RI)
     A*
     A                                  5 25'公司編號:'
     A            DPB01     R        B  5 39REFFLD(PB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 25'年　　度:'
     A            DPB03     R   -1   B  7 39REFFLD(PB03)
     A                                      EDTWRD('   ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  9 25'獎金類別:'
     A            DPB04     R        B  9 39REFFLD(PB04)
     A                                      VALUES('01' '02' '03' '04')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 42'(01.年終獎金02.夏季獎金'
     A                                 10 42' 03.紅利    04.其它)'
     A*
     A                                 13 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A  97                             15 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  96                             15 30'取消核定作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A  98                             15 30'取消核定作業處理完成！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11' '
     A                                      DSPATR(RI)
     A                                 16 66' '
     A                                      DSPATR(RI)
     A                                 17 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
