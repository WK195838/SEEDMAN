     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : TRW003D                                    *
     A*    PROGRAM NAME :代碼欄位視窗查詢                          *
     A*    AUTHOR       : A1266 HANK                                 *
     A*    CREATE DATE  : 2000.04.21                                 *
     A*    UPDATE DATE  : 2001.06.07 A1274 EVA                       *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYTDPF)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R DSPD0                     ASSUME
     A                                  1  2' '
     A*----------------------------------------------------------------
     A          R SFLSR3                    SFL
     A            RRN3           3Y 0H
     A            SEL            1A  B  6  4DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            TD11      R        O  6  8REFFLD(TD11 PYTDPF)
     A            TD05      R        O  6 11REFFLD(TD05 PYTDPF)
     A            TD13      R        O  6 22REFFLD(TD13 PYTDPF)
     A            TD01      R        O  6 37REFFLD(TD01 PYTDPF)
     A*----------------------------------------------------------------
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0006)
     A                                      WINDOW(3 11 14 55)
     A                                      CA12(12)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      USRRSTDSP
     A            DRRN3          3S 0H      SFLRCDNBR
     A                                  1 20'所得稅身分證查詢'
     A                                      DSPATR(HI)
     A                                  1  2' <TRW003>  '
     A                                  4  4'S'
     A                                  4  6'公司'
     A                                      DSPATR(UL)
     A                                  4 13'身分證號/姓名'
     A                                      DSPATR(UL)
     A                                  4 36'年度'
     A                                      DSPATR(UL)
     A            KTD11     R        B  5  8REFFLD(TD11 PYTDPF)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            KTD05     R        B  5 11REFFLD(TD05 PYTDPF)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD1                     WINDOW(SFLCR3)
     A                                 13  2'執行'
     A                                    +2'PF12=回前畫面'
