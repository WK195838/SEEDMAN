     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA702D                                    *
     A*    PROGRAM NAME :所得稅扣繳申報處理　                      *
     A*    AUTHOR       : A1070 CHOIE                                *
     A*    CREATE DATE  : 1990/06/01                                 *
     A*    UPDATE DATE  : 2001/08/07 A1274 EVA                       *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-----------------------------------------------------------***
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF13(13)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA702D>'
     A                                  1 26'＜＜所得稅扣繳申報處理＞＞'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A                                  4 29'公司整合:'
     A            DPY01          1A  B  4 41VALUES('S' 'R')
     A                                  5 29'媒體單位:'
     A                                      DSPATR(HI)
     A            DTD02     R        B  5 41REFFLD(TD02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  6 29'機徵機關:'
     A            DTD19     R        B  6 41REFFLD(TD19)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                  7 29'員工編號:'
     A            DTD12F    R        B  7 41REFFLD(EM02 PYEMPF)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  7 50'-'
     A            DTD12T    R        B  7 52REFFLD(EM02 PYEMPF)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  8 29'處理年度:'
     A            DTD01     R   -2   B  8 41REFFLD(TD01)
     A                                      EDTWRD('0  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  9 29'所得來源:'
     A            DSEL           1A  B  9 41VALUES('1' '2')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                  9 45'( 1-薪資系統 2-各類所得)'
     A                                 10 29'重新處理:'
     A            DOPT           1A  B 10 41VALUES('Y' 'N')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 45'( Y / N )'
     A                                 11  5'-----------------------------------
     A                                      ----------------------------------'
     A                                      COLOR(BLU)
     A                                 12  5'公司整合碼:'
     A                                      DSPATR(HI)
     A                                 12 19'"S" 為"SC" + "S1"'
     A                                      COLOR(BLU)
     A                                 13 19'"R" 為"R1" + "R2"'
     A                                      COLOR(BLU)
     A                                 15  5'-----------------------------------
     A                                      ----------------------------------'
     A                                      COLOR(BLU)
     A                                 16 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 17 11' '
     A                                      DSPATR(RI)
     A                                 17 66' '
     A                                      DSPATR(RI)
     A                                 18 11' '
     A                                      DSPATR(RI)
     A  97                             18 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  96                             18 30'批次作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 18 66' '
     A                                      DSPATR(RI)
     A                                 19 11' '
     A                                      DSPATR(RI)
     A                                 19 66' '
     A                                      DSPATR(RI)
     A                                 20 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 21'PF04 =欄位查詢'
     A                                 23 40'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
