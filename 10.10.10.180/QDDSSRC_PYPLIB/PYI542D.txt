     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI542D                                    *
     A*    PROGRAM NAME :員工理賠資料全頁查詢                      *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 91/09/23                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            R202      R        O  8  6
     A            EM03      R        O  8 16
     A            R203      R        O  8 32
     A            R203N         10O  O  8 34
     A            R204D          7  0O  8 48EDTWRD('   /  /  ')
     A            R205      R        O  8 61EDTCDE(J)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI542D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'理賠資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DR201     R        O  4 13REFFLD(R201)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            R201N         14   O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5'員工編號'
     A                                      DSPATR(UL)
     A                                  6 16'  員工姓名  '
     A                                      DSPATR(UL)
     A                                  6 32'理賠險種說明'
     A                                      DSPATR(UL)
     A                                  6 48'申請日期'
     A                                      DSPATR(UL)
     A                                  6 62'申請金額'
     A                                      DSPATR(UL)
     A            DR202     R        B  7  6DSPATR(CS HI)
     A                                      REFFLD(R202)
     A            DR203     R        B  7 32DSPATR(CS HI)
     A                                      REFFLD(R203)
     A            DR204     R   -1   B  7 48DSPATR(CS HI)
     A                                      EDTWRD('   /  /  ')
     A                                      REFFLD(R204)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA01=下一頁'
     A                                 23 56'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
