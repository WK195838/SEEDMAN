     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA611D                                    *
     A*    PROGRAM NAME :福委會獎金轉檔作業　　　　                *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/02/21                                   *
     A*    UPDATE DATE  : 90/04/16 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF13(13)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYA611D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'福委會獎金轉檔作業'
     A                                      DSPATR(RI)
     A*
     A                                  4 26'福委會編號:'
     A            DER01     R        B  4 40REFFLD(ER01 PYERPF)
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DER01N    R        O  4 46REFFLD(CM02 SYCMPF)
     A*
     A                                  6 28'獎金年度:'
     A            DPB03          3  0B  6 40EDTWRD('0   ')
     A  61                                  DSPATR(RI)
     A*
     A                                  8 28'發放年月:'
     A            DPB05          5  0B  8 40
     A                                      EDTWRD('0   /  ')
     A  62                                  DSPATR(RI)
     A*
     A                                 10 28'獎金類別:'
     A                                      DSPATR(HI)
     A            DPB04     R        B 10 40REFFLD(PB04 PYPBPF)
     A  63                                  DSPATR(RI)
     A            DPB04N    R        O 10 46REFFLD(SS02 PYSSPF)
     A*
     A                                 12 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 13 11' '
     A                                      DSPATR(RI)
     A                                 13 66' '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A  97                             14 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  96                             14 28'轉檔作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF13 =轉　　檔'
     A                                 24 67'【泛太資訊】'
