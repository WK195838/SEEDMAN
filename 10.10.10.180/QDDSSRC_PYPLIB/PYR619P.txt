     A****************************************************************
     A*    PRTF NAME    : PYR619P                                    *
     A*    PROGRAM NAME :全民健保投保資料明細表                    *
     A*    AUTHOR       : A1413 CLOUD                                *
     A*    CREATE DATE  : 2006/05/19                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      REF(PYR619WF)
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1REFFLD(CM03 PYRF)
     A                                      SPACEA(001)
     A                                    40
     A                                      '全民健保投保資料明細表'
     A                                      SPACEA(001)
     A                                   128
     A                                      '日期:'
     A            DDATE          6  0    136
     A                                      EDTWRD('0  /  /  ')
     A                                   145
     A                                      '頁次:'
     A                                   153
     A                                      PAGNBR
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門代號:'
     A            DEM04S         7        13
     A                                    21
     A                                      '-'
     A            DEM04E         7        23
     A                                    31
     A                                      '員工編號:'
     A            DEM02S    R             +1
     A                                      REFFLD(WF03)
     A                                    +1
     A                                      '-'
     A            DEM02E    R             +1
     A                                      REFFLD(WF03)
     A                                   128
     A                                      '時間:'
     A                                   136
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   146
     A                                      '<PYR619P>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======================'
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門名稱'
     A                                    26
     A                                      '員工姓名'
     A                                    53
     A                                      '身分證號'
     A                                    65
     A                                      '出生日期'
     A                                    76
     A                                      '異動類別'
     A                                    87
     A                                      '生效日期'
     A                                    98
     A                                      '補助'
     A                                   106
     A                                      '保額'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======================'
     A*****************************************************************
     A          R PH2
     A                                      SKIPB(002)
     A            CM03      R              1REFFLD(CM03 PYRF)
     A                                      SPACEA(001)
     A                                    40
     A                                      '全民健保投保資料明細表'
     A                                      SPACEA(001)
     A                                   128
     A                                      '日期:'
     A            DDATE          6  0    136
     A                                      EDTWRD('0  /  /  ')
     A                                   145
     A                                      '頁次:'
     A                                   153
     A                                      PAGNBR
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門代號:'
     A            DEM04S         7        13
     A                                    21
     A                                      '-'
     A            DEM04E         7        23
     A                                    31
     A                                      '員工編號:'
     A            DEM02S    R             +1
     A                                      REFFLD(WF03)
     A                                    +1
     A                                      '-'
     A            DEM02E    R             +1
     A                                      REFFLD(WF03)
     A                                   128
     A                                      '時間:'
     A                                   136
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   146
     A                                      '<PYR619P>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======================'
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門名稱'
     A                                    26
     A                                      '員工姓名'
     A                                    53
     A                                      '身分證號'
     A                                    65
     A                                      '出生日期'
     A                                    76
     A                                      '異動類別'
     A                                    87
     A                                      '生效日期'
     A                                    98
     A                                      '補助'
     A                                   106
     A                                      '保額'
     A                                      SPACEA(001)
     A                                    45
     A                                      '眷屬姓名'
     A                                    58
     A                                      '身分證號'
     A                                    70
     A                                      '出生日期'
     A                                    82
     A                                      '異動類別'
     A                                    93
     A                                      '生效日期'
     A                                   104
     A                                      '補助碼'
     A                                   113
     A                                      '眷屬稱謂'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======================'
     A*****************************************************************
     A          R PD1
     A                                      SPACEB(001)
     A*部門編號
     A  31        WF011     R              1
     A  31        WF012     R              2
     A  31        WF013     R              3
     A  31        WF014     R              4
     A*部門簡稱
     A  31        WF02      R              9
     A*員工編號
     A  32        WF03      R             26
     A*員工姓名
     A  32        WF04      R             37
     A*身份証號
     A  32        WF05      R             53
     A*出生日期(西元)
     A  32        WF06C     R   -2        65REFFLD(WF06 PYR619WF)
     A                                      EDTWRD('0  /  /  ')
     A*異動類別名稱
     A            WF07N          6        78
     A*生效日期(西元)
     A            WF08C     R   -2        88REFFLD(WF06 PYR619WF)
     A                                      EDTWRD('0  /  /  ')
     A*補助碼
     A            WF09      R            100
     A*投保金額
     A            WF10      R            105
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A          R PD2
     A                                      SPACEB(001)
     A*眷屬姓名
     A            WF11      R             42
     A                                      REFFLD(WF11)
     A*身份證號
     A            WF12      R             58
     A                                      REFFLD(WF12)
     A*出生日期
     A            WF13C     R   -2        70
     A                                      EDTWRD('0  /  /  ')
     A                                      REFFLD(WF13)
     A*異動類別
     A            WF14N          6A       84
     A*眷屬生效日期
     A            WF15C     R   -2        93
     A                                      EDTWRD('0  /  /  ')
     A                                      REFFLD(WF15)
     A*補助碼
     A            WF16      R            107
     A                                      REFFLD(WF16)
     A*眷屬稱謂
     A            WF17N          6A      115
     A*%%***********************************************************************
     A*
     A          R PE2
     A                                      SPACEB(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======================'
     A                                      SPACEA(001)
     A                                     1
     A                                      'PYR619P--->報表結束'
     A*%%***********************************************************************
     A*
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
