     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : TRW001D                                    *
     A*    PROGRAM NAME :代碼欄位視窗查詢                          *
     A*    AUTHOR       : A1266 HANK                                 *
     A*    CREATE DATE  : 2000.04.21                                 *
     A*    UPDATE DATE  : 2001.06.07 A1274 EVA                       *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYPTPF)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R DSPD0                     ASSUME
     A                                  1  2' '
     A*----------------------------------------------------------------
     A          R SFLSR3                    SFL
     A            RRN3           3Y 0H
     A            SEL            1A  B  6  4DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            PT02      R        O  6  7REFFLD(PT02 PYPTPF)
     A            EN03      R        O  6 18REFFLD(EN03 PYENPF)
     A            DPT03     R   -2   O  6 32REFFLD(PT03 PYPTPF)
     A                                      EDTWRD('0  /  ')
     A            DPT04     R   -2   O  6 43REFFLD(PT04 PYPTPF)
     A                                      EDTWRD('0  /  ')
     A*----------------------------------------------------------------
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0006)
     A                                      WINDOW(3 11 14 55)
     A                                      CA12(12)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      USRRSTDSP
     A            DRRN3          3S 0H      SFLRCDNBR
     A                                  1 20'工讀生資料查詢'
     A                                      DSPATR(HI)
     A                                  1  2' <TRW002>  '
     A                                  4  4'S'
     A                                  4  6'工讀生身分證號/姓名'
     A                                      DSPATR(UL)
     A                                  4 30'發薪年月'
     A                                      DSPATR(UL)
     A                                  4 41'給付年月'
     A                                      DSPATR(UL)
     A            KPT02     R        B  5  7REFFLD(PT02 PYPTPF)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            KPT03     R        H      REFFLD(PT03 PYPTPF)
     A            KPT04     R        H      REFFLD(PT04 PYPTPF)
     A*                                     EDTWRD('0  /  ')
     A*                                     DSPATR(CS)
     A*                                     DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD1                     WINDOW(SFLCR3)
     A                                 13  2'執行'
     A                                    +2'PF12=回前畫面'
