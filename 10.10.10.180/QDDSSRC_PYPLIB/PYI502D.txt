     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI502D                                    *
     A*    PROGRAM NAME :假別基本資料查詢                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 85/12/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  6  3DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            LC01      R        O  6  7
     A            LC02      R        O  6 14
     A            LC03N          6O  O  6 23
     A*           LC04      R        O  6 31EDTCDE(4)
     A            LC06      R        O  6 31EDTCDE(4)
     A                                  6 35'天內'
     A            LC05      R        O  6 42EDTCDE(4)
     A            LC10      R        O  6 48EDTCDE(4)
     A                                  6 52'天內'
     A            LC11      R        O  6 59EDTCDE(4)
     A            LC08      R        O  6 65EDTCDE(4)
     A                                  6 69'天外'
     A            LC09      R        O  6 76EDTCDE(4)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19970508  095653  ESPGMR      REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0160)
     A                                      SFLPAG(0016)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI502D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'假別基本資料查詢'
     A                                      DSPATR(RI)
     A                                  4  3'S'
     A                                      DSPATR(UL)
     A                                  4  5'假別'
     A                                      DSPATR(UL)
     A                                  4 12'假別簡稱'
     A                                      DSPATR(UL)
     A                                  4 23'薪別'
     A                                      DSPATR(UL)
     A                                  4 32'    計薪    '
     A                                      DSPATR(UL)
     A                                  4 49'    計薪    '
     A                                      DSPATR(UL)
     A                                  4 66'    扣薪    '
     A                                      DSPATR(UL)
     A            DLC01     R        B  5  7REFFLD(LC01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 23 36'PA1=下一頁'
     A                                 23 50'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
