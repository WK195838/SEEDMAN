     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA220D                                    *
     A*    PROGRAM NAME :差勤／加班申請單　　　　　                *
     A*    AUTHOR       : A1266 HANK                                 *
     A*    CREATE DATE  : 89/09/13                                   *
     A*    UPDATE DATE  : 90/03/20 A1274 EVA                         *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*------------------------------------------------------------***
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA220.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'差勤／加班申請單'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DMV01     R        B  4 13REFFLD(MV01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DMV02     R        B  5 13REFFLD(MV02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'起始日期:'
     A            DMV03     R   -1   B  6 13REFFLD(MV03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      EDTWRD('0  /  /  ')
     A                                  7  1'申請項目:'
     A            DMV10     R        B  7 13REFFLD(MV10)
     A                                      VALUES('1' '2')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  7 15'(1.差勤2.加班)'
     A                                  7 35'差勤假別:'
     A                                      DSPATR(HI)
     A            DMV04     R        B  7 47REFFLD(MV04)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 5)
     A                                  8 15'(1.新增5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      CF14(14)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA220.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'差勤／加班申請單'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4  2'公　　司:'
     A            DMV01N    R        O  4 14REFFLD(CM02 SYCMPF)
     A                                  4 60'申請日期:'
     A            DMV11     R   -1   O  4 72REFFLD(MV11)
     A                                      EDTWRD('   /  /  ')
     A                                  5  2'部　　門:'
     A            DEM04N        14A  O  5 14
     A                                  5 60'申請時間:'
     A            DMV12     R        O  5 72REFFLD(MV12)
     A                                      EDTWRD('  :  :  ')
     A                                  6  2'員　　工:'
     A            DMV02     R        O  6 15REFFLD(MV02)
     A            EM03      R        O  6 24
     A                                  6 60'入社日期:'
     A            DEM13     R   -1   O  6 72REFFLD(EM13)
     A                                      EDTWRD('   /  /  ')
     A                                  9 26'申請項目  :'
     A            DMV10     R        O  9 40REFFLD(MV10)
     A            DMV10N        10A  O  9 42
     A                                 11 26'假　　別  :'
     A            DMV04     R        O 11 40REFFLD(MV04)
     A            LC02      R        O 11 42
     A                                 13 26'期間（起）:'
     A            DMV03     R   -1   O 13 40REFFLD(MV03)
     A                                      EDTWRD('   /  /  ')
     A            DMV06     R        B 13 50REFFLD(MV06)
     A                                      EDTWRD('0  :  ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                 14 26'　　（迄）:'
     A            DMV05     R   -1   B 14 40REFFLD(MV05)
     A                                      EDTWRD('   /  /  ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DMV07     R        B 14 50REFFLD(MV07)
     A                                      EDTWRD('0  :  ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 16 26'天　　數  :'
     A            DMV09     R        B 16 40REFFLD(MV09)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 18 26'時　　數  :'
     A            DMV08     R        B 18 40REFFLD(MV08)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  98        ERR           70A  O 22  2DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 21'PF05 =列印報表'
     A                                 23 40'PF12 =回前畫面'
     A                                 23 59'PF14 =資料確認'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD3
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      CF14(14)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA220.3>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'差勤／加班申請單'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4  2'公　　司:'
     A            DMV01N    R        O  4 14REFFLD(CM02 SYCMPF)
     A                                  4 60'申請日期:'
     A            DMV11     R   -1   O  4 72REFFLD(MV11)
     A                                      EDTWRD('   /  /  ')
     A                                  5  2'部　　門:'
     A            DEM04N        14A  O  5 14
     A                                  5 60'申請時間:'
     A            DMV12     R        O  5 72REFFLD(MV12)
     A                                      EDTWRD('  :  :  ')
     A                                  6  2'員　　工:'
     A            DMV02     R        O  6 15REFFLD(MV02)
     A            EM03      R        O  6 24
     A                                  6 60'入社日期:'
     A            DEM13     R   -1   O  6 72REFFLD(EM13)
     A                                      EDTWRD('   /  /  ')
     A*
     A                                  8  2'申請項目:'
     A            DMV10     R        O  8 15REFFLD(MV10)
     A            DMV10N        10A  O  8 17
     A                                  9  2'加班日期:'
     A            DMV03     R   -1   O  9 15REFFLD(MV03)
     A                                      EDTWRD('   /  /  ')
     A*
     A                                 12  2'<<津貼次數>>'
     A                                 13 15'平日誤餐費:'
     A            DOV42     R        B 13 30REFFLD(OV42)
     A                                      EDTCDE(4)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                    +1'次'
     A                                 13 47'假日誤餐費:'
     A            DOV40     R        B 13 62REFFLD(OV40)
     A                                      EDTCDE(4)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                    +1'次'
     A*
     A                                 16  2'<<計薪加班>>'
     A                                 17 15'平日加班:'
     A            DOV21     R        B 17 27REFFLD(OV21)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                    +1'小時(1.33)'
     A                                 18 15'超時加班:'
     A            DOV22     R        B 18 27REFFLD(OV22)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                    +1'小時(1.66)'
     A*
     A*************************        18 15'客戶加班:'
     A            DOV23     R        H      REFFLD(OV23)
     A*                                     EDTCDE(4)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A* 69                                  DSPATR(PC)
     A* 69                                  DSPATR(RI)
     A*************************           +1'小時'
     A                                 19 15'假日加班:'
     A            DOV24     R        B 19 27REFFLD(OV24)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                    +1'小時'
     A*-----------------------------------------------------------***
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  98        ERR           70A  O 22  2DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 21'PF05 =列印報表'
     A                                 23 40'PF12 =回前畫面'
     A                                 23 59'PF14 =資料確認'
     A                                 24 67'【泛太資訊】'
