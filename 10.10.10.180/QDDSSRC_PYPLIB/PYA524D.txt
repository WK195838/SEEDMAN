     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA524D                                    *
     A*    PROGRAM NAME :提撥率設定檔設定                          *
     A*    AUTHOR       : A1346 WEST                                 *
     A*    CREATE DATE  : 91/02/25                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      OVERLAY
     A                                      CA03(03 'RETURN')
     A                                      CF18(18 'FULL PAGE INQ.')
     A                                      CF04(04 'PROMPT')
     A                                      CSRLOC(#LIN       #COL)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA524.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 33'誠實險費率設定'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A                                      DSPATR(HI)
     A            DHN01          2   B  4 13
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            CM02      R        O  4 16REFFLD(CM02)
     A                                  5  1'加保月份:'
     A            DHN02          2  0B  5 13EDTCDE(4)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'保　　額:'
     A            DHN03          8  0B  6 13EDTCDE(4)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                  7 15'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                      OVERLAY
     A                                      CA03(03 'RETURN')
     A                                      CA12(12 'PREVIOUSE')
     A                                  1  2'<PYA524.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 33'誠實險費率設定'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DHN01          2   O  4 13
     A            CM02      R        O  4 16REFFLD(CM02)
     A                                  5  1'加保月份:'
     A            DHN02          2  0O  5 13
     A                                      EDTCDE(4)
     A                                  6  1'保　　額:'
     A            DHN03          8  0O  6 13
     A                                      EDTCDE(4)
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  1'保　　費:'
     A            DHN04          5  0B  9 13
     A                                      EDTCDE(4)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 19'元'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
