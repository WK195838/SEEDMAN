     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYH113P                                    *
     A*    PROGRAM NAME :應提撥而未提撥之查詢列印                *
     A*    AUTHOR       : MAX CHI                                    *
     A*    CREATE DATE  : 94/03/22                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A          R PH1
     A                                      SKIPB(002)
     A            CM03          34A        1
     A                                      SPACEA(001)
     A                                     1
     A                                      '應提撥而未提撥之查詢列印'
     A                                      SPACEA(002)
     A                                     1
     A                                      '提撥類別:'
     A            MSG1          10        13
     A                                    90
     A                                      '日期:'
     A            DDATE          6  0     97
     A                                      EDTWRD('0  /  /  ')
     A                                   106
     A                                      '頁次:'
     A                                   113
     A                                      PAGNBR
     A                                      SPACEA(001)
     A                                     1
     A                                      '月 份:'
     A            DYMX           5  0O    14
     A                                      EDTWRD('   /  ')
     A                                    90
     A                                      '時間:'
     A                                    97
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   106
     A                                      '<PYH113P>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEA(001)
     A                                     1
     A                                      '部　　門'
     A                                    23
     A                                      '員　　工'
     A                                    49
     A                                      '應提撥金額'
     A                                    +4
     A                                      '未提撥金額'
     A                                    +5
     A                                      '備註'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEA(001)
     A*
     A          R PD1
     A                                      SPACEA(001)
     A            EM04           7         2
     A            DP02          16         9
     A            H402           8A       26
     A            EM03          14A       34
     A            H4             9  2     47
     A                                      EDTCDE(1)
     A            H6             9  2     63
     A                                      EDTCDE(1)
     A                                      SPACEA(001)
     A          R PT1
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------'
     A                                      SPACEA(001)
     A                                     1
     A                                      '合計'
     A            @H402          9  0     10
     A                                      EDTCDE(1)
     A                                    25
     A                                      '名'
     A            @H4           11  2     45
     A                                      EDTCDE(1)
     A                                    59
     A                                      '元'
     A            @H6           11  2     61
     A                                      EDTCDE(1)
     A                                    75
     A                                      '元'
     A*
     A          R PE9
     A                                      SPACEA(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
