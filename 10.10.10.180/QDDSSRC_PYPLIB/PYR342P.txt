     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR342P                                    *
     A*    PROGRAM NAME :補助金發放明細表－員工                  *
     A*    AUTHOR       : A1326 SANTOS                               *
     A*    CREATE DATE  : 90/03/21                                   *
     A*    UPDATE DATE  : 90/05/18 A1274 EVA                         *
     A*    SYSTEM       :員工福委會作業子系統                      *
     A****************************************************************
     A                                      REF(PYGDJF01)
     A*------------------------------------------------------------***
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R             34
     A                                      REFFLD(CM03 PYRF)
     A*
     A                                    37
     A                                      '綜合福利金發放明細表'
     A                                      SPACEB(001)
     A*
     A                                     1
     A                                      '部門編號:'
     A                                      SPACEB(001)
     A            DEM041         7        15
     A                                    +1
     A                                      '-'
     A            DEM042         7        +1
     A*
     A                                     1
     A                                      '員工編號:'
     A                                      SPACEB(001)
     A            DEM021    R             12
     A                                      REFFLD(GD02)
     A                                    21
     A                                      '-'
     A            DEM022    R             23
     A                                      REFFLD(GD02)
     A                                    36
     A                                      '申請年份:'
     A            DYEAR          3  0     48
     A                                    52
     A                                      '年'
     A                                    67
     A                                      '日期: '
     A            $DATE          6  0     76
     A                                      EDTWRD('  /  /  ')
     A                                    85
     A                                      '頁次: '
     A                                    93
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     1
     A                                      '工作地點:'
     A                                      SPACEB(001)
     A            PEM130    R             12
     A                                      REFFLD(DP02 PYDPPF)
     A                                    36
     A                                      '申請項目:'
     A            DGD071    R             48
     A                                      REFFLD(GD07)
     A                                    51
     A                                      '-'
     A            DGD072    R             53
     A                                      REFFLD(GD07)
     A                                    67
     A                                      '時間:'
     A                                    76
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    85
     A                                      '<PYR342P>'
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========================== '
     A                                      SPACEB(001)
     A*
     A                                     1
     A                                      '部門'
     A                                      SPACEB(001)
     A                                    24
     A                                      '員工'
     A                                    47
     A                                      '申請項目'
     A                                    67
     A                                      '核發金額'
     A                                    80
     A                                      '簽　名'
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========================== '
     A                                      SPACEB(001)
     A*------------------------------------------------------------***
     A          R PD1
     A                                      SPACEB(001)
     A  31        EM041     R              1
     A  31        EM042     R              2
     A  31        EM043     R              3
     A  31        EM044     R              4
     A  31        DP02      R              8
     A                                      REFFLD(DP02 PYDPPF)
     A            GD02      R             25
     A            EM03      R             33
     A            GD07      R             48
     A            #Y03N         16        50
     A            GD08      R             67
     A                                      EDTCDE(K)
     A*------------------------------------------------------------***
     A          R PT0
     A                                      SPACEB(001)
     A                                    33
     A                                      '-----------------------------------
     A                                      ------------------------------ '
     A                                    33
     A                                      '小計:'
     A                                      SPACEB(001)
     A            TT10          10  0     63
     A                                      EDTCDE(K)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      --------------------------- '
     A                                      SPACEB(001)
     A*------------------------------------------------------------***
     A          R PT1
     A                                      SPACEB(001)
     A                                     1
     A                                      '工作地點合計:'
     A            TT02          10  0     63
     A                                      EDTCDE(K)
     A                                    77
     A                                      '元'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========================== '
     A                                      SPACEB(001)
     A*------------------------------------------------------------***
     A          R PT3
     A                                      SPACEB(001)
     A                                     1
     A                                      '全部總計:'
     A            TT03          10  0     63
     A                                      EDTCDE(K)
     A                                    77
     A                                      '元'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========================== '
     A                                      SPACEB(001)
     A*------------------------------------------------------------***
     A          R PT2
     A                                      SPACEB(001)
     A                                     1
     A                                      'PYR342P--->報表結束'
     A                                      SPACEB(001)
     A*------------------------------------------------------------***
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
