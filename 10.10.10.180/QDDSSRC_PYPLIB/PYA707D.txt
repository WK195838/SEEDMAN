     A*  90/06/29  11:44:46    A1038       REL-R02M00  5728-PW1
     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYA707D                                    *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF13(13)
     A                                  1  2'<PYA707D>'
     A                                  1 20'＜＜薪資所得銀行媒體轉存磁片製作+
                                            ＞＞'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A                                  2  2'<PYA707.1>'
     A                                  4 16'公司編號　:'
     A            DBT01     R        B  4 30REFFLD(SH01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5 16'轉存銀行　:'
     A            DBT061    R        B  5 30REFFLD(BT061)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6 16'受理分行　:'
     A            DBT062    R        B  6 30REFFLD(BT062)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 16'公司帳號　:'
     A            DBT09     R        B  7 30REFFLD(BT09)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 16'所得類別　:'
     A            DBT03     R        B  9 30REFFLD(BT03)
     A                                      VALUES('1' '2' '5')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 34'1.下期薪資'
     A                                  9 47'2.上期薪資'
     A                                  9 60'5.公司獎金'
     A                                 11 16'存入年月　:'
     A            DBT04     R   -2   B 11 30REFFLD(BT04)
     A                                      EDTWRD('  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 12 16'存入日期　:'
     A            DBT05     R        B 12 30REFFLD(BT05)
     A                                      EDTCDE(4)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 14 16'重新製作　:'
     A            DOPT           1   B 14 30
     A                                      VALUES('Y' 'N')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 14 33'(Y/N)'
     A*
     A*                                18  2'列印份數:'
     A*           $CPY           3Y 0B 18 14EDTCDE(3)
     A*                                     CHECK(RB)
     A*                                19  2'列表機　:'
     A*           $PRTID         1Y 0B 19 14
     A*                                     CHECK(RB)
     A*                                     EDTCDE(3)
     A*           PRINTMARK     60A  O 19 17MSGID(UPT 0001 PTMF)
     A*                                21 29'***請準備８０格報表紙***'
     A*
     A  97                             22 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(RI)
     A  96                             22 23'　　　轉存作業處理中！！　　'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  95        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR2          70A  O 22  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR3          70A  O 22  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR4          70A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR5          70A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR6          70A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR7          70A  O 22  2MSGID(UPT 0042 PTMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF13 =處理開始'
     A*@                               24  2'彰銀核證總數:'
     A*@          DM406         10A  O    +1DSPATR(RI)
     A            DMARK         14A  O 24 66MSGID(UMI 0001 CEMF)
     A                                      DSPATR(HI)
