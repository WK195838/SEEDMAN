     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI101D                                    *
     A*    PROGRAM NAME :人事基本資料查詢                          *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 85/06/11                                   *
     A*    UPDATE DATE  : 89/08/19 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            EM02      R        O  8  4
     A            EM03      R        O  8 13
     A            EM04           7A  O  8 28
     A            DP02      R        O  8 36REFFLD(DP02 PYDPPF)
     A            DEM13     R   -2   O  8 53REFFLD(EM13)
     A                                      EDTWRD('0  /  /  ')
     A            EM31      R        O  8 63
     A            EM11      R        O  8 77
     A*-------------------------------------------------------------***
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI101.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'人事基本資料查詢'
     A                                      DSPATR(RI)
     A                                  4  2'公司編號:'
     A            DEM01     R        O  4 14REFFLD(EM01)
     A  60                                  DSPATR(RI)
     A            DEM01N        22O  O  4 17
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'    員工編號姓名    '
     A                                      DSPATR(UL)
     A                                  6 28'     部門編號        '
     A                                      DSPATR(UL)
     A                                  6 52'到職日期'
     A                                      DSPATR(UL)
     A                                  6 63'身份證號'
     A                                      DSPATR(UL)
     A                                  6 74'離職碼'
     A                                      DSPATR(UL)
     A            DEM02     R        B  7  4REFFLD(EM02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DEM03     R        B    +1REFFLD(EM03)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DEM04          7A  B  7 28DSPATR(PC)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            EM13D     R   -2   B  7 53REFFLD(EM13)
     A                                      EDTWRD('0  /  /  ')
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DEM31         10   B  7 63DSPATR(PC)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF12 =回前畫面'
     A                                    +2'PA01 =下一頁'
     A                                    +2'PA02 =上一頁'
     A                                 24 67'【泛太資訊】'
