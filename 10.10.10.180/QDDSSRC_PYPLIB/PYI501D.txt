     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI501D                                    *
     A*    PROGRAM NAME :職稱基本資料查詢                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 85/12/27                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  6  4DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            JT01      R        O  6 11
     A            JT02      R        O  6 19
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A  *DS3                                SFLLIN(0013)
     A                                      SFLSIZ(0064)
     A                                      SFLPAG(0032)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI501D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'職稱基本資料設定'
     A                                      DSPATR(RI)
     A                                  2 32'職稱基本資料查詢'
     A                                      DSPATR(RI)
     A                                  4  4'S'
     A                                      DSPATR(UL)
     A                                  4  7'職稱編號'
     A                                      DSPATR(UL)
     A                                  4 19'  職稱名稱  '
     A                                      DSPATR(UL)
     A                                  4 46'S'
     A                                      DSPATR(UL)
     A                                  4 49'職稱編號'
     A                                      DSPATR(UL)
     A                                  4 61'  職稱名稱  '
     A                                      DSPATR(UL)
     A            DJT01     R        B  5 11REFFLD(JT01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 23 36'PA1=下一頁'
     A                                 23 50'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
