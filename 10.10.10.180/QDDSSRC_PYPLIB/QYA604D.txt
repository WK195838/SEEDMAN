     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*    PROGRAM-ID   : PYA604D                                    *
     A*    PROGRAM NAME :福委會登錄作業維護                        *
     A*    AUTHOR       : A1276 VINCENT                              *
     A*    CREATE DATE  : 88/01/27                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A                                      CF03(03)
     A          R DSPD1
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CF04(04)
     A            #FLD          10A  H
     A            #REC          10A  H
     A                                  1  2'<PYA604.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 35'福委會作業'
     A                                      DSPATR(RI)
     A                                  2 64'時間:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'福委會編號:'
     A            DER01     R        B  4 15REFFLD(EM01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DER02     R        B  5 15REFFLD(EM02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            EM03      R        O  5 24
     A            EM033         12   O  5 39
     A            EM034          7   O  5 52
     A            EM032         19   O  5 60
     A                                  6  1'日　　期:'
     A            DER03          8Y 0B  6 15
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      EDTWRD('    /  /  ')
     A                                  6 26'(YYYY/MM/DD)'
     A                                  7  1' TYPE :'
     A            DER04          1   B  7 15VALUES('1' '2')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  7 17'( 1 表支出2表收入)'
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 15VALUES(1)
     A                                      EDTCDE(4)
     A                                  8 17'( 1.新增)'
     A*                                 8 28'2.修改'
     A*                                 8 37'4.刪除'
     A*                                 8 46'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF04=欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA12(12)
     A                                  1  2'<PYA604.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 35'福委會作業'
     A                                      DSPATR(RI)
     A                                  2 64'時間:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'福委會編號:'
     A            DER01     R        O  4 15REFFLD(EM01)
     A                                  5  1'員工編號:'
     A            DER02     R        O  5 15REFFLD(EM02)
     A            EM03      R        O  5 24
     A            EM033         12   O  5 39
     A            EM034          7   O  5 52
     A            EM032         19   O  5 60
     A                                  6  1'日　　期:'
     A            DER03          8Y 0O  6 15
     A                                      EDTWRD('    /  /  ')
     A                                  6 26'(YYYY/MM/DD)'
     A                                  7  1' TYPE :'
     A            DER04          1   O  7 15
     A                                  7 17'( 1 表支出2表收入)'
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0O  8 15
     A                                      EDTCDE(4)
     A                                  8 17'( 1.新增'
     A                                  8 28'2.修改'
     A                                  8 37'4.刪除'
     A                                  8 46'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 11  1'金　　額:'
     A            DER05          6  0B 11 15EDTCDE(1)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(PR)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
