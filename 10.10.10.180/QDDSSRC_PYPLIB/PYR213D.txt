     A****************************************************************/
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   */
     A*                                                              */
     A*    PROGRAM-ID   : PYA213D                                    */
     A*    PROGRAM NAME :出勤資料明細表列印                        */
     A*    AUTHOR       : A1226 PETER                                */
     A*    CREATE DATE  : 86/01/20                                   */
     A*    UPDATE DATE  : 88/10/01 A1274 EVA                         */
     A*    SYSTEM       :人事薪資系統                              */
     A****************************************************************/
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYR213>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'出勤資料明細表列印'
     A                                      DSPATR(RI)
     A                                  5 30'公司編號　　:'
     A            DT505     R        B  5 46REFFLD(T505)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 30'部門編號　起:'
     A                                      DSPATR(HI)
     A            DT506F    R        B  7 46REFFLD(T506)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 30'　　　　　迄:'
     A                                      DSPATR(HI)
     A            DT506T    R        B  8 46REFFLD(T506)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 30'員工編號　起:'
     A                                      DSPATR(HI)
     A            DT501F    R        B 10 46REFFLD(T501)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 11 30'　　　　　迄:'
     A                                      DSPATR(HI)
     A            DT501T    R        B 11 46REFFLD(T501)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 13 30'出勤年月　　:'
     A            DT502          5  0B 13 46
     A                                      EDTWRD('   /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 17'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 30'請準備８０格報表紙'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
