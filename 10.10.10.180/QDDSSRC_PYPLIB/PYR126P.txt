     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR126P                                    *
     A*    PROGRAM NAME :員工住宿明細表　                          *
     A*    AUTHOR       : A1336  KLAUS                               *
     A*    CREATE DATE  : 91/08/05                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1
     A                                      SPACEA(001)
     A                                     1
     A                                      '員工住宿明細表'
     A                                      SPACEA(002)
     A                                     1
     A                                      '申請別:'
     A            LI03N         14O       11
     A                                    51
     A                                      '日期:'
     A            DDATE          6  0     59
     A                                      EDTWRD('0  /  /  ')
     A                                    68
     A                                      '頁次:'
     A                                    76
     A                                      PAGNBR
     A                                      SPACEA(002)
     A                                     1
     A                                      '日　期:'
     A            DLI051    R   -1        11
     A                                      REFFLD(TR04)
     A                                      EDTWRD('   /  /  ')
     A                                    21
     A                                      '-'
     A            DLI052    R   -1        23
     A                                      REFFLD(TR04)
     A                                      EDTWRD('   /  /  ')
     A                                    51
     A                                      '時間:'
     A                                    59
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    69
     A                                      '<PYR126P>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門名稱'
     A                                      SPACEB(001)
     A                                    17
     A                                      '員工姓名'
     A                                    40
     A                                      '申請類別'
     A                                    51
     A                                      '住宿日期'
     A                                    64
     A                                      '退宿日期'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A          R PD1
     A  31        PDP021    R              1
     A                                      REFFLD(DP02 PYDPPF)
     A  32        LI02      R             17
     A  32        EM03      R             25
     A            LI03      R             45
     A            PLI05     R   -1        51
     A                                      REFFLD(LI05)
     A                                      EDTWRD('   /  /  ')
     A            PLI06     R   -1        64
     A                                      REFFLD(LI06)
     A                                      EDTWRD('   /  /  ')
     A                                      SPACEA(002)
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A                                     1
     A                                      'PYR126P--->報表結束'
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
