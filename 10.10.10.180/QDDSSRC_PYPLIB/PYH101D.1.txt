     A*%%TS  SD  20050413  103007  REENE       REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYH101D                                    *
     A*    PROGRAM NAME :退休金主檔維護                            *
     A*    AUTHOR       : MAX PENG                                   *
     A*    CREATE DATE  : 94/03/10                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :新制勞退系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYH4PF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  20050413  103007  REENE       REL-V4R4M0  5769-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYH101.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'退休金主檔維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DH401     R        B  4 13REFFLD(H401)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N32                                  DSPATR(PR)
     A  32                                  DSPATR(UL)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DH402     R        B  5 13REFFLD(H402)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DEM03         14A  O  5 22
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  20050413  103007  REENE       REL-V4R4M0  5769-PW1
     A                                      CHGINPDFT(UL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF06(06)
     A                                      CA12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYH101.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'退休金主檔維護'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4 14'公　　　司:'
     A            DH401     R        O  4 34REFFLD(H401)
     A            CM02          14A  O  4 37
     A                                  5 14'員　　　工:'
     A            DH402     R        O  5 34REFFLD(H402)
     A            EM03          14A  O  5 43
     A                                  6 14'個人帳戶　:'
     A            DH403     R        B  6 34REFFLD(H403)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                  7 14'適用新制碼:'
     A            DH404     R        B  7 34REFFLD(H404)
     A                                      VALUES('Y' 'N')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                  7 36'(Y適用N 不適用)'
     A                                  8 14'新舊選擇碼:'
     A            DH405     R        B  8 34REFFLD(H405)
     A                                      VALUES('1' '2')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                  8 36'(1.新 2.舊)'
     A                                  9 14'新制選擇日:'
     A            DH406          7  0B  9 34
     A                                      EDTWRD('0  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 10 14'員工自提率:'
     A            DH407     R        B 10 34REFFLD(H407)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                      EDTCDE(1)
     A                                 10 39'%'
     A                                 11 14'企業提撥率:'
     A            DH408     R        B 11 34REFFLD(H408)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                      EDTCDE(1)
     A                                 11 39'%'
     A                                 12 14'月提繳工資:'
     A            DH409     R        B 12 34REFFLD(H409)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                      EDTCDE(1)
     A                                 13 14'自提開始日期:'
     A            DH422          7  0B 13 34
     A                                      EDTWRD('0  /  /  ')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 14 14'自提停止日期:'
     A            DH423          7  0B 14 34
     A                                      EDTWRD('0  /  /  ')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 15 14'保留年資結清日期:'
     A            DH424          7  0B 15 34
     A                                      EDTWRD('0  /  /  ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 16 14'保留年資結清條件:'
     A            DH425     R        B 16 34REFFLD(H425)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 17 14'異動生效日期:'
     A            DH414          7  0B 17 34
     A                                      EDTWRD('0  /  /  ')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N33                                  DSPATR(PR)
     A  33                                  DSPATR(UL)
     A                                 17 45'異動項目:'
     A                                      DSPATR(HI)
     A            DH514          2A  B 17 57
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            H514N         14A  O 17 60
     A                                 18 14'自提累積金額:'
     A            DH412     R        O 18 34REFFLD(H412)
     A                                      EDTCDE(1)
     A                                 19 14'企提累積金額:'
     A            DH413     R        O 19 34REFFLD(H413)
     A                                      EDTCDE(1)
     A*                                20 14'上月自提金額:'
     A*           DH420     R        O 20 34REFFLD(H420)
     A*                                     EDTCDE(1)
     A*                                21 14'上月企提金額:'
     A*           DH421     R        O 21 34REFFLD(H421)
     A*                                     EDTCDE(1)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF05=提撥明細'
     A                                 23 38'PF06=異動查詢'
     A                                 23 56'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
