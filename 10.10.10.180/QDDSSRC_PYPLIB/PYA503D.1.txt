     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA503D                                    *
     A*    PROGRAM NAME :薪資項目參數設定                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 85/12/30                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03 'RETURN')
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA503.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'薪資項目參數設定'
     A                                      DSPATR(RI)
     A                                  4  1'薪資組別:'
     A            DSS18     R        B  4 13REFFLD(SS18)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  1'薪資代碼:'
     A            DSS01     R        B  5 13REFFLD(SS01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                  6 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03 'RETURN')
     A                                      CA12(12 'PREVIOUSE')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA503.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'薪資項目參數設定'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4 10'薪資組別:'
     A            DSS18     R        O  4 22REFFLD(SS18)
     A                                  4 30'薪資代碼:'
     A            DSS01     R        O  4 42REFFLD(SS01)
     A                                  6 10'薪資名稱:'
     A            DSS02     R        B  6 22REFFLD(SS02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7 10'項目屬性:'
     A            DSS09     R        B  7 22REFFLD(SS09)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      VALUES('1' '2' '3' '5')
     A                                  7 25'( 1-薪資    2-成本    3-公司+
     A                                      負擔5-公司獎金)'
     A                                  8 10'上期項目:'
     A            DSS17     R        B  8 22REFFLD(SS03)
     A                                      VALUES('A' 'B' ' ')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 25'( A-上期借支B-上期其它)'
     A                                  9 10'應免稅　:'
     A            DSS03     R        B  9 22REFFLD(SS03)
     A                                      VALUES('Y' 'N')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 25'( Y-應稅　　N-免稅)'
     A                                 10 10'扣稅方式:'
     A            DSS11     R        B 10 22REFFLD(SS11)
     A                                      VALUES('1' '2')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 25'( 1-固定　　2-非固定)'
     A                                 11 10'加扣項別:'
     A            DSS04     R        B 11 22REFFLD(SS04)
     A                                      VALUES('A' 'B')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 25'( A-加項　　B-減項)'
     A                                 12 10'項目別　:'
     A            DSS05     R        B 12 22REFFLD(SS05)
     A                                      VALUES('0' '1' '2')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 25'( 0-系統設定1-固定項目2-變動+
     A                                      項目)'
     A                                 13 10'薪資別　:'
     A            DSS06     R        B 13 22REFFLD(SS06)
     A                                      VALUES('A' 'B' 'C')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 25'( A-固定金額B-薪資比率C-變動+
     A                                      金額)'
     A                                 14 10'輸入方式:'
     A            DSS10     R        B 14 22REFFLD(SS10)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A*N31                             14 25'( 1-金額    2-次數　每次金額+
     A*                                       3-天數)'
     A N31                                  DSPATR(PR)
     A                                 14 25'( 1-金額    2-次數　每次金額'
     A            DSS19     R        B 14 58REFFLD(SS19)
     A                                      EDTCDE(2)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 65' 3-天數)'
     A                                 15  8'A.固定金額:'
     A            DSS07     R        B 15 22REFFLD(SS07)
     A                                      EDTCDE(2)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16  8'B.薪資比率:'
     A            DSS08     R        B 16 22REFFLD(SS08)
     A                                      EDTCDE(2)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 10'報表設定:薪資總表:下期-'
     A            DSS12     R        B 18 40REFFLD(SS12)
     A                                      RANGE(0 48)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(1)
     A                                 18 43'上期-'
     A            DSS15     R        B 18 51REFFLD(SS15)
     A                                      RANGE(0 48)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(1)
     A                                 18 54'( 0- 48, 0 : 不列印)'
     A                                 19 21'薪資單　:下期-'
     A            DSS13     R        B 19 40REFFLD(SS13)
     A                                      RANGE(0 48)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(1)
     A                                 19 43'上期-'
     A            DSS16     R        B 19 51REFFLD(SS16)
     A                                      RANGE(0 48)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(1)
     A                                 19 54'( 0- 48, 0 : 不列印)'
     A                                 20 21'成本項目:'
     A            DSS14     R        B 20 33REFFLD(SS14)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 20 37'(請輸入成本薪資項目)'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
