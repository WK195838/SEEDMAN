     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR618P                                    *
     A*    PROGRAM NAME :勞保投保資料明細表　　                    *
     A*    AUTHOR       : A1413 CLOUD                                *
     A*    CREATE DATE  : 2006/05/04                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      REF(PYR618WF)
     A*---------------------------------------------------------------*
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1REFFLD(CM03 PYRF)
     A                                      SPACEA(001)
     A                                     1
     A                                      '勞保投保資料明細表'
     A                                      SPACEA(002)
     A                                    71
     A                                      '日期:'
     A            DDATE          6  0     79
     A                                      EDTWRD('0  /  /  ')
     A                                    88
     A                                      '頁次:'
     A                                    95
     A                                      PAGNBR
     A                                      SPACEA(001)
     A                                    71
     A                                      '時間:'
     A                                    79
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    89
     A                                      '<PYR618>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===='
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門名稱'
     A                                    26
     A                                      '員工姓名'
     A                                    53
     A                                      '身份證號'
     A                                    65
     A                                      '出生日期'
     A                                    76
     A                                      '異動別'
     A                                    86
     A                                      '生效日期'
     A                                    98
     A                                      '金　　額'
     A                                      SPACEA(001)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===='
     A*---------------------------------------------------------------*
     A          R PD1
     A                                      SPACEB(001)
     A*部門編號
     A  31        WF011     R              1
     A  31        WF012     R              2
     A  31        WF013     R              3
     A  31        WF014     R              4
     A*部門簡稱
     A  31        WF02      R              9
     A*員工編號
     A  32        WF03      R             26
     A*員工姓名
     A  32        WF04      R             37
     A*身份証號
     A  32        WF05      R             53
     A*出生日期(西元)
     A  32        WF06C     R   -2        65REFFLD(WF06 PYR618WF)
     A                                      EDTWRD('0  /  /  ')
     A*異動類別名稱
     A            WF07N          6        76
     A*生效日期(西元)
     A            WF08C     R   -2        86REFFLD(WF06 PYR618WF)
     A                                      EDTWRD('0  /  /  ')
     A*投保薪資
     A            WF09      R             98
     A                                      EDTCDE(1)
     A*---------------------------------------------------------------*
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===='
     A                                      SPACEB(001)
     A                                     1
     A                                      'PYR618P--->報表結束'
     A                                      SPACEB(001)
     A*---------------------------------------------------------------*
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
