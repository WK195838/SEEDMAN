     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA1101D                                   *
     A*    PROGRAM NAME :健保投保級數查詢　　　　                  *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 91/12/09                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統　                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A            RRN            2Y 0H
     A            DOPT           1A  B  8  3DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            E301      R        O  8 12REFFLD(E301 PYE3PF)
     A                                      EDTCDE(2)
     A            E302      R        O  8 24REFFLD(E302 PYE3PF)
     A                                      EDTCDE(2)
     A            E303      R        O  8 38REFFLD(E303 PYE3PF)
     A                                      EDTCDE(2)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI1101D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'健保投保級數查詢'
     A                                      DSPATR(RI)
     A                                  4  2'投保費率:'
     A            E401      R        O  4 14REFFLD(E401 PYE4PF)
     A                                      EDTCDE(1)
     A                                  4 22'員工負擔:'
     A            E402      R        O  4 34REFFLD(E402 PYE4PF)
     A                                      EDTCDE(1)
     A            E405      R        O  4 42REFFLD(E405)
     A                                      EDTCDE(1)
     A                                  4 44'人'
     A                                  4 50'公司負擔:'
     A            E403      R        O  4 62REFFLD(E403 PYE4PF)
     A                                      EDTCDE(1)
     A            E404      R        O  4 69REFFLD(E404)
     A                                      EDTCDE(1)
     A                                  4 76'人'
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  6  3'S'
     A                                      DSPATR(UL)
     A                                  6  8'投保級數'
     A                                      DSPATR(UL)
     A                                  6 22'月投保金額'
     A                                      DSPATR(UL)
     A                                  6 36'本    人'
     A                                      DSPATR(UL)
     A            DE301     R        B  7 12REFFLD(E301 PYE3PF)
     A                                      EDTCDE(2)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PA01 =下一頁　'
     A                                 23 37'PA02 =上一頁　'
     A                                 24 67'【泛太資訊】'
