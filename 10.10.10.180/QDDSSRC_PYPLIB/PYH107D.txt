     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYH107D                                    *
     A*    PROGRAM NAME :員工退休金提撥明細                        *
     A*    AUTHOR       : SHARON A1386                               *
     A*    CREATE DATE  : 94/03/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :新制退休金系統                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYH6PF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            H603      R        O  9  8REFFLD(H603)
     A                                      EDTWRD('    /  ')
     A            H605      R        O  9 28REFFLD(H605)
     A                                      EDTCDE(1)
     A            H606      R        O  9 45REFFLD(H606)
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0012)
     A*                                     CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYH107D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工退休金提撥明細'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DH601     R        O  4 13REFFLD(H601)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DCM02         14O  O  4 16
     A                                  5  1'員工編號:'
     A            DH602     R        O  5 13REFFLD(H602)
     A            DEM03         14O  O    +1
     A                                  6  1'目前月提繳工資:'
     A            DH409          6Y 0O    +1EDTCDE(1)
     A                                  6 32'企業提撥率:'
     A            DH408          3Y 2O    +1EDTCDE(1)
     A                                    +1'%'
     A                                  6 54'員工自提率:'
     A            DH407          3Y 2O    +1EDTCDE(1)
     A                                    +1'%'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  5'薪資提撥月份'
     A                                      DSPATR(UL)
     A                                  8 22'員工提撥金額'
     A                                      DSPATR(UL)
     A                                  8 39'企業提撥金額'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                      OVERLAY
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA01=下一頁'
     A                                 23 56'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
