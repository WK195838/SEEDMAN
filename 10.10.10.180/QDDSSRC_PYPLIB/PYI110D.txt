     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI110D                                    *
     A*    PROGRAM NAME :全民健保資料查詢                          *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  :                                            *
     A*    UPDATE DATE  : 91/06/21 A1336 KLAUS                       *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*                                     REF(*LIBL/PYRF)
     A                                      REF(*LIBL/PYE8PF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            DE802     R        O  8  7REFFLD(E802)
     A            EM03          14A  O  8 17
     A            DE803     R   -2   O  8 33REFFLD(E803)
     A                                      EDTWRD('0  /  /  ')
     A            E804N          6A  O  8 45
     A            E805      R        O  8 53EDTCDE(1)
     A            E806      R        O  8 69EDTCDE(1)
     A*----------------------------------------------------------------
     A**                                    CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI110D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DATE           6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'全民健保資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DE801     R        O  4 13REFFLD(E801)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DE801N        14O  O  4 16
     A                                  5  2'S (=作業) : /-挑選'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5'員工編號'
     A                                      DSPATR(UL)
     A                                  6 17'  員工姓名  '
     A                                      DSPATR(UL)
     A                                  6 33'生效日期'
     A                                      DSPATR(UL)
     A                                  6 45'類別'
     A                                      DSPATR(UL)
     A                                  6 53'投保金額'
     A                                      DSPATR(UL)
     A                                  6 65'投保人數'
     A                                      DSPATR(UL)
     A            KE802     R        B  7  7REFFLD(E802)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            KE803     R   -2   B  7 33REFFLD(E803)
     A                                      EDTWRD('0  /  /  ')
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A                                 23 36'PA1 =下一頁　'
     A                                 23 53'PA2 =上一頁　'
