     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYA804D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 96/08/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PYA804                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :工讀生主檔維護                            *
     A*    REMARK       :             (A/U/D/I)                  *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.10.26 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  4VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            EN02          10A  O  9  9
     A            EN03          12O  O  9 25
     A            WEN05          6Y 0O  9 42EDTWRD('0  /  /  ')
     A            EN04          64O  O 10 10
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'PYA804'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'工讀生基本資料建立'
     A                                      DSPATR(HI)
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'公司:'
     A            DEN01          2A  B  4  9
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  1'輸入作業選擇:'
     A                                  5 17'2=修改'
     A                                  5 26'4=刪除'
     A                                  5 35'5=查詢'
     A                                  7  4'A'
     A                                      DSPATR(UL)
     A                                  7  9'身分證號'
     A                                      DSPATR(UL)
     A                                  7 25'員工姓名'
     A                                      DSPATR(UL)
     A                                  7 41'出生日期'
     A                                      DSPATR(UL)
     A            DBGN1         10A  B  8  9
     A          R DSPC11
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR4          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPC2
     A*                                     CA03(03)
     A                                      CF09(09)
     A                                      CF12(12)
     A                                  1  2'PYA804'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'工讀生基本資料建立'
     A                                      DSPATR(HI)
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  6  2'公司編號     :'
     A            DEN01          2A  B  6 19
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  8  2'身分證號     :'
     A            DEN02         10A  B  8 19
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'員工姓名     :'
     A            DEN03         12O  B 10 19
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A* 62                                  DSPATR(PC)
     A* 62                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                 12  2'出生日期     :'
     A            DEN05          6Y 0B 12 19EDTWRD('0  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  2'戶籍地址     :'
     A            DEN04         60O  B 14 19
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A* 62                                  DSPATR(PC)
     A* 62                                  DSPATR(RI)
     A* 30                                  DSPATR(UL)
     A*N30                                  DSPATR(PR)
     A*
     A  28        DMSG          78A  O 22  2DSPATR(HI)
     A*                                23  2'F3 =回主畫面'
     A  39                             23  2'F9 =確認'
     A                                 23 18'F12=回前畫面'
     A  98        ERR1          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(URE 0019 REMF)
     A                                      DSPATR(HI)
     A  91        ERR4          60A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          60A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
