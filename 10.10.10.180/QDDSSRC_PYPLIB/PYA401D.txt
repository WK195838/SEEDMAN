     A*%%TS  SD  20031219  155343  PPSPGMRYK   REL-V5R1M0  5722-WDS
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    DSPF NAME    : PYA401D                                    *
     A*    AUTHOR       : EVA                                        *
     A*    CREATE DATE  : 89/02/15                                   *
     A*    UPDATE DATE  : 90/05/18 KLAUS                             *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF13(13)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYA401D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'公司獎金資料試算'
     A                                      DSPATR(RI)
     A                                  4  3'公　　司:'
     A            DPB01     R        B  4 17REFFLD(PB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            CM03      R        O  4 21REFFLD(CM03 SYCMPF)
     A                                  5  3'部　　門:'
     A                                      DSPATR(HI)
     A            DEPT1          7A  B  5 17
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5 25'-'
     A            DEPT2          7A  B  5 27
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  6  3'獎金年度:'
     A            DPB03     R   -1   B  6 17REFFLD(PB03)
     A                                      EDTCDE(4)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7  3'獎金類別:'
     A            DPB04          2A  B  7 17VALUES('01' '02' '03' '04')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 20'(01.年終獎金02.夏季獎金'
     A                                  7 48'03.紅利04.其它 )'
     A*           SS02      R        O  7 21REFFLD(SS02 PYSSPF)
     A                                  8  3'獎金月數:'
     A            DDAYS          8Y 6B  8 17EDTCDE(3)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(PR)
     A                                  9  3'   Ｎ   :'
     A            DAMTS          3Y 1B  9 17EDTCDE(1)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(PR)
     A                                 10  3'年資率計算:'
     A            WRATS          7Y 0B 10 17EDTWRD('   /  /  ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                 10 27'~'
     A            WRATE          7Y 0B 10 29EDTWRD('   /  /  ')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                 12  3'使用出勤率檔(PYPEPF)'
     A                                 12 26'計算:'
     A            DOPT           1A  B 12 34VALUES('Y' 'N')
     A N63                                  DSPATR(UL)
     A                                 12 36'(Y/N)'
     A*
     A  96                             21 23'　　　獎金作業處理中！！　　'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  98                             21 23'　　　獎金作業處理完成！　　'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
     A*                                13  4'年終＝（月薪＋職務加給）Ｘ年資+
     A*                                     Ｘ出勤Ｘ獎金月數＋（月薪／３０）+
     A*                                     Ｘ年休補發'
     A*                                     COLOR(RED)
     A*                                14  4'年終＝（日薪Ｘ３０+職務加給）+
     A*                                     Ｘ年資Ｘ出勤Ｘ獎金月數）＋'
     A*                                     COLOR(RED)
     A*                                15 30'（日薪＋交通＋伙食）Ｘ年休補發'
     A*                                     COLOR(RED)
     A*                                16  4'夏季＝（月薪＋職務加給）Ｘ年資+
     A*                                     Ｘ獎金月數'
     A*                                     COLOR(BLU)
     A*                                17  4'夏季＝（日薪Ｘ３０+職務加給）+
     A*                                     Ｘ年資Ｘ獎金月數'
     A*                                     COLOR(BLU)
     A*                                18  4'紅利＝月薪Ｘ年資Ｘ出勤+
     A*                                     Ｘ獎金月數＋（職務加給ＸＮ）'
     A*                                     COLOR(PNK)
     A*                                19  4'紅利＝日薪Ｘ３０Ｘ年資Ｘ出勤+
     A*                                     Ｘ獎金月數＋（職務加給ＸＮ）'
     A*                                     COLOR(PNK)
     A*                                20  4'其它＝同夏季'
