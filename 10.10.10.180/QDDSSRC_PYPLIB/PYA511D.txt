     A*%%TS  SD  19950519  192835  QSECOFR     REL-V2R3M0  5738-PW1
     A*  91/12/20  13:48:02    FOPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYA511D                                    *
     A*    AUTHOR       : A1038    CATHY SUN                         *
     A*    CREATE DATE  : 79/05/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PYA511                                     *
     A*    SYSTEM       : MIRS                                       *
     A*    SUBSYSTEM    : PY                                         *
     A*    REMARK       :所得稅對照表設定                          *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*  91/12/20  13:43:04    FOPGMR      REL-R03M00  5728-PW1
     A          R DSPC1
     A*%%TS  SD  19950519  192835  QSECOFR     REL-V2R3M0  5738-PW1
     A                                      CHGINPDFT(CS UL)
     A                                      CA03(03 '回主畫面')
     A                                      CF17(17 '稅率表維護')
     A                                  1  2'<PYA511.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'所得稅對照表設定'
     A                                      DSPATR(RI)
     A                                  7 12'個人免稅額　　:'
     A            DTA02     R        B  7 30REFFLD(TA02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      EDTCDE(1)
     A                                      CHECK(RB)
     A                                  7 45'配偶免稅額　  :'
     A            DTA03     R        B  7 63REFFLD(TA03)
     A                                      EDTCDE(1)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                      CHECK(RB)
     A                                  9 12'撫養親屬寬減額:'
     A            DTA04     R        B  9 30REFFLD(TA04)
     A                                      EDTCDE(1)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      CHECK(RB)
     A                                  9 45'標準扣除額　　:'
     A            DTA05     R        B  9 63REFFLD(TA05)
     A                                      EDTCDE(1)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                      CHECK(RB)
     A                                 11 12'薪資扣除額　  :'
     A            DTA06     R        B 11 30REFFLD(TA06)
     A                                      EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                      CHECK(RB)
     A                                 11 45'最低稅額　　  :'
     A            DTA08     R        B 11 63REFFLD(TA08)
     A                                      CHECK(RB)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                      EDTCDE(1)
     A                                 13 12'獎金稅率　　　:'
     A            DTA07     R        B 13 30REFFLD(TA07)
     A                                      CHECK(RB)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                      EDTCDE(1)
     A                                 15 12'勞保費率　　　:'
     A            DTA09     R        B 15 30REFFLD(TA09)
     A                                      CHECK(RB)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                      EDTCDE(1)
     A                                 15 45'員工負擔比率  :'
     A            DTA10     R        B 15 63REFFLD(TA10)
     A                                      CHECK(RB)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                      EDTCDE(1)
     A                                 17 12'勞保墊償比率　:'
     A            DTA11     R        B 17 30REFFLD(TA11)
     A                                      CHECK(RB)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                      EDTCDE(1)
     A                                 17 45'公司負擔比率  :'
     A            DTA12     R        B 17 63REFFLD(TA12)
     A                                      CHECK(RB)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                      EDTCDE(1)
     A                                 23  2'PF03=回主畫面'
     A                                 23 18'PF17=所得稅率設定'
     A            DMARK         14A  O 24 66MSGID(UMI 0001 CEMF)
     A  98        ERR1          50A  O 22  2MSGID(UPT 0080 PTMF)
     A                                      DSPATR(HI)
     A          R SFLSR                     SFL
     A*  91/12/20  13:48:02    FOPGMR      REL-R03M00  5728-PW1
     A                                      CHGINPDFT(CS UL)
     A            RRN            2S 0O  8  7
     A            DTC02     R        B  8 18REFFLD(TC02)
     A                                      EDTCDE(1)
     A                                      CHECK(RB)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DTC03     R        B  8 33REFFLD(TC03)
     A                                      EDTCDE(1)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                      CHECK(RB)
     A            DTC04     R        B  8 50REFFLD(TC04)
     A                                      EDTCDE(1)
     A                                      CHECK(RB)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DTC05     R        B  8 65REFFLD(TC05)
     A                                      EDTCDE(1)
     A                                      CHECK(RB)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*  90/07/30  10:57:28    A1038       REL-R02M00  5728-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CA03(03 '回主畫面')
     A                                      CF12(12 '回前畫面')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(UPT2040 *LIBL/PTMF)
     A  97                                  SFLMSGID(UPT2030 *LIBL/PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A                                  1  2'<PYA511D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'所得稅對照表設定'
     A                                      DSPATR(RI)
     A                                  5  2'==================================-
     A                                      ===================================-
     A                                      =========='
     A                                  6  5'序號'
     A                                  6 17'所得稅上限'
     A                                  6 32'所得稅下限'
     A                                  6 47'所得稅率'
     A                                  6 65'累進差額'
     A                                  7  6'----'
     A                                  7 18'----------'
     A                                  7 33'----------'
     A                                  7 48'--------'
     A                                  7 64'----------'
     A          R DSPC11
     A                                 18  2'==================================-
     A                                      ===================================-
     A                                      =========='
     A                                 23  2'PF03=回主畫面'
     A                                    +1'PF12=回前畫面'
     A            DMARK         14A  O 24 66MSGID(UMI0001 CEMF)
     A  96        ERR3          50A  O 22  2MSGID(UPT 0080 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22  2MSGID(UMI 0020 CEMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 22  2MSGID(UPT 0130 PTMF)
     A                                      DSPATR(HI)
