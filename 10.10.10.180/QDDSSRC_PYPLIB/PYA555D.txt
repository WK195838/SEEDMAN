     A*%%TS  SD  20051215  124202  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYA301D                                    *
     A*    AUTHOR       : MAX.ALLEN                                  *
     A*    CREATE DATE  : 93/01/07                                   *
     A*    UPDATE DATE  :
     A*    PROGRAM NAME : PYA555                                     *
     A*    SYSTEM       : MIRS                                       *
     A*    SUBSYSTEM    : PY                                         *
     A*    REMARK       :人事考績登錄　　                          *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  20040105  163258  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA555.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'人事考績登錄 '
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DRVA01         2A  B  4 13
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            CM02          14A  O  4 16
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DRVA04         8O  B  5 13
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            NAME          14A  O  5 22
     A                                      DSPATR(HI)
     A                                  4 37'年  度:'
     A            DRVA02         3Y 0B  4 49
     A                                      EDTCDE(1)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  5 37'組  別:'
     A            DRVA08         2A  B  5 49
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                  6 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A*                                23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A*%%TS  SD  20051215  124202  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN            2S 0O 12  3
     A            NUMBER         2A  O 12  7
     A            DNAME         14A  O 12 10
     A            GRADE          2S 0B 12 28
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  20051215  124202  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A* 98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA555.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'人事考績登錄 '
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DRVA01         2A  O  4 13
     A            CM02          14A  O  4 16
     A                                  4 37'年  度:'
     A            DRVA02         3Y 0O  4 49
     A                                  5  1'員工編號:'
     A            DRVA04         8A  O  5 13
     A            NAME          14A  O  5 22
     A                                  5 37'組  別:'
     A            DRVA08         2A  O  5 49
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0O  6 13
     A                                  6 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8 12'產假:'
     A            MATER          1A  B  8 21
     A                                  8 26'核定:'
     A            AUTH           1A  B  8 35
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 10  2' RN '
     A                                 10  7'   考績名稱 '
     A                                      DSPATR(HI)
     A                                 10 26'分數'
     A                                 11  3'-- ----------------- ---------'
     A                                  8 42'加總:'
     A            TOTAL          3  0O  8 51
     A          R DSPC1
     A*%%TS  SD  20051215  124202  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  98                             23  2'已達最後一筆'
     A  99        ERRMSG        70A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24  2'PF03=回主畫面'
     A                                 24 19'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
