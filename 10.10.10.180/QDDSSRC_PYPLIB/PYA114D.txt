     A*%%TS  SD  20060425  104430  PPSPGMRCU   REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA114D                                    *
     A*    PROGRAM NAME :勞工保險資料維護　　　　                  *
     A*    AUTHOR       : A1413 CLOUD                                *
     A*    CREATE DATE  : 2006/04/18                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統　                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*---------------------------------------------------------------
     A          R DSPC1
     A*%%TS  SD  20060425  104430  PPSPGMRCU   REL-V4R4M0  5769-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA114.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'勞工保險資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DL301     R        B  4 13REFFLD(L301 PYL3PF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DL302     R        B  5 13REFFLD(L302 PYL3PF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            EM03      R        O  5 25
     A                                  6  1'生效日期:'
     A            DL303     R   -2   B  6 13REFFLD(L303 PYL3PF)
     A                                      EDTWRD('0  /  /  ')
     A                                      CHECK(RB)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 16'( 1.新增 2.修改 4.刪除 5.+
     A                                      查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 11  3' '
     A                                 11 44' '
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                      CHGINPDFT(UL)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF13(13)
     A                                      CF05(05)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA114.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'勞工保險資料維護'
     A                                      DSPATR(RI)
     A            MODE          12O  O  3 35DSPATR(HI)
     A                                  7 24'公　　司:'
     A            DL301     R        O  7 36REFFLD(L301 PYL3PF)
     A            CM02      R        O  7 48
     A                                  8 24'員　　工:'
     A            DL302     R        O  8 36REFFLD(L302 PYL3PF)
     A            EM03      R        O  8 48
     A                                  9 24'生效日期:'
     A            DL303     R   -2   O  9 36REFFLD(L303 PYL3PF)
     A                                      EDTWRD('0  /  /  ')
     A                                 11 24' (A1.勞保投保 A2.勞保調整 A3.-
     A                                      勞保退保)'
     A                                 12 24'異動類別:'
     A            DL307          2A  B 12 36
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                      VALUES('A1' 'A2' 'A3')
     A                                 13 24'投保薪資:'
     A            DL304     R        B 13 36REFFLD(L304 PYL3PF)
     A                                      EDTCDE(J)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 14 24'備　　註:'
     A            DL309     R        B 14 36REFFLD(L309 PYL3PF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF05 =參考資料'
     A                                 23 38'PF12 =回前畫面'
     A                                 23 56'PF13 =資料確認'
     A                                 24 67'【泛太資訊】'
