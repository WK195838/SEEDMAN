     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR125D                                    *
     A*    PROGRAM NAME :異動人員資料列印                          *
     A*    AUTHOR       : TOM T.                                     *
     A*    CREATE DATE  : 2000/09/01                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYR125D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'人事異動明細表列印'
     A                                      DSPATR(RI)
     A                                  3 36'依異動別'
     A                                  5 24'公司編號:'
     A            DTX01     R        B  5 36REFFLD(TX01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 24'部門編號:'
     A                                      DSPATR(HI)
     A            DEM041         7A  B  7 36
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                    +1'-'
     A            DEM042         7A  B    +1
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 24'員工編號:'
     A                                      DSPATR(HI)
     A            DTX021    R        B  9 36REFFLD(TX02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                    +1'-'
     A            DTX022    R        B    +1REFFLD(TX02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 11 24'異動日期:'
     A            DTX041    R   -2   B 11 36REFFLD(TX04)
     A                                      EDTWRD('0  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                    +1'-'
     A            DTX042    R   -2   B    +1REFFLD(TX04)
     A                                      EDTWRD('0  /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 13 24'異動類別:'
     A            DTX03          2A  B 13 36
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                      VALUES('D1' 'D2' 'D5' 'D6' 'Z1' -
     A                                      'Z2')
     A                                 13 41'D1.留職停薪'
     A                                 13 56'D2.留職復職'
     A                                 14 41'D5.長期病假'
     A                                 14 56'D6.長病假復職'
     A                                 15 41'Z1.離職Z2.退休'
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A  27                             21 27'***請準備８０格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF03 =回主畫面'
     A                                 23 28'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
