     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR102D                                    *
     A*    PROGRAM NAME :調動人員資料列印                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 1997/01/03                                 *
     A*    UPDATE DATE  : 2001/08/09 A1274 EVA                       *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYR102D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'人事調動明細表列印'
     A                                      DSPATR(RI)
     A                                  3 36'依調動別'
     A                                  5 23'公司編號　　　:'
     A            DTR01     R        B  5 41REFFLD(TR01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 23'部門編號　　起:'
     A                                      DSPATR(HI)
     A            DEM041         7A  B  7 41
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 23'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM042         7A  B  8 41
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 23'員工編號　　起:'
     A                                      DSPATR(HI)
     A            DTR021    R        B 10 41REFFLD(TR02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 11 23'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DTR022    R        B 11 41REFFLD(TR02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 13 23'日　　期　　起:'
     A            DTR041    R   -2   B 13 41REFFLD(TR04)
     A                                      EDTWRD('0  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 14 23'　　　　　　迄:'
     A            DTR042    R   -2   B 14 41REFFLD(TR04)
     A                                      EDTWRD('0  /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 16 23'調動類別　　　:'
     A                                      DSPATR(HI)
     A            DTR03     R        B 16 41REFFLD(TR03)
     A            TR03N         14O  O    +2
     A                                 18  2'執行環境:'
     A            $PENV          1A  B 18 14VALUES('0' '1' '2')
     A                                 18 18'(0-查詢 1-即時 2-批次)'
     A                                 19  2'列印份數:'
     A            $CPY           3Y 0B 19 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 14
     A  27                             21 27'***請準備１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF03 =回主畫面'
     A                                 23 28'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
