     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA516D                                    *
     A*    PROGRAM NAME :銀行基本資料建立　　                      *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 85/12/27                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03 'RETURN')
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA516.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'銀行基本資料設定'
     A                                      DSPATR(RI)
     A                                  4  1'銀行代號:'
     A            DBN01          7A  B  4 13
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  4 21'(拷貝:'
     A            CBN01          7A  B  4 30
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  4 38')'
     A                                  5  1'作業選擇:'
     A            DOPT           1Y 0B  5 13VALUES(1 2 4 5)
     A                                  5 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03 'RETURN')
     A                                      CA12(12 'PREVIOUSE')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA516.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'銀行基本資料設定'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  5 16'銀行代號:'
     A            DBN01          7A  O  5 28
     A                                  7 16'簡稱    :'
     A            DBN04     R        B  7 28REFFLD(BN04)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 16'全名－中:'
     A            DBN02     R        B  8 28REFFLD(BN02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 16'全名－英:'
     A            DBN03     R        B  9 28REFFLD(BN03)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  4'地址－中:'
     A            DBN05     R        B 11 16REFFLD(BN05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  4'地址－英:'
     A            DBN06     R        B 12 16REFFLD(BN06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 16'電話    :'
     A            DBN07     R        B 14 28REFFLD(BN07)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 16'傳真機號:'
     A            DBN08     R        B 15 28REFFLD(BN08)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16 16'電報號碼:'
     A            DBN09     R        B 16 28REFFLD(BN09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 16'本地銀行:'
     A            DBN10     R        B 17 28REFFLD(BN10)
     A                                      VALUES(' ' 'L')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 31'（Ｌ－本地）'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
