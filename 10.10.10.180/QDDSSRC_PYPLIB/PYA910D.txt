     A*%%TS  SD  19981219  084457  PPSPGMR40   REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA910D                                    *
     A*    PROGRAM NAME :調組作業                                  *
     A*    AUTHOR       : B1038 SHARON                               *
     A*    CREATE DATE  : 98/11/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A*                                     OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A            #REC          10   H
     A            #FLD          10   H
     C*
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA910.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 35'調組作業'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DXE01     R        B  4 13REFFLD(XE01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DXE01N        14A  O  4 17
     A                                  5  1'員工代號:'
     A                                      DSPATR(HI)
     A            DXE02     R        B  5 13REFFLD(XE02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'生效日期:'
     A            DXE07     R   -2   B  6 13REFFLD(XE07)
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 16'( 1.新增2.修改4.刪除5.查詢-
     A                                      )'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F4 =欄位查詢'
     A                                 23 44'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A**************************************************************
     A          R DSPD2
     A*%%TS  SD  19981219  084457  PPSPGMR40   REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA910.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 35'調組作業'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DXE01     R        O  4 13REFFLD(XE01)
     A            DXE01N        14A  O  4 17
     A                                  5  1'員工代號:'
     A            DXE02     R        O  5 13REFFLD(XE02)
     A                                  6  1'起始日期:'
     A            DXE07     R   -2   O  6 13REFFLD(XE07)
     A                                      EDTWRD('  /  /  ')
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0O  7 13EDTCDE(4)
     A                                  7 16'( 1.新增2.修改4.刪除5.查詢-
     A                                      )'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  1'中文姓名:'
     A            EM03      R        O  9 13REFFLD(EM03)
     A                                 10  1'英文姓名:'
     A            EM031D        30A  O 10 13
     A                                 11  1'原定工作區:'
     A            XE03      R        O 11 15
     A            DXE03N        30A  O 11 26
     A                                 12  1'原定工作組:'
     A            XE04      R        O 12 15
     A            DXE04N        30A  O 12 18
     A                                 13  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 14  1'新工作區:'
     A                                      DSPATR(HI)
     A            DXE05     R        B 14 13REFFLD(XE05)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DXE05N        30A  O 14 24
     A                                 15  1'新工作組:'
     A                                      DSPATR(HI)
     A            DXE06     R        B 15 13REFFLD(XE06)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DXE06N        30A  O 15 16
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F4 =欄位查詢'
     A                                 23 44'F5 =參考資料'
     A                                 23 61'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
