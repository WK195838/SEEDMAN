     A*%%TS  SD  20020801  091927  PPSPGMREL   REL-V4R4M0  5769-PW1
     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYA411D                                    *
     A*    AUTHOR       : A1326  SANTOS                              *
     A*    CREATE DATE  : 91/07/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PYA411                                     *
     A*    SYSTEM       : MIRS                                       *
     A*    SUBSYSTEM    : EL                                         *
     A*    REMARK       :生產獎金資料確認                          *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  20020801  091927  PPSPGMREL   REL-V4R4M0  5769-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA412.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'生產獎金資料確認'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A*                                     DSPATR(HI)
     A            DPP01     R        B  4 13REFFLD(PP01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DPP01N         8A  O  4 16
     A                                  4 25'發放年月:'
     A            DPP03          5Y 0B  4 37EDTWRD('   /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  4 45'部門:'
     A                                      DSPATR(HI)
     A            DPP04          5A  B  4 53
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DPP04N         8A  O  4 59
     A                                  4 68'註記:'
     A            DPP06     R        B  4 76REFFLD(PP06)
     A                                      VALUES('A' 'B')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'應計金額:'
     A            DPP09          9Y 0O  5 13EDTCDE(1)
     A                                  5 35'應計總工時:'
     A            DPP07     R        O  5 51REFFLD(PP08)
     A                                      EDTCDE(1)
     A                                  6  1'實際金額:'
     A            DPP10          9Y 0O  6 13EDTCDE(1)
     A                                  6 35'實際總工時:'
     A            DPP08     R        O  6 51REFFLD(PP08)
     A                                      EDTCDE(1)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 5)
     A                                  7 16'(1.確認5.查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A*%%TS  SD  20020731  100455  PPSPGMREL   REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN            3S 0O 11  3
     A            DPQ05     R        O 11  9REFFLD(PQ05)
     A            DEM03         10A  O 11 19
     A            DPQ08          4Y 1O 11 34EDTCDE(1)
     A            DPQ10          7Y 0O 11 42EDTCDE(1)
     A            DPQ07          4Y 1O 11 57EDTCDE(1)
     A            DPQ09          7Y 0O 11 65EDTCDE(1)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  20020801  091927  PPSPGMREL   REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA411.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'生產獎金資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DPP01     R        O  4 13REFFLD(PP01)
     A            DPP01N         8A  O  4 16
     A                                  4 25'發放年月:'
     A            DPP03          5Y 0O  4 37EDTWRD('   /  ')
     A                                  4 45'部門:'
     A            DPP04          5A  O  4 53
     A            DPP04N         8A  O  4 59
     A                                  4 68'註記:'
     A            DPP06     R        O  4 76REFFLD(PP06)
     A                                  5  1'應計金額:'
     A            DPP09          9Y 0O  5 13EDTCDE(1)
     A                                  5 35'應計總工時:'
     A            DPP07     R        O  5 51REFFLD(PP08)
     A                                      EDTCDE(1)
     A                                  6  1'實際金額:'
     A            DPP10          9Y 0O  6 13EDTCDE(1)
     A                                  6 35'實際總工時:'
     A            DPP08     R        O  6 51REFFLD(PP08)
     A                                      EDTCDE(1)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0O  7 13
     A                                  7 16'(1.確認5.查詢)'
     A                                  8  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  9  1'序號 工號'
     A                                  9 19'姓　名'
     A                                  9 30'實際工時'
     A                                  9 42'實際獎金'
     A                                  9 53'應計工時'
     A                                  9 65'應計獎金'
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A*
     A          R DSPC1                     OVERLAY
     A                                 21  2'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =================='
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A*                                   +2'PF04 =欄位查詢'
     A                                    +2'PF12=回前畫面'
     A*                                   +2'PF05=試算'
     A                                 24 67'【泛太資訊】'
