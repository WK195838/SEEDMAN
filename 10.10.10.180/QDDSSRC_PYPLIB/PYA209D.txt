     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA209D                                    *
     A*    PROGRAM NAME :刷卡資料建立                              *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 97/01/20                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA209.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'刷卡資料建立'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DT505     R        B  4 13REFFLD(T505)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DT501     R        B  5 13REFFLD(T501)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'考勤日期:'
     A            DT502     R   -1   B  6 13REFFLD(T502)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      EDTWRD('   /  /  ')
     A                                  7  1'刷卡別　:'
     A            DT507     R        B  7 13REFFLD(T507)
     A                                      VALUES(' ' '1' '2')
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 22'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A          R DSPD2
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA12(12)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA209.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'刷卡資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  6 27'公司編號:'
     A            DT505     R        O  6 39REFFLD(T505)
     A            DT505N        14O  O  6 48
     A                                  7 27'員工編號:'
     A            DT501     R        O  7 39REFFLD(T501)
     A            EM03      R        O  7 48
     A                                  8 27'刷卡日期:'
     A            DT502     R   -1   O  8 39REFFLD(T502)
     A                                      EDTWRD('   /  /  ')
     A                                  9 27'刷卡別　:'
     A            DT507     R        O  9 39REFFLD(T507)
     A                                 12 27'刷卡時間:'
     A            DT503     R        B 12 39REFFLD(T503)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 12 42':'
     A            DT504     R        B 12 44REFFLD(T504)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 19'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
