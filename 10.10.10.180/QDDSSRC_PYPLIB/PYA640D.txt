     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA540D                                    *
     A*    PROGRAM NAME :每月扣繳健保費分攤                        *
     A*    AUTHOR       : A1445 HONDA                                *
     A*    CREATE DATE  : 95/03/20                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                      CF13(13)
     A                                  1  2'<PYA640>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'每月扣繳健保費分攤'
     A                                      DSPATR(RI)
     A*---------------------------------------------------------------
     A                                  5 28'公司編號:'
     A            DPW01          2A  B  5 40
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DCM03         44A  O  5 43
     A*-------------------------------------------------------------
     A                                  8 28'年　　月:'
     A            DPW03          5Y 0B  8 40
     A                                      EDTWRD('0  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A*
     A                                 14 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11' '
     A                                      DSPATR(RI)
     A  97                             16 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  96                             16 28'試算作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 16 66' '
     A                                      DSPATR(RI)
     A                                 17 11' '
     A                                      DSPATR(RI)
     A                                 17 66' '
     A                                      DSPATR(RI)
     A                                 18 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
