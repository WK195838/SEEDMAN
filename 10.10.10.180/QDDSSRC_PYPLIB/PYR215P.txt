     A****************************************************************/
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    */
     A*                                                              */
     A*    PROGRAM-ID   : PYR215P                                    */
     A*    PROGRAM NAME :考勤刷卡資料表                            */
     A*    AUTHOR       : A1274 EVA                                  */
     A*    CREATE DATE  : 90/07/05                                   */
     A*    UPDATE DATE  :                                            */
     A*    SYSTEM       :人事薪資系統                              */
     A****************************************************************/
     A                                      REF(PYRF)
     A*-------------------------------------------------------------***
     A          R PH1                       SKIPB(002)
     A            CM03      R             28
     A                                      SPACEA(001)
     A*
     A            WYMD1          8  0     22EDTWRD('    /  /  ')
     A                                    +0' ~ '
     A            WYMD2          8  0     +0EDTWRD('    /  /  ')
     A                                    +0'考勤刷卡資料表'
     A                                      SPACEA(001)
     A*
     A                                     2'  '
     A                                      SPACEA(002)
     A*
     A                                     1'部門編號:'
     A            T506      R             14
     A            DP02      R             23REFFLD(DP02 PYDPPF)
     A                                      SPACEA(001)
     A*
     A                                     1'員工編號:'
     A            T501      R             14
     A            EM03      R             22
     A                                    47'列印頁次:'
     A                                    +1PAGNBR
     A                                      SPACEA(001)
     A*
     A                                     1'到職日期:'
     A            EM13      R             14EDTWRD('    /  /  ')
     A                                    47'列印日期:'
     A            DDATE          6  0     +1EDTWRD('0  /  /  ')
     A                                    +1'<PYR215P>'
     A                                      SPACEA(001)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A*
     A                                     1'日期'
     A                                     9'週'
     A                                    15'上班'
     A                                    26'下班'
     A                                    37'異常說明'
     A                                    51'假別統計'
     A                                    61'備　　註'
     A                                      SPACEA(001)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ==========='
     A*-------------------------------------------------------------***
     A          R PD1                       SPACEB(001)
     A            P01            4  0      2EDTWRD('0  /  ')
     A            P02            4         9
     A            P03            5        15
     A            P05            4        +0
     A            P04            5        +2
     A            P06            4        +0
     A******      P07            4        48
     A            P08       R             53REFFLD(LC02)
     A            P09           20O       61
     A*
     A*-------------------------------------------------------------***
     A          R PD2                       SPACEB(001)
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A*-------------------------------------------------------------***
     A          R PT1                       SPACEB(001)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ==========='
     A*-------------------------------------------------------------***
     A          R PT2                       SPACEB(001)
     A*
     A                                     1''
     A                                      SPACEA(001)
     A*
     A                                     1'本表最遲於'
     A            MONTH          2  0     +1EDTCDE(4)
     A                                    +1'月'
     A            DAY            2  0     +1EDTCDE(4)
     A                                    +1'日擲回人事課'
     A                                      SPACEA(001)
     A*
     A                                     1''
     A                                      SPACEA(001)
     A*
     A                                     1'請注意〔＊〕標示之異常狀況！'
     A                                      SPACEA(001)
     A*-------------------------------------------------------------***
     A          R PE9                       SPACEB(001)
     A                                    16'無符合列印範圍之資料'
