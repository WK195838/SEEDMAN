     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYH118P                                    *
     A*    PROGRAM NAME :離職員工提撥金額查詢表                  *
     A*    AUTHOR       : MAX CHI                                    *
     A*    CREATE DATE  : 94/03/14                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A          R PH1
     A                                      SKIPB(002)
     A            CM03          34A        1
     A                                      SPACEA(001)
     A                                     1
     A                                      '離職員工提撥金額查詢表'
     A                                      SPACEA(002)
     A                                    80
     A                                      '日期:'
     A            DDATE          6  0     87
     A                                      EDTWRD('0  /  /  ')
     A                                    96
     A                                      '頁次:'
     A                                   103
     A                                      PAGNBR
     A                                      SPACEA(001)
     A                                     1
     A                                      '離職年月:'
     A            DYMX           5S 0O    13
     A                                      EDTWRD('   /  ')
     A                                    80
     A                                      '時間:'
     A                                    87
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    96
     A                                      '<PYH118P>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===='
     A                                      SPACEA(001)
     A                                     1
     A                                      '部　　門'
     A                                    23
     A                                      '員  工'
     A                                    49
     A                                      '離職日期'
     A                                    +5
     A                                      '企業提撥金額'
     A                                    +5
     A                                      '員工提繳金額'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===='
     A                                      SPACEA(001)
     A*
     A          R PD1
     A                                      SPACEA(001)
     A            EM04           7         2
     A            DP02          16         9
     A            H602           8A       26
     A            EM03          14A       34
     A            EM19X          7S       48
     A                                      EDTWRD('   /  /  ')
     A            H606           6  0     +9
     A                                      EDTCDE(1)
     A            H605           6  0     86
     A                                      EDTCDE(1)
     A                                      SPACEA(001)
     A          R PT1
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----'
     A                                      SPACEA(001)
     A                                     2
     A                                      '小計'
     A            @H602          9  0     20
     A                                      EDTCDE(1)
     A                                    35'名'
     A            @H606          9  0     63
     A                                      EDTCDE(1)
     A                                    78'元'
     A            @H605          9  0     82
     A                                      EDTCDE(1)
     A                                    97'元'
     A          R PT2
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===='
     A                                      SPACEA(001)
     A                                     2
     A                                      '合計'
     A            @@H602         9  0     20
     A                                      EDTCDE(1)
     A                                    35'名'
     A            @@H606         9  0     63
     A                                      EDTCDE(1)
     A                                    78'元'
     A            @@H605         9  0     82
     A                                      EDTCDE(1)
     A                                    97'元'
     A*********************************************************8
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===='
     A                                     1
     A                                      '　　　'
     A                                      SPACEB(001)
     A                                     1
     A                                      '總經理'
     A                                    30
     A                                      '廠　長'
     A                                    60
     A                                      '會計'
     A                                    90
     A                                      '覆核'
     A                                   122
     A                                      '製　表'
     A*-------------------------------------------------------------***
     A          R PE9
     A                                      SPACEA(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
