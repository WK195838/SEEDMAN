     A*%%TS  SD  19981202  133501  PPSPGMR40   REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI902D                                    *
     A*    PROGRAM NAME : 工作組全頁查詢                           *
     A*    AUTHOR       : A1268  VNAESSA                              *
     A*    CREATE DATE  : 98/09/24                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 　　　　　　                             *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19981202  133501  PPSPGMR40   REL-V3R1M0  5763-PW1
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      VALUES('/' ' ')
     A            XB02      R        O  8  7
     A            XB03      R        O  8 10
     A            XB04      R        O  8 45
     A            DXB06     R   -2   O  8 60REFFLD(XB06)
     A                                      EDTWRD('  /  /  ')
     A*----------------------------------------------------------------
     A*                                     CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19981202  133501  PPSPGMR40   REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI902>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A                                  2 34'工作組全頁查詢'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司編號:'
     A            DXB01     R        O  4 13REFFLD(XB01)
     A            DXB01N        14A  O  4 16
     A                                  5  1'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  6'工作組／說明　　　　　　　　　　-
     A                                      '
     A                                      DSPATR(UL)
     A                                  6 44'歸屬工作區'
     A                                      DSPATR(UL)
     A                                  6 59'生效日期'
     A                                      DSPATR(UL)
     A            DDB02     R        B  7  7REFFLD(XB02)
     A                                      DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            DDB04     R        B  7 45REFFLD(XB04)
     A                                      DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 29'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
