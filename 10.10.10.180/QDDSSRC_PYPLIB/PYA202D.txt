     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA202D                                    *
     A*    PROGRAM NAME :請假資料建立                              *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 86/01/20                                   *
     A*    UPDATE DATE  : 91/06/17 A1336 KLAUS                       *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA202.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'請假資料建立'
     A                                      DSPATR(RI)
     A*
     A                                  4  1'公司編號:'
     A            DLV01     R        B  4 13REFFLD(LV01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'部門編號:'
     A                                      DSPATR(HI)
     A            DLV13     R        B  5 13REFFLD(LV13)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  6  1'員工編號:'
     A                                      DSPATR(HI)
     A            DLV02     R        B  6 13REFFLD(LV02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7  1'請假日期:'
     A            DLV03     R   -2   B  7 13REFFLD(LV03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      EDTWRD('0  /  /  ')
     A*
     A                                  8  1'假　　別:'
     A                                      DSPATR(HI)
     A            DLV04     R        B  8 13REFFLD(LV04)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A**                                8 34'特休年度:'
     A**          DYEAR          4  0H      EDTWRD('0    ')
     A**64                                  DSPATR(PC)
     A**64                                  DSPATR(RI)
     A*
     A                                  9  1'作業選擇:'
     A            DOPT           1Y 0B  9 13VALUES(1 2 4 5)
     A                                  9 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                 10  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF13(13)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA202.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'請假資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  6 28'公　　司:'
     A            DLV01     R        O  6 40REFFLD(LV01)
     A            DLV01N        14O  O  6 49
     A                                  7 28'部　　門:'
     A            DLV13     R        O  7 40REFFLD(LV13)
     A            DLV14     R        O  7 49REFFLD(LV14)
     A                                  8 28'員　　工:'
     A            DLV02     R        O  8 40REFFLD(LV02)
     A            EM03      R        O  8 49
     A                                  9 28'假　　別:'
     A            DLV04     R        O  9 40REFFLD(LV04)
     A            DLC02     R        O  9 49REFFLD(LC02)
     A*                                10 28'特休基準日:'
     A*           DEM113    R   -1   O 10 42REFFLD(EM113)
     A*                                     EDTWRD('   /  /  ')
     A                                 12 20'起始日期:'
     A            DLV03     R   -2   O 12 32REFFLD(LV03)
     A                                      EDTWRD('0  /  /  ')
     A                                 12 45'終止日期:'
     A            DLV05     R   -2   B 12 57REFFLD(LV05)
     A                                      EDTWRD('0  /  /  ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                 14 20'起始時間:'
     A            DLV06     R        B 14 32REFFLD(LV06)
     A                                      EDTWRD('  :  ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                 14 45'終止時間:'
     A            DLV07     R        B 14 57REFFLD(LV07)
     A                                      EDTWRD('  :  ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 16 20'天　　數:'
     A            DLV09     R        B 16 32REFFLD(LV09)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 16 45'時　　數:'
     A            DLV08     R        B 16 57REFFLD(LV08)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A**                               17 16'扣除車馬津貼:'
     A            DLV12     R        H      REFFLD(LV12)
     A**                                    EDTCDE(4)
     A**31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A**                               17 36'次'
     A**                               17 45'大夜津貼:'
     A            DLV11     R        H      REFFLD(LV11)
     A**                                    EDTCDE(4)
     A**31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A**                               17 65'次'
     A*                                18 20'請假扣款:'
     A*           DLV15     R   -2   B 18 32REFFLD(LV15)
     A*                                     EDTCDE(1)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A* 66                                  DSPATR(PC)
     A* 66                                  DSPATR(RI)
     A*                                18 39'元'
     A                                 18 45'計薪年月:'
     A            DLV96          4Y 0B 18 57EDTWRD('0  /  ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A            HLV96          5Y 0H
     A**                               19 45'特休年度:'
     A**          DYEAR          4  0H      EDTWRD('0    ')
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  98        ERR           70A  O 22  2DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 19'PF12 =回前畫面'
     A                                 23 38'PF13 =資料確認'
     A                                 24 67'【泛太資訊】'
