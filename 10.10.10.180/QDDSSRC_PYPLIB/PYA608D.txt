     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA608D                                    *
     A*    PROGRAM NAME :綜合福利金設定批次處理　　                *
     A*    AUTHOR       : A1328 JEFFREY                              *
     A*    CREATE DATE  : 90/02/14                                   *
     A*    UPDATE DATE  : 90/03/22 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF13(13)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA608D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'綜合福利金設定批次處理'
     A                                      DSPATR(RI)
     A                                  6 28'福委會編號　:'
     A            DEM01     R        B  6 45REFFLD(EM01)
     A  60                                  DSPATR(PC RI)
     A            DEM01N    R        O  6 49REFFLD(CM02 SYCMPF)
     A*
     A                                  8 28'年度福利金額:'
     A            DMNY      R        B  8 45REFFLD(GH04 PYGHPF)
     A                                      EDTCDE(J)
     A  61                                  DSPATR(PC RI)
     A*
     A                                 10 28'福利年度　　:'
     A            DYEAR     R   -1   B 10 45REFFLD(GH03 PYGHPF)
     A                                      EDTCDE(4)
     A  62                                  DSPATR(PC RI)
     A*
     A                                 12 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 13 11' '
     A                                      DSPATR(RI)
     A                                 13 66' '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A  97                             14 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  28                             14 28'資料處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'F03 =回主畫面'
     A                                 23 20'F13 =處理開始'
     A                                 24 67'【泛太資訊】'
