     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有                   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA134D                                    *
     A*    PROGRAM NAME :健保証明單暫存檔維護                      *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 92/02/25                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYA134WF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA134.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'健保証明單暫存檔維護'
     A                                      DSPATR(RI)
     A*
     A                                  5  1'公司編號:'
     A            DW341     R        B  5 13REFFLD(W341)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  6  1'員工編號:'
     A                                      DSPATR(HI)
     A            DW342     R        B  6 13REFFLD(W342)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7  1'年　　度:'
     A            DW348     R        B  7 13REFFLD(W348)
     A                                      EDTWRD('0   ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                  8 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*============================================================*
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            2S 0O  9  4
     A            DW345     R        B  9 10REFFLD(W345)
     A                                      CHECK(RB)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31 32                               DSPATR(UL)
     A N31
     AON32                                  DSPATR(PR)
     A            DW344     R        B  9 23REFFLD(W344)
     A                                      CHECK(RB)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31 32                               DSPATR(UL)
     A N31
     AON32                                  DSPATR(PR)
     A            DW346     R        B  9 40REFFLD(W346)
     A                                      CHECK(RB)
     A                                      EDTCDE(K)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DW347     R        B  9 53REFFLD(W347)
     A                                      CHECK(RB)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*================================================================
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0012)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A                                  1  2'<PYA134.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'健保証明單暫存檔維護'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4  2'公　　司:'
     A            DW341     R        O  4 14REFFLD(W341)
     A                                  5  2'員　　工:'
     A            DW342     R        O  5 14REFFLD(W342)
     A            DW343     R        O  5 23REFFLD(W343)
     A                                  5 60'年　　度:'
     A            DW348     R        O  5 72REFFLD(W348)
     A                                      EDTWRD('0   ')
     A                                  6  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  7  2'序號'
     A                                      DSPATR(UL)
     A                                  7  9'身份証號'
     A                                      DSPATR(UL)
     A                                  7 23'姓　名'
     A                                      DSPATR(UL)
     A                                  7 42'金　額'
     A                                      DSPATR(UL)
     A                                  7 53'備註'
     A                                      DSPATR(UL)
     A*================================================================
     A          R DSPC1                     OVERLAY
     A                                 21  2'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =================='
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PF13=確認'
     A                                 24 67'【泛太資訊】'
