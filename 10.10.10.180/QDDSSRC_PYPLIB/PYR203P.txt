     A****************************************************************/
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   */
     A*                                                              */
     A*    PROGRAM-ID   : PYR203P                                    */
     A*    PROGRAM NAME :請假資料明細表　　　　　　　　　          */
     A*    AUTHOR       :                                            */
     A*    CREATE DATE  :                                            */
     A*    UPDATE DATE  : 90/04/16 A1274 EVA                         */
     A*    SYSTEM       :人事薪資系統                              */
     A****************************************************************/
     A                                      REF(PYRF)
     A*--------------------------------------------------------------**
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1
     A                                      SPACEA(001)
     A*
     A                                     1'請假資料明細表          '
     A                                      SPACEA(002)
     A*
     A                                     1
     A                                      '日　　期:'
     A            DLV031    R   -2        13
     A                                      REFFLD(OV03)
     A                                      EDTWRD('0  /  /  ')
     A                                    22
     A                                      '-'
     A            DLV032    R   -2        24
     A                                      REFFLD(OV03)
     A                                      EDTWRD('0  /  /  ')
     A                                    90'日期:'
     A            DDATE          6  0     98EDTWRD('0  /  /  ')
     A                                   108'頁次:'
     A                                   116PAGNBR
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門代號:'
     A            DEM041         7        +1
     A                                    +1
     A                                      '-'
     A            DEM042         7        +1
     A                                    65'單位：天.時'
     A                                    90'時間:'
     A                                    98TIME
     A                                      EDTWRD('  :  :  ')
     A                                   108'<PYR203P>'
     A                                      SPACEA(001)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==============='
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門名稱'
     A                                    23
     A                                      '員工姓名'
     A                                    47
     A                                      '起始日期'
     A                                    57
     A                                      '終止日期'
     A                                    67
     A                                      '起始時間'
     A                                    77
     A                                      '終止時間'
     A                                    87
     A                                      '假別'
     A                                    95
     A                                      '天數'
     A                                   103
     A                                      '時數'
     A                                   111'特休年度'
     A*                                  109'扣除交通津貼'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==============='
     A*--------------------------------------------------------------**
     A          R PD1
     A                                      SPACEB(001)
     A  31        EM041     R              2
     A  31        EM042     R              3
     A  31        EM043     R              4
     A  31        EM044     R              5
     A  31        DP02      R              9REFFLD(DP02 PYDPPF)
     A  32        LV02      R             25
     A  32        EM03      R             33
     A            DLV03          6  0     48
     A                                      EDTWRD('0  /  /  ')
     A            DLV05          6  0     58
     A                                      EDTWRD('0  /  /  ')
     A            LV06      R             70
     A                                      EDTWRD('  :  ')
     A            LV07      R             80
     A                                      EDTWRD('  :  ')
     A            PD11           8O       87
     A            PD12           4  0     96EDTCDE(3)
     A            PD13           5  2    103EDTCDE(3)
     A            YEAR           4  0    114EDTCDE(4)
     A*           LV12      R            109
     A*
     A          R PT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                    77
     A                                      '員工小計:'
     A            PT11           5  0     95
     A                                      EDTCDE(3)
     A  60        PT12           5  2    103EDTCDE(3)
     A*
     A          R PT2
     A                                      SPACEA(001)
     A                                    77
     A                                      '單位小計:'
     A            PT21           5  0     95
     A                                      EDTCDE(3)
     A  60        PT22           5  2    103EDTCDE(3)
     A*
     A          R PT3
     A                                      SPACEA(001)
     A                                    77
     A                                      '部門小計:'
     A            PT31           5  0     95
     A                                      EDTCDE(3)
     A  60        PT32           5  2    103EDTCDE(3)
     A*
     A          R PT4
     A                                      SPACEA(001)
     A                                    77
     A                                      '部門中計:'
     A            PT41           5  0     95
     A                                      EDTCDE(3)
     A  60        PT42           5  2    103EDTCDE(3)
     A*
     A          R PT5
     A                                      SPACEA(001)
     A                                    77
     A                                      '部門合計:'
     A            PT51           5  0     95
     A                                      EDTCDE(3)
     A  60        PT52           5  2    103EDTCDE(3)
     A*
     A*--------------------------------------------------------------**
     A          R PE1
     A                                      SPACEB(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A*
     A                                     1'PYR203P--->報表結束'
     A                                      SPACEB(001)
     A*--------------------------------------------------------------**
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
