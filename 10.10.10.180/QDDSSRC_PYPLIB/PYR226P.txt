     A****************************************************************/
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   */
     A*                                                              */
     A*    PROGRAM-ID   : PYR226P                                    */
     A*    PROGRAM NAME :獎懲資料明細表　　　　　　　　　          */
     A*    AUTHOR       : A1336 KLAUS                                */
     A*    CREATE DATE  : 91/07/16                                   */
     A*    UPDATE DATE  :                                            */
     A*    SYSTEM       :人事薪資系統                              */
     A****************************************************************/
     A                                      REF(PYRF)
     A*--------------------------------------------------------------**
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1
     A                                      SPACEA(001)
     A                                     1
     A                                      '獎懲資料明細表          '
     A                                      SPACEA(002)
     A                                     1
     A                                      '獎懲日期:'
     A            DXX051    R   -1        13
     A                                      REFFLD(XX05)
     A                                      EDTWRD('   /  /  ')
     A                                    23
     A                                      '-'
     A            DXX052    R   -1        25
     A                                      REFFLD(XX05)
     A                                      EDTWRD('   /  /  ')
     A                                    96
     A                                      '日期:'
     A            DDATE          6  0    104
     A                                      EDTWRD('0  /  /  ')
     A                                   113
     A                                      '頁次:'
     A                                   121
     A                                      PAGNBR
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      '部門代號:'
     A            DXX021         7        +1
     A                                    +1
     A                                      '-'
     A            DXX022         7        +1
     A                                    96
     A                                      '時間:'
     A                                   104
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   113
     A                                      '<PYR226P>'
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ====================='
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      '部　　門'
     A                                    23
     A                                      '員　　工'
     A                                    47
     A                                      '獎懲類別'
     A                                    63
     A                                      '獎懲日期'
     A                                    75
     A                                      '次數'
     A                                    83
     A                                      '文件代號'
     A                                    95
     A                                      '獎懲原因'
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ====================='
     A                                      SPACEA(001)
     A*-------------------------------------------------------------***
     A          R PD1
     A            XX08      R             77
     A                                      EDTCDE(4)
     A N31        XX02      R              1
     A N31        XX03      R              8
     A N32        XX04      R             24
     A N32        EM03      R             +1
     A            XX06      R             +1
     A            XX06N         10O       +1
     A            PXX05          7  0     63
     A                                      EDTWRD('   /  /  ')
     A            XX09      R             83
     A            XX07      R             95
     A                                      SPACEA(001)
     A*-------------------------------------------------------------***
     A          R PT5
     A                                    21
     A                                      '公司總計==>'
     A            PT51           4  0     35
     A                                      EDTCDE(1)
     A                                    41
     A                                      '筆'
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ====================='
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      '董事長'
     A                                    20
     A                                      '總經理'
     A                                    40
     A                                      '副總經理'
     A                                    62
     A                                      '經理'
     A                                    82
     A                                      '課長'
     A                                   102
     A                                      '製表人'
     A*-------------------------------------------------------------***
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
