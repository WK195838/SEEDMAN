     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA911D                                    *
     A*    PROGRAM NAME : 戶號基本資料維護                        *
     A*    AUTHOR       : A1275                                      *
     A*    CREATE DATE  : 99/01/21                                  *
     A*    UPDATE DATE  : 99/01/29 VAN                               *
     A*    SYSTEM       : 租賃管理系統                             *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A*****                                 CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA911.1>'
     A                                  1 35'基本資料維護'
     A*                                     DSPATR(RI)
     A            $WSID         10   O  1 70
     A            $USER         10A  O  2  2
     A                                  2 33'員工出勤更新作業'
     A                                      DSPATR(RI)
     A            DDATE          6Y 0O  2 70EDTCDE(Y)
     A                                  4  1'公司編號:'
     A            DEM01     R        B  4 13REFFLD(EM01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A****        EM01N         20   O  4 16
     A                                  5  1'員工編號:'
     A            DEM02     R        B  5 13REFFLD(EM02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            EM03      R        O  5 22
     A                                  6  1'出勤年月:'
     A            DXK04          6Y 0B  6 13EDTWRD('    /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'Enter'
     A                                 23 10'F03=結束'
     A*****                               +2'F04=欄位查詢'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            3S 0H
     A            DNEW           1A  H
     A***         DDEL           1A  H
     A            XK04           8Y 0O  9  2EDTWRD('    /  /  ')
     A            XK06      R        O  9 18
     A            DXK07     R        B  9 30REFFLD(XK07)
     A                                      EDTCDE(1)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DXK08     R        B  9 42REFFLD(XK08)
     A                                      EDTCDE(1)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DXK09     R        B  9 54REFFLD(XK09)
     A                                      EDTCDE(1)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DXK10     R        B  9 69REFFLD(XK10)
     A                                      EDTCDE(1)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0130)
     A                                      SFLPAG(0013)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<RUA004.2>'
     A                                  1 35'基本資料維護'
     A            $WSID         10A  O  1 70
     A            $USER         10A  O  2  2
     A                                  2 33'員工出勤更新作業'
     A                                      DSPATR(RI)
     A            DDATE          6Y 0O  2 70EDTCDE(Y)
     A                                  4  1'公司編號:'
     A            DEM01     R        O  4 13REFFLD(EM01)
     A                                  5  1'員工編號:'
     A            DEM02     R        O  5 13REFFLD(EM02)
     A            EM03          14   O  5 22
     A                                  6  1'出勤年月:'
     A            DXK04          6Y 0O  6 13EDTWRD('    /  ')
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  8  2'  日期  '
     A                                      DSPATR(UL)
     A                                  8 16'勤別 '
     A                                      DSPATR(UL)
     A                                  8 27'輪班津貼'
     A                                      DSPATR(UL)
     A                                  8 40'計程車'
     A                                      DSPATR(UL)
     A                                  8 50'交通－台北'
     A                                      DSPATR(UL)
     A                                  8 65'交通－其他'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'Enter'
     A                                    +2'F03=結束'
     A                                    +2'F12 =回前畫面'
     A*****                               +2'F5 =參考資料'
     A                                    +2'PA1/PA2=上一頁/下一頁'
