     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR711P                                    *
     A*    PROGRAM NAME :各類所得扣繳資料表－明細　　              *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/08/23                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      REF(PYRF)
     A*------------------------------------------------------------***
     A          R PH1                       SKIPB(2)
     A            CM03      R              1
     A                                      SPACEA(1)
     A*
     A            DYEAR          3  0      2EDTCDE(4)
     A                                    +1'各類所得扣繳資料表－明細'
     A                                      SPACEA(2)
     A*
     A                                     1'作帳單位:'
     A            DEM041         7        13
     A                                    +1'-'
     A            DEM042         7        +1
     A                                   101'日期:'
     A            DDATE          6  0    109
     A                                      EDTWRD('  /  /  ')
     A                                   118'頁次:'
     A                                   126PAGNBR
     A                                      SPACEA(1)
     A*
     A                                     1'身份證號:'
     A            DTB051    R             13REFFLD(TB05 PYTBPF)
     A                                    +1'-'
     A            DTB052    R             +1REFFLD(TB05 PYTBPF)
     A                                    50'所得類別:'
     A            DTB041    R             +1REFFLD(TB04 PYTBPF)
     A                                    +1'-'
     A            DTB042    R             +1REFFLD(TB04 PYTBPF)
     A                                   101'時間:'
     A                                   109TIME
     A                                      EDTWRD('  :  :  ')
     A                                   118'<PYR711P>'
     A                                      SPACEA(1)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==================================='
     A*------------------------------------------------------------***
     A          R PD1                       SPACEB(001)
     A*
     A                                     1'所得人姓名:'
     A            TB13      R             +1REFFLD(TB13 PYTBPF)
     A                                    50'員工編號:'
     A            TB12      R             +1REFFLD(TB12 PYTBPF)
     A                                      SPACEA(1)
     A*
     A                                     1'證號資料　:'
     A            TB05      R             +1REFFLD(TB05 PYTBPF)
     A            PTB06         20O       48
     A                                      SPACEA(1)
     A*
     A                                     1'所得人地址:'
     A            TB14      R             +1REFFLD(TB14 PYTBPF)
     A                                      SPACEA(1)
     A*
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------------------------------'
     A                                      SPACEA(1)
     A*
     A                                     1'給付日期'
     A                                    13'格式代號'
     A                                    25'特定編號'
     A                                    42'作帳單位'
     A                                    58'所得年月'
     A                                    79'給付總額'
     A                                    94'扣繳率'
     A                                   107'扣繳稅額'
     A                                   124'給付淨額'
     A                                      SPACEA(1)
     A*
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------------------------------'
     A*
     A*------------------------------------------------------------***
     A          R PD2                       SPACEB(001)
     A            PTB18          7  0      1EDTWRD('   /  /  ')
     A            TB04      R             17
     A            TB09      R             25
     A            TB19      R             45
     A            PTB15          5  0     55EDTWRD('   /  ')
     A                                    63'-'
     A            PTB16          5  0     65EDTWRD('   /  ')
     A            TB07      R             77EDTCDE(J)
     A            PRATE          5  2     94EDTCDE(3)
     A                                    +1'%'
     A            TB08      R            105EDTCDE(J)
     A            PNET           9  0    122EDTCDE(J)
     A*------------------------------------------------------------***
     A          R PT1                       SPACEB(1)
     A*
     A                                     2'小　　　計:'
     A            PT11           9  0     77EDTCDE(J)
     A            PT12           9  0    105EDTCDE(J)
     A            PT13           9  0    122EDTCDE(J)
     A                                      SPACEA(1)
     A*
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------------------------------'
     A*------------------------------------------------------------***
     A          R PT2                       SPACEB(001)
     A*
     A                                     2'合　　　計:'
     A            PT21           9  0     77EDTCDE(J)
     A            PT22           9  0    105EDTCDE(J)
     A            PT23           9  0    122EDTCDE(J)
     A                                      SPACEA(1)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEA(001)
     A*------------------------------------------------------------***
     A          R PE1                       SPACEB(001)
     A                                     1'PYR711P--->報表結束'
     A*------------------------------------------------------------***
     A          R PE9
     A                                      SPACEB(001)
     A                                    16'無符合列印範圍之資料'
