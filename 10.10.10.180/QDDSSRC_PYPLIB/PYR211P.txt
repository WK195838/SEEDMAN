     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYR220P                                    *
     A*    PROGRAM NAME :刷卡資料異常報表
     A*    AUTHOR       : A1184                                      *
     A*    CREATE DATE  : 85/03/06                                   *
     A*    UPDATE DATE  : 89/08/25 A1274 EVA                         *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1                       SKIPB(002)
     A            CM03      R              1
     A                                    94'日期:'
     A            DDATE          6  0    102EDTWRD('0  /  /  ')
     A                                   111'頁次:'
     A                                   119PAGNBR
     A                                      SPACEA(001)
     A                                     1'刷卡資料異常報表'
     A                                    94'時間:'
     A                                   102TIME
     A                                      EDTWRD('  :  :  ')
     A                                   112'<PYR211P>'
     A                                      SPACEA(002)
     A                                     1'部　　門:'
     A            EM04           7        14
     A            DP02      R             23REFFLD(DP02 PYDPPF)
     A                                      SPACEA(001)
     A                                     1'==================================+
     A                                       ==================================+
     A                                       ==================================+
     A                                       ==========================='
     A                                      SPACEA(001)
     A                                    80'實際刷卡'
     A                                    99'應　　刷'
     A                                      SPACEA(001)
     A                                    96'上班'
     A                                   106'下班'
     A                                   114'更正狀態'
     A                                     1'員工編號'
     A                                    12'員工姓名'
     A                                    26'刷卡日期'
     A                                    39'星期'
     A                                    48'異常時間'
     A                                    60'異常狀態'
     A                                    77'上班'
     A                                    87'下班'
     A                                      SPACEA(001)
     A                                     1'==================================+
     A                                       ==================================+
     A                                       ==================================+
     A                                       ==========================='
     A*
     A          R PD1                       SPACEB(001)
     A  31        T603      R              2
     A  31        EM03      R             12
     A            PT601          7  0     27EDTWRD('   /  /  ')
     A            TFUN           8O       38
     A            T605      R             49EDTCDE(1)
     A                                    52':'
     A            T606      R             54EDTCDE(1)
     A            T604      R             59
     A            T604N         16O       60
     A            T612H          2  0     77EDTCDE(1)
     A                                    80':'
     A            T612M          2  0     82
     A                                      EDTCDE(1)
     A            T613H          2  0     86EDTCDE(1)
     A                                    89':'
     A            T613M          2  0     91EDTCDE(1)
     A            T614H          2  0     96EDTCDE(1)
     A                                    99':'
     A            T614M          2  0    101EDTCDE(1)
     A            T615H          2  0    105EDTCDE(1)
     A                                   108':'
     A            T615M          2  0    110EDTCDE(1)
     A            T607      R            114
     A            T607N         16O      115
     A*
     A          R PT1                       SPACEB(001)
     A                                     1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ----------------------------------+
     A                                       ---------------------------'
     A                                      SPACEA(001)
     A                                     1'人數小計:'
     A            PT11           3  0     13EDTCDE(3)
     A                                      SPACEA(001)
     A                                     1'==================================+
     A                                       ==================================+
     A                                       ==================================+
     A                                       ==========================='
     A*
     A          R PE9                       SPACEB(001)
     A                                    16'無符合列印範圍之資料'
