     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYR301D                                    *
     A*    PROGRAM NAME :薪資基本資料列印                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 1997/01/15                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<PYR301D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'薪資基本資料列印'
     A                                      DSPATR(RI)
     A*
     A                                  5 28'公司編號　　　:'
     A            DEM01     R        B  5 46REFFLD(EM01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 28'部門編號　　起:'
     A                                      DSPATR(HI)
     A            DEM041         7A  B  7 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM042         7A  B  8 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 28'員工編號　　起:'
     A                                      DSPATR(HI)
     A            DEM021    R        B 10 46REFFLD(EM02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 11 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM022    R        B 11 46REFFLD(EM02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 13 28'離職人員列印否:'
     A            DYN            1A  B 13 46VALUES('Y' 'N')
     A                                 13 50'(Y/N)'
     A*
     A*                                15 28'國　　　　　籍:'
     A*           DEM321         1A  B    +1
     A*                                     VALUES('L' 'F')
     A* 64                                  DSPATR(PC RI)
     A*                                   +1'(L-國內F-外籍)'
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 27'***請準備１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
