     A****************************************************************
     A*    PRTF NAME    : PYR328P                                    *
     A*    PROGRAM NAME : PYR328                                     *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 07/08/23                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : MIRS                                       *
     A*    REMARK       :工讀生人事主檔列印                        *
     A****************************************************************
     A                                      REF(*LIBL/PYPTPF)
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R             26
     A                                      REFFLD(CM03 SYCMPF)
     A                                    90
     A                                      '日期:'
     A            DDATE          6S 0     98
     A                                      EDTWRD('0  /  /  ')
     A                                   107
     A                                      '頁次:'
     A                                   115
     A                                      PAGNBR
     A                                      SPACEA(001)
     A                                    23
     A                                      '工讀生基本資料明細表'
     A                                    90
     A                                      '時間:'
     A                                    98
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   108
     A                                      '<PYR328P>'
     A                                      SPACEA(002)
     A                                     1
     A                                      '公司代號:'
     A            SPT01          2        13
     A                                    +1
     A                                      '-'
     A            EPT01          2        +1
     A  38                                21
     A                                      '計薪年月'
     A  39                                21
     A                                      '給付年月'
     A            DPT03          4  0     +1
     A                                      EDTWRD('0  /  ')
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEA(001)
     A                                     1
     A                                      '公  司'
     A                                    13
     A                                      '員　　工'
     A                                    25
     A                                      '身分証號'
     A                                    38
     A                                      '基本薪資'
     A                                    53
     A                                      '獎金'
     A                                    64
     A                                      '稅額'
     A                                    75
     A                                      '勞保'
     A                                    86
     A                                      '健保'
     A                                    97
     A                                      '勞退'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A          R PD1
     A                                      SPACEB(001)
     A  91        DCM02          6         2
     A            DEN03         10        13
     A            PT02      R             25
     A            PT05      R             38
     A                                      EDTCDE(1)
     A            PT06      R             49
     A                                      EDTCDE(1)
     A            PT07      R             63
     A                                      EDTCDE(1)
     A            PT08      R             74
     A                                      EDTCDE(1)
     A            PT09      R             85
     A                                      EDTCDE(1)
     A            PT10      R             96
     A                                      EDTCDE(1)
     A*
     A          R PT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      '小計:'
     A            PTO1           5  0     13
     A                                      EDTCDE(1)
     A            PTO12          9  0     37
     A                                      EDTCDE(1)
     A            PTO13          9  0     48
     A                                      EDTCDE(1)
     A            PTO14          7  0     61
     A                                      EDTCDE(1)
     A            PTO15          7  0     72
     A                                      EDTCDE(1)
     A            PTO16          7  0     83
     A                                      EDTCDE(1)
     A            PTO17          7  0     94
     A                                      EDTCDE(1)
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------'
     A                                      SPACEA(001)
     A*
     A          R PT2
     A                                      SPACEB(002)
     A                                     1
     A                                      '總計:'
     A            PTO2           5  0     13
     A                                      EDTCDE(1)
     A            PTO22          9  0     37
     A                                      EDTCDE(1)
     A            PTO23          9  0     48
     A                                      EDTCDE(1)
     A            PTO24          7  0     61
     A                                      EDTCDE(1)
     A            PTO25          7  0     72
     A                                      EDTCDE(1)
     A            PTO26          7  0     83
     A                                      EDTCDE(1)
     A            PTO27          7  0     94
     A                                      EDTCDE(1)
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEB(001)
     A                                     1
     A                                      'PYR328P--->報表結束'
     A                                      SPACEB(001)
     A*
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
