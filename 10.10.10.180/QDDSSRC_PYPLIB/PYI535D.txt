     A*%%TS  SD  20000926  135205  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI535D                                    *
     A*    PROGRAM NAME :薪資代碼查詢　　                          *
     A*    AUTHOR       : TOM T.                                     *
     A*    CREATE DATE  : 89/09/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  20000926  115459  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A            RRN            3Y 0H
     A            DOPT           1A  B  6  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            SS18      R        O  6  7
     A            SS01      R        O  6 17
     A            SS02      R        O  6 25
     A            SS04N          6O  O  6 44
     A            SS06N         10O  O  6 55
     A            SS09      R        O  6 71
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  20000926  135205  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0160)
     A                                      SFLPAG(0016)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI535D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'薪資代碼查詢'
     A                                      DSPATR(RI)
     A                                  3  1'項目說明:'
     A            DSS05     R        O  3 13REFFLD(SS05)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DSS05N        14O  O  3 15
     A                                  4  2'S'
     A                                      DSPATR(UL)
     A                                  4  5'組別'
     A                                      DSPATR(UL)
     A                                  4 13'薪資代碼'
     A                                      DSPATR(UL)
     A                                  4 25'  薪資名稱  '
     A                                      DSPATR(UL)
     A                                  4 41'加扣項說明'
     A                                      DSPATR(UL)
     A                                  4 54'薪資別說明'
     A                                      DSPATR(UL)
     A                                  4 67'項目屬性'
     A                                      DSPATR(UL)
     A            DSS18     R        B  5  7REFFLD(SS18)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DSS01     R        B  5 17REFFLD(SS01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 23 36'PA1=下一頁'
     A                                 23 50'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
