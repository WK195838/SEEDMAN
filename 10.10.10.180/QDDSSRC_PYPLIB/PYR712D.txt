     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR712D                                    *
     A*    PROGRAM NAME :薪資所得銀行不轉存資料表                  *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 2001/11/07                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A*------------------------------------------------------------***
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<PYR712D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 28'薪資所得銀行不轉存資料表'
     A                                      DSPATR(RI)
     A*
     A                                  5 26'公司編號:'
     A            DBC01     R        B  5 38REFFLD(BC01 PYBCPF)
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 26'存入月份:'
     A            DBC04     R   -1   B  7 38REFFLD(BC04)
     A                                      EDTWRD('0   /  ')
     A  61                                  DSPATR(PC RI)
     A                                  9 26'存入日期:'
     A            DBC05     R        B  9 38REFFLD(BC05)
     A                                      EDTCDE(4)
     A  62                                  DSPATR(PC RI)
     A                                 11 26'所得類別:'
     A            DBC03     R        B 11 38REFFLD(BC03 PYBCPF)
     A                                      VALUES('1' '2' '5')
     A  63                                  DSPATR(PC RI)
     A                                 11 42'1. 5日薪資'
     A                                 12 42'2. 20日薪資'
     A                                 13 42'5.公司獎金'
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 27'***請準備好８０格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
