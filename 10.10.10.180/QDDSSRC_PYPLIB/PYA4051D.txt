     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYA405D                                    *
     A*    AUTHOR       : A1138  DAVID.                              *
     A*    CREATE DATE  : 81/03/10                                   *
     A*    UPDATE DATE  : 90/05/18 KLAUS                             *
     A*    PROGRAM NAME : PYA405                                     *
     A*    SYSTEM       : MIRS                                       *
     A*    SUBSYSTEM    : PYS                                        *
     A*    REMARK       :公司獎金資料調整作業                      *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA405.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'公司獎金資料調整作業'
     A                                      DSPATR(RI)
     A*
     A                                  4  1'公司編號:'
     A            DPB01     R        B  4 13REFFLD(PB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DPB02     R        B  5 13REFFLD(PB02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'獎金年度:'
     A            DPB03          3Y 0B  6 13
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A                                  7  1'獎金類別:'
     A                                      DSPATR(HI)
     A            DPB04          2   B  7 13
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                    +1'(01.獎金02.股票紅利'
     A                                    +1'03.現金紅利)'
     A                                  8  1'發放日期:'
     A            DPB05          7Y 0B  8 13EDTWRD('   /  /  ')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                  8  1'作業選擇:'
     A            DOPT           1A  B  8 13VALUES('1' '2' '4' '5')
     A                                  8 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                 09  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*------------------------------------------------------------***
     A          R DSPC2
     A                                      CA03(03)
     A                                      CA12(12)
     A                                  1  2'<PYA405.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'公司獎金資料調整作業'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A*
     A                                  4  3'公　　司:'
     A            DPB01     R        O  4 15REFFLD(PB01)
     A            CM02      R        O  4 24
     A                                  5  3'部　　門:'
     A            DEM04          7A  O  5 15
     A            DP02      R        O  5 24REFFLD(DP02 PYDPPF)
     A                                  6  3'員　　工:'
     A            DPB02     R        O  6 15REFFLD(PB02)
     A            EM03      R        O  6 24
     A                                  7  3'年　　度:'
     A            DPB03          3S 0O  7 15
     A                                  8  3'類　　別:'
     A            DPB04          2A  O  8 15
     A                                    +1'(01.獎金02.股票紅利'
     A                                    +1'03.現金紅利)'
     A            SS02          14A  O  8 24
     A                                  9  3'發放日期:'
     A            DPB05          7Y 0O  9 15EDTWRD('   /  /  ')
     A
     A                                    +1'代扣稅款年月:'
     A            DOS03          5  0O    +1
     A                                      EDTWRD('   /  ')
     A                                  9  3'作業選擇:'
     A            DOPT           1A  O  9 15
     A                                  9 17'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                 10  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 12  3'發放金額:'
     A            DPB06     R        B 12 15REFFLD(PB06 PYPBPF)
     A                                      EDTCDE(1)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  66                                  DSPATR(PC RI)
     A                                 12 28'獎金稅率:'
     A            DPB07     R        B 12 40REFFLD(PB07 PYPBPF)
     A                                      EDTCDE(1)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  67                                  DSPATR(PC RI)
     A                                 12 54'扣繳稅額:'
     A            DPB08     R        B 12 66REFFLD(PB08 PYPBPF)
     A                                      EDTCDE(1)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  68                                  DSPATR(PC RI)
     A*
     A            DPB11     R        B 14 66REFFLD(PB11 PYPBPF)
     A                                      EDTCDE(1)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  71                                  DSPATR(PC RI)
     A*
     A                                 16  3'實領總額:'
     A            DPB12     R        O 16 15REFFLD(PB12 PYPBPF)
     A                                      EDTCDE(1)
     A*
     A                                 18  3'說　明　:'
     A            DPB13     R        B 18 15REFFLD(PB13 PYPBPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  73                                  DSPATR(PC RI)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
