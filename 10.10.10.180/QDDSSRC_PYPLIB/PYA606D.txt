     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA606D                                    *
     A*    PROGRAM NAME :綜合福利金年度餘額計算                    *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/04/17                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*------------------------------------------------------------***
     A          R DSPC1
     A                                      OVERLAY
     A                                      CA03(03 'RETURN')
     A                                      CF04(04 'PROMPT')
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYA606.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'綜合福利金年度餘額計算'
     A                                      DSPATR(RI)
     A*
     A                                  4  1'福委會　:'
     A            DGH01     R        B  4 13REFFLD(GH01 PYGHPF)
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DGH01N    R        O  4 23REFFLD(CM02 SYCMPF)
     A*
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DGH02     R        B  5 13REFFLD(GH02 PYGHPF)
     A  61                                  DSPATR(PC RI)
     A            DGH02N    R        O  5 23REFFLD(EM03 PYEMPF)
     A*
     A                                  6  1'福利年度:'
     A            DGH03     R   -1   B  6 13REFFLD(GH03 PYGHPF)
     A                                      EDTCDE(4)
     A  62                                  DSPATR(PC RI)
     A*
     A                                  7  1'預定離職日期:'
     A            DGH07     R   -1   B  7 17REFFLD(GH07 PYGHPF)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC RI)
     A*
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            DOPT           1Y 0H
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                      OVERLAY
     A                                      CA03(03 'RETURN')
     A                                      CA12(12 'PREVIOUSE')
     A                                  1  2'<PYA606.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'綜合福利金年度餘額計算'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A*
     A                                  4  1'福委會　:'
     A            DGH01     R        O  4 13REFFLD(GH01 PYGHPF)
     A            DGH01N    R        O  4 23REFFLD(CM02 SYCMPF)
     A*
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DGH02     R        O  5 13REFFLD(GH02 PYGHPF)
     A            DGH02N    R        O  5 23REFFLD(EM03 PYEMPF)
     A*
     A                                  6  1'福利年度:'
     A            DGH03     R   -1   O  6 13REFFLD(GH03 PYGHPF)
     A                                      EDTCDE(4)
     A                                  6 23'(中國年)'
     A*
     A                                  7  1'預定離職日期：'
     A            DGH07     R   -1   O  7 18REFFLD(GH07 PYGHPF)
     A                                      EDTWRD('   /  /  ')
     A*
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A                                  9 34'＜按比例計算＞'
     A                                      COLOR(PNK)
     A*
     A                                 11  1'年度福利金額：'
     A            DGH041    R        O 11 18REFFLD(GH04 PYGHPF)
     A                                      EDTCDE(J)
     A            DGH042    R        O 11 37REFFLD(GH04 PYGHPF)
     A                                      EDTCDE(J)
     A*
     A                                 13  1'個人申請總額：'
     A            DPAY1     R        O 13 18REFFLD(GH04 PYGHPF)
     A                                      EDTCDE(J)
     A            DPAY2     R        O 13 37REFFLD(GH04 PYGHPF)
     A                                      EDTCDE(J)
     A*
     A                                 15  1'福利金餘額：'
     A            DRST1     R        O 15 18REFFLD(GH04 PYGHPF)
     A                                      EDTCDE(J)
     A            DRST2     R        O 15 37REFFLD(GH04 PYGHPF)
     A                                      EDTCDE(J)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
