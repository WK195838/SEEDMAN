     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYH101D                                    *
     A*    PROGRAM NAME :退休金資料維護                            *
     A*    AUTHOR       : CLOUD                                      *
     A*    CREATE DATE  : 96/03/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :新制勞退系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYH3PF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYH101.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'退休金資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DH301     R        B  4 13REFFLD(H301 *LIBL/PYH3PF)
     A  60                                  DSPATR(PC RI)
     A N32                                  DSPATR(PR)
     A  32                                  DSPATR(UL)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DH302     R        B  5 13REFFLD(H302 *LIBL/PYH3PF)
     A  61                                  DSPATR(PC RI)
     A                                  6  1'生效日期:'
     A            DH303     R   -2   B  6 13REFFLD(H303 *LIBL/PYH3PF)
     A                                      EDTWRD('0  /  /  ')
     A  62                                  DSPATR(PC RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CHGINPDFT(UL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF04(04)
     A                                      CF13(13)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYH101.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'退休金資料維護'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4 14'公　　　司:'
     A            DH301     R        O  4 34REFFLD(H301)
     A            CM02          14A  O  4 37
     A                                  5 14'員　　　工:'
     A            DH302     R        O  5 34REFFLD(H302 *LIBL/PYH3PF)
     A            EM03          14A  O  5 43
     A                                  6 14'生效日期:'
     A            DH303          6  0O  6 34EDTWRD('0  /  /  ')
     A                                  7 14'異動類別:'
     A            DH304     R        B  7 34REFFLD(H304)
     A  60                                  DSPATR(PC RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                  7 36'(1.加保2.調整3.退保)'
     A                                  8 14'新舊選擇碼:'
     A            DH305     R        B  8 34REFFLD(H305)
     A                                      VALUES('1' '2')
     A  61                                  DSPATR(PC RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                  8 36'(1.新 2.舊)'
     A                                  9 14'新制選擇日:'
     A            DH306          6  0O  9 34EDTWRD('0  /  /  ')
     A* 62                                  DSPATR(PC RI)
     A*N31                                  DSPATR(PR)
     A* 31                                  DSPATR(UL)
     A                                 10 14'公司是否提撥:'
     A            DH307     R        B 10 34REFFLD(H307)
     A                                      VALUES('Y' 'N')
     A  63                                  DSPATR(PC RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 10 36'(Y提撥N不提撥)'
     A                                 11 14'個人帳戶　:'
     A            DH308     R        B 11 34REFFLD(H308)
     A  64                                  DSPATR(PC RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 12 14'員工自提率:'
     A            DH309     R        B 12 34REFFLD(H309)
     A  65                                  DSPATR(PC RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                      EDTCDE(1)
     A                                 12 39'%'
     A                                 13 14'企業提撥率:'
     A            DH310     R        B 13 34REFFLD(H310)
     A  66                                  DSPATR(PC RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                      EDTCDE(1)
     A                                 13 39'%'
     A                                 14 14'月提繳工資:'
     A                                      DSPATR(HI)
     A            DH311     R        B 14 34REFFLD(H311)
     A  67                                  DSPATR(PC RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                      EDTCDE(1)
     A                                 18 14'自提累積金額:'
     A            DH412     R        O 18 34REFFLD(H412 *LIBL/PYH4PF)
     A                                      EDTCDE(1)
     A                                 19 14'企提累積金額:'
     A            DH413     R        O 19 34REFFLD(H413 *LIBL/PYH4PF)
     A                                      EDTCDE(1)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF13 =異動執行'
     A                                 23 56'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
