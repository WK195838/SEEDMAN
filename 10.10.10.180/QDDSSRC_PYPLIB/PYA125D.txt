     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYA125D                                    *
     A*    AUTHOR       : TOM T.                                     *
     A*    CREATE DATE  : 89/08/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PYA125                                     *
     A*    SYSTEM       : MIRS                                       *
     A*    SUBSYSTEM    : PY                                         *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA125.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'人事異動資料建立'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DTX01     R        B  4 13REFFLD(TX01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DTX02     R        B  5 13REFFLD(TX02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'生效日期:'
     A            DTX04     R   -2   B  6 13REFFLD(TX04)
     A                                      EDTWRD('0  /  /  ')
     A                                      CHECK(RB)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'異動類別:'
     A            DTX03          2A  B  7 13
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                      VALUES('D1' 'D2' 'D5' 'D6' 'Z1' 'Z2-
     A                                      ')
     A                                  7 16'(D1.留職停薪  D2.留職復職  D5.-
     A                                      長期病假  D6.長病假復職'
     A                                  8 17'Z1.離職Z2.退休)'
     A                                  9  1'作業選擇:'
     A            DOPT           1A  B  9 13VALUES('1' '2' '4' '5')
     A                                  9 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                 10  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 15  3' '
     A                                 15 44' '
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                      CHGINPDFT(UL)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CA12(12)
     A                                      CF05(05)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA125.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'人事異動資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A            MODE          12O  O  3 35DSPATR(HI)
     A                                  7 24'公　　司:'
     A            DTX01     R        O  7 36REFFLD(TX01)
     A            CM02      R        O  7 45
     A                                  8 24'員　　工:'
     A            DTX02     R        O  8 36REFFLD(TX02)
     A            EM03      R        O  8 45
     A                                  9 24'生效日期:'
     A            DTX04     R   -2   O  9 36REFFLD(TX04)
     A                                      EDTWRD('0  /  /  ')
     A*                                12 24'金　　額:'
     A*           DTX05     R        B 12 36REFFLD(TX05)
     A*                                     EDTCDE(4)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A* 61                                  DSPATR(PC)
     A* 61                                  DSPATR(RI)
     A*                                13 24'天　　數:'
     A*           DTX07     R        B 13 36REFFLD(TX07)
     A*                                     EDTCDE(4)
     A* 34                                  DSPATR(UL)
     A*N34                                  DSPATR(PR)
     A* 62                                  DSPATR(PC)
     A* 62                                  DSPATR(RI)
     A                                 14 24'異動原因:'
     A  33                                  DSPATR(HI)
     A            DTX12     R        B 14 36REFFLD(TX12)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A            QJ02      R        O 14 40
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF05 =參考資料'
     A                                 23 56'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
