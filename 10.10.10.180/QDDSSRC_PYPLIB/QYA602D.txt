     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*    PROGRAM-ID   : PYA602D                                    *
     A*    PROGRAM NAME :福委會登錄作業維護                        *
     A*    AUTHOR       : A1276 VINCENT                              *
     A*    CREATE DATE  : 88/01/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #FLD          10A  H
     A            #REC          10A  H
     A                                  1  2'<PYA602>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 35'福委會作業'
     A                                      DSPATR(RI)
     A                                  2 64'時間:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'福委會編號:'
     A            DER01     R        B  4 15REFFLD(EM01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  1'員工編號自:'
     A                                      DSPATR(HI)
     A            DER02F    R        B  5 15REFFLD(EM02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A*                                 5 24'∼'
     A*           DER02T    R        B  5 29REFFLD(EM02)
     A* 63                                  DSPATR(PC)
     A* 63                                  DSPATR(RI)
     A                                  6  1'年度月份:'
     A            DYEAR          6Y 0B  6 15
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      EDTWRD('    /  ')
     A                                  6 24'(YYYYMM)'
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 15VALUES(2 4 5)
     A                                      EDTCDE(4)
     A                                  7 17'( 2.修改 4.刪除 5.查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF04=欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            DNEW           1A  H
     A            RRN            3Y 0H
     A            DDEL           1A  B 10  2
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            WSTFNO         8A  O 10  5
     A            DEM03     R        O 10 15REFFLD(EM03)
     A            DEM031        19O  O 10 30
     A            DTYPE          1A  O 10 52
     A            DER05          6  0B 10 58EDTCDE(1)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DER03          8Y 0B 10 68EDTWRD('    /  /  ')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            HTYPE          1A  H
     A            HER05          6S 0H
     A            HER07          6S 0H
     A            HER03          8Y 0H
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0012)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYA602>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 35'福委會作業'
     A                                      DSPATR(RI)
     A                                  2 64'時間:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'福委會編號:'
     A            DER01     R        O  4 15REFFLD(EM01)
     A                                  5  1'員工編號自:'
     A            DER02F    R        O  5 15REFFLD(EM02)
     A*
     A*           DER02T    R        O  5 29REFFLD(EM02)
     A                                  6  1'年度月份:'
     A            DYEAR          6Y 0O  6 15EDTWRD('    /  ')
     A                                  6 24'(YYYYMM)'
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0O  7 15EDTCDE(4)
     A                                  7 17'( 2.修改 4.刪除 5.查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  2'D'
     A                                  9  5'STF NO.'
     A                                  9 16'NAME'
     A                                  9 50'TYPE'
     A                                  9 58'AMOUNT'
     A                                  9 70'DATE'
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                      OVERLAY
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 23 46'* TYPE１表支出　２表收入'
     A                                 24 67'【泛太資訊】'
