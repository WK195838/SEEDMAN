     A*%%TS  SD  19921027  163533  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PTA120                                     *
     A*    PROGRAM NAME :代碼規則資料維護                          *
     A*    AUTHOR       : A1149  MAY                                 *
     A*    CREATE DATE  : 1992.10.27                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :基本資料系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PTRF)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<PTA120.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 71EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'代碼規則資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'代碼欄位:'
     A                                      DSPATR(HI)
     A            D#Y01     R        B  4 14REFFLD(#Y01)
     A N99                                  DSPATR(HI)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'代碼內容:'
     A            D#Y02     R        B  5 14REFFLD(#Y02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0B  6 14VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 18'( 1.新增'
     A                                  6 29'2.修改'
     A                                  6 38'4.刪除'
     A                                  6 47'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 23 45'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  19921027  163533  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                  1  2'<PTA120.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 71EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'代碼規則資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'代碼欄位:'
     A            D#Y01     R        O  4 14REFFLD(#Y01)
     A            D#Z02     R        O  4 26REFFLD(#Z02)
     A                                  5  2'代碼內容:'
     A            D#Y02     R        O  5 14REFFLD(#Y02)
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0O  6 14EDTCDE(4)
     A                                  6 18'( 1.新增'
     A                                  6 29'2.修改'
     A                                  6 38'4.刪除'
     A                                  6 47'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  2'代碼說明:'
     A            D#Y03     R        B  8 14REFFLD(#Y03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'代碼長度:'
     A            D#Z03     R        O 10 14REFFLD(#Z03)
     A                                      EDTCDE(1)
     A                                 10 18'位'
     A                                 11  2'說明長度:'
     A            D#Z04     R        O 11 14REFFLD(#Z04)
     A                                      EDTCDE(1)
     A                                 11 18'位'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 28'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
