     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR225P                                    *
     A*    PROGRAM NAME :個人加班請假記錄表列印                    *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 2001/10/18                                 *
     A*    UPDATE DATE  : 2001/11/05 A1274 EVA                       *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      REF(PYRF)
     A*------------------------------------------------------------***
     A          R PH1                       SKIPB(2)
     A            CM03      R              1
     A                                      SPACEA(1)
     A*
     A                                     1'個人加班請假記錄表'
     A                                      SPACEA(1)
     A*
     A                                     1' '
     A                                      SPACEA(1)
     A*
     A                                     1'部門代號:'
     A            DEM041         7        +1
     A                                    +1'-'
     A            DEM042         7        +1
     A                                   169'日期:'
     A            DDATE          6  0    178EDTWRD('0  /  /  ')
     A                                   187'頁次:'
     A                                   195PAGNBR
     A                                      SPACEA(1)
     A*
     A                                     1'日　　期:'
     A            POV031    R   -2        13REFFLD(OV03)
     A                                      EDTWRD('0  /  /  ')
     A                                    23'-'
     A            POV032    R   -2        25REFFLD(OV03)
     A                                      EDTWRD('0  /  /  ')
     A                                    40'考勤年度:'
     A            PYEAR          2  0     52EDTWRD('0  ')
     A                                    65'單位：天.時'
     A                                   169'時間:'
     A                                   178TIME
     A                                      EDTWRD('  :  :  ')
     A                                   187'<PYR225P>'
     A                                      SPACEA(1)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========================'
     A                                      SPACEA(1)
     A*
     A                                     1'部門名稱'
     A                                    24'員工編號'
     A                                    34'姓名'
     A                                    49'特休基準日'
     A                                    76'加班資料'
     A                                    88'請假資料'
     A                                      SPACEA(1)
     A*
     A                                    74'加班'
     A                                    81'補休'
     A            PHA            6O       88
     A            PHB            6O       95
     A            PHC            6O      102
     A            PHD            6O      109
     A            PHE            6O      116
     A            PHF            6O      123
     A            PHG            6O      130
     A            PHH            6O      137
     A            PHI            6O      144
     A            PHJ            6O      151
     A            PHK            6O      158
     A            PHL            6O      165
     A            PHM            6O      172
     A                                   179'遲到次數'
     A                                   189'應有年假'
     A                                      SPACEA(1)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========================'
     A                                      SPACEA(1)
     A*------------------------------------------------------------***
     A          R PD1
     A*
     A            EM041     R              1
     A            EM042     R              2
     A            EM043     R              3
     A            EM044     R              4
     A            DP02      R              8REFFLD(DP02 PYDPPF)
     A            EM02      R             25
     A            EM03      R             34
     A            EM113X         6  0     50EDTWRD('0  /  /  ')
     A                                    63'當期：'
     A            PD11           5  1     73EDTCDE(4)
     A            PD1II          5  1     80EDTCDE(4)
     A            PD1A           5  1     87EDTCDE(4)
     A            PD1B           5  1     94EDTCDE(4)
     A            PD1C           5  1    101EDTCDE(4)
     A            PD1D           5  1    108EDTCDE(4)
     A            PD1E           5  1    115EDTCDE(4)
     A            PD1F           5  1    122EDTCDE(4)
     A            PD1G           5  1    129EDTCDE(4)
     A            PD1H           5  1    136EDTCDE(4)
     A            PD1I           5  1    143EDTCDE(4)
     A            PD1J           5  1    150EDTCDE(4)
     A            PD1K           5  1    157EDTCDE(4)
     A            PD1L           5  1    164EDTCDE(4)
     A            PD1M           5  1    171EDTCDE(4)
     A            PD1O           5  1    181EDTCDE(4)
     A            PD1N           5  1    191EDTCDE(4)
     A                                      SPACEA(1)
     A*
     A                                    63'年度：'
     A            PD21           5  1     73EDTCDE(4)
     A            PD2II          5  1     80EDTCDE(4)
     A            PD2A           5  1     87EDTCDE(4)
     A            PD2B           5  1     94EDTCDE(4)
     A            PD2C           5  1    101EDTCDE(4)
     A            PD2D           5  1    108EDTCDE(4)
     A            PD2E           5  1    115EDTCDE(4)
     A            PD2F           5  1    122EDTCDE(4)
     A            PD2G           5  1    129EDTCDE(4)
     A            PD2H           5  1    136EDTCDE(4)
     A            PD2I           5  1    143EDTCDE(4)
     A            PD2J           5  1    150EDTCDE(4)
     A            PD2K           5  1    157EDTCDE(4)
     A            PD2L           5  1    164EDTCDE(4)
     A            PD2M           5  1    171EDTCDE(4)
     A            PD2O           5  1    181EDTCDE(4)
     A            PNEW           3A      194
     A                                      SPACEA(1)
     A*
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------'
     A                                      SPACEA(1)
     A*------------------------------------------------------------***
     A          R PT1
     A*
     A                                    34'部門合計：'
     A            PT01           5  0     50EDTCDE(4)
     A                                    59'人'
     A                                      SPACEA(1)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========================'
     A                                      SPACEA(1)
     A*------------------------------------------------------------***
     A          R PE1
     A*
     A                                     1'PYR205P--->報表結束'
     A                                      SPACEA(1)
     A*------------------------------------------------------------***
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
