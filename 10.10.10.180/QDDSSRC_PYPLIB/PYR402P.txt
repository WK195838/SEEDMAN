     A****************************************************************
     A*    PRTF NAME    : PYR402P                                    *
     A*    PROGRAM NAME : PYR402                                     *
     A*    AUTHOR       : A1118  PHILIP                              *
     A*    CREATE DATE  : 81/10/24                                   *
     A*    UPDATE DATE  : 89/02/01 A1274 EVA                         *
     A*    SYSTEM       : 90/05/14 A1274 EVA                         *
     A*    REMARK       :公司獎金資料列印                          *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R             22REFFLD(CM03 SYCMPF)
     A                                    60'頁次:'
     A                                    68PAGNBR
     A                                      SPACEA(002)
     A*
     A                                    29'獎金明細'
     A                                    56'獎金年度:'
     A            DPB051         3  0     +1
     A                                      EDTWRD('0  ')
     A                                      SPACEA(002)
     A*
     A                                     2'部門:'
     A            DP02      R             +1REFFLD(DP02 PYDPPF)
     A                                    29'員工姓名:'
     A            EM03      R             +1
     A                                    56'員工編號:'
     A            PB02      R             +1
     A                                      SPACEA(001)
     A*
     A                                     1''
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A*
     A                                     2'應付項目'
     A                                    17'應付金額'
     A                                    29'應付項目'
     A                                    44'應付金額'
     A                                    56'應扣項目'
     A                                    71'應扣金額'
     A                                      SPACEA(001)
     A*
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            ARR1      R              2REFFLD(SS02)
     A            AMT1           7  0     17
     A                                      EDTCDE(K)
     A            ARR2      R             29REFFLD(SS02)
     A            AMT2           7  0     44
     A                                      EDTCDE(K)
     A            ARR3      R             56REFFLD(SS02)
     A            AMT3           7  0     71
     A                                      EDTCDE(K)
     A          R PE1                       SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------'
     A                                      SPACEA(1)
     A                                     2'固定薪資'
     A                                      SPACEA(1)
     A*
     A                                     2'非固定薪資'
     A            PADD           7  0     17
     A                                      EDTCDE(K)
     A                                      SPACEA(1)
     A*
     A                                     2'免稅所得'
     A            PELS           7  0     17
     A                                      EDTCDE(N)
     A                                      SPACEA(1)
     A*
     A                                     2'應扣稅額'
     A            PSUB           7  0     17
     A                                      EDTCDE(K)
     A                                      SPACEA(1)
     A*
     A                                     1'*實發金額*'
     A            PTOT           7  0     17
     A                                      EDTCDE(K)
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
