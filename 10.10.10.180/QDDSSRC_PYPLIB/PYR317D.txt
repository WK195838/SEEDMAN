     A****************************************************************/
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   */
     A*                                                              */
     A*    PROGRAM-ID   : PYA317D                                    */
     A*    PROGRAM NAME :薪資加班明細表列印                        */
     A*    AUTHOR       : A1226 PETER                                */
     A*    CREATE DATE  : 86/01/20                                   */
     A*    UPDATE DATE  : 88/10/04 A1274 EVA                         */
     A*    SYSTEM       :人事薪資系統                              */
     A****************************************************************/
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*  90/06/18  19:46:25    A1084       REL-R02M00  5728-PW1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYR317D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'薪資加班資料明細表列印'
     A                                      DSPATR(RI)
     A                                  5 28'公司編號　　　:'
     A            DOV01     R        B  5 46REFFLD(ED01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 28'部門編號　　起:'
     A                                      DSPATR(HI)
     A            DEM041         7   B  7 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM042         7   B  8 46
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 28'員工編號　　起:'
     A                                      DSPATR(HI)
     A            DOV021    R        B 10 46REFFLD(ED02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 11 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DOV022    R        B 11 46REFFLD(ED02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 13 28'薪資年月　　　:'
     A            DOV96     R   -1   B 13 46REFFLD(OV96)
     A                                      EDTWRD('0   /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 27'***請準備１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3=回主畫面'
     A                                 23 18'PF4=欄位查詢'
     A                                 24 67'【泛太資訊】'
