     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA1141D                                   *
     A*    PROGRAM NAME :勞保投保級數查詢　　　　                  *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 91/12/09                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統　                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A            RRN            2Y 0H
     A            DOPT           1A  B  9  3DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            L101      R        O  9 12REFFLD(L101 PYL1PF)
     A                                      EDTCDE(2)
     A            L102      R        O  9 24REFFLD(L102 PYL1PF)
     A                                      EDTCDE(2)
     A            L103      R        O  9 38REFFLD(L103 PYL1PF)
     A                                      EDTCDE(2)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI1141D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'勞工保險級數查詢'
     A                                      DSPATR(RI)
     A                                  4  2'投保費率A:'
     A            L201      R        O  4 15REFFLD(L201 PYL2PF)
     A                                      EDTCDE(1)
     A                                  4 24'員工負擔:'
     A            L202      R        O  4 36REFFLD(L202 PYL2PF)
     A                                      EDTCDE(1)
     A                                  4 50'勞保墊償:'
     A            L203      R        O  4 62REFFLD(L203 PYL2PF)
     A                                      EDTCDE(1)
     A                                  5  2'投保費率B:'
     A            L2012     R        O  5 15REFFLD(L2012 PYL2PF)
     A                                      EDTCDE(1)
     A                                  5 24'公司負擔:'
     A            L204      R        O  5 36REFFLD(L204 PYL2PF)
     A                                      EDTCDE(1)
     A                                  5 50'勞保職災:'
     A            L205      R        O  5 62REFFLD(L205 PYL2PF)
     A                                      EDTCDE(1)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  3'S'
     A                                      DSPATR(UL)
     A                                  7  8'投保級數'
     A                                      DSPATR(UL)
     A                                  7 22'月投保金額'
     A                                      DSPATR(UL)
     A                                  7 36'日投保金額'
     A                                      DSPATR(UL)
     A            DL101     R        B  8 12REFFLD(L101 PYL1PF)
     A                                      EDTCDE(2)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PA01 =下一頁　'
     A                                 23 37'PA02 =上一頁　'
     A                                 24 67'【泛太資訊】'
