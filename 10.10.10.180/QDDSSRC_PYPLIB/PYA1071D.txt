     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*                                                              *
     A*    PROGRAM-ID   : PYA1071D                                   *
     A*    PROGRAM NAME :休假基本多筆資料建立－年度                *
     A*    AUTHOR       : A1346 WEST                                 *
     A*    CREATE DATE  : 91/11/05                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA1071.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'休假基本資料建立－年度'
     A                                      DSPATR(RI)
     A*
     A                                  4  1'公司編號:'
     A            DLT01     R        B  4 13REFFLD(LT01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'年　　度:'
     A            DLT03     R   -2   B  5 13REFFLD(LT03)
     A                                      EDTWRD('0  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'員工編號:'
     A            DLT02     R        B  6 13REFFLD(LT02)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                  7 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A*                                23 20'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*============================================================*
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            3S 0H
     A            DNEW           1A  H
     A*
     A            DLT02     R        B  9  2REFFLD(LT02)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DEM03         14O  O  9 11
     A*
     A            DLT10     R        B  9 26REFFLD(LT10)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 33'天'
     A*
     A            DLT13     R        B  9 46REFFLD(LT13)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 51'天'
     A            DLT14     R        B  9 56REFFLD(LT14)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 59'時'
     A*
     A            DLT15     R        B  9 64REFFLD(LT15)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 69'天'
     A            DLT16     R        B  9 74REFFLD(LT16)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 77'時'
     A*
     A            DLT17     R        B 10 26REFFLD(LT17)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 33'天'
     A            DLT18     R        B 10 38REFFLD(LT18)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 10 41'時'
     A*
     A            DLT11     R        B 10 46REFFLD(LT11)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 51'天'
     A            DLT12     R        B 10 56REFFLD(LT12)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 10 59'時'
     A            LTT1      R        O 10 64REFFLD(LT15)
     A                                      EDTCDE(M)
     A                                 10 69'天'
     A            LTT2      R        O 10 74REFFLD(LT16)
     A                                      EDTCDE(M)
     A                                 10 77'時'
     A*------------------------------------------------------------*
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0006)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      ROLLUP(25)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYA1071.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'休假基本資料建立－年度'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DLT01     R        O  4 13REFFLD(LT01)
     A                                  5  1'年　　度:'
     A            DLT03     R   -2   O  5 13REFFLD(LT03)
     A                                      EDTWRD('0  ')
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  7 29'今年年假'
     A                                  7 47'去年遞延'
     A                                  7 65'明年可預借'
     A                                  8  1'員工編號員工姓名    '
     A                                      DSPATR(UL)
     A                                  8 26'   去年已預借   '
     A                                      DSPATR(UL)
     A                                  8 46' 本年已休年假 '
     A                                      DSPATR(UL)
     A                                  8 64' 本年可休年假 '
     A                                      DSPATR(UL)
     A          R DSPC2                     OVERLAY
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                    +2'PF04=欄位查詢'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PF13=確認'
     A                                 24 67'【泛太資訊】'
