     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR410P                                    *
     A*    PROGRAM NAME :離職人員特休結算表                        *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 91/10/18                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                          *
     A****************************************************************
     A                                      REF(PYRF)
     A*------------------------------------------------------------***
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R             24
     A                                      SPACEA(001)
     A*
     A                                    31
     A                                      '離職人員特休結算表'
     A                                      SPACEA(004)
     A                                     1
     A                                      '員工編號 :'
     A            DEM02S         8        +1
     A                                    64
     A                                      '日期:'
     A            DDATE          6  0     72
     A                                      EDTWRD('0  /  /  ')
     A                                    83
     A                                      '頁次:'
     A                                    91
     A                                      PAGNBR
     A                                      SPACEA(001)
     A                                     1
     A                                      '離職日期 :'
     A            DAY            7  0     +1
     A                                      EDTWRD('0   /  /  ')
     A                                    64
     A                                      '時間:'
     A                                    72
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    83
     A                                      '<PYR410P>'
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========================='
     A                                      SPACEA(001)
     A                                    36
     A                                      '勞基法'
     A                                      SPACEB(001)
     A                                    45
     A                                      '歷年'
     A                                    54
     A                                      '在職'
     A                                    60
     A                                      '本年'
     A                                    66
     A                                      '本年'
     A                                    72
     A                                      '去年'
     A                                    78
     A                                      '去年'
     A                                    84
     A                                      '(天/時)'
     A                                      SPACEA(001)
     A                                     1
     A                                      '員工'
     A                                    10
     A                                      '姓名'
     A                                    27
     A                                      '到職日'
     A                                    36
     A                                      '總年假'
     A                                    44
     A                                      '總年假'
     A                                    54
     A                                      '年月'
     A                                    60
     A                                      '年假'
     A                                    66
     A                                      '已休'
     A                                    72
     A                                      '遞延'
     A                                    78
     A                                      '預借'
     A                                    87
     A                                      '折發金額'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========================='
     A                                      SPACEA(001)
     A*------------------------------------------------------------***
     A          R PD1
     A                                      SPACEA(001)
     A            EM02      R              1
     A
     A            EM03      R             10
     A*到職日
     A            DEM13          7  0     25
     A                                      EDTWRD('0   /  /  ')
     A*勞基法總年假
     A            DAY1           3  0     38
     A                                      EDTCDE(3)
     A                                    41
     A                                      '/'
     A            HOUR1          1  0     42
     A                                      EDTCDE(3)
     A*在職總年假
     A            DAY2           3  0     46
     A                                      EDTCDE(3)
     A                                    49
     A                                      '/'
     A            HOUR2          1  0     50
     A                                      EDTCDE(3)
     A*在職年月
     A            WRKY           4  0     52
     A                                      EDTCDE(3)
     A                                    56
     A                                      '/'
     A            WRKM           2  0     57
     A                                      EDTCDE(3)
     A*本年年假
     A            TDD            3  0     60
     A                                      EDTCDE(3)
     A                                    63
     A                                      '/'
     A            THH            1  0     64
     A                                      EDTCDE(3)
     A*本年已休
     A            LT11           3  0     66
     A                                      EDTCDE(3)
     A                                    69
     A                                      '/'
     A            LT12           1  0     70
     A                                      EDTCDE(3)
     A*去年遞延
     A            LT13           3  0     72
     A                                      EDTCDE(3)
     A                                    75
     A                                      '/'
     A            LT14           1  0     76
     A                                      EDTCDE(3)
     A*去年預借
     A            LT17           3  0     78
     A                                      EDTCDE(3)
     A                                    81
     A                                      '/'
     A            LT18           1  0     82
     A                                      EDTCDE(3)
     A*折發金額
     A            TMNY           7  0     87
     A                                      EDTCDE(J)
     A                                      SPACEA(001)
     A*-------------------------------------------------------------***
     A          R PT1
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      --------------------------'
     A                                      SPACEA(001)
     A                                    11
     A                                      '公司合計:'
     A            TT11           5  0     23
     A                                      EDTCDE(3)
     A                                    30
     A                                      '人'
     A            TT12           8  0     86
     A                                      EDTCDE(J)
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========================='
     A                                      SPACEA(001)
     A*-------------------------------------------------------------***
     A          R PE1
     A                                     1
     A                                      '總經理'
     A                                    20
     A                                      '管理部'
     A                                    40
     A                                      '人事課'
     A                                    60
     A                                      '承辦人'
     A*-------------------------------------------------------------***
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
