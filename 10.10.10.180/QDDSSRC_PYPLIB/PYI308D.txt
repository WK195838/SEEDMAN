     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI301D                                    *
     A*    PROGRAM NAME :員工調薪資料查詢                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 86/01/29                                   *
     A*    UPDATE DATE  : 92/02/14 A1336 KLAUS                       *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            PW02      R        O  8  7
     A            EM03      R        O  8 18
     A            PW03D     R   -2   O  8 36REFFLD(PW03)
     A                                      EDTWRD('0  /  ')
     A            PW04      R        O  8 47
     A            PW04N          6O  O  8 49
     A            SAR            7Y 0O  8 58EDTCDE(1)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI308D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'每月薪資調整全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DPW01     R        O  4 13REFFLD(PW01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DPW01N        14A  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  6'員工編號'
     A                                      DSPATR(UL)
     A                                  6 18'員工姓名'
     A                                      DSPATR(UL)
     A                                  6 34'年 月'
     A                                      DSPATR(UL)
     A                                  6 45'期   別'
     A                                      DSPATR(UL)
     A                                  6 58'給付總額'
     A                                      DSPATR(UL)
     A            DPW02     R        B  7  7REFFLD(PW02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DPW03     R   -2   B  7 36REFFLD(PW03)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      EDTWRD('0  /  ')
     A            DPW04     R        B  7 47REFFLD(PW04)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA1=下一頁'
     A                                 23 56'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
