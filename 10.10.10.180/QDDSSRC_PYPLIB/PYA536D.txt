     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA536D                                    *
     A*    PROGRAM NAME :員工考績等級批次作業
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 91/09/11                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資管理系統                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF13(13)
     A                                  1  2'<PYA536D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'員工考績等級批次作業'
     A                                      DSPATR(RI)
     A*
     A                                  6 28'公司編號:'
     A            DRV01     R        B  6 40REFFLD(RV01)
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  8 28'考績年度:'
     A            DRV03     R   -1   B  8 40REFFLD(RV03 PYRVPF)
     A                                      EDTCDE(1)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                 10 28'考績種類:'
     A            DRV04     R        B 10 40REFFLD(RV04 PYRVPF)
     A                                      VALUES('1' '2' '3')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 42'( 1-上半年 2-下半年 3-全年)'
     A*
     A                                 12 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 13 11' '
     A                                      DSPATR(RI)
     A                                 13 66' '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A  97                             14 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  98                             14 28'試算作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
