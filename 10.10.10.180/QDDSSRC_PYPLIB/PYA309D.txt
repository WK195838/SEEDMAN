     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA309D                                    *
     A*    PROGRAM NAME :時薪時數資料建立                          *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 85/08/27                                   *
     A*    UPDATE DATE  : 90/08/21 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*------------------------------------------------------------***
     A          R DSPC1
     A                                      CSRLOC(#LIN  #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA309.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'時薪時數資料建立'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DWH01     R        B  4 13REFFLD(WH01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DWH02     R        B  5 13REFFLD(WH02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            EM03      R        O    +2REFFLD(EM03 PYEMPF)
     A                                  6  1'上班年月:'
     A            DWH03     R   -2   B  6 13REFFLD(WH03)
     A                                      EDTWRD('0  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1  0B  7 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A                                 18  1'＊注意！本作業只能維護時薪人員-
     A                                      薪資資料！'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*-----------------------------------------------------------****
     A          R DSPC2
     A                                      CA03(03)
     A                                      CA12(12)
     A                                  1  2'<PYA309.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'時薪時數資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A*
     A                                  4  1'公司編號:'
     A            DWH01     R        O  4 13REFFLD(WH01)
     A            CM02      R        O  4 22REFFLD(CM02 SYCMPF)
     A                                  5  1'員工編號:'
     A            DWH02     R        O  5 13REFFLD(WH02)
     A            EM03      R        O  5 22REFFLD(EM03 PYEMPF)
     A                                  6  1'上班年月:'
     A            DWH03     R   -2   O  6 13REFFLD(WH03)
     A                                      EDTWRD('0  /  ')
     A*
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A                                  9  1'工作時數:'
     A            DWH04     R        B  9 13REFFLD(WH04)
     A                                      EDTCDE(K)
     A  60                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'小時'
     A*
     A                                 11  1'津貼時數:'
     A            DWH05     R        B 11 13REFFLD(WH05)
     A                                      EDTCDE(K)
     A  61                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'小時'
     A*
     A                                 13  1'健保月數:'
     A            DWH06     R        B 13 13REFFLD(WH06)
     A                                      EDTCDE(K)
     A  62                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'個月'
     A*
     A                                 15  1'勞保天數:'
     A            DWH07     R        B 15 13REFFLD(WH07)
     A                                      EDTCDE(K)
     A  63                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'天'
     A*
     A                                 17  1'計薪年月:'
     A            DWH96     R   -2   B 17 13REFFLD(WH96)
     A                                      EDTWRD('0  /  ')
     A  64                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
