     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    DSPF NAME    : PYA408D                                    *
     A*    PROGRAM NAME :褔利獎金資料建立　                        *
     A*    AUTHOR       : A1326 SANTOS                               *
     A*    CREATE DATE  : 90/02/21                                   *
     A*    UPDATE DATE  : 90/05/09 A1274 EVA                         *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPC1
     A                                      CSRLOC(#LIN #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03 '回主畫面')
     A                                      CF04(04)
     A                                      CF13(13)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYA408D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'褔利獎金資料建立'
     A                                      DSPATR(RI)
     A*
     A                                  4 28'福委會　:'
     A            DEM01     R        B  4 41REFFLD(PB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            CM03      R        O  4 45REFFLD(CM03 SYCMPF)
     A*
     A                                  6 28'部　　門:'
     A            DEM04S         5   B  6 41
     A  66                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  6 47'-'
     A            DEM04E         5   B  6 49
     A  67                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A*
     A                                  8 28'獎金年度:'
     A            DYEAR     R   -1   B  8 41REFFLD(PB03)
     A                                      EDTCDE(1)
     A  61                                  DSPATR(PC RI)
     A                                 10 28'獎金類別:'
     A                                      DSPATR(HI)
     A            DTYPE          2   B 10 41
     A  62                                  DSPATR(PC RI)
     A            SS02      R        O 10 45REFFLD(SS02 PYSSPF)
     A                                 12 28'發放日期:'
     A            DYMD           7Y 0B 12 41EDTWRD('0   /  /  ')
     A  63                                  DSPATR(PC RI)
     A                                 14 28'發放金額:'
     A            DMNY      R        B 14 41REFFLD(PB06 PYPBPF)
     A                                      EDTCDE(1)
     A  64                                  DSPATR(PC RI)
     A                                 16 28'發放說明:'
     A            DPB13         40O  B 16 41
     A*
     A  97                             20 28'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(RI)
     A  96                             20 28'　　　獎金作業處理中！！　　'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
