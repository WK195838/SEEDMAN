     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR405P                                    *
     A*    PROGRAM NAME :福利獎金資料列印－員工                    *
     A*    AUTHOR       : A1328 JEFFREY                              *
     A*    CREATE DATE  : 90/02/27                                   *
     A*    UPDATE DATE  : 90/07/23 A1274 EVA                         *
     A*    SYSTEM       :音樂講師管理系統                          *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R             34
     A*
     A                                    37'福利獎金資料表－員工'
     A                                      SPACEB(001)
     A*
     A                                     1'部門編號:'
     A                                      SPACEB(002)
     A            DEM041         7        13
     A                                    +1'-'
     A            DEM042         7        +1
     A                                    37'獎金類別:'
     A            DPB041    R             49REFFLD(PB04)
     A                                    52'-'
     A            DPB042    R             54REFFLD(PB04)
     A                                    86'日期:'
     A            DDATE          6  0     94EDTCDE(Y)
     A                                   104'頁次:'
     A                                   112PAGNBR
     A*
     A                                     1'員工編號:'
     A                                      SPACEB(001)
     A            DPB021    R             +1REFFLD(PB02)
     A                                    +1'-'
     A            DPB022    R             +1REFFLD(PB02)
     A*
     A                                    37'發放日期:'
     A            DPB051         7  0     49EDTWRD('   /  /  ')
     A                                    59'-'
     A            DPB052         7  0     61EDTWRD('   /  /  ')
     A                                    86'時間:'
     A                                    94TIME
     A                                      EDTWRD('  :  :  ')
     A                                   104'<PYR405P>'
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==============='
     A                                      SPACEB(001)
     A*
     A                                     1'部門'
     A                                      SPACEB(001)
     A                                    17'員工姓名'
     A                                    39'獎金類別'
     A                                    56'福利獎金'
     A                                    68'扣繳稅額'
     A                                    80'實領總額'
     A                                    91'發放日期'
     A                                   102'離職日期'
     A                                   113'簽名'
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==============='
     A                                      SPACEB(001)
     A*-----------------------------------------------------------***
     A          R PD1
     A                                      SPACEB(001)
     A            EM041     R              1
     A            EM042     R              2
     A            EM043     R              3
     A            EM044     R              4
     A            PDP02         10O        8
     A            PB02      R             18
     A            EM03      R             26
     A            PB04      R             40REFFLD(PB04)
     A            PSS02     R             42REFFLD(SS02 PYSSPF)
     A            PPB06          6  0     58EDTCDE(J)
     A            PPB08          6  0     70EDTCDE(J)
     A            PPB12          6  0     82EDTCDE(J)
     A            PBYMD          7  0     91EDTWRD('   /  /  ')
     A            PEM19          7  0    102
     A                                      EDTWRD('   /  /  ')
     A*
     A*-------------------------------------------------------------***
     A          R PT1
     A                                      SPACEB(002)
     A                                     1'單位小計:'
     A            PT11           3  0     +1EDTCDE(3)
     A                                    +1'人'
     A            PT12           8  0     55EDTCDE(J)
     A            PT13           8  0     67EDTCDE(J)
     A            PT17           8  0     79EDTCDE(J)
     A*-------------------------------------------------------------***
     A          R PT2
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==============='
     A                                      SPACEB(001)
     A*
     A                                    17'部門小計:'
     A                                      SPACEB(001)
     A            PT21           3  0     +1EDTCDE(3)
     A                                    +1'人'
     A            PT22           8  0     55EDTCDE(J)
     A            PT23           8  0     67EDTCDE(J)
     A            PT27           8  0     79EDTCDE(J)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==============='
     A                                      SPACEB(001)
     A*-------------------------------------------------------------***
     A          R PT3
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門中計:'
     A            PT31           3  0     +1
     A                                      EDTCDE(3)
     A                                    +1
     A                                      '人'
     A            PT32           8  0     55
     A                                      EDTCDE(J)
     A            PT33           8  0     67
     A                                      EDTCDE(J)
     A            PT37           8  0     79
     A                                      EDTCDE(J)
     A*-------------------------------------------------------------***
     A          R PT4
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門合計:'
     A            PT41           3  0     +1
     A                                      EDTCDE(3)
     A                                    +1'人'
     A            PT42           8  0     55EDTCDE(J)
     A            PT43           8  0     67EDTCDE(J)
     A            PT47           8  0     79EDTCDE(J)
     A*-------------------------------------------------------------***
     A*
     A          R PT5
     A                                      SPACEB(002)
     A*
     A                                    17'公司總計:'
     A                                      SPACEB(001)
     A            PT51           3  0     +1
     A                                      EDTCDE(3)
     A                                    +1
     A                                      '人'
     A            PT52           8  0     55
     A                                      EDTCDE(J)
     A            PT53           8  0     67
     A                                      EDTCDE(J)
     A            PT57           8  0     79
     A                                      EDTCDE(J)
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==============='
     A                                      SPACEB(001)
     A                                     1
     A                                      'PYR405P===>報表結束'
     A                                      SPACEB(001)
     A*-------------------------------------------------------------***
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
