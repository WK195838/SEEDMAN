     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA601D                                    *
     A*    PROGRAM NAME :福委會收支登錄作業　　　　                *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/02/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF13(13)
     A                                  1  2'<PYA601D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'福委會收支登錄作業'
     A                                      DSPATR(RI)
     A*
     A                                  5 26'福委會編號:'
     A            DER01     R        B  5 40REFFLD(ER01 PYERPF)
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DER01N    R        O  5 46REFFLD(CM02 SYCMPF)
     A*
     A                                  7 24'支出登錄日期:'
     A            DER03          8Y 0B  7 40EDTWRD('0   /  /  ')
     A  61                                  DSPATR(PC RI)
     A                                  7 51'(西元年)'
     A*
     A                                  9 24'每人支出金額:'
     A            DER05          6S 0B  9 40
     A  62                                  DSPATR(PC RI)
     A*
     A                                 11 28'收支狀況:'
     A            DER04     R        B 11 40REFFLD(ER04 PYERPF)
     A  63                                  DSPATR(PC RI)
     A                                 11 46'1.福委會支出'
     A                                 12 46'2.福委會收入'
     A*
     A                                 15 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 16 11' '
     A                                      DSPATR(RI)
     A                                 16 66' '
     A                                      DSPATR(RI)
     A                                 17 11' '
     A                                      DSPATR(RI)
     A  97                             17 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  96                             17 28'登錄作業處理中！！'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  98                             17 28'登錄作業完成　！！'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A                                 17 66' '
     A                                      DSPATR(RI)
     A                                 18 11' '
     A                                      DSPATR(RI)
     A                                 18 66' '
     A                                      DSPATR(RI)
     A                                 19 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
