     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA540D                                    *
     A*    PROGRAM NAME :休假日期資料設定維護                      *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 91/07/30                                   *
     A*    UPDATE DATE  :
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYT7PF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA540.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'休假日期資料設定'
     A                                      DSPATR(RI)
     A                                  5  1'公　　司:'
     A                                      DSPATR(HI)
     A            DT700     R        B  5 13REFFLD(T700)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5 17'(拷貝:'
     A                                      DSPATR(HI)
     A            DT700C    R        B  5 26REFFLD(T700)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  5 29')'
     A                                      DSPATR(HI)
     A*先保留
     A*                                 5 32'起始日期:'
     A*           STRDT          7Y 0B  5 44EDTWRD('   /  /  ')
     A* 63                                  DSPATR(PC RI)
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            3Y 0O  8  3
     A            DT701     R   -2   B  8 10REFFLD(T701)
     A                                      EDTWRD('0  /  /  ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  61                                  DSPATR(PC RI)
     A            DT702     R        B  8 22REFFLD(T702)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  62                                  DSPATR(PC RI)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0012)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA540.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'休假日期資料設定'
     A                                      DSPATR(RI)
     A                                  5  1'公　　司:'
     A            DT700     R        O  5 13REFFLD(T700)
     A            DCM02     R        O  5 16REFFLD(CM02 SYCMPF)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  7  1'序號'
     A                                  7 10'休假日期'
     A                                  7 22'說　　明'
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PA01 =下一頁'
     A                                 23 36'PA02 =上一頁'
     A                                 23 52'PA13 =資料確認'
     A                                 24  2'PF12 =回上一層'
     A                                 24 67'【泛太資訊】'
