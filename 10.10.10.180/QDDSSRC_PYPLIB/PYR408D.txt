     A****************************************************************/
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   */
     A*                                                              */
     A*    PROGRAM-ID   : PYR408D                                    */
     A*    PROGRAM NAME :特別休假代金簽呈表                        */
     A*    AUTHOR       :                                            */
     A*    CREATE DATE  : 90/04/19                                   */
     A*    UPDATE DATE  :                                            */
     A*    SYSTEM       :　　　　　　　　　　　　　                */
     A****************************************************************/
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*  90/06/18  19:46:25    A1084       REL-R02M00  5728-PW1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYR408D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'特別休假代金簽呈表'
     A                                      DSPATR(RI)
     A                                  4 28'公  司:'
     A            DEM01     R        B    +1REFFLD(EM01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  6 28'部門編號　:'
     A                                      DSPATR(HI)
     A            DEM04S         7   B  6 42
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                    +1'-'
     A            DEM04E         7   B    +1
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 28'員工編號  :'
     A                                      DSPATR(HI)
     A            DEM02S    R        B  8 42REFFLD(EM02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                    +1'-'
     A            DEM02E    R        B  8 53REFFLD(EM02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 10 28'特休年度  :'
     A            DYEAR          2  0B    +1EDTWRD('0  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 12 28'處理年月  :'
     A            DYM            5  0B    +1EDTWRD('0  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 14 28'報表排序  :'
     A            DSEL           1A  B    +1VALUES('1' '2')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                    +2'1.員工編號'
     A                                 15 45'2.部門編號'
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 27'***請準備１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行 PF3=回主畫面'
     A                                 23 24'PF4=欄位查詢'
     A                                 24 67'【泛太資訊】'
