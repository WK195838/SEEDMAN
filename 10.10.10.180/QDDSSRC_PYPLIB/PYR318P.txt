     A****************************************************************/
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    */
     A*                                                              */
     A*    PROGRAM-ID   : PYA318P                                    */
     A*    PROGRAM NAME :薪資請假明細表列印                        */
     A*    AUTHOR       : A1226 PETER                                */
     A*    CREATE DATE  : 86/01/20                                   */
     A*    UPDATE DATE  : 89/09/05 A1274 EVA                         */
     A*    SYSTEM       :人事薪資系統                              */
     A****************************************************************/
     A                                      REF(PYRF)
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1
     A                                    85
     A                                      '日期:'
     A            DDATE          6  0     93
     A                                      EDTWRD('0  /  /  ')
     A                                   102
     A                                      '頁次:'
     A                                   110
     A                                      PAGNBR
     A                                      SPACEA(001)
     A                                     1
     A                                      '薪資請假資料明細表'
     A                                    85
     A                                      '時間:'
     A                                    93
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                   103
     A                                      '<PYR318P>'
     A                                      SPACEA(002)
     A                                     1
     A                                      '薪資年月:'
     A            DLV96     R   -1        13
     A                                      REFFLD(LV96)
     A                                      EDTWRD('0  /  ')
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門代號:'
     A            DEM041         7        13
     A                                    +1
     A                                      '-'
     A            DEM042         7        +1
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==================='
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門名稱'
     A                                    25
     A                                      '員工姓名'
     A                                    45
     A                                      '起始日期'
     A                                    55
     A                                      '終止日期'
     A                                    65
     A                                      '起始時間'
     A                                    75
     A                                      '終止時間'
     A                                    85
     A                                      '假別'
     A                                    93
     A                                      '時數'
     A                                   102
     A                                      '超出'
     A                                   108
     A                                      '扣除交通津貼'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==================='
     A*
     A          R PD1
     A                                      SPACEB(001)
     A  31        EM041     R              1
     A  31        EM042     R              2
     A  31        EM043     R              3
     A  31        EM044     R              4
     A  31        DP02      R              8REFFLD(DP02 PYDPPF)
     A  32        DLV02     R             25
     A                                      REFFLD(LV02)
     A  32        EM03      R             33
     A            DLV03     R   -1        47
     A                                      REFFLD(LV03)
     A                                      EDTWRD('0   /  /  ')
     A            DLV05     R   -1        +1
     A                                      REFFLD(LV05)
     A                                      EDTWRD('0   /  /  ')
     A            DLV06     R             67
     A                                      REFFLD(LV06)
     A                                      EDTWRD('  :  ')
     A            DLV07     R             77
     A                                      REFFLD(LV07)
     A                                      EDTWRD('  :  ')
     A            PD11           8O       84
     A            PD12           5  2     94
     A                                      EDTCDE(3)
     A            PD13           5  2    102
     A                                      EDTCDE(4)
     A            DLV12     R            114
     A                                      EDTCDE(4)
     A                                      REFFLD(LV12)
     A*
     A          R PT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                    75
     A                                      '員工小計:'
     A            PT11           6  2     93
     A                                      EDTCDE(3)
     A            PT12           6  2    101
     A                                      EDTCDE(3)
     A*
     A          R PT2
     A                                      SPACEA(001)
     A                                    75
     A                                      '單位小計:'
     A            PT21           6  2     93
     A                                      EDTCDE(3)
     A            PT22           6  2    101
     A                                      EDTCDE(3)
     A*
     A          R PT3
     A                                      SPACEA(001)
     A                                    75
     A                                      '部門小計:'
     A            PT31           6  2     93
     A                                      EDTCDE(3)
     A            PT32           6  2    101
     A                                      EDTCDE(3)
     A*
     A          R PT4
     A                                      SPACEA(001)
     A                                    75
     A                                      '部門中計:'
     A            PT41           6  2     93
     A                                      EDTCDE(3)
     A            PT42           6  2    101
     A                                      EDTCDE(3)
     A*
     A          R PT5
     A                                      SPACEA(001)
     A                                    75
     A                                      '部門合計:'
     A            PT51           6  2     93
     A                                      EDTCDE(3)
     A            PT52           6  2    101
     A                                      EDTCDE(3)
     A*
     A          R PT6
     A                                      SPACEB(002)
     A                                    75
     A                                      '部門總計:'
     A            PT61           6  2     93
     A                                      EDTCDE(3)
     A            PT62           6  2    101
     A                                      EDTCDE(3)
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==================='
     A                                      SPACEB(001)
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
