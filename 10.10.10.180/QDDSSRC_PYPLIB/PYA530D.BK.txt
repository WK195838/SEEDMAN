     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA533D                                    *
     A*    PROGRAM NAME :勞保投保級數設定　　　　                  *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 89/08/21                                   *
     A*    UPDATE DATE  : 90/06/22 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統　                            *
     A****************************************************************
     A*%%TS  SD  19950315  135759  FOPGMR      REL-V2R2M0  5738-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A*%%TS  SD  19950222  201949  CHARLES     REL-V2R2M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            2Y 0H
     A            DE301     R        B  8  4REFFLD(E301)
     A                                      EDTCDE(2)
     A  60                                  DSPATR(PC RI)
     A            DE302     R        B  8 11REFFLD(E302)
     A                                      EDTCDE(2)
     A  61                                  DSPATR(PC RI)
     A            DE303          6  0B  8 23EDTCDE(2)
     A  62                                  DSPATR(PC RI)
     A            DE304          6  0B  8 32EDTCDE(2)
     A  63                                  DSPATR(PC RI)
     A            DE305          6  0B  8 41EDTCDE(2)
     A  64                                  DSPATR(PC RI)
     A            DE306          6  0B  8 50EDTCDE(2)
     A  65                                  DSPATR(PC RI)
     A            DE307          6  0B  8 60EDTCDE(2)
     A  66                                  DSPATR(PC RI)
     A            DE308          6  0B  8 69EDTCDE(2)
     A  67                                  DSPATR(PC RI)
     A*----------------------------------------------------------------
     A**                                    CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0070)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA530.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'全民健保級數設定'
     A                                      DSPATR(RI)
     A                                  4  2'投保費率:'
     A            DE401     R        B  4 14REFFLD(E401)
     A                                      EDTCDE(1)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                  4 22'員工負擔:'
     A            DE402     R        B  4 34REFFLD(E402)
     A                                      EDTCDE(1)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A            DE405     R        B  4 42REFFLD(E405)
     A                                      EDTCDE(1)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A                                  4 44'人'
     A                                  4 50'公司負擔:'
     A            DE403     R        B  4 62REFFLD(E403)
     A                                      EDTCDE(1)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A            DE404     R        B  4 69REFFLD(E404)
     A                                      EDTCDE(1)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A                                  4 76'人'
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  6  2'投保分級'
     A                                  7  2'級數'
     A                                      COLOR(PNK)
     A                                    +1'月投保金額'
     A                                      COLOR(PNK)
     A                                  7 23'本　人'
     A                                      COLOR(PNK)
     A                                  7 32'＋１眷'
     A                                      COLOR(PNK)
     A                                  7 41'＋２眷'
     A                                      COLOR(PNK)
     A                                  7 50'＋３眷'
     A                                      COLOR(PNK)
     A                                  7 60'公　司'
     A                                      COLOR(PNK)
     A                                  7 69'政　府'
     A                                      COLOR(PNK)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 20'PA1 =下一頁　'
     A                                 23 37'PA2 =上一頁　'
     A                                 24 67'【泛太資訊】'
