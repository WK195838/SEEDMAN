     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR105P                                    *
     A*    PROGRAM NAME :生日人員資料列印  　                    *
     A*    AUTHOR       : A1084 DERLERN                              *
     A*    CREATE DATE  : 79/06/19                                   *
     A*    UPDATE DATE  : 90/04/06 A1274 EVA                         *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      REF(PYRF)
     A*------------------------------------------------------------***
     A          R PH1                       SKIPB(2)
     A            CM03      R              1
     A                                    56'日期:'
     A            DDATE          6  0     64EDTWRD('0  /  /  ')
     A                                    73'頁次:'
     A                                    80PAGNBR
     A                                      SPACEA(1)
     A                                     1'生日人員明細表          '
     A                                    56'時間:'
     A                                    64TIME
     A                                      EDTWRD('  :  :  ')
     A                                    74'<PYR105P>'
     A                                      SPACEA(2)
     A                                     1'出生月份:'
     A            DMM            2  0     13EDTWRD('0  ')
     A                                    +1'月份'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===='
     A                                      SPACEA(1)
     A                                     1'部門編號'
     A                                    13'部門名稱'
     A                                    29'員工編號'
     A                                    42'員工姓名'
     A                                    57'性別'
     A                                    66'出生日期'
     A                                    77'簽　收'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===='
     A          R PD1                       SPACEB(1)
     A            EM041     R              2
     A            EM042     R              3
     A            EM043     R              4
     A            EM044     R              5
     A            DP02      R             +2REFFLD(DP02 PYDPPF)
     A            EM02      R             30
     A            EM03      R             41
     A            EM32      R             60
     A            PEM33          6  0     66
     A                                      EDTWRD('0  /  /  ')
     A*
     A          R PT1                       SPACEB(2)
     A                                     1'部門中計:'
     A            PT11           5  0     14EDTCDE(1)
     A                                    20'人'
     A                                      SPACEA(1)
     A*
     A          R PT2                       SPACEA(1)
     A                                     1'部門合計:'
     A            PT21           5  0     14EDTCDE(1)
     A                                    20'人'
     A*
     A          R PT3                       SPACEB(2)
     A                                     1'公司總計:'
     A            PT31           5  0     14EDTCDE(1)
     A                                    20'人'
     A                                      SPACEA(1)
     A*
     A*
     A*         R PE1                       SPACEB(1)
     A*                                    1'====================+
     A*                                      ====================+
     A*                                      ====================+
     A*                                      ===================='
     A*                                     SPACEA(1)
     A*                                    1'*接下頁*'
     A*         R PE2                       SPACEB(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===='
     A                                      SPACEA(1)
     A                                     1'PYR105P--->報表結束'
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
