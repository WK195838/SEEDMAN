     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR712P                                    *
     A*    PROGRAM NAME :薪資所得銀行不轉存資料表                  *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 2001/11/07                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      REF(PYRF)
     A*------------------------------------------------------------****
     A          R PH1                       SKIPB(002)
     A            CM03      R              1
     A                                      SPACEA(001)
     A*
     A                                     1'薪資所得銀行不轉存資料表'
     A                                    59'日期:'
     A            DDATE          6  0     67
     A                                      EDTWRD('0  /  /  ')
     A                                    76'頁次:'
     A                                    84PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
     A*
     A                                     1'存入日期:'
     A            DBC04          5  0     +1EDTWRD('0   /  ')
     A                                    +0'/'
     A            DBC05          2  0     +0EDTCDE(3)
     A                                    25'所得類別:'
     A            PS01          16A       +1
     A                                    59'時間:'
     A                                    67TIME
     A                                      EDTWRD('  :  :  ')
     A                                    76'<PYR712P>'
     A                                      SPACEA(001)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =========================='
     A                                      SPACEA(001)
     A*
     A                                     1'支票號碼'
     A                                    +3'部　門'
     A                                    39'員　工'
     A                                    65'帳　號'
     A                                    82'金　額'
     A                                      SPACEA(001)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =========================='
     A*---------------------------------------------------------------***
     A          R PD1                       SPACEB(001)
     A            BC06      R              1
     A            EM041     R             +3
     A            EM042     R             +0
     A            EM043     R             +0
     A            EM044     R             +0
     A            DP02      R             +1REFFLD(DP02 PYDPPF)
     A            BC02      R             +3
     A            EM03      R             +1
     A            PY06      R             +3REFFLD(PY06 PYPYPF)
     A            PMNY           9  0     +3EDTCDE(J)
     A*---------------------------------------------------------------***
     A          R PT1                       SPACEB(001)
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      --------------------------'
     A                                      SPACEA(001)
     A*
     A                                    39'合計:'
     A            PCOUNT         4  0     +5EDTCDE(3)
     A                                    +1'人'
     A            PTOTAL         9  0     80EDTCDE(J)
     A                                    +0'元'
     A*---------------------------------------------------------------***
     A          R PE1                       SPACEB(001)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =========================='
     A                                      SPACEA(001)
     A                                    65'續下頁'
     A*---------------------------------------------------------------***
     A          R PE2                       SPACEB(001)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =========================='
     A                                      SPACEA(001)
     A                                     2'PYR712P===>報表結束'
     A*---------------------------------------------------------------***
     A          R PE9                       SPACEB(001)
     A                                    16'無符合列印範圍之資料'
