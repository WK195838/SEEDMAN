     A****************************************************************
     A*    PRTF NAME    : PYR704P                                    *
     A*    PROGRAM NAME : PYR7040C                                   *
     A*    AUTHOR       : A1038  DAVID, CHEN                         *
     A*    CREATE DATE  : 81/03/25                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : MIRS                                       *
     A*    REMARK       :各類所得扣繳暨免扣繳總表                  *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1                       SKIPB(2)
     A            PCM03H    R              1REFFLD(CM03)
     A                                    97'日期:'
     A            DDATE          6  0     +1EDTWRD('0  /  /  ')
     A                                   114'頁次:'
     A                                    +1PAGNBR
     A                                      SPACEA(1)
     A            PTD01H    R   -2         2REFFLD(TD01)
     A                                      EDTWRD('0  ')
     A                                     4'年度各類所得扣繳暨免扣繳總表'
     A                                    97'時間:'
     A                                    +1TIME
     A                                      EDTWRD('  :  :  ')
     A                                    +2'<PYR704P>'
     A                                      SPACEA(2)
     A                                     1'媒體單位編號:'
     A            PTD02H    R             17REFFLD(TD02)
     A                                    23'公司代號:'
     A            DTD11          2A       35
     A            DTD11O        10O       39
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ======='
     A                                      SPACEA(1)
     A                                     1'格式代號'
     A                                    22'所得人代號'
     A                                    36'給付總額'
     A                                    48'扣繳總額'
     A                                    60'給付淨額'
     A                                    71'員工編號'
     A                                    83'所得人'
     A                                   119'流水號'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ======='
     A                                      SPACEA(1)
     A*
     A          R PD1                       SPACEA(1)
     A            TD04      R              2
     A            PARR2         16         5
     A            TD05      R             23
     A            TD07      R             35EDTCDE(1)
     A            TD08      R             47EDTCDE(1)
     A            TD0708         9  0     59EDTCDE(1)
     A            TD12      R             72
     A            TD13      R             83
     A            EM032P        19        97
     A            TD03      R            119
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       -------'
     A*
     A*---------------------------------------------------------------*
     A          R PE0                       SPACEA(1)
     A                                    24'小　計:'
     A            STD07          9  0     35EDTCDE(1)
     A            STD08          9  0     47EDTCDE(1)
     A            STD708         9  0     59EDTCDE(1)
     A                                    72'筆數:'
     A            SSUM           4  0     80EDTCDE(1)
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ======='
     A*---------------------------------------------------------------*
     A          R PE1                       SPACEA(1)
     A                                    24'合　計:'
     A            TTD07          9  0     35EDTCDE(1)
     A            TTD08          9  0     47EDTCDE(1)
     A            TTD708         9  0     59EDTCDE(1)
     A                                    72'筆數:'
     A            TSUM           4  0     80EDTCDE(1)
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ======='
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
