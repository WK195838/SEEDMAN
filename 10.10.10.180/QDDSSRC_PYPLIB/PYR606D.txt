     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR606D                                    *
     A*    PROGRAM NAME :綜合福利金餘額查詢表　                  *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/04/18                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                          *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      CSRLOC(#LIN #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYR606D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'綜合福利金餘額查詢表'
     A                                      DSPATR(RI)
     A                                  5 23'福委會　:'
     A            DEM01     R        B  5 35REFFLD(GD01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 23'部門編號:'
     A            DEM041         5A  B  7 35
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 41'-'
     A            DEM042         5A  B  7 43
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  9 23'員工編號:'
     A                                      DSPATR(HI)
     A            DEM021    R        B  9 35REFFLD(GD02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 44'-'
     A            DEM022    R        B  9 46REFFLD(GD02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 11 23'申請年度:'
     A            DYEAR          3Y 0B 11 35
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 18  2'執行環境:'
     A            $PENV          1A  B 18 14VALUES('0' '1' '2')
     A                                 18 18'(0-查詢 1-即時 2-批次)'
     A                                 19  2'列印份數:'
     A            $CPY           3Y 0B 19 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 14
     A  27                             21 27' ***請準備８０格報表紙  *** '
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF03 =回主畫面'
     A                                 23 30'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
