     A*%%TS  SD  20040102  103829  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PTA990                                     *
     A*    PROGRAM NAME :考績項目代碼檔維護　                      *
     A*    AUTHOR       : A1379  MAX                                 *
     A*    CREATE DATE  : 93.01.02                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :基本資料系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PTRF)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  20040102  095927  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PTA556.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'考績項目代碼維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別:'
     A            DRVB01         2A  B  4 12
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'考績項目代碼:'
     A            DRVB02         2A  B  5 18
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0B  6 14VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 18'( 1.新增'
     A                                  6 29'2.修改'
     A                                  6 38'4.刪除'
     A                                  6 47'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  20040102  103829  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                  1  2'<PTA990.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'考績項目代碼維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別:'
     A            DRVB01         2A  O  4 12
     A                                  5  2'考績項目代碼:'
     A            DRVB02         2A  O  5 18
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0O  6 14EDTCDE(4)
     A                                  6 18'( 1.新增'
     A                                  6 29'2.修改'
     A                                  6 38'4.刪除'
     A                                  6 47'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  2'考績名稱:'
     A            DRVB03        12O  B  8 14
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 28'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
