     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA906D                                    *
     A*    PROGRAM NAME :排班作業　                                *
     A*    AUTHOR       : A1268 VANESSA                              *
     A*    CREATE DATE  : 98/09/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA906.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 36'排班作業'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DXI01     R        B  4 13REFFLD(XI01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DXI01N        14A  O  4 17
     A                                  5  1'工作區  :'
     A                                      DSPATR(HI)
     A            DXI06     R        B  5 13REFFLD(XI06)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DXI06N    R        O  5 25REFFLD(XA03)
     A                                  6  1'工作組  :'
     A                                      DSPATR(HI)
     A            DXI02     R        B  6 13REFFLD(XI02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DXI02N    R        O  6 16REFFLD(XB03)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 16'( 1.新增2.修改4.刪除5.查詢-
     A                                      )'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 29'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            DNEW           1A  H
     A            RRN            3Y 0H
     A            DDEL           1A  B 10  2
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DXI03     R        B 10  7REFFLD(XI03)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A            DXI04     R   -2   B 10 15REFFLD(XI04)
     A                                      EDTWRD('  /  /  ')
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A                                 10 24'-'
     A            WEEK           1Y 0O 10 27EDTCDE(3)
     A            DXI05     R   -2   O 10 36REFFLD(XI05)
     A                                      EDTWRD('  /  /  ')
     A*----------------------------------------------------------------
     A*                                     CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA14(14)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                      SFLCSRRRN(&#CRRN)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #CRRN          5S 0H
     A                                  1  2'<PYA906.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 36'排班作業'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DXI01     R        O  4 13REFFLD(XI01)
     A            DXI01N        14A  O  4 17
     A                                  5  1'工作區  :'
     A            DXI06     R        O  5 13REFFLD(XA02)
     A            DXI06N    R        O  5 25REFFLD(XA03)
     A                                  6  1'工作組  :'
     A            DXI02     R        O  6 13REFFLD(XI02)
     A            DXI02N    R        O  6 16REFFLD(XB03)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0O  7 13EDTCDE(4)
     A                                  7 16'( 1.新增2.修改4.刪除5.查詢-
     A                                      )'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  2'D'
     A                                      DSPATR(UL)
     A                                  9  4'週班別'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 14'起始日期(星期) '
     A                                      DSPATR(UL)
     A                                  9 35'截止日期'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 23 43'F12=回前畫面'
     A                                 23 59'F13=自動排班'
     A                                 24 10'F14=排班異動'
     A                                 24 26'PA1=上一頁'
     A                                 24 43'PA2=下一頁'
     A                                 24 67'【泛太資訊】'
