     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYR907P                                    *
     A*    PROGRAM NAME :出勤津貼統計表                            *
     A*    AUTHOR       : B1038                                      *
     A*    CREATE DATE  : 87/10/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1
     A                                      SKIPB(2)
     A            CM03          22         1
     A                                    62
     A                                      'Date:'
     A            DDATE          6  0     69
     A                                      EDTWRD('0  /  /  ')
     A                                    79
     A                                      'Page:'
     A                                    86
     A                                      PAGNBR
     A                                      SPACEA(1)
     A                                     2'OVERTIME/SHIFT  ALLOWANCE/+
     A                                      TAXI FARE'
     A                                    62
     A                                      'Time:'
     A                                    69
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    79
     A                                      '<PYR907P>'
     A                                      SPACEA(1)
     A                                     2'Statistic Report ('
     A            PMM            4        20
     A            PYY            4  0     24
     A                                    28')'
     A                                      SPACEA(2)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ======================='
     A                                      SPACEA(1)
     A                                    30'----------------------Overtime Hou+
     A                                      rs---------------------------'
     A                                      SPACEA(1)
     A                                     2'STF NO.'
     A                                    10'NAME'
     A                                    30'Normal'
     A                                    48'Above 46 hrs    Hlday'
     A                                    73'Shift   Taxi   Tran'
     A                                      SPACEA(1)
     A                                    30'1-2   3-4   5+'
     A                                    48'1-2   3-4   5+'
     A                                    71'(1)   (2)'
     A                                      SPACEA(1)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ======================='
     A                                      SPACEA(1)
     A*
     A          R PD1
     A  31        XK03      R              2
     A  31        PEM03     R             10REFFLD(EM03)
     A            PXK121    R             29REFFLD(XK12)
     A                                      EDTCDE(1)
     A            PXK122    R             35REFFLD(XK12)
     A                                      EDTCDE(1)
     A            PXK123    R             41REFFLD(XK12)
     A                                      EDTCDE(1)
     A            PXK124    R             47REFFLD(XK12)
     A                                      EDTCDE(1)
     A            PXK125    R             53REFFLD(XK12)
     A                                      EDTCDE(1)
     A            PXK126    R             59REFFLD(XK12)
     A                                      EDTCDE(1)
     A            PXKH      R             65REFFLD(XK12)
     A                                      EDTCDE(1)
     A            PXK071         3  0     71EDTCDE(2)
     A            PXK072         3  0     76EDTCDE(2)
     A            PXK08          3  0     82EDTCDE(2)
     A            PXK09          4  0     88EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A          R PT
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------'
     A                                      SPACEA(1)
     A                                     2'Total :'
     A            PT01           3  1     29EDTCDE(1)
     A            PT02           3  1     35EDTCDE(1)
     A            PT03           3  1     41EDTCDE(1)
     A            PT04           3  1     47EDTCDE(1)
     A            PT05           3  1     53EDTCDE(1)
     A            PT06           3  1     59EDTCDE(1)
     A            PTH            3  1     65EDTCDE(1)
     A            PT071          3  0     71EDTCDE(2)
     A            PT072          3  0     76EDTCDE(2)
     A            PT08           3  0     82EDTCDE(2)
     A            PT09           4  0     87EDTCDE(2)
     A                                      SPACEA(1)
     A                                     2'Overtime hourse='
     A            PG01           4  1     28EDTCDE(1)
     A                                      SPACEA(1)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ========================='
     A                                      SPACEA(2)
     A                                    14'Prepared by :'
     A                                    27'_____________'
     A                                      SPACEA(2)
     A                                    14'Certified by :'
     A                                    28'_____________'
     A                                      SPACEA(2)
     A                                    14'Approved by :'
     A                                    27'_____________'
     A                                      SPACEA(1)
     A*
     A          R PE9
     A                                    16
     A                                      '無符合列印範圍之資料'
     A                                      SPACEA(1)
