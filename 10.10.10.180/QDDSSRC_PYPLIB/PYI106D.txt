     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYH101ID                                   *
     A*    PROGRAM NAME :退休金主檔資料查詢                        *
     A*    AUTHOR       : MAX PENG                                   *
     A*    CREATE DATE  : 94/03/11                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :新制退休金系統                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYH4PF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            H402      R        O  8  6
     A            EM03      R        O  8 16REFFLD(EM03 PYEMPF)
     A            H403      R        O  8 33REFFLD(H403)
     A            H407      R        O  8 55REFFLD(H407)
     A                                      EDTCDE(1)
     A            H408      R        O  8 71REFFLD(H408)
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI106D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'退休金提撥主檔查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DH401     R        O  4 13REFFLD(H401)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DED01N        14O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5'員工編號'
     A                                      DSPATR(UL)
     A                                  6 16'  員工姓名  '
     A                                      DSPATR(UL)
     A                                  6 33' 個人帳號 '
     A                                      DSPATR(UL)
     A                                  6 48'員工自提率%'
     A                                      DSPATR(UL)
     A                                  6 63'企業提撥率%'
     A                                      DSPATR(UL)
     A            DH402     R        B  7  6REFFLD(H402)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA01=下一頁'
     A                                 23 56'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
