     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYR906P                                    *
     A*    PROGRAM NAME :加班時間日報表                            *
     A*    AUTHOR       : B1038                                      *
     A*    CREATE DATE  : 87/10/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1
     A                                      SKIPB(2)
     A            CM03          22         1
     A                                    52
     A                                      'Date:'
     A            DDATE          6  0     59
     A                                      EDTWRD('0  /  /  ')
     A                                    69
     A                                      'Page:'
     A                                    76
     A                                      PAGNBR
     A                                      SPACEA(1)
     A                                     1'OVERTIME     Detail Report'
     A                                    31'('
     A            PDD            2  0     32
     A            PMM            4        35
     A            PYY            4  0     39
     A                                    43')'
     A                                    52
     A                                      'Time:'
     A                                    59
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    69
     A                                      '<PYR906P>'
     A                                      SPACEA(3)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEA(1)
     A                                     2'STF NO.'
     A                                    20'NAME'
     A                                    62'OverTime'
     A                                      SPACEA(1)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1
     A            XK03      R              2
     A            PEM03         30        20
     A            XK12      R             66
     A                                      EDTCDE(1)
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PT0
     A                                     1'  '
     A                                      SPACEA(2)
     A*----------------------------------------------------------------
     A          R PT1
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                      SPACEA(1)
     A                                     2'Total of Overtime hours:'
     A            PT11           4  1     65EDTCDE(1)
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEA(2)
     A*----------------------------------------------------------------
     A          R PE2
     A                                    10'-----------------------------'
     A                                      SPACEA(1)
     A                                    10'-----------------------------'
     A                                      SPACEA(1)
     A                                    10'-----------------------------'
     A                                      SPACEA(1)
     A                                    10'REASON:______________________'
     A                                      SPACEA(1)
     A                                    10'-----------------------------'
     A                                      SPACEA(4)
     A                                    10'PREPARED  BY :_______________'
     A                                      SPACEA(4)
     A                                    10'CERTIFIED BY :_______________'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE9
     A                                    16
     A                                      '無符合列印範圍之資料'
     A                                      SPACEA(1)
