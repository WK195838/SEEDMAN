     A*%%TS  SD  19970224  144406  ESPGMR      REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI609D                                    *
     A*    PROGRAM NAME :員工貸款資料查詢　                        *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 86/02/24  A1274 EVA                        *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19970224  144406  ESPGMR      REL-V3R1M0  5763-PW1
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            LN02      R        O  8  4
     A            LN02N         12   O  8 13
     A            LN03      R        O  8 26
     A            LN03N         12O  O  8 28
     A            LN11D     R   -1   O  8 41REFFLD(LN11)
     A                                      EDTWRD('0   /  /  ')
     A            LN04      R        O  8 51EDTCDE(1)
     A            LN05      R        O  8 62EDTCDE(1)
     A            LN06D     R   -2   O  8 67REFFLD(LN06)
     A                                      EDTWRD('0  /  ')
     A                                  8 73'-'
     A            LN09D     R   -2   O  8 75REFFLD(LN09)
     A                                      EDTWRD('0  /  ')
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19970224  144406  ESPGMR      REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI609D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工貸款資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DLN01     R        O  4 13REFFLD(LN01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DLN01N        14O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'     員　　工     '
     A                                      DSPATR(UL)
     A                                  6 26'  貸款類別  貸款日期'
     A                                      DSPATR(UL)
     A                                  6 51'貸款金額期數  期 間  '
     A                                      DSPATR(UL)
     A            DLN02     R        B  7  4REFFLD(LN02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DLN03     R        B  7 26REFFLD(LN03)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DLN11     R   -1   B  7 41REFFLD(LN11)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      EDTWRD('0   /  /  ')
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 23 36'PA1=下一頁'
     A                                 23 50'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
