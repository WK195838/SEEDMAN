     A*%%TS  SD  20000913  172314  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI524D                                    *
     A*    PROGRAM NAME :誠實險費率設定全頁查詢　                 *
     A*    AUTHOR       : A1346 WEST                                  *
     A*    CREATE DATE  : 91/02/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                             *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            HN02           2  0O  8 12EDTCDE(4)
     A            HN03           8  0O  8 18EDTCDE(4)
     A            HN04           5  0O  8 33EDTCDE(4)
     A*----------------------------------------------------------------
     A*                                     CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI524>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A                                  1 64'日期:'
     A            DDATE          7Y 0O  1 72EDTWRD('   /  /  ')
     A                                  2 29'誠實險費率設定全頁查詢'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司代碼:'
     A            DHN01          2   O  4 13
     A            CM02      R        O  4 16REFFLD(CM02)
     A                                  5  1'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5'加保月份'
     A                                      DSPATR(UL)
     A                                  6 17'保　　額'
     A                                      DSPATR(UL)
     A                                  6 29'保  費'
     A                                      DSPATR(UL)
     A            DHN02          2  0B  7 12EDTCDE(4)
     A                                      DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 29'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
