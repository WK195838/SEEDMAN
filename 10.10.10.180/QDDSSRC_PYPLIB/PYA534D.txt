     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA533D                                    *
     A*    PROGRAM NAME :團保費率設定                              *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 89/08/21                                   *
     A*    UPDATE DATE  : 91/06/21 A1336 KLAUS                       *
     A*    SYSTEM       :人事薪資系統　                            *
     A****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA534.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'團體保險費率設定'
     A                                      DSPATR(RI)
     A                                  5  1'保險代號:'
     A                                      DSPATR(HI)
     A            DX901          3A  B  5 13
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A**                                5  1'計劃代號:'
     A**          DX902          5A  B  5 13
     A**61                                  DSPATR(PC)
     A**61                                  DSPATR(RI)
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 17'( 1.新增'
     A                                  6 28'2.修改'
     A                                  6 37'4.刪除'
     A                                  6 46'5.查詢)'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF04=欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA534.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  2 32'團體保險費率設定'
     A                                      DSPATR(RI)
     A                                  5  1'保險代號:'
     A                                      DSPATR(HI)
     A            DX901     R        O  5 13REFFLD(X901 PYX9PF)
     A**                                5  1'計劃代號:'
     A**          DX902     R        O  5 13REFFLD(X902 PYX9PF)
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  1'應繳保費：'
     A            DX910         11Y 2B    +1EDTCDE(J)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  1'折扣費率：'
     A            DX906          5Y 2B    +1EDTCDE(J)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'%'
     A                                 12  1'投保金額：'
     A            DX905         11Y 2B    +1EDTCDE(J)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  1'內容說明：'
     A            DX909         32O  B    +1
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
