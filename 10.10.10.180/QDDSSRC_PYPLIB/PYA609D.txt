     A*%%TS  SD  20000811  104710  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A*--------------------------------------------------------------*
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      OVERLAY
     A                                      CA03(03 'RETURN')
     A                                      CF18(18 'FULL PAGE INQ.')
     A                                      CF04(04 'PROMPT')
     A                                      CSRLOC(#LIN       #COL)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA609.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工貸款資料設定'
     A                                      DSPATR(RI)
     A*
     A                                  4  1'公司編號:'
     A            DLN01     R        B  4 13REFFLD(LN01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DLN02     R        B  5 13REFFLD(LN02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'貸款類別:'
     A                                      DSPATR(HI)
     A            DLN03     R        B  6 13REFFLD(LN03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'貸款日期:'
     A            DLN11     R   -1   B  7 13REFFLD(LN11)
     A                                      EDTWRD('0   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                  8 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC2
     A*%%TS  SD  20000811  104710  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CA03(03 'RETURN')
     A                                      CA12(12 'PREVIOUSE')
     A                                  1  2'<PYA609.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工貸款資料設定'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4  4'公司編號:'
     A            DLN01          2A  O  4 16
     A            CM02      R        O  4 25REFFLD(CM02)
     A                                  5  4'員工編號:'
     A            DLN02     R        O  5 16REFFLD(LN02)
     A            EM03      R        O  5 25
     A                                  6  4'貸款類別:'
     A            DLN03     R        O  6 16REFFLD(LN03)
     A            LN03N1        12O  O  6 25
     A                                  7  4'貸款日期:'
     A            DLN11     R   -1   O  7 16REFFLD(LN11)
     A                                      EDTWRD('0   /  /  ')
     A  67                                  DSPATR(RI)
     A                                  9  4'契約編號:'
     A            DLN12     R        B  9 16REFFLD(LN12)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  4'貸款總額:'
     A            DLN04     R        B 10 16REFFLD(LN04)
     A                                      EDTCDE(1)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  4'償還期數:'
     A            DLN05     R        B 11 16REFFLD(LN05)
     A                                      EDTCDE(1)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  4'償還資料:'
     A                                 12 16'第一期年月:'
     A            DLN06     R   -1   B 12 30REFFLD(LN06)
     A                                      EDTWRD('0   /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 16'第一期金額:'
     A            DLN07     R        B 13 30REFFLD(LN07)
     A                                      EDTCDE(1)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 16'每一期金額:'
     A            DLN08     R        B 14 30REFFLD(LN08)
     A                                      EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 40'（除第一期與最後一期外）'
     A                                 15 16'最後月份　:'
     A            DLN09     R   -1   B 15 30REFFLD(LN09)
     A                                      EDTWRD('0   /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16 16'最後期金額:'
     A            DLN10     R        B 16 30REFFLD(LN10)
     A                                      EDTCDE(1)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18  4'貸款保證人:'
     A            DLN13     R        B 18 18REFFLD(LN13)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 41'關係企業註記:'
     A            DLN16     R        B 18 57REFFLD(LN16)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19  4'電話　　:'
     A            DLN14     R        B 19 16REFFLD(LN14)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 20  4'地址　　:'
     A            DLN15     R        B 20 16REFFLD(LN15)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
