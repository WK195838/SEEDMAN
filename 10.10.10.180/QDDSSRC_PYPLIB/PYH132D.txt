     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYH132D                                    *
     A*    PROGRAM NAME :月提繳工資調整作業                      *
     A*    AUTHOR       : MAX PENG                                   *
     A*    CREATE DATE  : 94/09/15                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :勞退新制子系統                        *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYA131WF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYH132.1>'
     A            CONAME        32A  O  1 24MSGID(UPT 9900 PTMF)
     A                                  1 62'日期:'
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'月提繳工資調整作業'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DW1301    R        B    +1REFFLD(W1301)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DW1302    R        B    +1REFFLD(W1302)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'調整年月:'
     A            DW1303    R   -1   B    +1REFFLD(W1303S)
     A                                      EDTWRD('0   /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 15'( 1.新增'
     A                                  7 26'2.修改'
     A                                  7 35'4.刪除'
     A                                  7 44'5.查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                 23  1' PF03 =回主畫面'
     A                                 23 20' PF18 =全頁查詢'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYH132.2>'
     A            CONAME        32A  O  1 24MSGID(UPT 9900 PTMF)
     A                                  1 62'日期:'
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'月提繳工資調整作業'
     A                                      DSPATR(RI)
     A                                  2 62'作業:'
     A            DFUN           6O  O  2 70DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DW1301    R        O    +1REFFLD(W1301)
     A            CM02      R        O    +7REFFLD(CM02 SYCMPF)
     A                                  5  1'員工編號:'
     A            DW1302    R        O    +1REFFLD(W1302)
     A            EM03      R        O    +1REFFLD(EM03 PYEMPF)
     A                                  6  1'調整年月:'
     A            DW1303    R   -1   O    +1REFFLD(W1303S)
     A                                      EDTWRD('0   /  ')
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  8  1'薪資收入:'
     A            DW1304    R        B    +1REFFLD(W1304)
     A                                      EDTCDE(J)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                  9  1'調整後資料'
     A                                      COLOR(PNK)
     A                                  9 40'調整前資料'
     A                                      COLOR(PNK)
     A                                 10  1'月提繳金額:'
     A            DW1308    R        B    +1REFFLD(W1308)
     A                                      EDTCDE(J)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A                                 10 40'月提繳金額:'
     A            W1308B    R        O    +1
     A                                      EDTCDE(J)
     A*
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  1' PF03 =回主畫面'
     A                                 23 20' PF04 =欄位查詢'
     A                                 23 38' PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
