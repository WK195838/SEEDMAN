     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA907D                                    *
     A*    PROGRAM NAME :出勤資料維護                              *
     A*    AUTHOR       : B1038 SHARON                               *
     A*    CREATE DATE  : 98/10/30                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA907.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'出勤資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DXK01     R        O  4 13REFFLD(XF01)
     A            DXK01N        14A  O  4 16
     A                                  5  1'出勤日期:'
     A            DXK04     R   -2   O  5 13REFFLD(XK04)
     A                                      EDTWRD('0  /  /  ')
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  1'工作組別:'
     A            XK02      R        O  7 13
     A            XK02N         30A  O  7 16
     A                                  8  1'員工編號:'
     A            XK03      R        O  8 13
     A            PEM031        20A  O  8 22
     A            DEM03     R        O  8 53REFFLD(EM03)
     A                                  9  1'原定班別:'
     A            XK05      R        O  9 13
     A            XF04S     R        O  9 16REFFLD(XF04)
     A                                      EDTWRD('  :  ')
     A                                  9 22'~'
     A            XF05S     R        O  9 24REFFLD(XF05)
     A                                      EDTWRD('  :  ')
     A            XF03S     R        O  9 30REFFLD(XF03)
     A                                 10  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 11  1'出勤狀態:'
     A                                      DSPATR(HI)
     A            DXK06     R        B 11 13REFFLD(XK06)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DXF04     R        O 11 16REFFLD(XF04)
     A                                      EDTWRD('  :  ')
     A                                 11 22'~'
     A            DXF05     R        O 11 24REFFLD(XF05)
     A                                      EDTWRD('  :  ')
     A            DXF03     R        O 11 30REFFLD(XF03)
     A                                 12  1'休假起迄:'
     A            DHOLF     R   -2   B 12 13REFFLD(XK04)
     A                                      EDTWRD('0  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                 12 22'~'
     A            DHOLT     R   -2   B 12 24REFFLD(XK04)
     A                                      EDTWRD('0  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A                                 13  1'加班時數:'
     A            DXK12     R        B 13 13REFFLD(XK12)
     A                                      EDTCDE(1)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A                                 14  1'遲到時間:'
     A            DXK13     R        B 14 13REFFLD(XK13)
     A                                      EDTCDE(Z)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A                                 14 18'分'
     A                                 15  1'早退時間:'
     A            DXK14     R        B 15 13REFFLD(XK14)
     A                                      EDTCDE(Z)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A                                 15 18'分'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 13'F3 =回主畫面'
     A                                 23 30'F4 =欄位查詢'
     A                                 23 47'F5 =參考資料'
     A                                 23 64'F12 =回前畫面'
     A                                 24 67'【泛太資訊】'
