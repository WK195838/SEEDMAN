     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA993D                                    *
     A*    PROGRAM NAME :員工薪資資料轉檔作業　　                  *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 89/10/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資管理系統                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF13(13)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A                                  1  2'<PYA991.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'員工薪資資料轉檔作業'
     A                                      DSPATR(RI)
     A                                 12 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 13 11' '
     A                                      DSPATR(RI)
     A                                 13 66' '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A  97                             14 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  28                             14 28'轉檔作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
