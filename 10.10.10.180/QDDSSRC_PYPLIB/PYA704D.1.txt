     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA704D                                    *
     A*    PROGRAM NAME :所得稅扣繳申報核定                        *
     A*    AUTHOR       : A1274  EVA                                 *
     A*    CREATE DATE  : 89/03/14                                   *
     A*    UPDATE DATE  : 91/01/08 A1336 KLAUS                       *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*------------------------------------------------------------****
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF13(13)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA704D>'
     A                                  1 26'＜＜所得稅扣繳申報核定＞＞'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A                                  4 29'媒體單位:'
     A                                      DSPATR(HI)
     A            DTD02     R        B  4 41REFFLD(TD02 PYTDPF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5 29'機徵機關:'
     A            DTD19     R        B  5 41REFFLD(TD19)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                  6 29'單位統編:'
     A            DTD17     R        B  6 41REFFLD(TD17 PYTDPF)
     A            TD17N          6O  O  6 51
     A                                  7 29'年　　度:'
     A            DTD01     R   -2   B  7 41REFFLD(TD01 PYTDPF)
     A                                      EDTWRD('0  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8 29'作業選擇:'
     A            DSEL           1A  B  8 41VALUES('1' '2')
     A                                  8 43' 1-核定 2-申報'
     A                                  9 29'重新核定:'
     A            DOPT           1A  B  9 41VALUES('Y' 'N')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 43'(Y/N)'
     A                                 11 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 12 11' '
     A                                      DSPATR(RI)
     A                                 12 66' '
     A                                      DSPATR(RI)
     A                                 13 11' '
     A                                      DSPATR(RI)
     A  98                             13 23'流水號:'
     A  98        DFA04F    R        O 13 33REFFLD(FA04)
     A  98                             13 41'-'
     A  98        DFA04T    R        O 13 43REFFLD(FA04)
     A                                 13 66' '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A  98                             14 23'總件數:'
     A  96                             14 30'申報資料處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A  98        COUNT     R        O 14 33REFFLD(FA04)
     A                                      EDTCDE(3)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A  97                             15 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11' '
     A                                      DSPATR(RI)
     A                                 16 66' '
     A                                      DSPATR(RI)
     A                                 17 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*                                19  2'磁帶機　:'
     A*           $TDEV         10A  B 19 14
     A*                                21 28'***請準備好磁帶***'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 21'PF04 =欄位查詢'
     A                                 23 40'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
