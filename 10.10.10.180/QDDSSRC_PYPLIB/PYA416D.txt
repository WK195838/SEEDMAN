     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*                                                              *
     A*    PROGRAM-ID   : PYA416D                                    *
     A*    PROGRAM NAME :公司獎金出勤維護－年度                    *
     A*    AUTHOR       : A1386 SHARON                               *
     A*    CREATE DATE  : 93/07/14                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(PYFLIBYK/PYPEPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA416.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'公司獎金出勤維護－年度'
     A                                      DSPATR(RI)
     A*
     A                                  4  1'公司編號:'
     A            DPE01     R        B  4 13REFFLD(PE01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DCM03         34A  O    +1
     A                                  5  1'出勤年度:'
     A            DPE03     R   -1   B  5 13REFFLD(PE03)
     A                                      EDTWRD('   ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'員工編號:'
     A                                      DSPATR(HI)
     A            DPE02     R        B  6 13REFFLD(PE02)
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13VALUES(1 2 4 5)
     A                                  7 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*============================================================*
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            3S 0H
     A            DNEW           1A  H
     A*
     A            DPE02     R        B  9  2REFFLD(PE02)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DPE02C        14O  O  9 11
     A*
     A            DPE04     R        B  9 27REFFLD(PE04)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DPE05     R        B  9 39REFFLD(PE05)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DPE06     R        B  9 51REFFLD(PE06)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*------------------------------------------------------------*
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      ROLLUP(25)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYA401.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'公司獎金出勤維護－年度'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DPE01     R        O  4 13REFFLD(PE01)
     A            DCM03         34A  O    +1
     A                                  5  1'出勤年度:'
     A            DPE03     R   -1   O  5 13REFFLD(PE03)
     A                                      EDTWRD('   ')
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  8  1'員工編號'
     A                                      DSPATR(UL)
     A                                    +1'員工姓名'
     A                                      DSPATR(UL)
     A                                  8 26'工作時數'
     A                                      DSPATR(UL)
     A                                  8 38'請假時數'
     A                                      DSPATR(UL)
     A                                  8 50'出勤率'
     A                                      DSPATR(UL)
     A          R DSPC2                     OVERLAY
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PF13=確認'
     A                                 24 67'【泛太資訊】'
