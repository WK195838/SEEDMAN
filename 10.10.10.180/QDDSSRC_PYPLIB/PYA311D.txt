     A*%%TS  SD  19990202  151706  PPSPGMR40   REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*    PROGRAM-ID   : PYA311D                                    *
     A*    PROGRAM NAME :薪資月彙總維護                          *
     A*    AUTHOR       : A1277  KARL                                *
     A*    CREATE DATE  : 88/02/02                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(PYDBFLIB40/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*
     A          R DSPD1
     A*%%TS  SD  19990202  151706  PPSPGMR40   REL-V3R1M0  5763-PW1
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #FLD          10A  H
     A            #REC          10A  H
     A                                  1  2'<PYA313>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A*           $USERN        10A  O  2  2
     A            $USERN        10A  O  2  2
     A                                  2 28'COST CENTRE薪資月彙總維護'
     A                                      DSPATR(RI)
     A                                  2 64'時間:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'公司編號:'
     A            DBG01     R        B  4 19REFFLD(BG01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  1' COST CENTRE :'
     A            DBG021    R        B  5 19REFFLD(BG021)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DBG022    R        B  5 24REFFLD(BG022)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DBG023    R        B  5 31REFFLD(BG023)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'預算年月:'
     A            DBG03     R        B  6 19REFFLD(BG03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      EDTWRD('    /  ')
     A                                  6 30'(拷貝:'
     A            DBG03C    R        B  6 41REFFLD(BG03)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                      EDTWRD('    /  ')
     A                                  6 50')'
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 15VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 19'( 1.新增'
     A                                  7 30'2.修改'
     A                                  7 39'4.刪除'
     A                                  7 48'5.查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A*                                23 19'PF04=欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A*%%TS  SD  19990202  151706  PPSPGMR40   REL-V3R1M0  5763-PW1
     A  54                                  SFLNXTCHG
     A            DNEW           1A  H
     A            RRN            3Y 0H
     A            DDEL           1A  B 10  2
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DBG04     R        B 10  9REFFLD(BG04)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DBX03     R        O 10 21REFFLD(BX03)
     A            DBG05     R        B 10 43REFFLD(BG05)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19990202  151706  PPSPGMR40   REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0010)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYA311>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 28'COST CENTRE薪資月彙總維護'
     A                                      DSPATR(RI)
     A                                  2 64'時間:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'公司編號:'
     A            DBG01     R        O  4 19REFFLD(BG01)
     A                                  5  1' COST CENTRE :'
     A            DBG021    R        O  5 19REFFLD(BG021)
     A            DBG022    R        O  5 24REFFLD(BG022)
     A            DBG023    R        O  5 31REFFLD(BG023)
     A                                  6  1'預算年月:'
     A            DBG03     R        O  6 19REFFLD(BG03)
     A                                      EDTWRD('    /  ')
     A                                  6 30'(拷貝:'
     A            DBG03C    R        O  6 41REFFLD(BG03)
     A                                      EDTWRD('    /  ')
     A                                  6 50')'
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0O  7 15EDTCDE(4)
     A                                  7 19'( 1.新增'
     A                                  7 30'2.修改'
     A                                  7 39'4.刪除'
     A                                  7 48'5.查詢)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  5'薪資項目'
     A                                  9 21'名稱'
     A                                  9 43'預算金額'
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                      OVERLAY
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
