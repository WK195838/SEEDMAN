     A*%%TS  SD  20010322  083342  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR405P                                    *
     A*    PROGRAM NAME :福利獎金資料列印-員工                   *
     A*    AUTHOR       : A1328 JEFFREY                              *
     A*    CREATE DATE  : 90/03/22                                   *
     A*    UPDATE DATE  : 90/04/06 A1274 EVA                         *
     A*    SYSTEM       :音樂講師管理系統                          *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-----------------------------------------------------------***
     A          R DSPD1
     A*%%TS  SD  20010322  083342  PPSPGMRYH   REL-V4R2M0  5769-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYR405D>'
     A            CONAME        32A  O  1 24MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'福利獎金資料表-員工'
     A                                      DSPATR(RI)
     A                                  4 28'福委會編號　　:'
     A            DPB01     R        B  4 46REFFLD(ED01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  6 28'發放日期　　起:'
     A            DPB051         7Y 0B  6 46
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                      EDTWRD('   /  /  ')
     A                                  7 28'　　　　　　迄:'
     A            DPB052         7Y 0B  7 46
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                      EDTWRD('   /  /  ')
     A                                  9 28'獎金類別　　起:'
     A                                      DSPATR(HI)
     A            DPB041    R        B  9 46REFFLD(PB04)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 10 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DPB042    R        B 10 46REFFLD(PB04)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 12 28'部門編號　　起:'
     A                                      DSPATR(HI)
     A            DEM041         7A  B 12 46
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 13 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM042         7A  B 13 46
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 14 28'員工編號　　起:'
     A                                      DSPATR(HI)
     A            DPB021    R        B 14 46REFFLD(PB02)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 15 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DPB022    R        B 15 46REFFLD(PB02)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A  27                             21 27'***請準備１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 20'PF03 =回主畫面'
     A                                 23 38'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
