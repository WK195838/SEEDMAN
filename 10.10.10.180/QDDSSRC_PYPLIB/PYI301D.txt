     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI301D                                    *
     A*    PROGRAM NAME :薪資基本資料查詢                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 86/01/29                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            PY02      R        O  8  7
     A            EM03      R        O  8 16
     A            DPY04          7A  O  8 34
     A            PY06      R        O  8 45
     A            PY09N          6O  O  8 61
     A            PY03      R        O  8 71EDTCDE(2)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0013)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI301D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'薪資基本資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DPY01     R        O  4 13REFFLD(PY01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DPY01N        14   O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5'員工編號'
     A                                      DSPATR(UL)
     A                                  6 17'  員工姓名  '
     A                                      DSPATR(UL)
     A                                  6 33'存入銀行'
     A                                      DSPATR(UL)
     A                                  6 45' 銀行帳號 '
     A                                      DSPATR(UL)
     A                                  6 59'銀行類別'
     A                                      DSPATR(UL)
     A                                  6 70'底　　薪'
     A                                      DSPATR(UL)
     A            DPY02     R        B  7  7DSPATR(CS HI)
     A                                      REFFLD(PY02)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF12=回前畫面'
     A                                 23 36'PA1=下一頁'
     A                                 23 50'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
