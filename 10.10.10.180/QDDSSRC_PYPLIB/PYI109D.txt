     A*%%TS  SD  20070321  111857  HEFPGMR     REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI109D                                   *
     A*    PROGRAM NAME :退休金資料異動查詢                        *
     A*    AUTHOR       : A1413 CLOUD                                *
     A*    CREATE DATE  : 96/03/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :新制退休金系統                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYH3PF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            H302      R        O  8  6
     A            EM03      R        O  8 16REFFLD(EM03 PYEMPF)
     A            DH303     R   -2      8 33REFFLD(H303)
     A                                      EDTWRD('0  /  /  ')
     A            DH304N         6A  O  8 45
     A            H309      R        O  8 55REFFLD(H309)
     A                                      EDTCDE(1)
     A            H310      R        O  8 62REFFLD(H310)
     A                                      EDTCDE(1)
     A            H311      R        O  8 69REFFLD(H311)
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  20070321  111857  HEFPGMR     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI106D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'退休金資料異動查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DH301     R        O  4 13REFFLD(H301)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DED01N        14O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5'員工編號'
     A                                      DSPATR(UL)
     A                                  6 16'  員工姓名  '
     A                                      DSPATR(UL)
     A                                  6 32' 異動日期 '
     A                                      DSPATR(UL)
     A                                  6 45'異動'
     A                                      DSPATR(UL)
     A                                  6 53'自提%'
     A                                      DSPATR(UL)
     A                                  6 61'企提%'
     A                                      DSPATR(UL)
     A                                  6 70'金額'
     A                                      DSPATR(UL)
     A            DH302     R        B  7  6REFFLD(H302)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA01=下一頁'
     A                                 23 56'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
