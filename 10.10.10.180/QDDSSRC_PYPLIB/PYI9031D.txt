     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI903D                                    *
     A*    PROGRAM NAME : 班別全頁查詢                             *
     A*    AUTHOR       : A1268  VNAESSA                             *
     A*    CREATE DATE  : 98/09/25                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 　　　　　　                             *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1   B  8  2DSPATR(CS PC HI)
     A                                      VALUES('/' ' ')
     A            XF02      R        O  8  5
     A            DXF03     R  -10   O  8  8REFFLD(XF03)
     A            XF04      R        O  8 30EDTWRD('0 :  ')
     A                                  8 36'~'
     A            XF05      R        O  8 38EDTWRD('0 :  ')
     A            DXF06          1Y 0O  8 47
     A            DXF07          1Y 0O  8 53
     A            DXF08          1Y 0O  8 59
     A            DXF09          1Y 0O  8 65
     A            DXF11     R   -2   O  8 69REFFLD(XF11)
     A                                      EDTWRD('  /  /  ')
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*                                     CSRLOC(#LIN #COL)
     A                                      OVERLAY
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0013)
     A                                      CA03(03)
     A                                      CF08(08)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI903>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A                                  2 36'班別全頁查詢'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司編號:'
     A            DXF01     R        O  4 13REFFLD(XF01)
     A            DXF01N        14      4 16
     A                                  5  1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ---------'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5'班別代碼／說明       '
     A                                      DSPATR(UL)
     A                                  6 30'時間起迄   '
     A                                      DSPATR(UL)
     A                                  6 44'輪班計程 交通津貼'
     A                                  7 44'津貼車費台北其他'
     A                                      DSPATR(UL)
     A                                  6 68'生效日期'
     A                                      DSPATR(UL)
     A            DDF02     R        B  7  5REFFLD(XF02)
     A                                      DSPATR(CS PC HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 29'F8 =假勤別全頁查詢'
     A                                 23 53'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
