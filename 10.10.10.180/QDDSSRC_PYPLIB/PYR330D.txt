     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYR330D                                    *
     A*    PROGRAM NAME :工讀生年度薪資勞健退休金列印              *
     A*    AUTHOR       : A1490 TERESA                               *
     A*    CREATE DATE  : 2007/09/19                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYENPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYR330D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 26'工讀生年度薪資勞健退休金列印'
     A                                      DSPATR(RI)
     A                                  9 26'公司編號:'
     A            SEN01     R        B  9 38REFFLD(EN01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  9 41'~'
     A            EEN01     R        B  9 43REFFLD(EN01)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 11 26'員工編號:'
     A            SEN02     R        B 11 38REFFLD(EN02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 11 49'~'
     A            EEN02     R        B 11 51REFFLD(EN02)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 13 26'年  度:'
     A            DYY            4  0B 13 38EDTWRD('0   ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 19  2'執行環境:'
     A            $PENV          1A  B 19 14VALUES('0' '1' '2')
     A                                 19 18'(0-查詢 1-即時 2-批次)'
     A                                 20  2'列印份數:'
     A            $CPY           3Y 0B 20 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 21  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 21 14
     A  27                             21 27'***請準備１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 24 67'【泛太資訊】'
