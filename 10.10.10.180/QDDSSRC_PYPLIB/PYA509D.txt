     A*  92/01/09  11:19:43    FOPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYA509D                                    *
     A*    AUTHOR       : A1084  CHATRLES                            *
     A*    CREATE DATE  : 81/01/09                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PYA509                                     *
     A*    SYSTEM       : MIRS                                       *
     A*    SUBSYSTEM    : PY                                         *
     A*    REMARK       :人事統計資料設定                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A                                      CF03(03)
     A          R SFLSR                     SFL
     A*  92/01/09  11:19:43    FOPGMR      REL-R03M00  5728-PW1
     A  54                                  SFLNXTCHG
     A            RRN            2Y 0O  8 16EDTCDE(3)
     A            DR103     R        B  8 37REFFLD(R103)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A                                      DSPATR(CS)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8 40'∼'
     A            DR104     R        B  8 45REFFLD(R104)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A                                      DSPATR(CS)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*  92/01/09  11:19:43    FOPGMR      REL-R03M00  5728-PW1
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A            DRRN           2S 0H      SFLRCDNBR
     A                                  1  2'<PYA509.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'人事統計資料設定'
     A                                      DSPATR(RI)
     A                                  4  1'報表名稱:'
     A            DCHK           1A  B  4 13VALUES('1' '2')
     A                                  4 16'(1.年齡級距2.年資級距)'
     A                                  5  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  7 20' ----         ----------'
     A                                  7 51'----------'
     A                                  6 14'序號'
     A  31                              6 37'年資級距'
     A N31                              6 37'年齡級距'
     A                                  7  2'             ----                 -
     A                                        --------                         -
     A                                               '
     A          R DSPC11
     A*  91/12/23  15:24:09    FOPGMR      REL-R03M00  5728-PW1
     A                                 18  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                 23  2'PF3 =回主畫面'
     A                                 24 66MSGCON(014 UMI0001 *LIBL/CEMF)
     A  98        ERR1          50A  O 22  2MSGID(UPT 2030 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UPT 2040 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22  2MSGID(UPT 0080 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 22  2MSGID(UMI 0010 CEMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 22  2MSGID(UMI 0020 CEMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 22  2MSGID(UPT 1003 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR9          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
