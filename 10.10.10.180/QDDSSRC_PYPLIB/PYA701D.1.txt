     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF13(13)
     A                                  1  2'<PYA701D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 28'薪資所得銀行媒體轉存作業'
     A                                      DSPATR(RI)
     A                                  5 16'  公司別:'
     A            DPY01     R        B  5 28REFFLD(PY01)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6 16'轉存銀行:'
     A            DBT061    R        B  6 28REFFLD(BT061)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 16'所得類別:'
     A            DBT03     R        B  7 28REFFLD(BT03)
     A                                      VALUES('1' '2' '4')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  7 30'(1- 5日薪'
     A                                  7 42'2- 20日薪'
     A                                  7 55'4-公司獎金)'
     A                                  9 16'存入日期:'
     A            DBT045         6Y 0B  9 28EDTWRD('0  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 10 16'計薪年月:'
     A            DPH03          4Y 0B 10 28EDTWRD('0  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 11 16'獎金年度:'
     A            DPB03          2Y 0B 11 28EDTWRD('0  ')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A* 91                             12 16'獎金類別:'
     A* 91        DPB04     R        B 12 28REFFLD(PB04)
     A*                                     VALUES('01' '02' '03' '04')
     A* 65                                  DSPATR(PC)
     A* 65                                  DSPATR(RI)
     A* 91                                +1'(01-年終獎金02-夏季獎金'
     A* 91                                +1'03-紅利04-其他)'
     A  97                             21 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(RI)
     A  96                             21 23'　　　轉存作業處理中！！　　'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A*
     A  95                             21 23'　　轉存作業處理完成！！  '
     A                                      DSPATR(RI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
