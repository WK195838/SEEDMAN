     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA104D                                    *
     A*    PROGRAM NAME :教育訓練資料建立                          *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 85/08/27                                   *
     A*    UPDATE DATE  : 90/06/23 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA104.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'教育訓練資料建立'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DED01     R        B  4 13REFFLD(ED01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DED02     R        B  5 13REFFLD(ED02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'受訓日期:'
     A            DED03     R   -2   B  6 13REFFLD(ED03)
     A                                      EDTWRD('0  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'訓練類別:'
     A            DED11     R        B  7 13REFFLD(ED11)
     A                                      VALUES('1' '2' '3' '4' '5')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 16'(1.內訓2.新人訓練3.讀書會4.-
     A                                      研討會5.外訓)'
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  8 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CHGINPDFT(UL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CA12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYA104.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'教育訓練資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4 24'公　　司  :'
     A            DED01     R        O  4 38REFFLD(ED01)
     A            CM02      R        O  4 47
     A                                  5 24'部　　門  :'
     A            DEM04          7A  O  5 38
     A            DP02      R        O    +1REFFLD(DP02 PYDPPF)
     A                                  6 24'員　　工  :'
     A            DED02     R        O  6 38REFFLD(ED02)
     A            EM03      R        O  6 47
     A                                  7 24'訓練類別  :'
     A            DED11     R        O  7 38REFFLD(ED11)
     A            ED11N         12   O  7 40
     A                                  8 24'訓練課程　:'
     A                                      DSPATR(HI)
     A            DED05     R        B  8 38REFFLD(ED05)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            ED05N     R        O  8 47REFFLD(EC02)
     A                                 10 24'起迄日期　:'
     A            DED03     R   -2   O 10 38REFFLD(ED03)
     A                                      EDTWRD('0  /  /  ')
     A                                 10 48'--'
     A            DED04     R   -2   B 10 51REFFLD(ED04)
     A                                      EDTWRD('0  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 12 24'時　　數  :'
     A            DED09     R        B 12 38REFFLD(ED09)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 14 22'訓練主辦單位:'
     A            DED10     R        B 14 38REFFLD(ED10)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 16 22'公司負擔金額:'
     A            DED07     R        B 16 38REFFLD(ED07)
     A                                      EDTCDE(1)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 18 22'員工負擔金額:'
     A            DED08     R        B 18 38REFFLD(ED08)
     A                                      EDTCDE(1)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04=欄位查詢'
     A                                 23 38'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
