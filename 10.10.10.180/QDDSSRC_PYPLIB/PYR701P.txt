     A****************************************************************
     A*    PRTF NAME    : PYR701P                                    *
     A*    PROGRAM NAME : PYR7010C                                   *
     A*    AUTHOR       : A1037  JOSNSON                             *
     A*    CREATE DATE  : 81/03/05                                   *
     A*    UPDATE DATE  : 88/10/12 A1274 EVA                         *
     A*    UPDATE DATE  : 96/05/23 A1491 EASON   增加分行          *
     A*    SYSTEM       : MIRS                                       *
     A*    REMARK       :薪資所得銀行轉存資料列印                  *
     A****************************************************************
     A                                      REF(PYRF)
     A*----------------------------------------------------------------
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1
     A                                    59
     A                                      '日期:'
     A            DDATE          6  0     67
     A                                      EDTWRD('0  /  /  ')
     A                                    76
     A                                      '頁次:'
     A                                    84
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      '銀行轉存明細表'
     A                                    59
     A                                      '時間:'
     A                                    67
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    76
     A                                      '<PYR701P>'
     A                                      SPACEA(002)
     A                                     1
     A                                      '儲金帳號:'
     A*           XXXX           8A       +1
     A            PBN041    R             +1
     A                                      REFFLD(BN04)
     A*                                   59'公司帳號:'
     A*           BT09      R             71
     A                                      SPACEA(001)
     A                                     1
     A                                      '所得類別:'
     A*           DBT03     R             +1
     A*                                     REFFLD(BT03)
     A            PS01          16A       +1
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEA(001)
     A*                                    1'部門編號'
     A                                     3
     A                                      '員工編號'
     A                                    29
     A                                      '存入分行'
     A                                    39
     A                                      '存入帳號'
     A                                    52
     A                                      '入帳金額'
     A                                    65
     A                                      '入帳日期'
     A*                                   65'入帳銀行'
     A*                                   76
     A*                                     '身份証號'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A* 31        EM041     R              1
     A* 31        EM042     R              2
     A* 31        EM043     R              3
     A* 31        EM044     R              4
     A* 31        DP02      R              8REFFLD(DP02 PYDPPF)
     A            BT02      R              3
     A            EM03      R             11
     A            BT062     R             32
     A                                    36'-'
     A            BT07      R             37
     A            BT08      R             53
     A                                      EDTCDE(1)
     A            BT04W          6  0     66
     A                                      EDTWRD('0  /  /  ')
     A*           BN04      R             66
     A*           EM31      R             76
     A*----------------------------------------------------------------
     A          R PT1
     A                                      SPACEB(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      --------------'
     A                                      SPACEA(001)
     A                                    30
     A                                      '部門小計:'
     A*----------------------------------------------------------------
     A            PT11           9  0     43
     A                                      EDTCDE(1)
     A                                    56
     A                                      '筆數:'
     A            PT12           3  0     64
     A                                      EDTCDE(3)
     A                                    67
     A                                      '筆'
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PT2
     A                                      SPACEB(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      --------------'
     A                                      SPACEA(001)
     A                                    34
     A                                      '合計:'
     A            PT21           9  0     43
     A                                      EDTCDE(1)
     A                                    56
     A                                      '筆數:'
     A            PT22           3  0     64
     A                                      EDTCDE(3)
     A                                    67
     A                                      '筆'
     A*----------------------------------------------------------------
     A          R PE1
     A                                      SPACEB(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEA(001)
     A                                    65
     A                                      '續下頁'
     A*----------------------------------------------------------------
     A          R PE2
     A                                      SPACEB(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEA(001)
     A*                                    2'PYR701P===>報表結束'
     A                                     1
     A                                      '　　　'
     A                                      SPACEB(001)
     A                                     1
     A                                      '主管'
     A                                    60
     A                                      '製表'
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
