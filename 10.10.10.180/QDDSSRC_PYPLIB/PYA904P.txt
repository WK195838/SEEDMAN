     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PRTF NAME    : PYA904P                                    *
     A*    PROGRAM-ID   : PYA904                                     *
     A*    PROGRAM NAME :勞健保及勞退保額調整報表                  *
     A*    AUTHOR       : A1445 HONDA                                *
     A*    CREATE DATE  : 2005/12/15                                 *
     A*    UPDATE DATE  : 2006/03/17                                 *
     A*    REMARK       :勞健保及勞退保額調整報表                  *
     A****************************************************************
     A          R PH0
     A                                      SKIPB(001)
     A          R PH1                       TEXT('表頭')
     A                                      SKIPB(001)
     A                                    56'國泰產險人事管理系統'
     A                                   110'製表日期:'
     A            DDATE          6  0    122EDTWRD('0  /  /  ')
     A                                      SPACEA(1)
     A                                     1'PYA904P'
     A            EMDP           7A       16TEXT('部門代碼')
     A            DP02          16O       25TEXT('部門簡稱')
     A                                    54'勞健保及勞退保額調整報表'
     A                                   110'製表時間:'
     A                                   122TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A            @UID          10A        1
     A                                    25'生效年月'
     A            @DAT           4  0     37EDTWRD('0  /  ')
     A                                    53'====================+
     A                                       ========'
     A                                   110'列印頁次:'
     A                                   125PAGNBR
     A                                      SPACEA(002)
     A*         R DT1
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       ------------'
     A                                      SPACEA(002)
     A*
     A                                     1'員工編號　姓　　名'
     A                                    26'出生年月日'
     A                                    38'單位'
     A                                    45'三個月平均薪'
     A                                    62'舊勞退     勞退'
     A                                    84'舊勞保     勞保'
     A                                   106'舊健保    健保'
     A                                   124'變動別'
     A                                      SPACEA(001)
     A*
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       ------------'
     A                                      SPACEA(002)
     A*--------------------------------------------------------------
     A          R PD1
     A            LO02           8A        1TEXT('員工號碼')
     A*
     A            LO04          14A       13TEXT('員工姓名')
     A*
     A            EM33P          6  0     28EDTWRD('0  /  /  ')
     A                                      TEXT('出生日期')
     A*
     A            LO05           7A       39TEXT('部門別')
     A*
     A            LO15           7  0     49EDTCDE(1)
     A                                      TEXT('平均薪')
     A*
     A            LO11           7  0     60TEXT('舊勞退額')
     A                                      EDTCDE(1)
     A*
     A            LO12           7  0     71TEXT('新勞退額')
     A                                      EDTCDE(1)
     A*
     A            LO07           7  0     82TEXT('舊勞保額')
     A                                      EDTCDE(1)
     A*
     A            LO08           7  0     93TEXT('新勞保額')
     A                                      EDTCDE(1)
     A*
     A            LO09           7  0    104TEXT('舊健保額')
     A                                      EDTCDE(1)
     A*
     A            LO10           7  0    114TEXT('新健保額')
     A                                      EDTCDE(1)
     A*
     A            LO13           1A      128TEXT('變動別')
     A*
     A                                      SPACEA(001)
     A*--------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ============'
     A                                      SPACEA(002)
     A                                    42'舊勞退        勞退'
     A                                    70'舊勞保        勞保'
     A                                    98'舊健保        健保'
     A                                      SPACEA(002)
     A                                     3'合　　計:'
     A            DCOUNT         3  0     18TEXT('部門列印人數')
     A                                      EDTCDE(3)
     A                                    23'人'
     A            DPLO11         9  0     38TEXT('舊勞退合計')
     A                                      EDTCDE(1)
     A            DPLO12         9  0     52TEXT('新勞退合計')
     A                                      EDTCDE(1)
     A            DPLO07         9  0     66TEXT('舊勞保合計')
     A                                      EDTCDE(1)
     A            DPLO08         9  0     80TEXT('新勞保合計')
     A                                      EDTCDE(1)
     A            DPLO09         9  0     94TEXT('舊健保合計')
     A                                      EDTCDE(1)
     A            DPLO10         9  0    108TEXT('新健保合計')
     A                                      EDTCDE(1)
     A                                      SPACEA(2)
     A*--------------------------------------------------------------
     A          R PH2                       TEXT('表頭')
     A                                      SKIPB(001)
     A                                    56'國泰產險人事管理系統'
     A                                   110'製表日期:'
     A            DDATE          6  0    122EDTWRD('0  /  /  ')
     A                                      SPACEA(1)
     A                                     1'PYA904P'
     A                                    54'勞健保及勞退保額調整報表'
     A                                   110'製表時間:'
     A                                   122TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A            @UID          10A        1
     A                                    25'生效年月'
     A            @DAT           4  0     37EDTWRD('0  /  ')
     A                                    53'====================+
     A                                       ========'
     A                                   110'列印頁次:'
     A                                   125PAGNBR
     A                                      SPACEA(002)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       ------------'
     A                                      SPACEA(002)
     A*
     A                                    17'合計人數'
     A                                    40'舊勞退額　　　新勞退額'
     A                                    68'舊勞保額　　　新勞保額'
     A                                    96'舊健保額　　　新健保額'
     A                                      SPACEA(001)
     A*
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       ------------'
     A                                      SPACEA(002)
     A*--------------------------------------------------------------
     A          R PE2
     A*
     A                                     3'合　　計:'
     A            CCOUNT         4  0     18TEXT('公司列印人數')
     A                                      EDTCDE(3)
     A                                    23'人'
     A            CMLO11         9  0     38TEXT('舊勞退合計')
     A                                      EDTCDE(1)
     A            CMLO12         9  0     52TEXT('新勞退合計')
     A                                      EDTCDE(1)
     A            CMLO07         9  0     66TEXT('舊勞保合計')
     A                                      EDTCDE(1)
     A            CMLO08         9  0     80TEXT('新勞保合計')
     A                                      EDTCDE(1)
     A            CMLO09         9  0     94TEXT('舊健保合計')
     A                                      EDTCDE(1)
     A            CMLO10         9  0    108TEXT('新健保合計')
     A                                      EDTCDE(1)
     A                                      SPACEA(2)
     A*--------------------------------------------------------------
     A*
     A*
     A*%%SS
     A*%%CP+999CRTPRTF
     A*%%CP+    IGCDTA(*YES)
     A*%%CP+    DEVTYPE(*SCS)
     A*%%CP     PAGESIZE(*N       198      *N     )
     A*%%***********************************************************************
