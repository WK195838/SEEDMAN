     A****************************************************************
     A*    PRTF NAME    : PYR107P                                    *
     A*    PROGRAM NAME : PYR107                                     *
     A*    AUTHOR       : A1084  DERLERN                             *
     A*    CREATE DATE  : 79/06/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : MIRS                                       *
     A*    REMARK       :試用期滿人員資料列印                      *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1
     A                                    52
     A                                      '日期:'
     A            DDATE          6  0     60
     A                                      EDTWRD('0  /  /  ')
     A                                    69
     A                                      '頁次:'
     A                                    76
     A                                      PAGNBR
     A            DYM            4  0      2
     A                                      EDTWRD('0  /  ')
     A                                      SPACEB(001)
     A                                     7
     A                                      '試用期滿人員明細表'
     A                                    52
     A                                      '時間:'
     A                                    60
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    70
     A                                      '<PYR107P>'
     A                                     1
     A                                      '部門代號:'
     A                                      SPACEB(002)
     A            DEM041         7        13
     A                                    19
     A                                      '-'
     A            DEM042         7        21
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     1
     A                                      '部門名稱'
     A                                      SPACEB(001)
     A                                    25
     A                                      '員工姓名'
     A                                    46
     A                                      '性別'
     A                                    52
     A                                      '到職日期'
     A                                    64
     A                                      '試用期滿日'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A          R PD1
     A                                      SPACEB(001)
     A            EM041     R              1
     A            EM042     R              2
     A            EM043     R              3
     A            EM044     R              4
     A            DP02      R              8REFFLD(DP02 PYDPPF)
     A            EM02      R             +1
     A            EM03      R             33
     A            EM32      R             49
     A            PEM13     R   -2        53
     A                                      REFFLD(EM13)
     A                                      EDTWRD('0  /  /  ')
     A            PEM20     R   -2        65
     A                                      REFFLD(EM20)
     A                                      EDTWRD('0  /  /  ')
     A*           PEM201         3A       78
     A*
     A          R PT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門人數小計:'
     A            PT11           5  0     +1
     A                                      EDTCDE(1)
     A                                    +1
     A                                      '人'
     A*
     A          R PT2
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門人數中計:'
     A            PT21           5  0     +1
     A                                      EDTCDE(1)
     A                                    +1
     A                                      '人'
     A*
     A          R PT3
     A                                      SPACEA(001)
     A                                     1
     A                                      '部門人數合計:'
     A            PT31           5  0     +1
     A                                      EDTCDE(1)
     A                                    +1
     A                                      '人'
     A*
     A          R PT4
     A                                      SPACEB(002)
     A                                     1
     A                                      '部門人數總計:'
     A            PT41           5  0     +1
     A                                      EDTCDE(1)
     A                                    +1
     A                                      '人'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     1
     A                                      'PYR107P--->報表結束'
     A                                      SPACEB(001)
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
