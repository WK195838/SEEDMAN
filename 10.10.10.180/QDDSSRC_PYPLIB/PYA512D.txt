     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA512D                                    *
     A*    PROGRAM NAME :部門基本資料建立                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 85/12/26                                   *
     A*    UPDATE DATE  : 90/08/06 A1274 EVA                         *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      CA03(03 'RETURN')
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA512.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'部門基本資料設定'
     A                                      DSPATR(RI)
     A                                  4  1'部門代碼:'
     A            DDP01          7   B  4 13
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  1'作業選擇:'
     A            DOPT           1Y 0B  5 13VALUES(1 2 4 5)
     A                                  5 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03 'RETURN')
     A                                      CA12(12 'PREVIOUSE')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA512.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'部門基本資料設定'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A*
     A                                  5  1'部門編號:'
     A            DDP01          7   O  5 13
     A*
     A                                  7  1'部門簡稱:'
     A            DDP02     R        B  7 13REFFLD(DP02)
     A  60                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                  9  1'部門名稱:'
     A            DDP08     R        B  9 13REFFLD(DP08 PYDPPF)
     A  61                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A                                 13  1'稅籍歸屬公司:'
     A            DDP09     R        B 13 17REFFLD(DP09 PYDPPF)
     A  62                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DP09N     R        O 13 20REFFLD(CM03 SYCMPF)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
