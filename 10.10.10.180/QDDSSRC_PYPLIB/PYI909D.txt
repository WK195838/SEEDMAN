     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI909D                                    *
     A*    PROGRAM NAME :支援作業全頁查詢                          *
     A*    AUTHOR       : B1038 SHARON                               *
     A*    CREATE DATE  : 98/11/04                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 　　　  　                             *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1   B  8  2DSPATR(CS PC HI)
     A                                      VALUES('/' ' ')
     A            XD02      R        O  8  5
     A            XD03      R        O  8 22
     A            XD04      R        O  8 35
     A            XD05      R        O  8 45
     A            XD06      R        O  8 57
     A            DXD07     R   -2   O  8 62REFFLD(XD07)
     A                                      EDTWRD('  /  /  ')
     A                                  8 71'~'
     A            DXD08     R   -2   O  8 73REFFLD(XD08)
     A                                      EDTWRD('  /  /  ')
     A            DEM03     R        O  9  5REFFLD(EM03)
     A            DEM031        30   O  9 23
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*                                     CSRLOC(#LIN #COL)
     A                                      OVERLAY
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0006)
     A                                      CA03(03)
     A                                      CF02(02)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PYI909>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A                                  2 32'支援作業全頁查詢'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司編號:'
     A            DXD01     R        O  4 13REFFLD(XD01)
     A            DXD01N        14      4 16
     A                                  5  1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ---------'
     A                                  6  2'S'
     A                                  6  4'員工代碼/姓名'
     A                                  6 22'原定工作區/工作組'
     A                                  6 44'支援工作區/工作組'
     A                                  6 67'起迄日期'
     A            DXD02     R        B  7  5REFFLD(XD02)
     A                                      DSPATR(CS PC HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 29'F2 =明細資料'
     A                                 23 47'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
