     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PYA322D                                    *
     A*    AUTHOR       : JADI                                       *
     A*    CREATE DATE  : 98/12/14                                   *
     A*    UPDATE DATE  :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03 '回主畫面')
     A                                      CF13(13)
     A                                      CF18(18)
     A                                  1  2'<PYA322.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 27'公司年度調薪(AI)試算 '
     A                                      DSPATR(RI)
     A*
     A                                  4  3'公司編號:'
     A            DAI01     R        B  4 16REFFLD(AI01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A*N32                                  DSPATR(PR)
     A                                  5  3'調薪年度:'
     A            DAI02     R   -2   B  5 16REFFLD(AI02)
     A                                      EDTWRD('  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  3'調薪月份:'
     A            DAI03     R        B  6 16REFFLD(AI03)
     A                                      EDTWRD('  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  3'調薪別:'
     A            DAI04     R        O  7 16REFFLD(AI04)
     A                                      DFTVAL('AI')
     A            DOPT           1Y 0B  8 14VALUES(1 3 4 5)
     A                                      EDTCDE(4)
     A                                  8 18'( 1.新增'
     A                                    +1'3.重印'
     A                                    +1'4.刪除'
     A                                    +1'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A*                                23 28'F4 =欄位查詢'
     A                                 23 27'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03)
     A*                                     CF05(05)
     A                                      CF12(12)
     A                                  1  2'<PYA322.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 27'公司年度調薪(AI)試算 '
     A                                      DSPATR(RI)
     A*
     A                                  4  3'公司編號:'
     A            DAI01     R        O  4 16REFFLD(AI01)
     A                                  5  3'調薪年度:'
     A            DAI02     R   -2   O  5 16REFFLD(AI02)
     A                                      EDTWRD('  ')
     A                                  6  3'調薪月份:'
     A            DAI03     R        O  6 16REFFLD(AI03)
     A                                      EDTWRD('  ')
     A                                  7  3'調薪別:'
     A            DAI04     R        O  7 16REFFLD(AI04)
     A                                      DFTVAL('AI')
     A            DOPT           1Y 0O  8 14
     A                                      EDTCDE(4)
     A                                  8 18'( 1.新增'
     A                                    +1'3.重印'
     A                                    +1'4.刪除'
     A                                    +1'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                 10  3'基準日期:'
     A            AI05           6Y 0O 10 16EDTWRD('    /  /  ')
     A                                 11  1'<<調薪公式>>'
     A                                 12  5'調薪比例＝'
     A            AI06      R        O 12 20
     A            AI07      R        O 12 25
     A                                 12 31'+'
     A            AI08      R        O 12 33
     A                                 13  7'A:本薪'
     A                                 14  7'B:（職等上限＋下限）／２'
     A                                 15  7'C:職等基本調薪額'
     A                                 16  5'尾數處理至'
     A            AT05      R        B 16 18VALUES('0' '1' '2' '3')
     A                                 16 20'(0:個位數1:十位數2:百位數'
     A                                 16 52'3:千位數)'
     A                                 17 13'採'
     A            AT06      R        B 17 18VALUES('H' 'P' 'M')
     A                                 17 20'(H:四捨五入P:無條件進位'
     A                                 17 47'M:無條件捨去)'
     A                                 18  5'產生試算檔及印表:'
     A            AT07      R        B 18 25VALUES('Y' 'N')
     A                                      DFTVAL('Y')
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
