     A****************************************************************
     A*    PRTF NAME    : PYR109P                                    *
     A*    PROGRAM NAME : PYR109                                     *
     A*    AUTHOR       : A1084  DERLERN                             *
     A*    CREATE DATE  : 79/06/20                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : MIRS                                       *
     A*    REMARK       :留職期滿人員資料列印                      *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1                       SKIPB(2)
     A            CM03      R              1
     A                                    64'日期:'
     A            DDATE          6  0     72EDTWRD('0  /  /  ')
     A                                    81'頁次:'
     A                                    89PAGNBR
     A                                      SPACEA(1)
     A                                     1'留職期滿人員明細表'
     A                                    64'時間:'
     A                                    72TIME
     A                                      EDTWRD('  :  :  ')
     A                                    82'<PYR109P>'
     A                                      SPACEA(2)
     A                                     1'留職期滿年月:'
     A            DYM            4  0     17EDTWRD('0  /  ')
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ============'
     A                                      SPACEA(1)
     A                                     1'部門名稱'
     A                                    26'員工姓名'
     A                                    46'性別'
     A                                    52'留職停薪日'
     A                                    64'留職天數'
     A                                    74'復職日期'
     A                                    85'留職原因'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ============'
     A                                      SPACEA(1)
     A          R PD1                       SPACEA(1)
     A            EM041     R              2
     A            EM042     R              3
     A            EM043     R              4
     A            EM044     R              5
     A            DP02      R              9REFFLD(DP02 PYDPPF)
     A            EM02      R             +1
     A            EM03      R             +0
     A            EM32      R             +1
     A            PTX06     R   -2        54REFFLD(TX06)
     A                                      EDTWRD('0  /  /  ')
     A            TX11      R             65
     A                                      EDTCDE(1)
     A            PTX04     R   -2        75REFFLD(TX04)
     A                                      EDTWRD('0  /  /  ')
     A            PD11          10O       85
     A*
     A*         R PE1
     A*                                    1'====================+
     A*                                      ====================+
     A*                                      ====================+
     A*                                      ===================='
     A*                                     SPACEA(1)
     A*                                    1'*接下頁*'
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ============'
     A                                      SPACEA(1)
     A                                     1'PYR109P--->報表結束'
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
