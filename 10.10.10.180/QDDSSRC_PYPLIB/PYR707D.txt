     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR707D                                    *
     A*    PROGRAM NAME :薪資所得銀行轉存資料列印－銀行別          *
     A*    AUTHOR       : TOM T.                                     *
     A*    CREATE DATE  : 2000/11/17                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<PYR707D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 26'薪資所得銀行轉存資料表－銀行'
     A                                      DSPATR(RI)
     A*
     A                                  5 26'公司編號:'
     A            DBT01     R        B  5 38REFFLD(BT01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 26'轉存銀行:'
     A            DBT061    R        B  7 38REFFLD(BT061)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 26'存入月份:'
     A            DBT04     R   -2   B  9 38REFFLD(BT04)
     A                                      EDTWRD('0  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 11 26'存入日期:'
     A            DBT05     R        B 11 38REFFLD(BT05)
     A                                      EDTCDE(4)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 13 26'所得類別:'
     A            DBT03     R        B 13 38REFFLD(BT03)
     A                                      VALUES('1' '5' '2')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 13 42'1.下期薪資'
     A                                 14 42'2.上期獎金'
     A                                 15 42'5.公司獎金'
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 27'***請準備好８０格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
