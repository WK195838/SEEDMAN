     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI114D                                    *
     A*    PROGRAM NAME :勞工保險資料全頁查詢                      *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 89/08/22                                   *
     A*    UPDATE DATE  : 90/01/31 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*--------------------------------------------------------------**
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            L302           8A  O  8  6
     A            EM03      R        O  8 16
     A            DTX04          6  0O  8 33
     A                                      EDTWRD('0  /  /  ')
     A            L307           2A  O  8 44
     A            TX03N         12   O  8 47
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI114D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'勞工保險資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DTX01          2A  O  4 13
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DTX01N        14O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5'員工編號'
     A                                      DSPATR(UL)
     A                                  6 16'  員工姓名  '
     A                                      DSPATR(UL)
     A                                  6 32'生效日期'
     A                                      DSPATR(UL)
     A                                  6 44'   異動類別  '
     A                                      DSPATR(UL)
     A            KTX02          8A  B  7  6
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            KTX04          6  0B  7 33
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      EDTWRD('0  /  /  ')
     A            KTX03          2A  B  7 44DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 36'PA01=下一頁'
     A                                 23 50'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
