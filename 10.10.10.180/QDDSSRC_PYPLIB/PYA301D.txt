     A*%%TS  SD  20070414  140053  CWDEV2      REL-V4R4M0  5769-PW1
     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYA301D                                    *
     A*    AUTHOR       : A1070  CHOIE                               *
     A*    CREATE DATE  : 79/06/01                                   *
     A*    UPDATE DATE  : 91/06/19 A1336 KLAUS                       *
     A*    PROGRAM NAME : PYA301                                     *
     A*    SYSTEM       : MIRS                                       *
     A*    SUBSYSTEM    : PY                                         *
     A*    REMARK       :薪資基本資料建立                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA301.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'薪資基本資料建立'
     A                                      DSPATR(RI)
     A*
     A                                  4  1'公司編號:'
     A            DPY01     R        B  4 13REFFLD(PY01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DPY02     R        B  5 13REFFLD(PY02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                  6 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A                                 23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            2S 0O 13  3
     A            DPD03     R        B 13  6REFFLD(PD03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A            SS02      R        O 13  9
     A            SS56           2A  O 13 25
     A            DPD04     R        B 13 28REFFLD(PD04)
     A                                      CHECK(RB)
     A                                      EDTCDE(2)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A  *DS3                                SFLLIN(0006)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA301.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'薪資基本資料建立'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  5  2'公　　司:'
     A            DPY01     R        O  5 14REFFLD(PY01)
     A            DPY01N        14O  O  5 23
     A                                  4 43'銀行代號:'
     A                                      DSPATR(HI)
     A            DPY04          7A  B  4 55
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            BN04      R        O  4 63
     A                                  6  2'員　　工:'
     A            DPY02     R        O  6 14REFFLD(PY02)
     A            EM03      R        O  6 23
     A                                  5 43'存入帳號:'
     A            DPY06     R        B  5 55REFFLD(PY06)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7  2'基本（底）薪:'
     A            DPY03     R        B  7 18REFFLD(PY03)
     A                                      EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'薪資總額:'
     A            DSUM           6Y 0O  8 14EDTCDE(2)
     A                                  9  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                 10  2'序'
     A                                 10 23'薪資'
     A                                 10 43'序'
     A                                 10 63'薪資'
     A                                 11  2'號'
     A                                 11  7'   薪資名稱 '
     A                                      DSPATR(HI)
     A                                 11 23'別'
     A                                 11 32'金額'
     A                                 11 43'號'
     A                                 11 48'   薪資名稱 '
     A                                      DSPATR(HI)
     A                                 11 64'別'
     A                                 10 72'金額'
     A                                 12  3'-- ----------------- ---- ---------
     A                                      -'
     A                                 12 44'-- ----------------- ---- ---------
     A                                      -'
     A*
     A          R DSPC1                     OVERLAY
     A                                 20  2'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =================='
     A                                 21  2'薪資別第一碼：0-系統設定+
     A                                       1-固定項目+
     A                                       2-變動項目'
     A                                 22  2'薪資別第二碼：A-固定金額+
     A                                       B-薪資比率+
     A                                       C-變動金額'
     A*                                22  2'存入日期=左邊輸入下期存入日期+
     A*                                     ，右邊為上期存入日期，輸入99表+
     A*                                     月底日'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24  2'PF03=回主畫面'
     A                                    +2'PF04=欄位查詢'
     A                                    +2'PF13=確認執行'
     A                                    +2'PF12=回前畫面'
     A*                                   +2'PF05=試算'
     A                                 24 67'【泛太資訊】'
