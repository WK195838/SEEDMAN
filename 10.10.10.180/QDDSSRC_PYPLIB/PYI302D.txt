     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI301D                                    *
     A*    PROGRAM NAME :員工調薪資料查詢                          *
     A*    AUTHOR       : A1232 ERIC                                 *
     A*    CREATE DATE  : 86/01/29                                   *
     A*    UPDATE DATE  : 89/08/25 A1274 EVA                         *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            SH02      R        O  8  8
     A            EM03      R        O  8 19
     A            SH03D     R   -1   O  8 37REFFLD(SH03)
     A                                      EDTWRD('   /  /  ')
     A            SH04      R        O  8 49
     A            SH05      R        O  8 51
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI302D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工調薪資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DSH01     R        O  4 13REFFLD(PY01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DSH01N        14   O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  6'員工編號'
     A                                      DSPATR(UL)
     A                                  6 19'  員工姓名  '
     A                                      DSPATR(UL)
     A                                  6 36'生效日期'
     A                                      DSPATR(UL)
     A                                  6 49'    調薪原因     '
     A                                      DSPATR(UL)
     A            DSH02     R        B  7  8DSPATR(CS HI)
     A                                      REFFLD(SH02)
     A            DSH03     R   -1   B  7 37DSPATR(CS HI)
     A                                      REFFLD(SH03)
     A                                      EDTWRD('   /  /  ')
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA1=下一頁'
     A                                 23 56'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
