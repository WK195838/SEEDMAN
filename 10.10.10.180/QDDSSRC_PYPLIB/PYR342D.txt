     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR342D                                    *
     A*    PROGRAM NAME :綜合福利金發放明細表　                  *
     A*    AUTHOR       : A1326 SANTOS                               *
     A*    CREATE DATE  : 90/03/21                                   *
     A*    UPDATE DATE  : 90/04/06 A1274 EVA                         *
     A*    SYSTEM       :                                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYGDJF01)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      CSRLOC(#LIN #COL)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #REC          10A  H
     A            #FLD          10A  H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYR342D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'綜合福利金發放明細表'
     A                                      DSPATR(RI)
     A                                  5 23'福委會　:'
     A            DEM01     R        B  5 35REFFLD(GD01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 23'部　　門:'
     A            DEM041         7A  B  7 35
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                    +1'-'
     A            DEM042         7A  B    +1
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  9 23'員工編號:'
     A                                      DSPATR(HI)
     A            DEM021    R        B  9 35REFFLD(GD02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 44'-'
     A            DEM022    R        B  9 46REFFLD(GD02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 11 23'申請項目:'
     A                                      DSPATR(HI)
     A            DGD071    R        B 11 35REFFLD(GD07)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 11 38'-'
     A            DGD072    R        B 11 40REFFLD(GD07)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 13 23'申請年度:'
     A            DYEAR          3Y 0B 13 35
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 15 23'計薪年月:'
     A            DGD091         5Y 0B 15 35EDTWRD('0   /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 15 42'-'
     A            DGD092         5Y 0B 15 44EDTWRD('0   /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 18  2'執行環境:'
     A            $PENV          1A  B 18 14VALUES('0' '1' '2')
     A                                 18 18'(0-查詢 1-即時 2-批次)'
     A                                 19  2'列印份數:'
     A            $CPY           3Y 0B 19 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 14
     A  27                             21 27' ***請準備８０格報表紙  *** '
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF03 =回主畫面'
     A                                 23 30'PF04 =欄位查詢'
     A                                 24 67'【泛太資訊】'
