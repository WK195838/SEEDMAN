     A*--------------------------------------------------------------*
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      OVERLAY
     A                                      CA03(03 'RETURN')
     A                                      CA12(12 'PREVIOUSE')
     A                                  1  2'<PYA508D>'
     A                                  1 28'＜＜假日資料設定＞＞    '
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A                                  2  2'<PYA508.2>'
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4 27'公　　司:'
     A            WT700     R        B  4 39REFFLD(CM01)
     A                                      DSPATR(PC)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 19'PF12 =回前畫面'
     A            DMARK         14A  O 24 66MSGID(UMI 0001 CEMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UMI 0030 CEMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 22  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A*
     A          R SFLSR                     SFL
     A*%%TS  SD  19960628  171659  CPPGMR      REL-V3R1M0  5763-PW1
     A            RRN            2S 0O  8  4
     A            DOP            1A  B  8 13VALUES(' ' '2' '4' '5' '3')
     A            T701D     R   -2   O  8 20REFFLD(T701)
     A                                      EDTWRD('0  /  /  ')
     A            T702      R        O  8 33
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A            DRRN           2S 0H      SFLRCDNBR
     A                                  1  2'<PYA508D>'
     A                                  1 28'＜＜假日資料設定＞＞    '
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A                                  2  2'<PYA508.1>'
     A                                  4  2'公　　司:'
     A            WT700     R        O  4 14REFFLD(CM01)
     A                                  5  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  6  2'序號'
     A                                  6 10'作業'
     A                                  6 19'休假日期'
     A                                  6 34'說　　明'
     A                                  7  3'----'
     A                                  7 11'----'
     A                                  7 20'--------'
     A                                  7 33'--------------'
     A          R DSPC11
     A                                 18  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                 19  2'作業='
     A                                 19 10'2-修改'
     A                                 19 20'3-複製'
     A                                 19 30'4-刪除'
     A                                 19 40'5-查詢'
     A                                 23  2'PF03 =回主畫面'
     A                                 23 19'PF06 =新增資料'
     A                                 23 36'PA1 =下一頁'
     A                                 23 50'PA2 =上一頁'
     A            DMARK         14A  O 24 66MSGID(UMI 0001 CEMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 22  2MSGID(UPT 2030 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UPT 2040 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A          R DSPC2
     A                                      OVERLAY
     A                                      CA03(03 'RETURN')
     A                                      CA12(12 'PREVIOUSE')
     A                                  1  2'<PYA508D>'
     A                                  1 28'＜＜假日資料設定＞＞    '
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A                                  2  2'<PYA508.2>'
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4 27'公　　司:'
     A            WT700     R        O  4 39REFFLD(CM01)
     A                                      DSPATR(PC)
     A                                  6 27'休假日期:'
     A            WT701     R   -1   B  6 39REFFLD(T701)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                      EDTWRD('0  /  /  ')
     A                                  8 27'說　　明:'
     A            DT702     R        B  8 39REFFLD(T702)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 19'PF12 =回前畫面'
     A            DMARK         14A  O 24 66MSGID(UMI 0001 CEMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UMI 0030 CEMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 22  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A*
