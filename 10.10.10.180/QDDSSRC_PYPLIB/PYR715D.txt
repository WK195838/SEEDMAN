     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR715D                                    *
     A*    PROGRAM NAME :年度應稅所得明細表－員工                  *
     A*    AUTHOR       : A1491 EASON                                *
     A*    CREATE DATE  : 97/01/03                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A*------------------------------------------------------------***
     A          R DSPD1                     RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYR715D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 28'年度應稅所得明細表－員工'
     A                                      DSPATR(RI)
     A*
     A                                  8 28'公司編號　　　:'
     A            DEM01     R        B  8 46REFFLD(ED01)
     A  61                                  DSPATR(PC RI)
     A                                 10 28'員工編號　　起:'
     A                                      DSPATR(HI)
     A            DEM021    R        B 10 46REFFLD(ED02)
     A  62                                  DSPATR(PC RI)
     A                                 11 28'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DEM022    R        B 11 46REFFLD(ED02)
     A  62                                  DSPATR(PC RI)
     A                                 13 28'扣繳年度　　　:'
     A            DYEAR          2  0B 13 46EDTWRD('0  ')
     A  63                                  DSPATR(PC RI)
     A                                    +1'(西元年)'
     A*
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 14
     A*
     A  27                             21 26'*** 請準備198格報表紙 ***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI BL)
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
