     A*%%TS  SD  20040420  173326  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PTA915                                     *
     A*    PROGRAM NAME :標準年齡維護                              *
     A*    AUTHOR       : A1397  BARRY                               *
     A*    CREATE DATE  : 93.04.20                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :基本資料系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PTRF)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  20040420  173326  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      OVERLAY
     A                                  1  2'<PTA915.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DDATE          6Y 0O  1 71EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'標準年齡資格維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司:'
     A            DSG01          2A  B  4 10
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A            CM02          14A  O  4 16
     A                                  5  2'性別:'
     A            DSG02          1A  B  5 10
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                  5 12'(M.男生 F.女生)'
     A                                  6  2'標準年齡:'
     A            DSG03          2S 0B  6 14
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                  7  2'作業選擇:'
     A            DOPT           1Y 0B  7 14VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                  7 16'( 1.新增'
     A                                  7 27'2.修改'
     A                                  7 36'4.刪除'
     A                                  7 45'5.查詢)'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  20040420  173326  PPSPGMRYK   REL-V4R4M0  5769-PW1
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A  32                              9  2'資格:'
     A  32        DSG04          2A  B  9 10
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R DSBTTM
     A                                      OVERLAY
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 28'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
