     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA205D                                    *
     A*    PROGRAM NAME :臨時卡資料維護                            *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 86/02/13                                   *
     A*    UPDATE DATE  : 89/08/25 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19970213  112652  ESPGMR      REL-V3R1M0  5763-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA205.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 63'日期:'
     A            DDATE          6Y 0O  1 71EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 33'臨時卡資料建立'
     A                                      DSPATR(RI)
     A                                  4  1'臨時卡號:'
     A            DT401     R        B  4 13REFFLD(T401)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  1'生效日期:'
     A            DT402     R   -1   B  5 13REFFLD(T402)
     A                                      EDTWRD('   /  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 16'( 1.新增2.修改4.刪除5.查詢-
     A                                      )'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*
     A          R DSPD2
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA205.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 63'日期:'
     A            DDATE          6Y 0O  1 71EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 33'臨時卡資料建立'
     A                                      DSPATR(RI)
     A                                  4  1'臨時卡號:'
     A            DT401     R        O  4 13REFFLD(T401)
     A                                  5  1'生效日期:'
     A            DT402     R   -1   O  5 13REFFLD(T402)
     A                                      EDTWRD('   /  /  ')
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0O  6 13EDTCDE(4)
     A                                  6 16'( 1.新增2.修改4.刪除5.查詢-
     A                                      )'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  1'公司編號:'
     A            DT403     R        B  8 13REFFLD(T403)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DT403N        14O  O  8 16
     A                                  9  1'員工代號:'
     A                                      DSPATR(HI)
     A            DT404     R        B  9 13REFFLD(T404)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DT404N        14   O  9 19
     A*                                10  1'原　　因:'
     A*                                     DSPATR(HI)
     A*           DT405     R        B 10 13REFFLD(T405)
     A* 62                                  DSPATR(PC)
     A* 62                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A*           QJ02      R        O 10 17
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF04=欄位查詢'
     A                                 23 38'PF05=參考資料'
     A                                 23 56'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
