     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI532D                                    *
     A*    PROGRAM NAME :員工考績等級設定全頁查詢                  *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 2001/09/11                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            RV02      R        O  8  5
     A            EM03      R        O  8 14
     A            DRV03     R   -1   O  8 31REFFLD(RV03)
     A                                      EDTCDE(4)
     A            RV04      R        O  8 37REFFLD(RV04)
     A            RV04N          8   O  8 39
     A            RV05      R        O  8 52REFFLD(RV05)
     A            RV06      R        O  8 64REFFLD(RV06)
     A            RV07      R        O  8 73REFFLD(RV07)
     A                                      EDTCDE(3)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0300)
     A                                      SFLPAG(0013)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI532D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 28'員工考績等級設定全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DRV01     R        O  4 13REFFLD(RV01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DRV01N        14O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'員工編號　姓名　　　  '
     A                                      DSPATR(UL)
     A                                  6 29'年度'
     A                                      DSPATR(UL)
     A                                  6 37'  種類  '
     A                                      DSPATR(UL)
     A                                  6 48'主管考績'
     A                                      DSPATR(UL)
     A                                  6 59'人評會考績'
     A                                      DSPATR(UL)
     A                                  6 72'考績率'
     A                                      DSPATR(UL)
     A            KRV02     R        B  7  5REFFLD(RV02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            KRV03     R   -1   B  7 31REFFLD(RV03)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KRV04     R        B  7 37REFFLD(RV04)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA01=下一頁'
     A                                 23 56'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
