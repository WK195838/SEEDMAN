     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA603                                     *
     A*    PROGRAM NAME : 福委會所得扣繳申報作業　　　             *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 88/01/20                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R DSPC1
     A  28                                  FRCDTA
     A  28                                  LOCK
     A                                      CA03(03)
     A                                      CF13(13)
     A                                      CSRLOC(#LIN #COL)
     A*
     A            #LIN           3  0H
     A            #COL           3  0H
     A*
     A                                  1  2'<PYA603D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A*
     A            $USERN        10A  O  2  2
     A                                  2 29'福委會所得扣繳申報作業'
     A                                      DSPATR(RI)
     A*
     A                                  5 28'媒體單位:'
     A            DTD02     R        B  5 40REFFLD(TD02 PYTDPF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 28'年　　度:'
     A            DTD01     R   -1   B  7 40REFFLD(TD01 PYTDPF)
     A                                      EDTWRD('   ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A*HANK900104
     A*                                 9 28'重新處理:'
     A*           DOPT           1   B  9 40
     A*                                     VALUES('Y' 'N')
     A* 62                                  DSPATR(PC)
     A* 62                                  DSPATR(RI)
     A*HANK900104                       9 43'( Y / N )'
     A*
     A                                 12 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 13 11' '
     A                                      DSPATR(RI)
     A                                 13 66' '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A  27                             14 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  28                             14 28'作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A  29                             14 28'作業處理完成！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
