     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*                                                              *
     A*    PROGRAM-ID   : PYA1072D                                   *
     A*    PROGRAM NAME :休假基本多筆資料建立－員工編號            *
     A*    AUTHOR       : A1355 ERIC                                 *
     A*    CREATE DATE  : 91/11/05                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*------------------------------------------------------------------------*
     A          R DSPC1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA1072.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 29'休假基本資料建立－員工編號'
     A                                      DSPATR(RI)
     A*
     A                                  4  1'公司編號:'
     A            DLT01     R        B  4 13REFFLD(LT01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DLT02     R        B  5 13REFFLD(LT02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            EM03      R        O  5 23REFFLD(EM03)
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 16'( 1.新增 2.修改 4.刪除 5.+
     A                                      查詢)'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 19'PF04=欄位查詢'
     A*                                23 38'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            3Y 0H
     A            DNEW           1A  H
     A            WLT03          2Y 0H
     A            DLT03     R   -2   B 10  2REFFLD(LT03)
     A                                      EDTWRD('0  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A            DLT10     R        B 10 12REFFLD(LT10)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 18'天'
     A            DLT13     R        B 10 33REFFLD(LT13)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 38'天'
     A            DLT14     R        B 10 43REFFLD(LT14)
     A                                      EDTCDE(M)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 46'時'
     A            DLT15     R        B 10 52REFFLD(LT15)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 57'天'
     A            DLT16     R        B 10 62REFFLD(LT16)
     A                                      EDTCDE(M)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 66'時'
     A            DLT17     R        B 11 12REFFLD(LT17)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 18'天'
     A            DLT18     R        B 11 23REFFLD(LT18)
     A                                      EDTCDE(M)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 26'時'
     A            DLT11     R        B 11 33REFFLD(LT11)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 38'天'
     A            DLT12     R        B 11 43REFFLD(LT12)
     A                                      EDTCDE(M)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 46'時'
     A            LTT1      R        O 11 52REFFLD(LT13)
     A                                      EDTCDE(M)
     A                                 11 57'天'
     A            LTT2      R        O 11 62REFFLD(LT14)
     A                                      EDTCDE(M)
     A                                 11 66'時'
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0006)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      ROLLUP(25)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A*
     A                                  1  2'<PYA1072.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72
     A                                  2 29'休假基本資料建立－員工編號'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DLT01     R        O  4 13REFFLD(LT01)
     A                                  5  1'員工編號:'
     A            DLT02     R        O  5 13REFFLD(LT02)
     A            EM03      R        O  5 23REFFLD(EM03)
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  1'年度'
     A                                  7 10'　今年年假'
     A                                  7 35'　　去年延遞'
     A                                  7 55'　明年可預借'
     A                                  8 10'去年已預借'
     A                                  8 35'本年已修年假'
     A                                  8 55'本年可修年假'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*         R DSPC2                     OVERLAY
     A          R DSPC2
     A*                                21  2'====================+
     A*                                      ====================+
     A*                                      ====================+
     A*                                      =================='
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PF13=確認'
     A                                 24 67'【泛太資訊】'
