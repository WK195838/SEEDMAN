     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYH122D                                    *
     A*    PROGRAM NAME :退休金提撥異動維護                        *
     A*    AUTHOR       : A1386 SHARON                               *
     A*    CREATE DATE  : 94/04/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :勞退新制作業                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYH5PF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYH122.1>'
     A            CONAME        32A  O  1 24MSGID(UPT 9900 PTMF)
     A                                  1 62'日期:'
     A            DATE           7Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'退休金提撥異動維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DH501     R        B    +1REFFLD(H501)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DH502     R        B    +1REFFLD(H502)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'異動日期:'
     A            DH503     R        B    +1REFFLD(H503)
     A                                      EDTWRD('    /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'  異動別:'
     A            DH504     R        B    +1REFFLD(H504)
     A                                      VALUES('1' '2' '3')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                    +1'( 1.開始'
     A                                    +1'2.調整'
     A                                    +1'3.停止)'
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  8 15'( 1.新增'
     A                                  8 26'2.修改'
     A                                  8 35'4.刪除'
     A                                  8 44'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                 23  1' PF03 =回主畫面'
     A                                 23 20' PF04 =欄位查詢'
     A                                 23 40' PF18 =全頁查詢'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYH122.2>'
     A            CONAME        32A  O  1 24MSGID(UPT 9900 PTMF)
     A                                  1 62'日期:'
     A            DATE           7Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'退休金提撥異動維護'
     A                                      DSPATR(RI)
     A                                  2 62'作業:'
     A            DFUN           6O  O  2 70DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DH501     R        O    +1REFFLD(H501)
     A            CM02      R        O    +1REFFLD(CM02 SYCMPF)
     A                                  5  1'員工編號:'
     A            DH502     R        O    +1REFFLD(H502)
     A            EM03      R        O    +1REFFLD(EM03 PYEMPF)
     A                                  6  1'異動日期:'
     A            DH503     R        O    +1REFFLD(H503)
     A                                      EDTWRD('    /  /  ')
     A                                  7  1'  異動別:'
     A            DH504     R        O    +1REFFLD(H504)
     A                                    +1'( 1.開始'
     A                                    +1'2.調整'
     A                                    +1'3.停止)'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  9  1'異動項目:'
     A                                      DSPATR(HI)
     A            DH514     R        B    +1REFFLD(H514)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            H514N         10A  O    +1
     A                                 11  1'異動前資料'
     A                                      COLOR(PNK)
     A                                 11 40'異動後資料'
     A                                      COLOR(PNK)
     A                                 12  1'本次月提繳工資:'
     A            DH506     R        B    +1REFFLD(H506)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  1'本次企業提撥率:'
     A            DH507     R        B    +1REFFLD(H507)
     A                                      EDTCDE(1)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'%'
     A                                 16  1'本次員工自提率:'
     A            DH505     R        B    +1REFFLD(H505)
     A                                      EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'%'
     A                                 12 40'上次月提繳工資:'
     A            WH506     R        O    +1REFFLD(H506)
     A                                      EDTCDE(1)
     A                                 14 40'上次企業提撥率:'
     A            WH507     R        O    +1REFFLD(H507)
     A                                      EDTCDE(1)
     A                                    +1'%'
     A                                 16 40'上次員工自提率:'
     A            WH505     R        O    +1REFFLD(H505)
     A                                      EDTCDE(1)
     A                                    +1'%'
     A*
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  1' PF03 =回主畫面'
     A                                 23 20' PF04 =欄位查詢'
     A                                 23 40' PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
