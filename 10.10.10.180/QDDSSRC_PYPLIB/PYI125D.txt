     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYI125D                                    *
     A*    PROGRAM NAME :人事異動資料查詢                          *
     A*    AUTHOR       : TOM T.                                     *
     A*    CREATE DATE  : 89/08/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            TX02      R        O  8  6
     A            EM03      R        O  8 16
     A            DTX04     R   -2   O  8 33REFFLD(TX04)
     A                                      EDTWRD('0  /  /  ')
     A            TX03           2A  O  8 44
     A            TX03N         12   O  8 47
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0300)
     A                                      SFLPAG(0013)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI125D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'人事異動資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DTX01     R        O  4 13REFFLD(TX01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DTX01N        14O  O  4 16
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5'員工編號'
     A                                      DSPATR(UL)
     A                                  6 16'  員工姓名  '
     A                                      DSPATR(UL)
     A                                  6 32'生效日期'
     A                                      DSPATR(UL)
     A                                  6 44'   異動類別  '
     A                                      DSPATR(UL)
     A            KTX02     R        B  7  6REFFLD(TR02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            KTX04     R   -2   B  7 33REFFLD(TX04)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      EDTWRD('0  /  /  ')
     A            KTX03          2A  B  7 44DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA01=下一頁'
     A                                 23 56'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
