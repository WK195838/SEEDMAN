     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI215D                                    *
     A*    PROGRAM NAME :員工出勤異常資料查詢                      *
     A*    AUTHOR       : A1184                                      *
     A*    CREATE DATE  : 85/03/16                                   *
     A*    UPDATE DATE  : 89/08/25 A1274 EVA                         *
     A*    SYSTEM       : PY                                         *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  3DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            T603      R        O  8  7
     A            EM03      R        O  8 16REFFLD(EM03 PYEMPF)
     A            T601D          7  0O  8 32EDTWRD('   /  /  ')
     A            T604      R        O  8 44
     A            T604N         16O  O  8 46
     A            T605      R        O  8 65EDTCDE(1)
     A                                  8 68'時'
     A            T606      R        O  8 73EDTCDE(1)
     A                                  8 76'分'
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI215.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 63'日期:'
     A            DDATE          6Y 0O  1 71EDTWRD('0  /  /  ')
     A                                  2 32'刷卡異常資料查詢'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  2'公司別:'
     A            DT602     R        O  4 12REFFLD(T602)
     A            CM02      R        O  4 15
     A                                  6  3'S 員工編號                刷卡+
     A                                      日期   異常別         -
     A                                            異常時間    '
     A                                      DSPATR(UL)
     A            DT603     R        B  7  7REFFLD(T603)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DT601          7  0B  7 32
     A                                      EDTWRD('   /  /  ')
     A                                      DSPATR(CS)
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            DT604     R        B  7 44REFFLD(T604)
     A                                      DSPATR(CS)
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF03=回主畫面'
     A                                 23 28'PF12=回前畫面'
     A                                 23 46'PA01=下一頁'
     A                                 23 64'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
