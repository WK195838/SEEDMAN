     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYR903D                                    *
     A*    PROGRAM NAME :計程車費月報表
     A*    AUTHOR       : B1038 SHARON                               *
     A*    CREATE DATE  : 1998/10/26                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A                                      RTNCSRLOC(&#REC &#FLD)
     A            #REC          10   H
     A            #FLD          10   H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYR903D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 33'計程車費月報表'
     A                                      DSPATR(RI)
     A                                  6 25'公司編號  :'
     A            DXK01     R        B  6 39REFFLD(XK01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DXK01N        22A  O  6 42
     A                                  8 25'工作區起:'
     A                                      DSPATR(HI)
     A            DXK15F    R        B  8 39REFFLD(XK15)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  9 25'　    迄:'
     A                                      DSPATR(HI)
     A            DXK15T    R        B  9 39REFFLD(XK15)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 11 25'列印月份  :'
     A            DXK04          4Y 0B 11 39EDTWRD('  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 18  2'執行環境:'
     A            $PENV          1A  B 18 14VALUES('0' '1' '2')
     A                                 18 18'(0-查詢 1-即時 2-批次)'
     A                                 19  2'列印份數:'
     A            $CPY           3Y 0B 19 14EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20  2'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 14
     A  27                             21 27'***請準備１３２格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 27'*** 報表處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
