     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI205D                                    *
     A*    PROGRAM NAME :臨時卡資料查詢　                          *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 86/02/14                                   *
     A*    UPDATE DATE  : 89/08/25 A1274 EVA                         *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  7  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            T401      R        O  7  7
     A            T402D          7  0O  7 16EDTWRD('   /  /  ')
     A            T403      R        O  7 27
     A            T403N         12A  O  7 30
     A            T404      R        O  7 44
     A            T404N         12A  O  7 50
     A            T405      R        O  7 64
     A            T405N         12A  O  7 67
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0150)
     A                                      SFLPAG(0015)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* 96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYI205D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 33'臨時卡資料查詢'
     A                                      DSPATR(RI)
     A                                  5  2'S'
     A                                      DSPATR(UL)
     A                                  5  5'臨時卡號　生效日期'
     A                                      DSPATR(UL)
     A                                  5 27'   公司編號  '
     A                                      DSPATR(UL)
     A                                  5 44'    員工編號    '
     A                                      DSPATR(UL)
     A                                  5 64'  原　　因   '
     A                                      DSPATR(UL)
     A            DT401     R        B  6  7REFFLD(T401)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DT402     R   -1   B  6 16REFFLD(T402)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      EDTWRD('   /  /  ')
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PA01=下一頁'
     A                                 23 56'PA02=上一頁'
     A                                 24 67'【泛太資訊】'
