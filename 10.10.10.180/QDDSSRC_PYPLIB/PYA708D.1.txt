     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYA708D                                    *
     A*    AUTHOR       : A1138  DAVID. CHEN                         *
     A*    CREATE DATE  : 81/03/20                                   *
     A*    UPDATE DATE  : 89/02/24 A1274 EVA                         *
     A*    PROGRAM NAME : PYA708                                     *
     A*    SYSTEM       : MIRS                                       *
     A*    SUBSYSTEM    : PY                                         *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A                                      CA03(03)
     A*--------------------------------------------------------------**
     A          R DSPC1
     A                                  1  2'<PYA708D>'
     A                                  1 26'＜＜所得稅課稅資料維護＞＞'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2  2'<PYA708.1>'
     A                                  3 29'公司編碼:'
     A            DTD11     R        B  3 41REFFLD(TD11)
     A  60                                  DSPATR(RI PC)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5 29'年　　度:'
     A            DTD01     R   -1   B  5 41REFFLD(TD01)
     A  61                                  DSPATR(RI PC)
     A                                      EDTCDE(4)
     A                                  7 29'格式代號:'
     A            DTD04     R        B  7 41REFFLD(TD04)
     A  62                                  DSPATR(RI PC)
     A                                  9 29'員工代號:'
     A            DTD12     R        B  9 41REFFLD(TD12)
     A  63                                  DSPATR(RI PC)
     A                                 11 29'課稅次數:為今年度第'
     A            DTD18     R        B 11 53REFFLD(TD18)
     A  65                                  DSPATR(RI PC)
     A                                      EDTCDE(4)
     A                                 11 56'次課稅'
     A*
     A                                 13 29'作業選擇:'
     A            DOPT           1A  B 13 41
     A  64                                  DSPATR(RI PC)
     A                                 13 46'1.新增'
     A                                 14 46'2.修改'
     A                                 15 46'4.刪除'
     A                                 16 46'5.查詢'
     A                                 18  3'◎格式代號:'
     A                                 18 18'50-薪資　'
     A                                 18 30'51-租賃　　　　　'
     A                                 18 50'5A,5B,5C,5D,5E-其他利息'
     A                                 19 18'53-權利金'
     A                                 19 30'54-盈餘　　　　　'
     A                                 19 50'9A-執行業務報酬　'
     A                                 20 18'9B-稿費　'
     A                                 20 30'91-競技及機會中獎'
     A                                 20 50'92-其他所得　　　'
     A  96        ERR1          70A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          70A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          70A  O 22  2MSGID(UPT 0012 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          70A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          70A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          70A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A            DMARK         14A  O 24 66MSGID(UMI 0001 CEMF)
     A                                      DSPATR(HI)
     A*-----------------------------------------------------------*
     A*  92/03/20  11:31:36    FOPGMR      REL-R03M00  5728-PW1
     A          R DSPC2
     A*%%TS  SD  19980525  232347  PPSPGMR40   REL-V3R1M0  5763-PW1
     A                                      CA12(12)
     A                                  1  2'<PYA708D>'
     A                                  1 26'＜＜所得稅課稅資料維護＞＞'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A                                  2  2'<PYA708.2>'
     A                                  4  2'公司編碼:'
     A            DTD11     R        O  4 14REFFLD(TD11)
     A            CM03      R        O  4 18
     A                                  5  2'年　　度:'
     A            DTD01     R   -1   O  5 14REFFLD(TD01)
     A                                  6  2'格式代號:'
     A            DTD04     R        O  6 14REFFLD(TD04)
     A            A2TD04        16A  O  6 18
     A                                  7  2'員工編號:'
     A            DTD12     R        O  7 14REFFLD(TD12)
     A            EM033         12   O  7 25
     A            EM032         19   O  7 38
     A                                  9  2'給付總額:'
     A            TD07      R        B  9 14EDTCDE(1)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'扣繳稅額:'
     A            TD08      R        B 10 14EDTCDE(1)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  2'證號別　:'
     A            TD06      R        B 12 14
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 17'( " "-身份證號 1-統一編號 3-+
     A                                      外籍無地址 4-外籍有地址)'
     A                                 13  2'證號錯誤:'
     A            TD10      R        B 13 14
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 17'( " "-無誤　　 A-錯誤　　 B-+
     A                                      無從查對)'
     A                                 14  2'身份証號:'
     A            TD05      R        B 14 14REFFLD(TD05)
     A                                 15  2'特定編號:'
     A            TD09      R        B 15 14
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 27'(註)'
     A                                 16  2'所得姓名:'
     A            TD13      R        B 16 14
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17  2'所得地址:'
     A            TD14      R        B 17 14
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18  2'所得月份:'
     A            TD15D     R   -1   B 18 14EDTWRD('   /  ')
     A                                      REFFLD(TD15)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 21'-'
     A            TD16D     R   -1   B 18 23EDTWRD('   /  ')
     A                                      REFFLD(TD16)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  4 54'媒體申報:'
     A            TD02      R        O  4 66
     A                                  4 71'-'
     A            TD03      R        O  4 73
     A                                 20  4'註：租賃所得為房屋稅籍編號，執行+
     A                                      業務所得為業別，其它所得為所得者+
     A                                      代號'
     A  90        ERR1          70A  O 22  2MSGID(UPT 0012 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR2          70A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR3          70A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR4          70A  O 22  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  86        ERR5          70A  O 22  2MSGID(UPT 0045 PTMF)
     A                                      DSPATR(HI)
     A  85        ERR6          70A  O 22  2MSGID(UPT 0044 PTMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A            DMARK         14A  O 24 66MSGID(UMI 0001 CEMF)
     A                                      DSPATR(HI)
