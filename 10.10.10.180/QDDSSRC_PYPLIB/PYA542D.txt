     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PYA542D                                    *
     A*    PROGRAM NAME :員工理賠資料記錄維護　                    *
     A*    AUTHOR       : A1336 KLAUS                                *
     A*    CREATE DATE  : 91/09/23                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      CA03(03 'RETURN')
     A                                      CF04(04)
     A                                      CF18(18 'FULL PAGE INQ.')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            #REC          10A  H
     A            #FLD          10A  H
     A                                  1  2'<PYA542.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工理賠資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DR201     R        B  4 13REFFLD(R201)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  5  1'員工編號:'
     A                                      DSPATR(HI)
     A            DR202     R        B  5 13REFFLD(R202)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'理賠險種:'
     A                                      DSPATR(HI)
     A            DR203     R        B  6 13REFFLD(R203)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            R203N         10O  O  6 16
     A                                  7  1'申請日期:'
     A            DR204     R   -2   B  7 13REFFLD(R204)
     A                                      EDTWRD('0  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                  8 16'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF04 =欄位查詢'
     A                                    +2'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03 'RETURN')
     A                                      CA12(12 'PREVIOUSE')
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA542.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工理賠資料維護'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DR201     R        O  4 13REFFLD(R201)
     A            R201N     R        O  4 22REFFLD(CM02)
     A                                  5  1'員工編號:'
     A            DR202     R        O  5 13REFFLD(R202)
     A            R202N     R        O  5 22REFFLD(EM03)
     A                                  6  1'理賠險種:'
     A            DR203     R        O  6 13REFFLD(R203)
     A            R203N         10O  O  6 22
     A                                  7  1'申請日期:'
     A            DR204     R   -2   O  7 13REFFLD(R204)
     A                                      EDTWRD('0  /  /  ')
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  1'申請金額:'
     A            DR205     R        B  9 13REFFLD(R205)
     A                                      EDTCDE(J)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  1'理賠事由:'
     A            DR206     R        B 11 13REFFLD(R206)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
