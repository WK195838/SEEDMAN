     A****************************************************************
     A*            泛太資訊科技開發股份有限公司－版權所有          *
     A*                                                              *
     A*    PROGRAM-ID   : PYA532                                     *
     A*    PROGRAM NAME :員工考績等級設定                        *
     A*    AUTHOR       : TOM T.                                     *
     A*    CREATE DATE  : 89/09/15                                   *
     A*    UPDATE DATE  : 91/07/04 A1336 KLAUS                       *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPD1
     A                                      CA03(03 'RETURN')
     A**                                    CF18(18 'FULL PAGE INQ.')
     A                                      CF04(04 'PROMPT')
     A                                      CSRLOC(#LIN       #COL)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PYA532.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工考績等級設定'
     A                                      DSPATR(RI)
     A                                  4  1'公司編號:'
     A                                      DSPATR(HI)
     A            DRV01     R        B  4 13REFFLD(RV01 PYRVPF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            CM02      R        O  4 16REFFLD(CM02)
     A                                  5  1'部門編號:'
     A                                      DSPATR(HI)
     A            DRV08     R        B  5 13REFFLD(RV08 PYRVPF)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                  6  1'考績年度:'
     A            DRV03     R   -1   B  6 13REFFLD(RV03 PYRVPF)
     A                                      EDTCDE(1)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7  1'考績種類:'
     A            DRV04     R        B  7 13REFFLD(RV04 PYRVPF)
     A                                      VALUES('1' '2' '3')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 15'( 1-上半年 2-下半年 3-全年)'
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                  8 15'(1.新增2.修改4.刪除5.查詢)-
     A                                      '
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF04 =欄位查詢'
     A**                               23 38'PF18 =全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DRV02     R        B 12  3REFFLD(RV02 PYRVPF)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            EM03      R        O 12 14REFFLD(EM03)
     A            DRV05     R        B 12 34REFFLD(RV05 PYRVPF)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DRV06     R        B 12 46REFFLD(RV06 PYRVPF)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*           DRV07     R        B 12 55REFFLD(RV07)
     A*                                     EDTCDE(J)
     A* 62                                  DSPATR(PC)
     A* 62                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A            DRV07          3  0B 12 58
     A                                      EDTCDE(L)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 63'%'
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA532.2>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'員工考績等級設定'
     A                                      DSPATR(RI)
     A                                  2 64'作業:'
     A            DFUN           6A  O  2 72DSPATR(RI)
     A                                  4  1'公司編號:'
     A            DRV01     R        O  4 13REFFLD(RV01 PYRVPF)
     A            CM02      R        O  4 16REFFLD(CM02)
     A                                  5  1'部門編號:'
     A            DRV08     R        O  5 13REFFLD(RV08 PYRVPF)
     A            DRV09     R        O    +1REFFLD(RV09)
     A                                  6  1'考績年度:'
     A            DRV03     R   -1   O  6 13REFFLD(RV03 PYRVPF)
     A                                      EDTCDE(1)
     A                                  7  1'考績種類:'
     A            DRV04     R        O  7 13REFFLD(RV04 PYRVPF)
     A                                  7 15'( 1-上半年 2-下半來3-全年)'
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0O  8 13
     A                                  8 15'(1.新增2.修改4.刪除5.查詢)'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      -------'
     A                                 10  2'員工編號'
     A                                      DSPATR(HI)
     A                                 10 13'員工姓名'
     A                                 10 30'主管考績'
     A                                 10 41'人評會考績'
     A                                 10 54'考績率'
     A**                                9 54'金　　額'
     A**                                9 65'考績率'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      -------'
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 19'PF4 =欄位查詢'
     A                                 23 35'PF13=資料確定'
     A                                 23 53'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
