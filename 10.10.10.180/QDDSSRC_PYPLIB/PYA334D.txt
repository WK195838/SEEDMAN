     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA334D                                    *
     A*    PROGRAM NAME :薪資項目轉檔功能　　                      *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/08/14                                  *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF13(13)
     A                                  1  2'<PYA334D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('0  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'獎金項目轉檔'
     A                                      DSPATR(RI)
     A                                  5 28'公司編號:'
     A            DPB01     R        B  5 40REFFLD(PB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 28'獎金項目:'
     A            DPB04     R        B  7 40REFFLD(PB04)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                    +1'(01年終獎金02夏季獎金'
     A                                  8 43'(03紅利04其他)'
     A                                 10 28'發放日期:'
     A            DPB05          7  0B 10 40
     A                                      EDTWRD('0   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A*
     A                                 13 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A  97                             15 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  96                             15 28'批次作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11' '
     A                                      DSPATR(RI)
     A                                 16 66' '
     A                                      DSPATR(RI)
     A                                 17 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*
     A                                 19  2'注意：產生批次工作檔PYA334WF  '
     A                                      COLOR(PNK)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
