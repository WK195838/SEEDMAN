     A****************************************************************
     A*    PRTF NAME    : PYR531P                                    *
     A*    PROGRAM NAME :考績等級分數設定對照表                    *
     A*    AUTHOR       : TOM                                        *
     A*    CREATE DATE  : 20000914                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : MIRS                                       *
     A*    REMARK       :在職人員資料列印                          *
     A****************************************************************
     A                                      REF(PYRF)
     A          R PH1
     A                                      SKIPB(002)
     A            CM03      R              1
     A                                     1
     A                                      '考績等級分數設定對照表'
     A                                      SPACEB(001)
     A*                                    1
     A*                                     '公司:'
     A*           DRR01     R              9
     A*                                     REFFLD(RR01)
     A*           CM02      R             12
     A*                                     REFFLD(CM02)
     A                                    51
     A                                      '日期:'
     A                                      SPACEB(001)
     A            DDATE          6  0     59
     A                                      EDTWRD('0  /  /  ')
     A                                    68
     A                                      '頁次:'
     A                                    76
     A                                      PAGNBR
     A                                     1
     A                                      '考績項目:'
     A                                      SPACEB(001)
     A            DRR021    R             +1
     A                                      REFFLD(RR02)
     A                                    +1
     A                                      '-'
     A            DRR022    R             +1
     A                                      REFFLD(RR02)
     A                                    28
     A                                      '考績等級:'
     A            DRR031    R             40
     A                                      REFFLD(RR03)
     A                                    43
     A                                      '-'
     A            DRR032    R             45
     A                                      REFFLD(RR03)
     A                                    51
     A                                      '時間:'
     A                                    60
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    69
     A                                      '<PYR531P>'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     1
     A                                      '考績項目'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '考績等級'
     A                                    +1
     A                                      '分數上限'
     A                                    +1
     A                                      '分數下限'
     A                                    +1
     A                                      '考績比率'
     A                                    +5
     A                                      '金額'
     A                                    +3
     A                                      '發放月數'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A          R PD1
     A                                      SPACEB(001)
     A            RR02      R              2
     A            RR02N          6O        4
     A            RR03      R             16
     A            RR06      R             26
     A                                      EDTCDE(1)
     A            RR07      R             37
     A                                      EDTCDE(1)
     A            RR04      R             48
     A                                      EDTCDE(1)
     A                                    55
     A                                      '%'
     A            RR05      R             59
     A                                      EDTCDE(J)
     A            RR04N          3  2     +8
     A                                      EDTWRD('0 .  ')
     A          R PT5
     A                                      SPACEB(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                     1
     A                                      'PYR531P--->報表結束'
     A                                      SPACEB(001)
     A*
     A          R PE9
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
