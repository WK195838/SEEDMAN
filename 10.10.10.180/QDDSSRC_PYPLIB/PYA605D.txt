     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA605D                                    *
     A*    PROGRAM NAME :福委會收支轉檔作業　　　　                *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  : 79/06/29                                   *
     A*    UPDATE DATE  : 90/02/20 A1274 EVA                         *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*-------------------------------------------------------------***
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF13(13)
     A                                  1  2'<PYA605D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'福委會收支轉檔作業'
     A                                      DSPATR(RI)
     A                                  5 28'公司編號:'
     A            DEM01          2   B  5 40
     A  60                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DEM01N    R        O  5 46REFFLD(CM02 SYCMPF)
     A*
     A                                  7 28'年　　月:'
     A            DYM            5  0B  7 40
     A                                      EDTWRD('0   /  ')
     A  61                                  DSPATR(RI)
     A*
     A                                  9 26'福委會編號:'
     A            DER01     R        O  9 40REFFLD(ER01 PYERPF)
     A            DER01N    R        O  9 46REFFLD(CM02 SYCMPF)
     A*
     A                                 12 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A                                 13 11' '
     A                                      DSPATR(RI)
     A                                 13 66' '
     A                                      DSPATR(RI)
     A                                 14 11' '
     A                                      DSPATR(RI)
     A  97                             14 23'請按<Ｆ１３>鍵後開始處理'
     A                                      DSPATR(HI)
     A  96                             14 28'轉檔作業處理中！！'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 14 66' '
     A                                      DSPATR(RI)
     A                                 15 11' '
     A                                      DSPATR(RI)
     A                                 15 66' '
     A                                      DSPATR(RI)
     A                                 16 11'                                  -
     A                                                            '
     A                                      DSPATR(RI)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +2'PF13 =轉　　檔'
     A                                 24 67'【泛太資訊】'
