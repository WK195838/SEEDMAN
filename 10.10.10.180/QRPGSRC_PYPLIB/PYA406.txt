     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    PROGRAM NAME : PYA406                                     *
     H*    PROGRAM NAME :兼職人員薪水試算作業                      *
     H*    AUTHOR       : A1274 EVA                                  *
     H*    CREATE DATE  : 89/03/08                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :人事薪資                                  *
     H****************************************************************
     FSYCMPF  IF  E           K        DISK
     FPYPMPF  IF  E           K        DISK
     FPYDPPF  IF  E           K        DISK
     FPYPBLF02IF  E           K        DISK
     FPYSSLF03IF  E           K        DISK
     FPYA406D CF  E                    WORKSTN
     I*------------------------------------------------------------***
     I/COPY PYDDSSRC,PYPMDS01
     I*
     ILDA        UDS
     I                                      101 110 $USER
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I                                      210 2160DDATE
     I*
     I                                      501 502 DPB01
     I                                      507 508 DPB04
     I                                      509 5160WYMD
     I                                      509 5120WPB03
     I                                      509 5140WPB05
     I                                      515 5160WPB05D
     I                                      531 535 DEPT1
     I                                      536 540 DEPT2
     I                                      541 545 WDEPT1
     I                                      546 550 WDEPT2
     I*
     I            DS
     I                                        1   7 KDEPT
     I                                        1   1 KK1
     I                                        2   2 KK2
     I                                        3   3 KK3
     I                                        4   7 KK4
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C                     MOVEL'01'      KPM01   2
     C           KEYPM     KLIST
     C                     KFLD           KPM01            .類別
     C                     KFLD           DPB01            .公司編號
     C*
     C                     MOVEL'5'       KSS09   1
     C           KEYSS     KLIST
     C                     KFLD           KSS09            .項目屬性
     C                     KFLD           KCM13   2        .薪資組別
     C                     KFLD           DPB04            .薪資類別
     C*
     C           KEYPB     KLIST
     C                     KFLD           DPB01            .公司編號
     C                     KFLD           WPB03            .獎金年度
     C                     KFLD           DPB04            .獎金類別
     C                     KFLD           WPB05            .發放年月
     C                     KFLD           WPB05D           .發放日期
     C*
     C           KEYDP     KLIST
     C                     KFLD           KK1
     C                     KFLD           KK2
     C                     KFLD           KK3
     C                     KFLD           KK4
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR RTN010                     *INI. RTN.
     C*
  01-C           *IN03     DOWEQ'0'
    C                     EXSR RTN100                     *MAIN
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL ROUTINE
     C*==============================================================*
01===C           RTN010    BEGSR
|    C*
|    C           *NAMVAR   DEFN *LDA      LDA
|    C                     IN   *NAMVAR
|    C*-----------------------------------------------------------***
|    C                     MOVEL*BLANK    DPB01            .公司編號
|    C                     MOVEL*BLANK    CM03             .公司名稱
|    C                     MOVEL*BLANK    DEPT1            .部門－起
|    C                     MOVEL*BLANK    DEPT2            .部門－迄
|    C                     MOVEL*BLANK    DPB04            .薪資類別
|    C                     MOVEL*BLANK    SS02             .薪資名稱
|    C                     Z-ADDDDATE     DYMD             .發放日期
|    C*-----------------------------------------------------------***
|    C                     MOVEL$COR      DPB01
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          RTN100.... MAIN ROUTINE
     C*==============================================================*
02===C           RTN100    BEGSR
|    C*
|    C                     SETON                     97
|    C                     EXFMTDSPC1
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO END100
| 01-C                     END
|    C*
|    C                     EXSR RTN110                     *CHK SCREEN1
|    C*
| 02-C           *IN99     IFEQ '0'
|   C           *IN13     ANDEQ'1'
|   C                     EXSR RTN120                     *PROCESS
| 02-C                     END
|    C*
02===C           END100    ENDSR
     C*==============================================================*
     C*          RTN110....CHECK SCREEN1
     C*==============================================================*
03===C           RTN110    BEGSR
|    C*---------------------------------------------------------------
|    C* << CHACK 公司別NOT *BLANK >>
| 01-C           DPB01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO END110
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DPB01     CM001I  2         公司代號
|    C                     PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 02-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 02-C                     END
|    C   99                GOTO END110
|    C*
| 03-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 03-C                     END
|    C   99                GOTO END110
|    C*
|    C*+<<DATA CHECK>>+++++++++++++++++++++++++++++++++++++++++++++++++
|    C*公司編號
|    C           DPB01     CHAINCM0                  40
| 04-C           *IN40     IFEQ '0'
|   C                     MOVELCM13      KCM13            .薪資組別
|   C                     MOVELCM03      CM03             .公司名稱
| 04*C                     ELSE
|   C                     MOVEL*BLANK    KCM13
|   C                     MOVEL*BLANK    CM03
| 04-C                     END
|    C*　　　　
|    C           KEYPM     CHAINPM0                  41
| 05-C           *IN41     IFEQ '1'
|   C                     MOVEL'UMI0440' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6099
| 05-C                     END
|    C   99                GOTO END110
|    C*
|    C*部門別起迄值DEPT1 DEPT2
| 06-C           DEPT1     IFEQ *BLANK
|   C                     MOVEL*BLANK    WDEPT1
| 06*C                     ELSE
|   C                     MOVELDEPT1     KDEPT     P
|   C                     MOVELDEPT1     WDEPT1    P
|   C           KEYDP     CHAINDP0                  40
| 07-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6699
| 07-C                     END
|   C   99                GOTO END110
| 06-C                     END
|    C*
| 08-C           DEPT2     IFEQ *BLANK
|   C                     MOVEL*ALL'9'   DEPT2
|   C                     MOVEL*HIVAL    WDEPT2
| 08*C                     ELSE
| 09-C           DEPT2     IFNE *ALL'9'
|   C                     MOVELDEPT2     KDEPT     P
|   C                     MOVELDEPT2     WDEPT2    P
|   C           KEYDP     CHAINDP0                  40
| 10-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6799
| 10-C                     END
|   C   99                GOTO END110
| 09-C                     END
| 08-C                     END
|    C*
|    C*部門別區間DEPT1 DEPT2
| 11-C           DEPT1     IFGT DEPT2
|   C                     MOVEL'UPT1011' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     666799
| 11-C                     END
|    C   99                GOTO END110
|    C*
|    C*薪資類別
| 12-C           DPB04     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 12-C                     END
|    C   99                GOTO END110
|    C*
| 13-C           DPB04     IFNE *BLANK
|   C           KEYSS     CHAINSS0                  42
| 14-C           *IN42     IFEQ '1'
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 14-C                     END
|   C   99                GOTO END110
| 13-C                     END
|    C*
|    C*發放日期．薪資年度
| 15-C           DYMD      IFEQ *ZERO                      .YYMMDD
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 15*C                     ELSE
|   C*
|   C                     CALL 'P30'
|   C                     PARM DYMD      P3001I  80
|   C                     PARM DFMT      P3002I  1
|   C                     PARM DTYPE     P3003I  1
|   C                     PARM           P3011O  1
|   C*
| 16-C           P3011O    IFEQ 'N'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 16-C                     END
|   C*
|   C                     CALL 'P31'
|   C                     PARM DYMD      P3101I  80       .YYMMDD
|   C                     PARM DFMT      P3102I  1
|   C                     PARM DTYPE     P3103I  1
|   C                     PARM '2'       P3104I  1
|   C                     PARM '1'       P3105I  1
|   C                     PARM           P3101O  80
|   C                     Z-ADDP3101O    WYMD             .YYYYMMDD
|   C*
| 15-C                     END
|    C   99                GOTO END110
|    C*
|    C*
|    C*----------------------------------------------------------***
|    C           KEYPB     CHAINPB0                  40
| 17-C           *IN40     IFEQ '0'
|   C           PB96      ANDNE*ZERO
|   C                     MOVEL'UPT2120' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     639099
| 17-C                     END
|    C   99                GOTO END110
|    C*
03===C           END110    ENDSR
     C*==============================================================*
     C*          RTN120.... PROCESS
     C*==============================================================*
04===C           RTN120    BEGSR
|    C*
|    C                     SETON                     23
|    C                     WRITEDSPC1
|    C*
|    C                     OUT  *NAMVAR
|    C                     CALL 'PYA4061C'
|    C                     SETON                     97
|    C                     SETOF                     23
|    C*
04===C                     ENDSR
