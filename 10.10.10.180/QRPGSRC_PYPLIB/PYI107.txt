     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   :PYI107                                      *
     H*    PROGRAM NAME :休假基本資料查詢                          *
     H*    AUTHOR       :A1355 ERIC                                  *
     H*    CREATE DATE  :                                            *
     H*    UPDATE DATE  :
     H*    SYSTEM       :人事薪資                                  *
     H****************************************************************
     FPYLTPF  IF  E           K        DISK
     FPYLTLF02IF  E           K        DISK
     F            LT0                               KRENAMELTL1
     FPYEMPF  IF  E           K        DISK
     FPYI107D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    S#01       35  7               部門代號
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      152 161 $USERN
     I                                      211 2160DDATE
     I                                      201 2080$A8YMD
     I*DEFIND STRAT KEY
     I            DS
     I                                        1  11 @KLTO
     I            DS
     I                                        1  11 @KLTN
     I                                        1   8 DLT02
     I                                        9  110DLT03
     I            DS
     I                                        1   7 EM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     I            DS
     I                                        1 245 S#01
     I                                        1 245 CM202O
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C           *LIKE     DEFN LT03      WLT03
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DLT01     PARM           I107I1  2         公司編號
     C           DLT02     PARM           I107I2  8         員工編號
     C           DLT03     PARM           I107I3  30        年度　　
     C                     PARM           @RTNC   2          FUN KEY
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C*
     C           KEYLT     KLIST                           *PYLTPF
     C                     KFLD           DLT01
     C                     KFLD           DLT02
     C                     KFLD           WLT03
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           LT01
     C                     KFLD           LT02
     C           KEYLT1    KLIST                           *PYLTPF
     C                     KFLD           DLT01
     C                     KFLD           WLT03
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
  01-C           @RTNC     DOWEQ*BLANK
    C                     EXSR R1000                      *SCR PROCESS
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C* INIT VALUE SET *---------------------------------------------*
|    C                     MOVEL*BLANK    @RTNC
|    C                     Z-ADD14        @PRCD            .PAGE SIZE
|    C*--------------------------------------------------------------*
|    C*檢核是否對全部的部門有權利並帶出管制的部門
|    C                     CALL 'PYSCM2'
|    C                     PARM DLT01     CM201I  4        公司
|    C                     PARM           CM201O  1        權利
|    C                     PARM           CM202O           部門代號
|    C*　　　　　　　
|    C*--------------------------------------------------------------*
|    C                     EXSR R1A00                      .SFL INIT
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....SCREEN  PROCESS
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     MOVEL'03'      @RTNC
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C                     MOVEL'12'      @RTNC
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN25     IFEQ '1'                        *NEXT PAGE
|   C                     EXSR R1N00
|   C                     GOTO E1000
| 03-C                     END
|    C*
| 04-C           @KLTO     IFNE @KLTN                      *START KEY
|   C                     EXSR R1A00                      *SFL INIT
|   C                     GOTO E1000
| 04-C                     END
|    C*
|    C                     EXSR R1C00                      *OPTION
|    C*
02===C           E1000     ENDSR
     C*=============================================================*
     C*          R1A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
03===C           R1A00     BEGSR
|    C*
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C                     MOVEL'N'       @BOTOM  1        *BOTTOM
|    C*
|    C                     SETOF                     51    *SFLDSPCTL
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
|    C                     EXSR R1A20                      *SFL HEAD
|    C   99                GOTO E1A00
|    C* <<SFL INITIAITAL DATA ADD>>
|    C                     MOVEL@KLTN     @KLTO            *KEY SAVE
|    C*
|    C           DLT03     ADD  1911      WLT03
|    C*
| 01-C           DLT02     IFEQ *BLANK
| 02-C           DLT03     IFEQ *ZERO
|   C           *LOVAL    SETLLLT0
| 02*C                     ELSE
|   C           KEYLT1    SETLLLTL1
| 02-C                     END
|    C*
| 01*C                     ELSE
|   C           KEYLT     SETLLLT0
| 01-C                     END
|    C*
|    C                     EXSR R1A10                      *ADD SFL DTA
|    C*
| 03-C           RRN       IFGT 0
|   C                     SETON                     50    *SFLDSP
| 03*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 03-C                     END
|    C*
03===C           E1A00     ENDSR
     C*=============================================================*
     C*          R1A10 .... ADD SUBFILE DATA
     C*=============================================================*
04===C           R1A10     BEGSR
|    C*
|    C                     ADD  @PRCD     @DRCD
| 01-C           RRN       DOWLT@DRCD
|   C*
|    C           DLT03     ADD  1911      WLT03
|   C*
| 02-C           DLT02     IFEQ *BLANK
| 03-C           DLT03     IFEQ *ZERO
|   C           DLT01     READELT0                      46
| 03*C                     ELSE
|   C           DLT01     READELTL1                     46
| 03-C                     END
|   C*
| 02*C                     ELSE
|   C           DLT01     READELT0                      46
| 02-C                     END
|   C*
| 04-C           *IN46     IFEQ '0'
|   C*
|   C                     MOVEL*BLANK    EM03
|   C           KEYEM     CHAINEM0                  40
|   C*檢查部門權利
| 05-C           CM201O    IFNE 'Y'
|   C                     Z-ADD1         I       20
|   C           EM04      LOKUPS#01,I                   40
| 06-C           S#01,I    IFEQ *BLANK
|   C                     SETOF                         40
| 06-C                     END
|   C  N40                GOTO TAG1
| 05-C                     END
|   C*
|   C                     EXSR R1A11                      *WRITE SFL
| 04*C                     ELSE                            *EOF
|   C                     Z-ADDRRN       @DRCD
|   C                     MOVE 'Y'       @BOTOM
|   C                     SETON                     9653  *SFLEND
| 04-C                     END
|   C*
|   C           TAG1      TAG
| 01-C                     END
|    C*
|    C                     Z-ADDRRN       DRRN
|    C*
04===C                     ENDSR
     C*=============================================================*
     C*          R1A11 .... WRITE SUBFILE DATA
     C*=============================================================*
05===C           R1A11     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << SUBFILE DATA VALUE SET >>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     ADD  1         RRN
|    C*
|    C*年度　
|    C           LT03      SUB  1911      LT03D
|    C                     Z-ADDLT10      DLT10
|    C                     WRITESFLSR
|    C*
05===C                     ENDSR
     C*=============================================================*
     C*          R1A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
06===C           R1A20     BEGSR
|    C*
|    C* << CHACK 公司別NOT *BLANK >>
|    C                     MOVEL*BLANK    DLT01N
| 01-C           DLT01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO E1A20
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DLT01     CM001I  2         公司代號
|    C           DLT01N    PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 02-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 02-C                     END
|    C   99                GOTO E1A20
|    C*
| 03-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 03-C                     END
|    C   99                GOTO E1A20
|    C*
|    C*年度　
|    C           DLT03     ADD  1911      WLT03
|    C*
06===C           E1A20     ENDSR
     C*=============================================================*
     C*          R1C00 ....挑選資料
     C*=============================================================*
07===C           R1C00     BEGSR
|    C*
|    C                     READCSFLSR                  4242
| 01-C           *IN42     DOWEQ'0'
|   C*
| 02-C           DOPT      IFNE *BLANK
|   C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
|   C                     MOVELLT01      I107I1
|   C                     MOVELLT02      I107I2
|   C                     MOVELLT03D     I107I3
|   C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|   C                     MOVEL'00'      @RTNC
|   C                     SETON                     42
| 02-C                     END
|   C*
|   C  N42                READCSFLSR                    42
| 01-C                     END
|    C*
07===C                     ENDSR
     C*=============================================================*
     C*          R1N00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
08===C           R1N00     BEGSR
|    C*
| 01-C           @BOTOM    IFEQ 'Y'                        *EOF
|   C*********************SETON                         96*LST PAG
|   C*********************SETON                         99*LST PAG
|   C                     Z-ADDRRN       DRRN
| 01*C                     ELSE
|   C                     EXSR R1A10                      *ADD SFL DTA
| 01-C                     END
|    C*
08===C                     ENDSR
     C*==============================================================*
