     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    PROGRAM-ID   : PYR2222                                    *
     H*    PROGRAM NAME :員工出勤簡表　　　　                      *
     H*    AUTHOR       : A1274 EVA                                  *
     H*    CREATE DATE  : 1999/08/02                                 *
     H*    UPDATE DATE  : 2000/01/06 A1274                           *
     H*    SYSTEM       :人事薪資                                  *
     H****************************************************************
     FPYEMPF  IP  E           K        DISK
     FPYDPPF  IF  E           K        DISK
     FPYLAPF  IF  E           K        DISK
     FPYLVPF  IF  E           K        DISK
     FPYLUPF  IF  E           K        DISK
     FPYOVPF  IF  E           K        DISK
     FPYR222WFO   E                    DISK
     E*---------------------------------------------------------------*
     I*PYEMPF
     IEM0
     I                                              EM02  L1
     I                                              EM01  L2
     I*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*LOCAL DATA AREA
     ILDA        UDS
     I                                      101 110 $USER
     I                                      125 1270$CPY
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I                                      211 2160$C8YMD
     I                                      217 218 $PRTCD
     I                                      219 219 $PENV
     I                                      500 500 $DATA
     I* PROGRAM USE DATA DEFINE ++++++++++++++++++++++++++++++++++++++
     I*公司
     I                                      501 502 DOV01
     I*部門
     I                                      609 615 DEM041
     I                                      616 622 DEM042
     I*員工編號
     I                                      513 520 DOV021
     I                                      521 528 DOV022
     I*出勤月份(YYMM) IN CHINESE
     I                                      529 5320DOV031
     I                                      533 5360DOV032
     I*月份(YYYYMM) IN ENGLISH
     I                                      537 5420WOV031
     I                                      543 5480WOV032
     I                                      547 5480WOV32M
     I                                      601 6080DYMD
     I            DS
     I                                        1   80WYMD
     I                                        1   60WYM
     I                                        1   40WYY
     I                                        5   60WMM
     I                                        7   80WDD
     I            DS
     I                                        1   80KYMD
     I                                        1   60KYM
     I                                        1   40KYY
     I                                        5   60KMM
     I                                        7   80KDD
     I            DS
     I                                        1   7 KEM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     I            DS
     I                                        1   80LA03
     I                                        1   60LA03YM
     I                                        7   80LA03D
     I            DS
     I                                        1   80LV03
     I                                        1   60LV03YM
     I                                        7   80LV03D
     I            DS
     I                                        1   80OV03
     I                                        1   60OV03YM
     I                                        7   80OV03D
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN *LDA      LDA
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C*PYDPPF 部門基本資料檔
     C           KEYDP     KLIST
     C                     KFLD           EM041
     C                     KFLD           EM042
     C                     KFLD           EM043
     C                     KFLD           EM044
     C*PYLAPF 考勤主檔
     C           KEYLA1    KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C           KEYLA2    KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           WYMD             *異常日期
     C*PYLVPF 請假主檔
     C           KEYLV1    KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C           KEYLV2    KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           WYMD             *日期
     C*PYLUPF 加班轉休歷史檔
     C           KEYLU     KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           WYY              *YEAR
     C                     KFLD           WMM              *MONTH
     C*PYOVPF 加班主檔
     C           KEYOV1    KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C           KEYOV2    KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           WYMD             *加班日期
     C*==============================================================*
     C*                    MAIN RUTINE
     C*==============================================================*
     C  N10                EXSR R1000
     C*
     C                     EXSR R2000                      .DETAIL
     CL1 10                EXSR R8100                      .L1
     CL2 10                EXSR R8200                      .L2
     C*==============================================================*
     C*          R0100 ... INITIAL WORK FILE FIELD
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     MOVEL*BLANK    W2201
|    C                     MOVEL*BLANK    W2202
|    C                     MOVEL*BLANK    W2203
|    C                     MOVEL*BLANK    W2204
|    C                     MOVEL*BLANK    W2205
|    C                     Z-ADD*ZERO     W2206
|    C                     Z-ADD*ZERO     W2208
|    C                     Z-ADD*ZERO     W2209
|    C                     Z-ADD*ZERO     W2210
|    C                     Z-ADD*ZERO     W2211
|    C                     Z-ADD*ZERO     W2212
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R0200 ... INITIAL WORK FILE FIELD
     C*==============================================================*
02===C           R0200     BEGSR
|    C*
|    C                     Z-ADD*ZERO     W2206
|    C                     Z-ADD*ZERO     W2208
|    C                     Z-ADD*ZERO     W2209
|    C                     Z-ADD*ZERO     W2210
|    C                     Z-ADD*ZERO     W2211
|    C                     Z-ADD*ZERO     W2212
|    C*
02===C                     ENDSR
     C*==============================================================*
     C*          R1000 ... INITIAL PROCESS
     C*==============================================================*
03===C           R1000     BEGSR
|    C*
|    C                     SETON                     10
|    C*INIT. PYR222WF FIELD
|    C                     EXSR R0100
|    C*YYYYMM00
|    C                     Z-ADDWOV031    WYM
|    C                     Z-ADD00        WDD
|    C                     Z-ADDDYMD      KYMD
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          R2000 ... DETAIL
     C*==============================================================*
04===C           R2000     BEGSR
|    C*
|    C*公司　部門　員工編號　員工姓名
|    C                     MOVELEM01      W2201
|    C                     MOVELKEM04     W2202
|    C                     MOVELEM02      W2204
|    C                     MOVELEM03      W2205
|    C*
|    C*部門名稱
|    C           KEYDP     CHAINDP0                  40
|    C  N40                MOVELDP02      W2203
|    C   40                MOVEL*BLANK    W2203
|    C*
| 01-C           WYM       DOWLEWOV032
|   C*出勤年份　出勤月份
|   C           WYM       SUB  191100    W2206
|   C*
|   C*員工篩選：除一般員工外．當月離職員工也包含在內
| 02-C           EM19      IFNE *ZERO
|   C           EM19      ANDLTKYMD
|   C                     GOTO E2000
| 02-C                     END
|   C*
|   C                     ADD  1         KMM
|   C*
| 03-C           KMM       IFGT 12
|   C                     Z-ADD1         KMM
|   C                     ADD  1         KYY
| 03-C                     END
|   C*
|   C*
|   C*遲到次數：含早退次數
|   C                     EXSR R2001                      .PYLAPF
|   C*
|   C*事假及病假時數：LV04='I' 'S'
|   C                     EXSR R2002                      .PYLVPF
|   C*
|   C*積假時數：補休時數－已休時數
|   C                     EXSR R2003                      .PYLUPF
|   C*
|   C*加班時數：計薪OV21,OV23,OV24,OV25
|   C                     EXSR R2004                      .PYOVPF
|   C*
|   C                     WRITEW222
|   C*
|   C                     EXSR R0200
|   C*
|   C*CHECK 是否大於出勤月份迄值
|   C                     ADD  1         WMM
|   C*
| 04-C           WMM       IFGT 12
|   C                     Z-ADD1         WMM
|   C                     ADD  1         WYY
| 04-C                     END
|   C*
| 01-C                     END
|    C*
|    C                     Z-ADDWOV031    WYM
|    C                     Z-ADDDYMD      KYMD
|    C*
04===C           E2000     ENDSR
     C*==============================================================*
     C*          R2001 ... DETAIL 1
     C*==============================================================*
05===C           R2001     BEGSR
|    C*遲到次數
|    C           KEYLA2    SETLLLA0
|    C           KEYLA1    READELA0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
| 02-C           LA03YM    IFNE WYM
|   C                     LEAVE
| 02-C                     END
|   C*
|   C                     Z-ADD0         K2208
|   C           LA04      ADD  LA05      K2208   50
|   C                     ADD  K2208     W2208
|   C           KEYLA1    READELA0                      44
| 01-C                     END
|    C*
05===C           E2001     ENDSR
     C*==============================================================*
     C*          R2002 ... DETAIL 2
     C*==============================================================*
06===C           R2002     BEGSR
|    C*
|    C*事假及病假時數
|    C           KEYLV2    SETLLLV0
|    C           KEYLV1    READELV0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
| 02-C           LV03YM    IFNE WYM                        *起始日期
|   C                     LEAVE
| 02-C                     END
|   C*
|   C                     Z-ADD*ZERO     K2209A  51
|   C                     Z-ADD*ZERO     K2209B  51
|   C                     Z-ADD*ZERO     K2210A  51
|   C                     Z-ADD*ZERO     K2210B  51
|   C*
| 03-C           LV04      IFEQ 'I'
|   C           LV09      MULT 8         K2209A           .事假
|   C           LV08      ADD  K2209A    K2209B
| 03-C                     END
|   C*
| 04-C           LV04      IFEQ 'S'                        .病假
|   C           LV09      MULT 8         K2210A
|   C           LV08      ADD  K2210A    K2210B
| 04-C                     END
|   C*
|   C                     ADD  K2209B    W2209
|   C                     ADD  K2210B    W2210
|   C*
|   C           KEYLV1    READELV0                      44
| 01-C                     END
|    C*
06===C           E2002     ENDSR
     C*==============================================================*
     C*          R2003 ... DETAIL 3
     C*==============================================================*
07===C           R2003     BEGSR
|    C*積假時數
|    C           KEYLU     CHAINLU0                  40
| 01-C           *IN40     IFEQ '0'
|   C           LU05      SUB  LU06      W2211
| 01-C                     END
|    C*
07===C           E2003     ENDSR
     C*==============================================================*
     C*          R2004 ... DETAIL 4
     C*==============================================================*
08===C           R2004     BEGSR
|    C*加班時數
|    C           KEYOV2    SETLLOV0
|    C           KEYOV1    READEOV0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
| 02-C           OV03YM    IFNE WYM
|   C                     LEAVE
| 02-C                     END
|   C*
|   C                     ADD  OV21      W2212
|   C                     ADD  OV23      W2212
|   C                     ADD  OV24      W2212
|   C                     ADD  OV25      W2212
|   C*
|   C           KEYOV1    READEOV0                      44
| 01-C                     END
|    C*
08===C           E2004     ENDSR
     C*==============================================================*
     C*          R8100 ... LEVEL BREAK L1
     C*==============================================================*
09===C           R8100     BEGSR
|    C*
|    C                     EXSR R0100                      .清空欄位
|    C*
09===C                     ENDSR
     C*==============================================================
     C*          R8200 ... LEVEL BREAK L2
     C*==============================================================
10===C           R8200     BEGSR
|    C*
|    C                     EXSR R0100                      .清空欄
|    C*
10===C                     ENDSR
