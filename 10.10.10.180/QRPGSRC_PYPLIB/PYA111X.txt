     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    PROGRAM-ID   : PYA111                                     *
     H*    PROGRAM NAME :全民健保資料建立                          *
     H*    AUTHOR       : A1448 JENNIFER                             *
     H*    CREATE DATE  : 95/05/15                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :人事薪資系統                              *
     H*--------------------------------------------------------------*
     H*00A需求單號: 1041046                                        *
     H*00A 2015/12/02 DEREK健保補助                                *
     H****************************************************************
     FPYE5PF  UF  E           K        DISK                      A
     FPYE2LF07IF  E           K        DISK
     F            E20                               KRENAMEE27
     FPYE2LF04UF  E           K        DISK                      A
     FPYE5LF01IF  E           K        DISK
     F            E50                               KRENAMEE5X
     FPYEMPF  IF  E           K        DISK
     FPYPMPF  IF  E           K        DISK
     FPT#YPF  IF  E           K        DISK
     FSYCMPF  IF  E           K        DISK
     FPYE1PF  IF  E           K        DISK
 00A+FPYE9PF  IF  E           K        DISK
     FPYA111D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    TFUN    5   5  6
     E*---------------------------------------------------------------*
     I*
     I/COPY QDDSSRC,PYPMDS01
     I*
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I           UDS
     I                                      101 110 $USER
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      203 2080DDATE
     I                                      201 2080$A8YMD
     I*生效日期(西元年)
     I            DS
     I                                        1   80TE512
     I                                        1   60DYM
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           DE501            公司別
     C                     KFLD           DE502            員工編號
     C           KEYE5     KLIST                           *PYE5PF
     C                     KFLD           DE507            眷屬證號
     C                     KFLD           E512             生效日期
     C           KEY#Y     KLIST                           *PYZYPF
     C                     KFLD           #Y01             代碼欄位
     C                     KFLD           #Y02             代碼編號
     C           KEYPM     KLIST                           *PYPMPF
     C                     KFLD           PM01             類別
     C                     KFLD           PM02             公司編號
     C           KEYE1     KLIST                           *PYE1PF
     C                     KFLD           DE501            公司別
     C                     KFLD           DE502            員工編號
     C           KEYE2     KLIST                           *PYE2PF
     C                     KFLD           DE501            公司別
     C                     KFLD           DE502            眷屬證號
     C                     KFLD           DE507            生效日期
     C           KEYE21    KLIST                           *PYE2PF
     C                     KFLD           E201             公司別
     C                     KFLD           E202             眷屬證號
     C                     KFLD           E207             生效日期
     C           KEYE22    KLIST                           *PYE2PF
     C                     KFLD           DE501            公司別
     C                     KFLD           TMPFD4           員工編號
     C                     KFLD           DE507            生效日期
     C           KEYE51    KLIST                           *PYEMPF
     C                     KFLD           DE501            公司別
     C                     KFLD           DE502            員工編號
     C                     KFLD           DE507            眷屬證號
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C                     MOVE 'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     MOVEL*BLANK    DE507            眷屬證號
|    C                     Z-ADD0         DE512            生效日期
|    C                     Z-ADD1         DOPT
|    C*公司
|    C                     MOVEL$COR      DE501
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETON                     32
| 01*C                     ELSE
|   C                     SETOF                     32
| 01-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 02-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 03-C           *IN99     IFEQ '0'
|   C                     EXSR R1C00
|   C                     MOVEL'SC02'    SCID             *SCR2
| 03-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1A00     BEGSR
|    C*
|    C* AUT CHECK
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM 'PYI111'  S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C   99                GOTO E1A00
|    C*
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* CALL SUB-PROGRAM
|    C                     CALL 'PYI111'
|    C           DE501     PARM DE501     I202I0  2         公司編號
|    C           DE502     PARM DE502     I202I1  8         員工編號
|    C           DE507     PARM DE507     I202I2 10         眷屬證號
|    C           DE512     PARM DE512     I202I3  60        生效日期
|    C                     PARM           @RTNC   2
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* RETURN CODE CHECK
| 02-C           @RTNC     IFEQ '03'                       *PF3
|   C                     SETON                     03
| 02-C                     END
|    C*
03===C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 ....SCREEN CHECK
     C*==============================================================*
04===C           R1B00     BEGSR
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*+<<DATA CHECK>>+++++++++++++++++++++++++++++++++++++++++++++++++
|    C*員工編號
| 02-C           DE502     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 02-C                     ENDIF
|    C*眷屬身份證字號
| 03-C           DE507     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 03*C                     ELSE
|   C*
|   C*CHECK 輸入的眷屬證號是否為有效的身份證ID
|   C*
|   C                     CALL 'P02'
|   C                     PARM DE507     P03I1  10
|   C                     PARM           P03O1   1
|   C                     PARM           P03O2   1
| 04-C           P03O1     IFEQ 'N'
|   C                     MOVEL'UPT0045' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 04-C                     END
| 03-C                     ENDIF
|    C   99                GOTO E1B00
|    C*
|    C*---------------------------------------------------------------
|    C*生效日期-不可空白, CHECK日期格式是否正確?
| 05-C           DE512     IFEQ 0
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 05-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'P30'
|    C                     PARM DE512     P3001I  80
|    C                     PARM '1'       P3002I  1
|    C                     PARM '1'       P3003I  1
|    C                     PARM           P3011O  1
|    C*
| 06-C           P3011O    IFEQ 'N'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 06-C                     END
|    C   99                GOTO E1B00
|    C*
|    C* CHECK 資料在新增時,是否存在?
|    C                     CALL 'P31'
|    C                     PARM DE512     P3101I  80
|    C                     PARM '1'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           E512      PARM           P3111O  80
|    C                     Z-ADDP3111O    TE512
|    C           KEYE5     CHAINE50                  40
| 07-C           DOPT      IFEQ 1                          *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     626399
| 07-C                     END
|    C   99                GOTO E1B00
|    C*
|    C* CHECK 資料在非新增時,是否不存在?
| 08-C           DOPT      IFNE 1                          *UPD,DEL,INQ
|   C           *IN40     ANDEQ'1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     626399
| 08-C                     END
|    C   99                GOTO E1B00
|    C*
|    C* TMPFD1--存放上一筆資料的異動類別
|    C* TMPFD4--存放上一筆資料的員工編號
|    C*
|    C                     Z-ADD0         COUNT   20
|    C                     MOVEL*BLANK    TMPFD1  1        異動類別
|    C                     MOVEL*BLANK    TMPFD4  8        員工編號
|    C*
|    C*抓輸入的眷屬證號在健保檔中有幾筆記錄
|    C*
|    C           KEYE51    SETLLE5X
|    C           KEYE51    READEE5X                      46
| 09-C           *IN46     DOWEQ'0'
|   C*
|   C* COUNT記錄有幾筆眷屬
|   C*
|   C                     ADD  1         COUNT
|   C*
|   C           KEYE51    READEE5X                      46
| 09-C                     ENDDO
|    C*
|    C*若該名眷屬在健保檔中有1筆以上的記錄
|    C*
| 10-C           COUNT     IFGT 0
|   C                     CALL 'P31'
|   C                     PARM DE512     P3101I  80
|   C                     PARM '1'       P3102I  1
|   C                     PARM '1'       P3103I  1
|   C                     PARM '2'       P3104I  1
|   C                     PARM '1'       P3105I  1
|   C                     PARM           P3111O  80
|   C*
| 11-C                     SELEC
|   C*
|   C*若健保眷屬檔有該名眷屬資料,且作業選擇為1(新增) ,
|   C*要判斷--生效日期必>最近一筆日期記錄
|   C*
|   C           DOPT      WHEQ 1
| 12-C           P3111O    IFLE E512
|   C                     MOVEL'UPT0043' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 12-C                     ENDIF
|   C*
|   C*若健保眷屬檔有該名眷屬資料,且作業選擇為2 OR 4 (修改刪除)
|   C*要判斷--生效日期必=最近一筆日期記錄
|   C*
|   C           DOPT      WHEQ 2
|   C           DOPT      OREQ 4
| 13-C           P3111O    IFNE E512
|   C                     MOVEL'UPT0043' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 13-C                     ENDIF
|   C*
| 11-C                     ENDSL
|   C*
|   C*抓最後一筆資料的員工編號
|   C                     MOVELE502      TMPFD4           員工編號
|   C*
|   C*抓最後一筆資料的異動類別--新增
| 14-C           DOPT      IFEQ 1
|   C                     MOVELE513      TMPFD1           異動類別
| 14-C                     ENDIF
|   C*
|   C*抓最後一筆資料的異動類別--修改
| 15-C           DOPT      IFEQ 2
|   C           KEYE5     SETLLE50
|   C           DE507     REDPEE50                      47
|   C  N47                MOVELE513      TMPFD1           異動類別
| 15-C                     ENDIF
|   C*
| 10-C                     ENDIF
|    C*
04===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....MOVE  SCREEN TO SCID
     C*==============================================================*
05===C           R1C00     BEGSR
|    C*
|    C                     MOVE TFUN,DOPT DFUN
|    C*
| 01-C           DOPT      IFEQ 1
|   C                     EXSR R1CA0
| 01*C                     ELSE
|   C                     EXSR R1CB0
| 01-C                     END
|    C*
| 02-C           DOPT      IFNE 1
|   C           DOPT      ANDNE2
|   C                     SETON                     313334
| 02-C                     END
|    C*
| 03-C           DOPT      IFEQ 5
|   C           *LOVAL    SETLLE50
| 03-C                     END
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1CA0 ....清除畫面變數
     C*==============================================================*
06===C           R1CA0     BEGSR
|    C*
|    C*判斷若該名眷屬若沒有記錄,則可新增全部欄位資料
|    C*  若該名眷屬有超過一筆以上記錄,只可新增部份欄位資料
|    C                     SETOF                     313334
|    C*
| 01-C                     SELEC
|   C*
|   C*無該名眷屬資料
|   C           COUNT     WHEQ 0
|   C                     MOVEL*BLANKS   DE506            眷屬姓名
|   C                     Z-ADD0         DE508            出生日期
|   C                     MOVEL*BLANKS   DE509            眷屬稱謂
|   C                     MOVEL*BLANKS   DE509N           稱謂名稱
|   C                     MOVEL*BLANKS   DE513            異動類別
|   C*                    MOVEL*BLANKS   DE501            公司別
|   C*                    MOVEL*BLANKS   DCM02            公司名稱
|   C*                    MOVEL*BLANKS   DE502            員工編號
|   C*                    MOVEL*BLANKS   DEM03            員工姓名
|   C                     MOVEL*BLANKS   DE511            補助碼
|   C                     MOVEL*BLANKS   DE511N           補助名稱
|   C                     MOVEL'Y'       DFLG             第一筆資料
|   C*
|   C*已有該眷屬資料
|   C           COUNT     WHGT 0
|   C*
|   C                     SETON                     3334
|   C           DE507     SETLLE50
|   C           DE507     READEE50                      46
|   C*
|   C                     MOVELE506      DE506            眷屬姓名
|   C*
| 02-C           E508      IFNE 0
|   C                     Z-ADDE508      DE508            出生日期
|   C                     Z-ADDE508      WYMD             出生日期
| 02*C                     ELSE
|   C                     Z-ADD0         DE508            出生日期
| 02-C                     ENDIF
|   C*
| 03-C           TMPFD1    IFEQ '3'
|   C                     SETOF                     34
| 03-C                     ENDIF
|   C*
|   C                     MOVELE509      DE509            稱謂
|   C*
|   C*帶出稱謂中文
|   C                     MOVEL*BLANK    #Y01
|   C                     MOVEL*BLANK    #Y02
|   C                     MOVEL*BLANK    DE509N           稱謂名稱
|   C                     MOVEL'RELATION'#Y01
|   C                     MOVELE509      #Y02
|   C           KEY#Y     CHAIN#Y0                  40
|   C  N40                MOVEL#Y03      DE509N           稱謂名稱
|   C*
|   C*                    MOVEL*BLANKS   DE513            異動類別
|   C*                    MOVEL*BLANK    DE501            公司別
|   C*                    MOVEL*BLANK    DCM02            公司名稱
|   C*
|   C*                    MOVEL*BLANK    DE502            員工編號
|   C*                    MOVEL*BLANK    DEM03            員工姓名
|   C*
|   C*                    MOVEL*BLANK    DE511            補助
|   C*                    MOVEL*BLANK    DE511N           補助名稱
|   C*
| 01-C                     ENDSL
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R1CB0 ....搬值到畫面
     C*==============================================================*
07===C           R1CB0     BEGSR
|    C*
|    C                     CALL 'P31'
|    C                     PARM DE512     P3101I  80
|    C                     PARM '1'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C                     PARM           P3111O  80
|    C*判斷若該名眷屬若沒有記錄或是修改第一筆記錄,則可修改全部欄位
|    C*  若該名眷屬有超過一筆以上記錄,只可修改部份欄位
|    C                     SETOF                     313334
|    C*
| 01-C           COUNT     IFGT 1
|   C                     SETON                     3334
| 01-C                     ENDIF
|    C*
|    C*抓該眷屬是否修改第一筆記錄,若為第一筆則可以修改全部欄位
|    C                     MOVEL*BLANK    DFLG    1
|    C           DE507     SETLLE50
|    C           DE507     READEE50                      47
| 02-C  N47      P3111O    IFEQ E512
|   C                     SETOF                     33
|   C                     MOVEL'Y'       DFLG
| 02-C                     ENDIF
|    C*
|    C*註:在R1B00時有CHAIN PYE5PF檔,但後來又去比對同一人
|    C*   前一筆資料的日期,所以在R1CB0帶出資料時,還要
|    C*   再重新CHAIN資料,以確保不會帶錯資料來修改,刪除,
|    C*   查詢
|    C*
|    C                     Z-ADDP3111O    E512
|    C           KEYE5     CHAINE50                  40
|    C*
|    C                     MOVELE506      DE506            眷屬姓名
|    C*
| 03-C           E508      IFNE 0
|   C                     Z-ADDE508      DE508            出生日期
| 03*C                     ELSE
|   C                     Z-ADD0         DE508            出生日期
| 03-C                     ENDIF
|    C*
| 04-C           TMPFD1    IFEQ '3'
|   C                     SETOF                     34
| 04-C                     ENDIF
|    C*
|    C                     MOVELE509      DE509            稱謂
|    C*
|    C*帶出稱謂中文
|    C                     MOVEL*BLANK    #Y01
|    C                     MOVEL*BLANK    #Y02
|    C                     MOVEL*BLANK    DE509N           稱謂名稱
|    C                     MOVEL'RELATION'#Y01
|    C                     MOVELE509      #Y02
|    C           KEY#Y     CHAIN#Y0                  40
|    C  N40                MOVEL#Y03      DE509N           稱謂名稱
|    C*
|    C                     MOVELE513      DE513            異動別
|    C*
|    C                     MOVELE501      DE501            公司別
|    C                     MOVEL*BLANK    DCM02            公司名稱
|    C           E501      CHAINCM0                  40
|    C  N40                MOVELCM02      DCM02            公司名稱
|    C*
|    C                     MOVELE502      DE502            員工編號
|    C                     MOVEL*BLANK    DEM03            員工姓名
|    C           KEYEM     CHAINEM0                  40
|    C  N40                MOVELEM03      DEM03            員工姓名
|    C*
|    C                     MOVELE511      DE511            補助
|    C*帶出補助中文
|00A+C           DE511     CHAINE90                  40
|00A+C           *IN40     IFEQ '0'
|00A+C                     CALL 'P65'
|00A+C                     PARM E904      P6501I 80        被分割字串
|00A+C                     PARM 32        P6502I  30       分割長度
|00A+C           DE511N    PARM           P6511O 80        分割後字串1
|00A+C                     PARM           P6512O 80        分割後字串2
|00A+C                     ELSE                                        2
|00A+C                     MOVEL*BLANK    DE511N            補助名稱
|00A+C                     END                                         2
|00A-C*                    MOVEL*BLANK    #Y01
|00A-C*                    MOVEL*BLANK    #Y02
|00A-C*                    MOVEL*BLANK    DE511N            補助名稱
|00A-C*                    MOVEL'INSR'    #Y01
|00A-C*                    MOVELE511      #Y02
|00A-C*          KEY#Y     CHAIN#Y0                  40
|00A-C* N40                MOVEL#Y03      DE511N            補助名稱
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R2000  ...'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
08===C           R2000     BEGSR
|    C*
|    C                     EXFMTDSPD2
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'                        *欄位查詢
| 03-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C                     EXSR R2E00
| 03-C                     END
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 04-C           *IN12     IFEQ '1'
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 04-C                     END
|    C*
|    C*要判斷生效日期不能<=薪資確認年月(R2B00有加入這段檢核)
|    C*
| 05-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C*
|   C*第一次健保記錄
|   C*
| 06-C           DFLG      IFEQ 'Y'
|   C                     EXSR R2B00                      *SCR CHK
| 06-C                     ENDIF
|   C*
|   C*非第一次健保記錄和檢核生效日期是否小於薪資確認日期
|   C*
|   C                     EXSR R2B10                      *SCR CHK
|   C                     EXSR R2B20                      *SCR CHK
| 05-C                     END
|    C*
|    C*檢核生效日期是否小於薪資確認日期
|    C*
| 07-C           DOPT      IFEQ 4
|   C                     EXSR R2B20                      *SCR CHK
| 07-C                     END
|    C*
| 08-C           *IN99     IFEQ '0'
|   C                     EXSR R2D00                      *FILE UPD
|   C                     MOVEL'SC01'    SCID             *SCR2
| 08-C                     END
|    C*
08===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....CHECK 'SC02' SCREEN--眷屬第一次健保記錄
     C* 註:必需檢核-- 眷屬姓名 , 出生日期 , 稱謂
     C*
     C*==============================================================*
09===C           R2B00     BEGSR
|    C*
|    C*眷屬姓名-不可空白
| 01-C           DE506     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     ENDIF
|    C   99                GOTO E2B00
|    C*
|    C*出生日期-不可為0,日期要判斷為日期格式
| 02-C           DE508     IFEQ 0
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 02*C                     ELSE
|   C*
|   C                     CALL 'P30'
|   C                     PARM DE508     P3001I  80
|   C                     PARM '1'       P3002I  1
|   C                     PARM '1'       P3003I  1
|   C                     PARM           P3011O  1
|   C*
| 03-C           P3011O    IFEQ 'N'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 03-C                     END
| 02-C                     ENDIF
|    C   99                GOTO E2B00
|    C*
|    C                     CALL 'P31'
|    C                     PARM DE508     P3101I  80
|    C                     PARM '1'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C                     PARM           P3111O  80
|    C                     Z-ADDP3111O    WYMD    80
|    C*
|    C*稱謂--不可空白,不可不存在檔案中
| 04-C           DE509     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 04*C                     ELSE
|   C                     MOVEL*BLANK    #Y01
|   C                     MOVEL*BLANK    #Y02
|   C                     MOVEL*BLANK    DE509N            稱謂名稱
|   C                     MOVEL'RELATION'#Y01
|   C                     MOVELDE509     #Y02
|   C           KEY#Y     CHAIN#Y0                  40
|   C  N40                MOVEL#Y03      DE509N            稱謂名稱
|   C   40                MOVEL'UPT0043' ERRID
|   C   40                MOVEL'PTMF'    ERRF
|   C   40                SETON                     6299
| 04-C                     ENDIF
|    C   99                GOTO E2B00
|    C*
09===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2B10 ....CHECK 'SC02' SCREEN--眷屬非第一次健保記錄
     C* 註:必需檢核-- 公司別,員工編號,異動別,補助碼
     C*
     C*==============================================================*
10===C           R2B10     BEGSR
|    C*
|    C*稱謂--若該名眷屬前一筆記錄為退保,目前該筆資料就可輸入稱謂
|    C*稱謂--不可空白,不可不存在檔案中
| 01-C           TMPFD1    IFEQ '3'
| 02-C           DE509     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 02*C                     ELSE
|   C                     MOVEL*BLANK    #Y01
|   C                     MOVEL*BLANK    #Y02
|   C                     MOVEL*BLANK    DE509N            稱謂名稱
|   C                     MOVEL'RELATION'#Y01
|   C                     MOVELDE509     #Y02
|   C           KEY#Y     CHAIN#Y0                  40
|   C  N40                MOVEL#Y03      DE509N            稱謂名稱
|   C   40                MOVEL'UPT0043' ERRID
|   C   40                MOVEL'PTMF'    ERRF
|   C   40                SETON                     6299
| 02-C                     ENDIF
| 01-C                     ENDIF
|    C   99                GOTO E2B10
|    C*
|    C*
|    C*異動類別-不可空白,不可為1,2,3以外的值
| 03-C           DE513     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 03*C                     ELSE
|   C*
| 04-C           DE513     IFNE '1'
|   C           DE513     ANDNE'2'
|   C           DE513     ANDNE'3'
|   C                     MOVEL'UPT0043' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 04-C                     ENDIF
| 03-C                     ENDIF
|    C   99                GOTO E2B10
|    C*
| 05-C                     SELEC
|   C*
|   C*此眷屬無健保記錄
|   C           COUNT     WHEQ 0
| 06-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C*
|   C*無健保記錄時,異動類別不為2或3
| 07-C           DE513     IFNE '1'
|   C                     MOVEL'UPT0043' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 07-C                     ENDIF
|   C*
| 06-C                     ENDIF
|   C*
|   C*此眷屬有健保記錄
|   C           COUNT     WHGT 0
| 08-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C*
| 09-C                     SELEC
|   C*
|   C*上一筆記錄的異動類別為1或2時,目前這筆記錄異動類別不為1
|   C           TMPFD1    WHEQ '1'
|   C           TMPFD1    OREQ '2'
|   C*
| 10-C           DE513     IFEQ '1'
|   C                     MOVEL'UPT0043' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 10-C                     ENDIF
|   C*
|   C*上一筆記錄的異動類別為1或2時,目前這筆記錄員工編號不可
|   C*變更資料
| 11-C           DE502     IFNE TMPFD4
|   C                     MOVEL'UPT0043' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     646699
| 11-C                     ENDIF
|   C*
|   C*上一筆記錄的異動類別為3時,目前這筆記錄異動類別不為2或3
|   C           TMPFD1    WHEQ '3'
|   C*
| 12-C           DE513     IFNE '1'
|   C                     MOVEL'UPT0043' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 12-C                     ENDIF
|   C*
| 09-C                     ENDSL
|   C*
| 08-C                     ENDIF
|   C*
| 05-C                     ENDSL
|    C   99                GOTO E2B10
|    C*---------------------------------------------------------------
|    C*公司別不可空白--且要存在
|    C                     MOVEL*BLANKS   DCM02
| 13-C           DE501     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6699
| 13-C                     END
|    C   99                GOTO E2B10
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DE501     CM001I  2         公司代號
|    C           DCM02     PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 14-C           CM099O    IFEQ 'N'
|   C                     SETON                     6699
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 14-C                     END
|    C   99                GOTO E2B10
|    C*
| 15-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6699
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 15-C                     END
|    C   99                GOTO E2B10
|    C*
|    C*員工編號不可空白,要存在檔案中
| 16-C           DE502     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6499
| 16*C                     ELSE
|   C                     MOVEL*BLANK    DEM03            員工姓名
|   C           KEYEM     CHAINEM0                  40
|   C  N40                MOVELEM03      DEM03            員工姓名
|   C   40                MOVEL'UPT0043' ERRID
|   C   40                MOVEL'PTMF'    ERRF
|   C   40                SETON                     6499
| 16-C                     ENDIF
|    C   99                GOTO E2B10
|    C*
|    C*員工最近一筆的健保狀態不可為退保
|    C*
|    C           KEYEM     SETGTE10
|    C           KEYEM     REDPEE10                      47
| 17-C           *IN47     IFEQ *OFF
|   C*
| 18-C           E104      IFEQ '3'
|   C*
|   C                     MOVEL'UPT0043' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6499
|   C*
| 18-C                     ENDIF
| 17-C                     ENDIF
|    C   99                GOTO E2B10
|    C*
|    C*補助不可為空白,要存在檔案中
|    C*          DE511     IFEQ *BLANK
|    C*                    MOVEL'UPT0010' ERRID
|    C*                    MOVEL'PTMF'    ERRF
|    C*                    SETON                     6599
|    C*                    ELSE
|    C*
|    C*補助要存在檔案中
| 19-C           DE511     IFNE *BLANK
|00A+C           DE511     CHAINE90                  40
|00A+C           *IN40     IFEQ '1'
|00A+C                     MOVEL'UPT0043' ERRID
|00A+C                     MOVEL'PTMF'    ERRF
|00A+C                     SETON                     6599
|00A+C                     MOVEL*BLANK    DE511N            補助名稱
|00A+C                     ELSE
|00A+C                     CALL 'P65'
|00A+C                     PARM E904      P6501I 80        被分割字串
|00A+C                     PARM 32        P6502I  30       分割長度
|00A+C           DE511N    PARM           P6511O 80        分割後字串1
|00A+C                     PARM           P6512O 80        分割後字串2
|00A+C                     END
|00A-C*                    MOVEL*BLANK    #Y01
|00A-C*                    MOVEL*BLANK    #Y02
|00A-C*                    MOVEL*BLANK    DE511N            補助名稱
|00A-C*                    MOVEL'INSR'    #Y01
|00A-C*                    MOVELDE511     #Y02
|00A-C*          KEY#Y     CHAIN#Y0                  40
|00A-C* N40                MOVEL#Y03      DE511N            補助名稱
|00A-C*  40                MOVEL'UPT0043' ERRID
|00A-C*  40                MOVEL'PTMF'    ERRF
|00A-C*  40                SETON                     6599
| 19-C                     ENDIF
|    C   99                GOTO E2B10
|    C*
10===C           E2B10     ENDSR
     C*==============================================================*
     C*          R2B20 ....CHECK 'SC02' SCREEN--檢核特殊欄位
     C* 註:檢核欄位-- 生效日期
     C*
     C*==============================================================*
11===C           R2B20     BEGSR
|    C*
|    C* CHECK作業選擇不為5(查詢)時,
|    C*生效日期不能小於等於下期薪資確認年月
|    C*
|    C                     MOVEL'01'      PM01
|    C                     MOVELDE501     PM02
|    C           KEYPM     CHAINPM0                  40
| 01-C           *IN40     IFEQ '1'
|   C                     Z-ADD*ZERO     PM201
|   C                     Z-ADD*ZERO     PM202
|   C                     Z-ADD*ZERO     PM205
|   C                     Z-ADD*ZERO     PM206
|   C*                    MOVEL*BLANK    PM207
| 01-C                     END
|    C*
|    C*SPECIAL AUTHORITY CAN UPDATE AFTER薪資試算
| 02-C           DOPT      IFNE 5
| 03-C           DYM       IFLE PM206
|   C                     MOVEL'UPT0039' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6799
| 03-C                     END
| 02-C                     END
|    C   99                GOTO E2B20
|    C*
|    C*
11===C           E2B20     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
12===C           R2D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D20
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*==============================================================*
13===C           R2D10     BEGSR
|    C* HEAD
|    C                     EXSR R2D11
|    C                     WRITEE50
|    C                     CLEARE50
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
14===C           R2D11     BEGSR
|    C*
|    C                     MOVELDE501     E501             公司別
|    C                     MOVELDE507     E507             眷屬證號
|    C                     CALL 'P31'
|    C                     PARM DE512     P3101I  80
|    C                     PARM '1'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           E512      PARM           P3111O  80
|    C                     Z-ADDWYMD      E508             出生日期
|    C                     MOVELDE506     E506             眷屬姓名
|    C                     MOVELDE509     E509             稱謂
|    C                     MOVELDE513     E513             異動別
| 01-C           E513      IFEQ '3'
|   C                     MOVEL'Y'       E505
| 01*C                     ELSE
|   C                     MOVEL*BLANK    E505
| 01-C                     ENDIF
|    C                     MOVELDE502     E502             員工編號
|    C                     MOVELDE511     E511             補助
|    C**
|    C                     Z-ADD$A8YMD    E597             更新日期
|    C                     TIME           E598             更新時間
|    C                     MOVEL$USER     E599             使用者代號
|    C**
|    C*
|    C                     EXSR R2D12
|    C*
14===C                     ENDSR
     C*==============================================================*
     C*          R2D12 ....SCREEN FIELD TO FILE -- PYE2PF
     C*==============================================================*
15===C           R2D12     BEGSR
|    C**
|    C*先將該眷屬在PYE2PF給予刪除,之後再新增最新狀態寫回PYE2PF
|    C**
|    C           KEYE22    CHAINE20                  40
|    C*
|    C  N40                DELETE20
|    C**
|    C*根據員工所建立的眷屬資料給予序號
|    C**
|    C                     Z-ADD0         TMPFD3  30
|    C           KEYEM     SETGTE27                  47
|    C           KEYEM     REDPEE27                      46
|    C*
|    C  N46      E204      ADD  1         TMPFD3           序號
|    C   46                Z-ADD1         TMPFD3           序號
|    C*
|    C*將PYE5PF最近的眷屬資料寫入到PYE2PF
|    C*
|    C           KEYE2     CHAINE20                  40
|    C*
|    C                     MOVELE501      E201             公司編號
|    C                     MOVELE502      E202             員工編號
|    C                     MOVELE507      E207             眷屬證號
|    C                     Z-ADDE512      E203             生效日期
|    C*
| 01-C           E513      IFEQ '3'
|   C                     MOVEL'Y'       E205             停保
| 01*C                     ELSE
|   C                     MOVEL*BLANK    E205             停保
| 01-C                     ENDIF
|    C*
|    C*          TMPFD4    IFNE DE507
|    C   40                Z-ADDTMPFD3    E204             序號
|    C*                    ENDIF
|    C*
|    C                     MOVELE506      E206             眷屬姓名
|    C                     MOVELE508      E208             出生日期
|    C                     MOVELE509      E209             眷屬稱謂
|    C                     MOVELE511      E211             補助碼
|    C**
|    C                     Z-ADD$A8YMD    E297             更新日期
|    C                     TIME           E298             更新時間
|    C                     MOVEL$USER     E299             使用者代號
|    C**
|    C   40                WRITEE20
|    C  N40                UPDATE20
|    C                     CLEARE20
|    C*
15===C                     ENDSR
     C*==============================================================*
     C*          R2D20 ....FILE PROCESS : UPD
     C*==============================================================*
16===C           R2D20     BEGSR
|    C* DETAIL
|    C                     EXSR R2D11
|    C                     UPDATE50
|    C                     CLEARE50
|    C*
16===C                     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS
     C*==============================================================*
17===C           R2D30     BEGSR
|    C*
|    C                     DELETE50
|    C                     EXSR R2D31
|    C*
17===C                     ENDSR
     C*==============================================================*
     C*          R2D31 ....SCREEN FIELD TO FILE -- PYE2PF
     C*==============================================================*
18===C           R2D31     BEGSR
|    C**
|    C*當PYE5PF成功殺了一筆資料,PYE2PF也要殺掉那筆記錄
|    C*註:眷屬筆數也要扣掉1,因為R1B00有計算眷屬筆數
|    C**
|    C           KEYE2     CHAINE20                  40
|    C*
|    C  N40                DELETE20
|    C  N40                SUB  1         COUNT
|    C*
| 01-C           COUNT     IFGT 0
|   C**
|   C*根據員工所建立的眷屬資料給予序號
|   C**
|   C                     Z-ADD0         TMPFD3  30
|   C           KEYEM     SETGTE27                  47
|   C           KEYEM     REDPEE27                      46
|   C*
|   C  N46      E204      ADD  1         TMPFD3           序號
|   C   46                Z-ADD1         TMPFD3           序號
|   C*
|   C*將PYE5PF最近的眷屬資料寫入到PYE2PF
|   C*
|   C           DE507     SETGTE50
|   C           DE507     REDPEE50                      46
|   C*
| 02-C           *IN46     IFEQ *OFF
|   C                     MOVELE501      E201             公司編號
|   C                     MOVELE502      E202             員工編號
|   C                     MOVELE507      E207             眷屬證號
|   C*
|   C           KEYE21    CHAINE20                  40
|   C*
|   C                     Z-ADDE512      E203             生效日期
|   C*
| 03-C           E513      IFEQ '3'
|   C                     MOVEL'Y'       E205             停保
| 03*C                     ELSE
|   C                     MOVEL*BLANK    E205             停保
| 03-C                     ENDIF
|   C*
|   C*          TMPFD4    IFNE DE507
|   C   40                Z-ADDTMPFD3    E204             序號
|   C*                    ENDIF
|   C*
|   C                     MOVELE506      E206             眷屬姓名
|   C                     MOVELE508      E208             出生日期
|   C                     MOVELE509      E209             眷屬稱謂
|   C                     MOVELE511      E211             補助碼
|   C**
|   C                     Z-ADD$A8YMD    E297             更新日期
|   C                     TIME           E298             更新時間
|   C                     MOVEL$USER     E299             使用者代號
|   C**
|   C   40                WRITEE20
|   C  N40                UPDATE20
|   C                     CLEARE20
|   C*
| 02-C                     ENDIF
|   C*
| 01-C                     ENDIF
|    C**
18===C                     ENDSR
     C*==============================================================*
     C*          R2E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
19===C           R2E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*稱謂
| 01-C           #LIN      IFEQ 10
|   C           #COL      ANDEQ58
|   C                     CALL 'PTIZ21'
|   C                     PARM 'RELATION'P201I1 10        代碼欄位
|   C           DE509     PARM DE509     P201I2 10        代碼編號
|   C                     MOVEL*BLANKS   #Y01
|   C                     MOVEL*BLANKS   #Y02
|   C                     MOVEL*BLANKS   DE509N           稱謂中文
|   C                     MOVEL'RELATION'#Y01
|   C                     MOVELDE509     #Y02
|   C           KEY#Y     CHAIN#Y0                  40
|   C  N40                MOVEL#Y03      DE509N           稱謂中文
|   C   40                MOVEL*BLANKS   DE509N           稱謂中文
| 01-C                     ENDIF
|    C*
|    C*員工編號DE502
|    C*          #LIN      IFEQ 13
|    C*          #COL      ANDGE47
|    C*          #COL      ANDLE56
|    C*                    CALL 'PYI101'
|    C*                    PARM DE501     P101I1  2         公司別
|    C*          DE502     PARM DE502     P101I2 10         員工　
|    C*                    PARM           P101I3 10         員工　
|    C*                    PARM           @RTNC   2          FUN KEY
|    C*                    END
|    C*
|    C           KEYEM     CHAINEM0                  40    *NF
|    C   40                MOVEL*BLANKS   DEM03
|    C  N40                MOVELEM03      DEM03
|    C*
|    C*補助
| 02-C           #LIN      IFEQ 14
|   C           #COL      ANDEQ9
|00A+C                     CALL 'TRW006'
|00A+C           DE511     PARM DE511     P120O1  1
|00A+C           DE511     CHAINE90                  40
|00A+C           *IN40     IFEQ '0'
|00A+C                     CALL 'P65'
|00A+C                     PARM E904      P6501I 80        被分割字串
|00A+C                     PARM 32        P6502I  30       分割長度
|00A+C           DE511N    PARM           P6511O 80        分割後字串1
|00A+C                     PARM           P6512O 80        分割後字串2
|00A+C                     ELSE                                        2
|00A+C                     MOVEL*BLANK    DE511N            補助名稱
|00A+C                     END                                         2
|   C*00A-                CALL 'PTIZ21'
|   C*00A-                PARM 'INSR'    P201I1           代碼欄位
|   C*00A-      DE511     PARM DE511     P201I2           代碼編號
|   C*00A-                MOVEL*BLANKS   #Y01
|   C*00A-                MOVEL*BLANKS   #Y02
|   C*00A-                MOVEL*BLANKS   DE511N           補助中文
|   C*00A-                MOVEL'INSR'    #Y01
|   C*00A-                MOVELDE511     #Y02
|   C*00A-      KEY#Y     CHAIN#Y0                  40
|   C*00A-N40                MOVEL#Y03      DE511N           補助中文
|   C*00A- 40                MOVEL*BLANKS   DE511N           補助中文
| 02-C                     ENDIF
|    C*
19===C                     ENDSR
     C*==============================================================*
**
新增修改      刪除查詢
