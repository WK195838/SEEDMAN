     H****************************************************************
     H*            泛太資訊科技開發股份有限公司－版權所有          *
     H*--------------------------------------------------------------*
     H*    PROGRAM NAME : PYA305                                     *
     H*    AUTHOR       : A1046  JOHNSON CHEN                        *
     H*    CREATE DATE  : 79/05/31                                   *
     H*    UPDATE DATE  : 790823 CATHY SUN                           *
     H*    SYSTEM       : MIRS                                       *
     H*    SUBSYSTEM    : PY                                         *
     H****************************************************************
     FPYOSPF  UF  E           K        DISK                      A
     FPYPYPF  IF  E           K        DISK
     FPYEMPF  IF  E           K        DISK
     FSYCMPF  IF  E           K        DISK
     FPYPMPF  IF  E           K        DISK
     FPYSSLF01IF  E           K        DISK
     FPYA3051DCF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*: FUNCTION DATA :
     E                    @A01        5  1               作業權利
     E                    WFUN    5   5  6
     I*
     I/COPY QDDSSRC,PYPMDS01
     I*
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I            DS
     I                                        1   7 EM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     I           UDS
     I                                      101 110 $USER
     I                                      203 2080DDATE
     I                                      203 2060DYM
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      139 139 $RMK01
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C           *LIKE     DEFN SS09      KSS09
     C           *LIKE     DEFN SS05      KSS05
     C           *LIKE     DEFN SS18      KSS18
     C           *LIKE     DEFN SS01      KSS01
     C           *LIKE     DEFN OS05      KOS05
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           DOS01
     C                     KFLD           DOS02
     C           KEYSS     KLIST                           *PYSSPF
     C                     KFLD           KSS09
     C                     KFLD           KSS05
     C                     KFLD           KSS18
     C                     KFLD           KSS01
     C           KEYSS1    KLIST                           *PYSSPF
     C                     KFLD           KSS09
     C                     KFLD           KSS05
     C                     KFLD           KSS18
     C           KEYOS     KLIST
     C                     KFLD           DOS01
     C                     KFLD           DOS02
     C                     KFLD           WOS03
     C                     KFLD           DOS04
     C                     KFLD           KOS05
     C           KEYOS1    KLIST
     C                     KFLD           DOS01
     C                     KFLD           DOS02
     C                     KFLD           WOS03
     C                     KFLD           DOS04
     C           KEYPM     KLIST
     C                     KFLD           PM01
     C                     KFLD           DOS01
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR RTN010                     *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
    C*
  02-C           SCID      CASEQ'SC01'    RTN100
    C           SCID      CASEQ'SC02'    RTN300
  02-C                     END
    C*
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL VALUE
     C*==============================================================*
01===C           RTN010    BEGSR
|    C*
|    C                     MOVEL'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C                     MOVEL*BLANK    DOS02
|    C                     Z-ADDDYM       DOS03
|    C                     MOVEL*BLANK    DOS04
|    C                     MOVEL'1'       DOPT
|    C*
|    C                     MOVEL'2'       KSS05
|    C*
|    C                     MOVEL$COR      DOS01
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          RTN100....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           RTN100    BEGSR
|    C*
|    C                     EXFMTDSPC1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO END100
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'
|   C                     EXSR R1E00
|   C                     GOTO END100
| 02-C                     END
|    C*
|    C                     EXSR RTN110                     *SCR CHK
|    C*
| 03-C           *IN99     IFEQ '0'
|   C                     MOVEL'SC02'    SCID             *SCR2
| 03-C                     END
|    C*
02===C           END100    ENDSR
     C*==============================================================*
     C*          R1E00 ....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
03===C           R1E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*員工編號DPB02
| 01-C           #LIN      IFEQ 5
|   C           #COL      ANDGE13
|   C           #COL      ANDLE17
|   C                     CALL 'PYI101'
|   C                     PARM DOS01     P101I1  2         公司別
|   C           DOS02     PARM DOS02     P101I2  8         員工　
|   C                     PARM           @RTNC   2          FUN KEY
| 01-C                     END
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          RTN110....SCREEN CHECK
     C*==============================================================*
04===C           RTN110    BEGSR
|    C*
|    C* CHK FUN AUT
|    C                     MOVE DOPT      WNUM    10
| 01-C           @A01,WNUM IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO END110
|    C*---------------------------------------------------------------
|    C* << CHACK 公司別NOT *BLANK >>
| 02-C           DOS01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO END110
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DOS01     CM001I  2         公司代號
|    C                     PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 03-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 03-C                     END
|    C   99                GOTO END110
|    C*
| 04-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C   99                GOTO END110
|    C*
|    C*+<<DATA CHECK>>+++++++++++++++++++++++++++++++++++++++++++++++++
|    C           DOS01     CHAINCM0                  40
|    C*
|    C                     MOVELCM13      KSS18
|    C*
|    C                     MOVEL'01'      PM01
|    C           KEYPM     CHAINPM0                  40
| 05-C           *IN40     IFEQ '1'
|   C                     Z-ADD*ZERO     PM201
|   C                     Z-ADD*ZERO     PM202
|   C                     Z-ADD*ZERO     PM205
|   C                     Z-ADD*ZERO     PM206
|   C*                    MOVEL*BLANK    PM207
| 05-C                     END
|    C*
| 06-C           DOS02     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     619899
| 06*C                     ELSE
|   C           KEYEM     CHAINEM0                  40
| 07-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     619699
| 07-C                     END
| 06-C                     END
|    C   99                GOTO END110
|    C*---------------------------------------------------------------
|    C*部門別
|    C                     CALL 'PYSCM1'
|    C                     PARM EM01      CM101I  2         公司代號
|    C                     PARM EM04      CM102I  7         部門代號
|    C                     PARM           CM103O 14         部門名稱
|    C                     PARM           CM199O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 08-C           CM199O    IFEQ 'N'
|   C                     SETON                     6199
|   C                     MOVEL'US#0098' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 08-C                     END
|    C   99                GOTO END110
|    C*
| 09-C           CM199O    IFEQ *BLANK
|   C                     SETON                     606199
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 09-C                     END
|    C   99                GOTO END110
|    C*---------------------------------------------------------------
|    C*
| 10-C           DOPT      IFEQ '1'
|   C           EM11      ANDEQ'Y'
|   C                     MOVEL'UMI0400' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     619499
| 10-C                     END
|    C   99                GOTO END110
|    C*
| 11-C           DOS04     IFEQ *BLANK
|   C                     MOVEL'1'       DOS04
| 12-C*          PM207     IFEQ 'Y'
|   C           PM206     IFLE PM202
|   C                     MOVEL'2'       DOS04
| 12-C                     END
| 11-C                     END
|    C*
|    C*上期無薪不可輸入上期
|    C**         DOS04     IFEQ '2'
|    C**         EM10      ANDEQ'3'
|    C**                   MOVEL'UPT0030' ERRID
|    C**                   MOVEL'PTMF'    ERRF
|    C**                   SETON                     638999
|    C**                   END
|    C*  99                GOTO END110
|    C*
|    C           KEYEM     CHAINPY0                  40
| 13-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     609699
|   C                     SETON                     61
| 13-C                     END
|    C   99                GOTO END110
|    C*
|    C                     Z-ADDDOS03     DOS03M  20
| 14-C           DOS03M    IFLT 1
|   C           DOS03M    ORGT 12
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     629599
| 14-C                     END
|    C   99                GOTO END110
|    C*
|    C                     MOVELEM13      EM13YM  60
|    C                     CALL 'P39'
|    C                     PARM DOS03     P3901I  60
|    C                     PARM '1'       P3902I  1
|    C                     PARM '1'       P3903I  1
|    C                     PARM '2'       P3904I  1
|    C                     PARM '1'       P3905I  1
|    C                     PARM           P3911O  60
|    C                     Z-ADDP3911O    WOS03   60
| 15-C           WOS03     IFLT EM13YM
|   C                     MOVEL'UMI0360' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     629399
| 15-C                     END
|    C   99                GOTO END110
|    C*
|    C*SPECIAL AUTHORITY CAN UPDATE AFTER薪資試算
| 16-C           $RMK01    IFNE 'Y'
|   C           DOPT      ANDNE'5'
| 17-C           P3911O    IFLE PM201
|   C           DOS04     ANDEQ'2'
|   C           P3911O    ORLE PM205
|   C           DOS04     ANDEQ'1'
|   C                     MOVEL'UMI0530' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     629299
| 17-C                     END
| 16-C                     END
|    C   99                GOTO END110
|    C*
| 18-C           DOPT      IFNE '5'
| 19-C           P3911O    IFLE PM202
|   C           DOS04     ANDEQ'2'
|   C           P3911O    ORLE PM206
|   C           DOS04     ANDEQ'1'
|   C                     MOVEL'UMI0540' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     629199
| 19-C                     END
| 18-C                     END
|    C   99                GOTO END110
|    C*
|    C*                    MOVEL*BLANK    KOS05
|    C*                    CALL 'P39'
|    C*                    PARM DOS03     P3901I  60
|    C*                    PARM DFMT      P3902I  1
|    C*                    PARM DTYPE     P3903I  1
|    C*                    PARM '2'       P3904I  1
|    C*                    PARM '1'       P3905I  1
|    C*                    PARM           P3911O  60
|    C                     Z-ADDP3911O    WOS03   60
|    C*911030    KEYOS1    SETLLOS0
|    C*911030    KEYOS     READEOS0                      40
|    C*
|    C           KEYOS1    CHAINOS0                  40
|    C*
| 20-C           DOPT      IFEQ '1'                        *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     609799
|   C                     SETON                     6162
| 20-C                     END
|    C   99                GOTO END110
|    C*
| 21-C           DOPT      IFNE '1'                        *UPD,DEL,INQ
|   C           *IN40     ANDEQ'1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     609699
|   C                     SETON                     6162
| 21-C                     END
|    C   99                GOTO END110
|    C*
|    C                     MOVEL'1'       KSS09
|    C*
04===C           END110    ENDSR
     C*==============================================================*
     C*          RTN300....'SC02' SCREEN MAIN ROUTINE --- SFLSR
     C*==============================================================*
05===C           RTN300    BEGSR
|    C*
| 01-C           DOPT      CASEQ'1'       RTN310           *ADD
|   C           DOPT      CASEQ'2'       RTN320           *UPD
|   C           DOPT      CASEQ'4'       RTN330           *DLT
|   C           DOPT      CASEQ'5'       RTN340           *INQ
| 01-C                     END
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          RTN310....ADD DATA --- SFLSR
     C*==============================================================*
06===C           RTN310    BEGSR
|    C*
|    C                     SETON                     31    *CAN INPUT
|    C                     MOVELWFUN,1    DFUN
|    C*
|    C                     EXSR RTN391                     *INT SFLSR
|    C                     EXSR RTN398
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           SCID      ANDEQ'SC02'
|   C*
|   C                     WRITEDSPC2
|   C                     EXFMTSFLCR
|   C                     MOVEA*ALL'0'   *IN,60
|   C*
| 02-C           *IN03     IFEQ '0'
| 03-C           *IN12     IFEQ '0'
|   C*
|   C                     EXSR RTN380                     *CHECK
| 04-C           *IN99     IFEQ '0'                         SAVE
|   C           *IN13     ANDEQ'1'                         SAVE
|   C                     EXSR RTN39S                     *SAVE
|   C                     MOVEL'SC01'    SCID
| 04-C                     END
|   C*
| 03*C                     ELSE
|   C           *LOVAL    SETLLOS0
|   C                     MOVE 'SC01'    SCID
| 03-C                     END                             *IN12 ENDIF
| 02-C                     END                             *IN03 ENDIF
|   C*
| 01-C                     END                             *ENDDO
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          RTN320....UPD DATA --- SFLSR
     C*==============================================================*
07===C           RTN320    BEGSR
|    C*
|    C                     SETON                     31
|    C                     MOVELWFUN,2    DFUN
|    C*
|    C                     EXSR RTN391                     *INT SFLSR
|    C                     EXSR RTN398                     *READ DATA
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           SCID      ANDEQ'SC02'
|   C*
|   C                     WRITEDSPC2
|   C                     EXFMTSFLCR
|   C                     MOVEA*ALL'0'   *IN,60
|   C*
| 02-C           *IN03     IFEQ '0'
| 03-C           *IN12     IFEQ '0'
|   C*
|   C                     EXSR RTN380                      CHK SFLSR
|   C*
| 04-C           *IN99     IFEQ '0'                         SAVE
|   C           *IN13     ANDEQ'1'                         SAVE
|   C                     EXSR RTN39S                      ADD D
|   C                     MOVE 'SC01'    SCID
| 04-C                     END
|   C*
| 03*C                     ELSE
|   C           *LOVAL    SETLLOS0
|   C                     MOVE 'SC01'    SCID
| 03-C                     END                             *IN12 ENDIF
| 02-C                     END                             *IN03 ENDIF
|   C*
| 01-C                     END                             *ENDDO
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          RTN330....DEL DATA --- SFLSR
     C*==============================================================*
08===C           RTN330    BEGSR
|    C*
|    C                     SETOF                     31    *NO INPUT
|    C                     MOVELWFUN,4    DFUN
|    C*
|    C                     EXSR RTN391                     *INT SFLSR
|    C                     EXSR RTN398                     *READ DATA TO SFLSR
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           SCID      ANDEQ'SC02'
|   C*
|   C                     WRITEDSPC2
|   C                     EXFMTSFLCR
|   C                     MOVEA*ALL'0'   *IN,60
|   C*
| 02-C           *IN03     IFEQ '0'
| 03-C           *IN12     IFEQ '0'
|   C*
| 04-C           *IN13     IFEQ '1'
|   C                     EXSR RTN39E                      DLT D
|   C                     MOVE 'SC01'    SCID
| 04-C                     END
|   C*
| 03*C                     ELSE
|   C           *LOVAL    SETLLOS0
|   C                     MOVE 'SC01'    SCID
| 03-C                     END                             *IN12 ENDIF
| 02-C                     END                             *IN03 ENDIF
|   C*
| 01-C                     END                             *ENDDO
|    C*
08===C                     ENDSR
     C*==============================================================*
     C*          RTN340....INQ DATA --- SFLSR
     C*==============================================================*
09===C           RTN340    BEGSR
|    C*
|    C                     SETOF                     31    *NO INPUT
|    C                     MOVELWFUN,5    DFUN
|    C*
|    C                     EXSR RTN391                     *INT SFLSR
|    C                     EXSR RTN398                     *READ DATA TO SFLSR
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           SCID      ANDEQ'SC02'
|   C*
|   C                     WRITEDSPC2
|   C                     EXFMTSFLCR
|   C                     MOVEA*ALL'0'   *IN,60
|   C*
| 02-C           *IN03     IFEQ '0'
| 03-C           *IN12     IFEQ '0'
|   C*
| 04-C           *IN13     IFEQ '1'
|   C           *LOVAL    SETLLOS0
|   C                     MOVE 'SC01'    SCID
| 04-C                     END
|   C*
| 03*C                     ELSE
|   C           *LOVAL    SETLLOS0
|   C                     MOVE 'SC01'    SCID
| 03-C                     END                             *IN12 ENDIF
| 02-C                     END                             *IN03 ENDIF
|   C*
| 01-C                     END                             *ENDDO
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*          RTN380....CHECK 'SC02' --- SFLSR
     C*==============================================================*
10===C           RTN380    BEGSR
|    C*
10===C           END380    ENDSR
     C*==============================================================*
     C*          RTN391....INIT 'SC02' -- SFLSR
     C*==============================================================*
11===C           RTN391    BEGSR
|    C*
|    C                     Z-ADD1         DRRN
|    C                     Z-ADD0         WRRN    20
|    C                     Z-ADD0         RRN
|    C                     Z-ADD24        DSPRC   20
|    C                     Z-ADD24        WPRCD   20       *PAG RCD
|    C                     MOVE 'N'       WBOTM   1        *BOTTOM
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     525053
|    C                     SETON                     51    *SFLDSPCTL
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          RTN398....FILE TO SCREEN --- SFLSR(FOR A,U,D,I)
     C*==============================================================*
12===C           RTN398    BEGSR
|    C*
|    C*SFL HEADING
| 01-C                     SELEC
| 01-C           DOS04     WHEQ '2'
|   C                     MOVEL'上期'  DOS04N
| 01-C           DOS04     WHEQ '1'
|   C                     MOVEL'下期'  DOS04N
| 01-C           DOS04     WHEQ '3'
|   C                     MOVEL'外勞'  DOS04N
| 01-C                     END
|    C*
|    C                     SETON                     54    *54 FOR BLANK LOOP
|    C*
|    C                     MOVEL*BLANK    KSS01
|    C           KEYSS     SETLLSS0
|    C           KEYSS1    READESS0                      44*NF/EOF
|    C*
| 02-C           *IN44     DOWEQ'0'
|   C*
|   C           DOS04     IFEQ '1'                        上期
| 03-C           DOS04     OREQ '2'                        下期
|   C*          SS17      ANDNE' '                        上期項目
|   C                     EXSR RTN39B
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
| 03-C                     END
|   C*
|   C           KEYSS1    READESS0                      44*NF/EOF
| 02-C                     END
|    C*
|    C                     SETON                     53
|    C                     Z-ADDRRN       WRRN       50
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          RTN39B....FILE TO SCREEN --- MOVE
     C*==============================================================*
13===C           RTN39B    BEGSR
|    C*
|    C                     MOVELSS01      DOS05
|    C                     MOVELSS01      KOS05
|    C                     CALL 'P39'
|    C                     PARM DOS03     P3901I  60
|    C                     PARM '1'       P3902I  1
|    C                     PARM '1'       P3903I  1
|    C                     PARM '2'       P3904I  1
|    C                     PARM '1'       P3905I  1
|    C                     PARM           P3911O  60
|    C                     Z-ADDP3911O    WOS03   60
|    C           KEYOS     CHAINOS0                  40
| 01-C           *IN40     IFEQ '0'
|   C                     Z-ADDOS06      DOS06
|   C*                    Z-ADDOS07      DOS07
| 01*C                     ELSE
|   C                     Z-ADD*ZERO     DOS06
|   C*                    Z-ADD*ZERO     DOS07
| 01-C                     END
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          RTN39D....SCREEN TO FILE --- D
     C*==============================================================*
14===C           RTN39D    BEGSR
|    C*
|    C                     MOVELDOS01     OS01
|    C                     MOVELDOS02     OS02
|    C                     Z-ADDWOS03     OS03
|    C                     MOVELDOS04     OS04
|    C                     MOVELDOS05     OS05
|    C                     Z-ADDDOS06     OS06
|    C*                    Z-ADDDOS07     OS07
|    C                     Z-ADDDOS08     OS08
|    C*
|    C                     Z-ADD$A8YMD    OS97
|    C                     TIME           WTIME  120
|    C                     MOVELWTIME     OS98
|    C                     MOVEL$USER     OS99
|    C*
14===C                     ENDSR
     C*==============================================================*
     C*          RTN39E....SCREEN TO FILE --- DETAIL DELETE
     C*==============================================================*
15===C           RTN39E    BEGSR
|    C*
|    C                     MOVEL*BLANK    KOS05
|    C                     CALL 'P39'
|    C                     PARM DOS03     P3901I  60
|    C                     PARM '1'       P3902I  1
|    C                     PARM '1'       P3903I  1
|    C                     PARM '2'       P3904I  1
|    C                     PARM '1'       P3905I  1
|    C           WOS03     PARM           P3911O  60
|    C           KEYOS     SETLLOS0
|    C           KEYOS1    READEOS0                      40
| 01-C           *IN40     DOWEQ'0'
|   C                     DELETOS0
|   C           KEYOS1    READEOS0                      40
| 01-C                     END
|    C*
15===C                     ENDSR
     C*==============================================================*
     C*          RTN39S....SAVE D
     C*==============================================================*
16===C           RTN39S    BEGSR
|    C*
| 01-C           DOPT      IFEQ '2'
|   C                     EXSR RTN39E
| 01-C                     END
|    C*
|    C                     READCSFLSR                  5757
| 02-C           *IN57     DOWEQ'0'
|   C*
| 03-C           DOS06     IFNE *ZERO
|   C*          DOS07     ORNE *ZERO
|   C                     EXSR RTN39D
|   C                     WRITEOS0
| 03-C                     END
|   C*
|   C                     READCSFLSR                    57
| 02-C                     END
|    C*
16===C                     ENDSR
**
新增修改      刪除查詢
