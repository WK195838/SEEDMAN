     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   :PYI201                                      *
     H*    PROGRAM NAME :加班資料查詢　　                          *
     H*    AUTHOR       :A1232 ERIC                                  *
     H*    CREATE DATE  :86/01/20                                    *
     H*    UPDATE DATE  :91/06/17 A1336 KLAUS                        *
     H*    SYSTEM       :人事薪資                                  *
     H****************************************************************
     FPYOVPF  IF  E           K        DISK
     FPYEMPF  IF  E           K        DISK
     FPYI201D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    S#01       35  7               部門代號
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      152 161 $USERN
     I                                      211 2160DDATE
     I                                      201 2080$A8YMD
     I*DEFIND STRAT KEY
     I           IDS
     I                                        1  15 @KOVO
     I           IDS
     I                                        1  15 @KOVN
     I                                        1   8 DOV02
     I                                        9  150DOV03
     I            DS
     I                                        1   7 EM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     I            DS
     I                                        1 245 S#01
     I                                        1 245 CM202O
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C           *LIKE     DEFN OV03      WOV03
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DOV01     PARM           I201I1  2         公司編號
     C           DOV02     PARM           I201I2  8         員工編號
     C           DOV03     PARM           I201I3  70        加班日期
     C                     PARM           @RTNC   2          FUN KEY
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C*
     C           KEYOV     KLIST                           *PYOVPF
     C                     KFLD           DOV01
     C                     KFLD           DOV02
     C                     KFLD           WOV03
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           OV01
     C                     KFLD           OV02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
     C           @RTNC     DOWEQ*BLANK
     C                     EXSR R1000                      *SCR PROCESS
     C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C* INIT VALUE SET *---------------------------------------------*
     C                     MOVEL*BLANK    @RTNC
     C                     Z-ADD14        @PRCD            .PAGE SIZE
|    C*--------------------------------------------------------------*
     C*檢核是否對全部的部門有權利並帶出管制的部門
     C                     CALL 'PYSCM2'
     C                     PARM DOV01     CM201I  4        公司
     C                     PARM           CM201O  1        權利
     C                     PARM           CM202O           部門代號
     C*　　　　　　　
     C*--------------------------------------------------------------*
     C                     EXSR R1A00                      .SFL INIT
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1000 ....SCREEN  PROCESS
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C                     WRITEDSPC1
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C                     Z-ADD*ZERO     #LIN
     C                     Z-ADD*ZERO     #COL
     C*
     C           *IN03     IFEQ '1'                        *END
     C                     MOVEL'03'      @RTNC
     C                     GOTO E1000
     C                     END
     C*
     C           *IN12     IFEQ '1'                        *PREVIOUS
     C                     MOVEL'12'      @RTNC
     C                     GOTO E1000
     C                     END
     C*
     C           *IN25     IFEQ '1'                        *NEXT PAGE
     C                     EXSR R1N00
     C                     GOTO E1000
     C                     END
     C*
     C           @KOVO     IFNE @KOVN                      *START KEY
     C                     EXSR R1A00                      *SFL INIT
     C                     GOTO E1000
     C                     END
     C*
     C                     EXSR R1C00                      *OPTION
     C*
     C           E1000     ENDSR
     C*=============================================================*
     C*          R1A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
     C           R1A00     BEGSR
     C*
     C                     Z-ADD0         DRRN
     C                     Z-ADD0         RRN
     C                     Z-ADD0         @DRCD            *DSP RCD
     C                     MOVEL'N'       @BOTOM  1        *BOTTOM
     C*
     C                     SETOF                     51    *SFLDSPCTL
     C                     SETON                     52    *SFLCLR
     C                     WRITESFLCR
     C                     SETOF                     52
     C*
     C                     SETON                     51    *SFLDSPCTL
     C                     SETOF                     5053  *SFLDSP,END
     C*
     C                     EXSR R1A20                      *SFL HEAD
     C   99                GOTO E1A00
     C* <<SFL INITIAITAL DATA ADD>>
     C                     MOVEL@KOVN     @KOVO            *KEY SAVE
     C*
     C           KEYOV     SETLLOV0
     C                     EXSR R1A10                      *ADD SFL DTA
     C*
     C*
     C           RRN       IFGT 0
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     99    *NO DTA
     C                     MOVEL'UPT2050' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C           E1A00     ENDSR
     C*=============================================================*
     C*          R1A10 .... ADD SUBFILE DATA
     C*=============================================================*
     C           R1A10     BEGSR
     C*
     C                     ADD  @PRCD     @DRCD
     C           RRN       DOWLT@DRCD
     C*
     C           DOV01     READEOV0                      46
     C           *IN46     IFEQ '0'
     C*
     C                     MOVEL*BLANK    EM03
|   C           KEYEM     CHAINEM0                  40
     C*檢查部門權利
     C           CM201O    IFNE 'Y'
     C                     Z-ADD1         I       20
     C           EM04      LOKUPS#01,I                   40
     C           S#01,I    IFEQ *BLANK
     C                     SETOF                         40
     C                     END
     C  N40                GOTO TAG1
     C                     END
|   C*
     C                     EXSR R1A11                      *WRITE SFL
     C                     ELSE                            *EOF
     C                     Z-ADDRRN       @DRCD
     C                     MOVE 'Y'       @BOTOM
     C                     SETON                     9653  *SFLEND
     C                     END
     C*
     C           TAG1      TAG
     C                     END
     C*
     C                     Z-ADDRRN       DRRN
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1A11 .... WRITE SUBFILE DATA
     C*=============================================================*
     C           R1A11     BEGSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* << SUBFILE DATA VALUE SET >>
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     ADD  1         RRN
     C                     CALL 'P31'
     C                     PARM OV03      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM DFMT      P3104I  1
     C                     PARM DTYPE     P3105I  1
     C           OV03D     PARM           P3101O  80
     C                     WRITESFLSR
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
     C           R1A20     BEGSR
     C*
     C* << CHACK 公司別NOT *BLANK >>
     C                     MOVEL*BLANK    DOV01N
     C           DOV01     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6099
     C                     END
     C   99                GOTO E1A20
     C*
     C                     CALL 'PYSCM0'
     C                     PARM DOV01     CM001I  2         公司代號
     C           DOV01N    PARM           CM002O 22         公司名稱
     C                     PARM           CM003O 32         代碼說明
     C           DFMT      PARM           CM004O  1         日期格式
     C           DTYPE     PARM           CM005O  1         日期曆別
     C                     PARM           CM098O 50         備用欄
     C                     PARM           CM099O  1         存在否？
     C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
     C           CM099O    IFEQ 'N'
     C                     SETON                     6099
     C                     MOVEL'US#0099' ERRID
     C                     MOVEL'S#MF'    ERRF
     C                     END
     C   99                GOTO E1A20
     C*
     C           CM099O    IFEQ *BLANK
     C                     SETON                     6099
     C                     MOVEL'UPT2010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C   99                GOTO E1A20
|    C*
     C                     CALL 'P31'
     C                     PARM DOV03     P3101I  80
     C                     PARM DFMT      P3102I  1
     C                     PARM DTYPE     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           WOV03     PARM           P3101O  80
     C*
     C           E1A20     ENDSR
     C*=============================================================*
     C*          R1C00 ....挑選資料
     C*=============================================================*
     C           R1C00     BEGSR
     C*
     C                     READCSFLSR                  4242
     C           *IN42     DOWEQ'0'
     C*
     C           DOPT      IFNE *BLANK
     C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVELOV01      I201I1
     C                     MOVELOV02      I201I2
     C                     MOVELOV03D     I201I3
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVEL'00'      @RTNC
     C                     SETON                     42
     C                     END
     C*
     C  N42                READCSFLSR                    42
     C                     END
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1N00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
     C           R1N00     BEGSR
     C*
     C           @BOTOM    IFEQ 'Y'                        *EOF
     C                     SETON                         96*LST PAG
     C                     Z-ADDRRN       DRRN
     C                     ELSE
     C                     EXSR R1A10                      *ADD SFL DTA
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
