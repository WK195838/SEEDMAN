     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : PYA209                                     *
     H*    PROGRAM NAME :考勤資料建立                              *
     H*    AUTHOR       : A1226 PETER                                *
     H*    CREATE DATE  : 97/01/20                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :人事薪資系統                              *
     H****************************************************************
     FPYT5PF  O   E           K        DISK
     FPYEMPF  IF  E           K        DISK
     FPYA209D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    WFUN    5   5  6
     E*---------------------------------------------------------------*
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*
     I            DS
     I                                        1   7 DT506
     I                                        1   7 EM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     I*
     I           UDS
     I                                      101 110 $USER
     I                                      187 1920DDATE
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C           *LIKE     DEFN T502      WT502
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYT5     KLIST                           *PYEMPF
     C                     KFLD           DT505
     C                     KFLD           DT501
     C                     KFLD           DT502
     C           KEYEM     KLIST
     C                     KFLD           DT505
     C                     KFLD           DT501
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     MOVE 'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     MOVEL*BLANK    DT505
|    C                     MOVEL*BLANK    DT501
|    C                     Z-ADD0         DT502
|    C                     MOVEL*BLANK    DT507
|    C                     Z-ADD1         DOPT    10
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
|    C*公司別
|    C                     MOVEL$COR      DT505
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'
|   C                     EXSR R1E00                      .PROMPT
|   C                     GOTO E1000
| 02-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 03-C           *IN99     IFEQ '0'
|   C                     EXSR R1C00
|   C                     MOVE 'SC02'    SCID
| 03-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*
|    C*員工編號DOV02
| 01-C           #LIN      IFEQ 5
|   C           #COL      ANDGE13
|   C           #COL      ANDLE17
|   C                     CALL 'PYI101'
|   C                     PARM DT505     P101I1  2         公司別
|   C           DT501     PARM DT501     P101I2  8         員工　
|   C                     PARM           @RTNC   2          FUN KEY
| 01-C                     END
|    C*
03===C           E1E00     ENDSR
     C*==============================================================*
     C*          R1B00 ....SCREEN CHECK
     C*==============================================================*
04===C           R1B00     BEGSR
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*公司別
|    C                     MOVEL*BLANK    DT505N
| 02-C           DT505     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DT505     CM001I  2         公司代號
|    C           DT505N    PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 03-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
| 04-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C   99                GOTO E1B00
|    C*員工編號
| 05-C           DT501     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 05-C                     END
|    C   99                GOTO E1B00
|    C*
|    C           KEYEM     CHAINEM0                  40
| 06-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 06-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*部門別
|    C                     CALL 'PYSCM1'
|    C                     PARM EM01      CM101I  2         公司代號
|    C                     PARM EM04      CM102I  7         部門代號
|    C                     PARM           CM103O 14         部門名稱
|    C                     PARM           CM199O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 07-C           CM199O    IFEQ 'N'
|   C                     SETON                     6199
|   C                     MOVEL'US#0098' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 07-C                     END
|    C   99                GOTO E1B00
|    C*
| 08-C           CM199O    IFEQ *BLANK
|   C                     SETON                     606199
|   C                     MOVEL'US#1111' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 08-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*
| 09-C           EM11      IFEQ 'Y'
|   C                     MOVEL'UMI0400' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     606199
| 09-C                     END
|    C   99                GOTO E1B00
|    C*考勤日期
| 10-C           DT502     IFEQ 0
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 10-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'P30'
|    C                     PARM DT502     P3001I  80
|    C                     PARM DFMT      P3002I  1
|    C                     PARM DTYPE     P3003I  1
|    C                     PARM           P3011O  1
| 11-C           P3011O    IFEQ 'N'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 11-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'P31'
|    C                     PARM DT502     P3101I  80
|    C                     PARM DFMT      P3102I  1
|    C                     PARM DTYPE     P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           WT502     PARM           P3101O  80
|    C*
04===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....'SC02' SCREEN INITIAL
     C*==============================================================*
05===C           R1C00     BEGSR
|    C*
|    C                     MOVE WFUN,DOPT DFUN
|    C*
|    C                     EXSR R1CA0
|    C*
|    C                     SETON                     31
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1CA0 ....CLEAR FIELD WHEN ADD
     C*==============================================================*
06===C           R1CA0     BEGSR
|    C*
|    C                     Z-ADD*ZERO     DT503
|    C                     Z-ADD*ZERO     DT504
|    C*
06===C           E1A00     ENDSR
     C*==============================================================*
     C*          R2000 ....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
07===C           R2000     BEGSR
|    C*
|    C                     EXFMTDSPD2
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'
|   C                     MOVE 'SC01'    SCID
|   C                     GOTO E2000
| 02-C                     END
|    C*
|    C                     EXSR R2B00                      *CHK SCR
|    C*
| 03-C           *IN99     IFEQ '0'
|   C                     EXSR R2D00                      *OV
|   C                     MOVE 'SC01'    SCID
| 03-C                     END
|    C*
07===C           E2000     ENDSR
      *==============================================================*
      *          R2B00 ....CHECK 'SC02' SCREEN
      *==============================================================*
08===C           R2B00     BEGSR
|    C*刷卡時
| 01-C           DT503     IFLT *ZERO
|   C           DT503     ORGT 23
|   C                     MOVEL'UPT0043' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO E2B00
|    C*刷卡分
| 02-C           DT504     IFLT *ZERO
|   C           DT504     ORGT 60
|   C                     MOVEL'UPT0043' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E2B00
|    C*
08===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
09===C           R2D00     BEGSR
|    C*
|    C                     EXSR R2D10
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*=============================================================*
10===C           R2D10     BEGSR
|    C*
|    C                     EXSR R2D11
|    C                     WRITET50
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
11===C           R2D11     BEGSR
|    C*
|    C                     MOVELDT501     T501
|    C                     Z-ADDWT502     T502
|    C                     Z-ADDDT503     T503
|    C                     Z-ADDDT504     T504
|    C                     MOVELDT505     T505
|    C                     MOVELDT506     T506
|    C                     MOVELDT507     T507
|    C                     MOVEL*BLANKS   T508
|    C                     Z-ADD$A8YMD    T597
|    C                     TIME           T598
|    C                     MOVEL$USER     T599
|    C*
11===C                     ENDSR
     C*==============================================================*
**
新增修改      刪除查詢
