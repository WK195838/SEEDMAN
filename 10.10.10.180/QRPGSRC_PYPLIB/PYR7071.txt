     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : PYR7071                                    *
     H*    PROGRAM NAME :薪資所得銀行轉存資料列印－銀行別          *
     H*    AUTHOR       : TOM T.                                     *
     H*    CREATE DATE  : 2000/11/17                                 *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :人事薪資                                  *
     H****************************************************************
     FPYR707D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*LOCAL DATA AREA
     ILDA        UDS
     I                                      101 110 $USER
     I                                      187 1920DDATE
     I                                      125 1270$CPY
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I                                      217 218 $PRTCD
     I                                      219 219 $PENV
     I                                      500 500 $DATA
     I* PROGRAM USE DATA DEFINE ++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DBT01
     I                                      503 505 DBT061
     I                                      522 522 DBT03
     I                                      523 5280WBT045
     I                                      523 5260DBT04
     I                                      527 5280DBT05
     I                                      531 5360WBT04
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN *LDA      LDA
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C*==============================================================*
     C*          IDENTIFY A PARAMETER LIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C           *IN10     PARM *IN10     IN10    1
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C   10                EXSR R0200                      *INIT MSG.
     C  N10                EXSR R0100                      *INIT VALUE
     C*
     C                     MOVEL*BLANK    @PRT    1        *PRINT
  01-C           *IN03     DOUEQ'1'
    C           @PRT      OREQ 'Y'
    C                     EXSR R1000                      *SCRN
  01-C                     END
     C*
     C   03                SETON                     LR
     C                     RETRN
     C*==============================================================*
     C*          R0100 ....SCREEN INIT VALUE SET
     C*==============================================================*
01===CSR         R0100     BEGSR
|    C*
|    C                     SETON                     1027
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     MOVEL*BLANK    DBT061
|    C                     Z-ADD0         DBT04
|    C                     Z-ADD0         DBT05
|    C                     MOVEL'5'       DBT03
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
|    C*公司別
|    C                     MOVEL$COR      DBT01             公司別
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R0200 ....INITIAL VALUE
     C*==============================================================*
02===CSR         R0200     BEGSR
|    C*
|    C                     IN   LDA
|    C                     SETON                     2799  報表格式MESSAGE
|    C*
| 01-C           $PENV     IFNE '2'                        NOT-BATCH
|   C           $DATA     ANDEQ'N'                        NO-DATA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     GOTO E0200
| 01-C                     END
|    C*
| 02-C           $PENV     IFEQ '0'                        INQUIRY
|   C                     MOVEL'UPT2142' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 02-C                     END
|    C*
| 03-C           $PENV     IFEQ '1'                        ON-LINE
|   C                     MOVEL'UPT2140' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 03-C                     END
|    C*
| 04-C           $PENV     IFEQ '2'                        BATCH
|   C                     MOVEL'UPT2110' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C*
02===C           E0200     ENDSR
     C*===============================================================*
     C*          R1000  ...SCREEN MAIN PROCESS
     C*===============================================================*
03===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'
|   C                     EXSR R1E00                      .PROMPT
|   C                     GOTO E1000
| 02-C                     END
|    C*
|    C                     EXSR R1B00                      .CHECK      ?
|    C*
| 03-C           *IN99     IFEQ *ZERO
|   C                     SETOF                     27
|   C                     SETON                     28    .執行中
|   C                     WRITEDSPD1
|   C                     SETOF                     28
|   C*
|   C                     OUT  LDA
|   C                     MOVEL'Y'       @PRT
| 03-C                     END
|    C*
03===C           E1000     ENDSR
     C*===============================================================*
     C*          R1B00  ...CHECK DATA FROM SCREEN KEY-IN
     C*===============================================================*
04===C           R1B00     BEGSR
|    C*
|    C* << CHACK 公司別NOT *BLANK >>
| 01-C           DBT01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 01-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DBT01     CM001I  2         公司代號
|    C                     PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 02-C           CM099O    IFEQ 'N'
|   C                     SETON                     6199
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
| 03-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6199
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*+<<DATA CHECK>>+++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
| 04-C           DBT061    IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*日期檢核DBT04 DBT05
| 05-C           DBT04     IFEQ 0
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 05*C                     ELSE
|   C                     CALL 'P30'
|   C                     PARM WBT045    P3001I  80
|   C                     PARM DFMT      P3002I  1
|   C                     PARM DTYPE     P3003I  1
|   C                     PARM           P3011O  1
| 06-C           P3011O    IFEQ 'N'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 06-C                     END
| 05-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'P39'
|    C                     PARM DBT04     P3901I  60
|    C                     PARM DFMT      P3902I  1
|    C                     PARM DTYPE     P3903I  1
|    C                     PARM '2'       P3904I  1
|    C                     PARM '1'       P3905I  1
|    C           WBT04     PARM           P3911O  60
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
|    C* CHK FUN AUT
| 07-C           $PENV     IFNE '2'
|   C           $INQ      ANDNE'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 07-C                     END
|    C   99                GOTO E1B00
|    C*
04===CSR         E1B00     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
05===C           R1E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*
|    C*印表機
| 01-C           #LIN      IFEQ 19
|   C           #COL      ANDGE14
|   C           #COL      ANDLE15
|   C                     CALL 'P64I'
|   C           $PRTCD    PARM $PRTCD    P64II1  2
| 01-C                     END
|    C*
05===C           E1E00     ENDSR
     C*==============================================================*
