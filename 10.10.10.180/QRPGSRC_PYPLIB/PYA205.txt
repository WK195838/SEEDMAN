     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : PYA205                                     *
     H*    PROGRAM NAME :臨時卡資料維護                            *
     H*    AUTHOR       : A1226 PETER                                *
     H*    CREATE DATE  : 97/02/13                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :人事薪資系統                              *
     H****************************************************************
     FPYT4PF  UF  E           K        DISK                      A
     FPYEMPF  IF  E           K        DISK
     F*PYQJPF  IF  E           K        DISK
     FPYA205D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      187 1920DDATE
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C           *LIKE     DEFN $A8YMD    WT402
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYT4     KLIST                           *MAIN FILE
     C                     KFLD           DT401
     C                     KFLD           WT402
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           DT403
     C                     KFLD           DT404
     C*          KEYQJ     KLIST
     C*                    KFLD           DT405
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N99                EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C                     MOVE 'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     Z-ADD1         DOPT
|    C                     MOVEL*BLANK    DT401
|    C                     Z-ADD*ZERO     DT402
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 02-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 03-C           *IN99     IFEQ '0'                        *SCR CHK OK
|   C                     EXSR R1C00                      *PRE-SC02
|   C                     MOVEL'SC02'    SCID
| 03-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
03===C           R1B00     BEGSR
|    C*
|    C*
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*
|    C* DATA CHECK *--------------------------------------------------*
|    C*臨時卡號
| 02-C           DT401     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
| 03-C           DT402     IFEQ *ZERO
|   C                     MOVEL'UPT0012' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'P30'
|    C                     PARM DT402     P3001I  80
|    C                     PARM $PFMT     P3002I  1
|    C                     PARM $PTYPE    P3003I  1
|    C                     PARM           P3011O  1
|    C*
| 04-C           P3011O    IFEQ 'N'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'P31'
|    C                     PARM DT402     P3101I  80
|    C                     PARM $PFMT     P3102I  1
|    C                     PARM $PTYPE    P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           WT402     PARM           P3101O  80
|    C*
|    C           KEYT4     CHAINT40                  40
|    C*
| 05-C           DOPT      IFEQ 1                          *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6061
|   C                     SETON                     99
| 05-C                     END
|    C   99                GOTO E1B00
|    C*
| 06-C           DOPT      IFNE 1                          *UPD,DEL,INQ
|   C           *IN40     ANDEQ'1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6061
|   C                     SETON                     99
| 06-C                     END
|    C   99                GOTO E1B00
|    C*CHK生效日期不可小於前一筆之有效止日
| 07-C           DOPT      IFEQ 1
|   C           DT401     SETLLT40
|   C           KEYT4     REDPET40                      46
| 08-C           *IN46     IFEQ '0'
|   C           WT402     ANDLET402
|   C                     MOVEL'UMI0720' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6061
|   C                     SETON                     99
| 08-C                     END
|   C   99                GOTO E1B00
| 07-C                     END
|    C*
03===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
04===C           R1C00     BEGSR
|    C*
| 01-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     SETON                     31    *CAN INPUT
| 01*C                     ELSE                            *DEL, INQ
|   C                     SETOF                     31    *INP PROTECT
| 01-C                     END
|    C*
| 02-C           DOPT      IFEQ 1                          *ADD
|   C                     EXSR R1C10
| 02*C                     ELSE                            *U/D/I
|   C                     EXSR R1C20
| 02-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)----------------------------------*
| 03-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 5                          *INQ
|   C           *LOVAL    SETLLT40
| 03-C                     END
|    C*---------------------------------------------------------------*
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R1C10 ....'SC02' FIELD INIT : ADD (NO COPY)
     C*==============================================================*
05===C           R1C10     BEGSR
|    C*
|    C                     MOVEL$COR      DT403
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C                     MOVEL*BLANK    DT403N
|    C                     MOVEL*BLANK    DT404
|    C                     MOVEL*BLANK    DT404N
|    C*                    MOVEL*BLANK    DT405
|    C*                    MOVEL*BLANK    QJ02
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*  R1C20 ....'SC02' FIELD INIT : UPD, DEL, INQ, (ADD & COPY)
     C*==============================================================*
06===C           R1C20     BEGSR
|    C*
|    C                     MOVELT403      DT403
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C                     MOVEL*BLANKS   DT403N
|    C                     CALL 'PYSCM0'
|    C                     PARM DT403     CM001I  2         公司代號
|    C           DT403N    PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C                     MOVELT404      DT404
|    C                     MOVEL*BLANKS   DT404N
|    C           KEYEM     CHAINEM0                  40
|    C  N40                MOVELEM03      DT404N
|    C*                    MOVELT405      DT405
|    C*                    MOVEL*BLANKS   QJ02
|    C*          KEYQJ     CHAINQJ0                  40
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢
     C*==============================================================*
07===C           R1A00     BEGSR
|    C*
|    C* AUT CHECK
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM 'PYI205'  S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C   99                GOTO E1A00
|    C*
|    C                     CALL 'PYI205'
|    C           DT401     PARM DT401     P205I1  8
|    C           DT402     PARM DT402     P205I2  70
|    C                     PARM           @RTNC   2
|    C*
| 02-C           @RTNC     IFEQ '03'
|   C                     SETON                       03
| 02-C                     END
|    C*
07===C           E1A00     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
08===C           R2000     BEGSR
|    C*
|    C                     EXFMTDSPD2
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'                        *WINDOW
|   C                     EXSR R2E00
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 03-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 03-C                     END
|    C*
| 04-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     EXSR R2B00                      *SC02 CHK
| 04-C                     END
|    C*
| 05-C           *IN05     IFEQ '1'                        *CHECK ONLY
|   C                     GOTO E2000
| 05-C                     END
|    C*
| 06-C           *IN99     IFEQ '0'                        *CHK OK
|   C                     EXSR R2D00                      *FILE UPD
|   C                     MOVE 'SC01'    SCID
| 06-C                     END
|    C*
08===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....'SC02' CHECK
     C*==============================================================*
09===C           R2B00     BEGSR
|    C*公司別
|    C                     MOVEL*BLANK    DT403N
| 01-C           DT403     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO E2B00
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DT403     CM001I  2         公司代號
|    C           DT403N    PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 02-C           CM099O    IFEQ 'N'
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E2B00
|    C*
| 03-C           CM099O    IFEQ *BLANK
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 03-C                     END
|    C   99                GOTO E2B00
|    C*員工編號
|    C                     MOVEL*BLANKS   EM03
| 04-C           DT404     IFEQ *BLANKS
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 04-C                     END
|    C   99                GOTO E2B00
|    C*
|    C           KEYEM     CHAINEM0                  40
| 05-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 05-C                     END
|    C   99                GOTO E2B00
|    C*原因
|    C*                    MOVEL*BLANK    QJ02
|    C*          DT405     IFNE *BLANKS
|    C*          KEYQJ     CHAINQJ0                  40
|    C*          *IN40     IFEQ '1'
|    C*                    MOVEL'UPT2010' ERRID
|    C*                    MOVEL'PTMF'    ERRF
|    C*                    SETON                     6299
|    C*                    END
|    C*  99                GOTO E2B00
|    C*                    END
|    C*
09===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
10===C           R2D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D20
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*==============================================================*
11===C           R2D10     BEGSR
|    C*
|    C                     EXSR R2D11
|    C                     WRITET40
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
12===C           R2D11     BEGSR
|    C*
|    C                     MOVELDT401     T401
|    C                     Z-ADDWT402     T402
|    C                     MOVELDT403     T403
|    C                     MOVELDT404     T404
|    C                     MOVEL*BLANKS   T405
|    C                     MOVELEM31      T406
|    C*
|    C                     Z-ADD$A8YMD    T497
|    C                     TIME           T498
|    C                     MOVEL$USER     T499
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R2D20 ....FILE PROCESS : UPD
     C*==============================================================*
13===C           R2D20     BEGSR
|    C*
|    C                     EXSR R2D11
|    C                     UPDATT40
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS
     C*==============================================================*
14===C           R2D30     BEGSR
|    C*
|    C                     DELETT40
|    C*
14===C                     ENDSR
     C*==============================================================*
     C*          R2E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
15===C           R2E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*員工編號
| 01-C           #LIN      IFEQ 9
|   C           #COL      ANDGE13
|   C           #COL      ANDLE17
|   C*
|   C                     CALL 'PYI101'
|   C                     PARM DT403     P101I1  2         公司別
|   C           DT404     PARM DT404     P101I2  8         員工　
|   C                     PARM           @RTNC   2
| 01-C                     END
|    C*原因
|    C*          #LIN      IFEQ 10
|    C*          #COL      ANDGE13
|    C*          #COL      ANDLE14
|    C*
|    C*                    CALL 'PYI519'
|    C*          DT405     PARM DT405     I519I1  2
|    C*                    PARM           @RTNC   2
|    C*                    END
|    C*
15===C                     ENDSR
     C*==============================================================*
