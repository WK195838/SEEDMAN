     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : PTA110                                     *
     H*    PROGRAM NAME : 代碼欄位資料維護                         *
     H*    AUTHOR       : A1139 JANE                                 *
     H*    CREATE DATE  : OCT 27,92'                                 *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       : 泛太總帳                                 *
     F****************************************************************
     FPT#ZPF  UF  E           K        DISK                      A
     FPTA110D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E*---------------------------------------------------------------*
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      111 1160$CHYMD
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      152 161 $USERN
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I                                      203 2080DDATE
     I*DEFINE COPY KEY
     I*           DS
     I*                                       1   7 K##C
     I*                                       1   2 D##01C
     I*                                       3   7 D##02C
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEY#Z     KLIST                           *MAIN FILE
     C                     KFLD           D#Z01
     C*          KEY##C    KLIST                           *COPY REC.
     C*                    KFLD           D##01C
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     MOVE 'SC01'    SCID    4
|    C*
     C                     IN   PTDA01
     C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C*                    CALL 'P31'
     C*                    PARM $A8YMD    P3101I  80
     C*                    PARM '2'       P3102I  1
     C*                    PARM '1'       P3103I  1
     C*                    PARM $PFMT     P3104I  1
     C*                    PARM $PTYPE    P3105I  1
     C*          DDATE     PARM           P3101O  80
     C*
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     Z-ADD1         DOPT
|    C                     MOVEL*BLANK    D#Z01
|    C*--------------------------------------------------------------*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C*                    Z-ADD*ZERO     #LIN
|    C*                    Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 02-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 03-C           *IN99     IFEQ '0'                        *SCR CHK OK
|   C                     EXSR R1C00                      *PRE-SC02
|   C                     MOVEL'SC02'    SCID
| 03-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1A00     BEGSR
|    C*
|    C* AUT CHECK
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM 'PTI110'  S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C   99                GOTO E1A00
|    C*
|    C* CALL SUB-PROGRAM
|    C                     CALL 'PTI110'
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C           D#Z01     PARM D#Z01     P110I1 10
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     PARM           @RTNC   2
|    C* RETURN CODE CHECK
| 02-C           @RTNC     IFEQ '03'                       *PF3
|   C                     SETON                     03
| 02-C                     END
|    C*
03===C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
04===C           R1B00     BEGSR
|    C*
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*
|    C* DATA CHECK *--------------------------------------------------*
     C           D#Z01     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6099
     C                     END
     C   99                GOTO E1B00
     C*
|    C           KEY#Z     CHAIN#Z0                  40
|    C*
| 02-C           DOPT      IFEQ 1                          *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
| 03-C           DOPT      IFNE 1                          *UPD,DEL,INQ
|   C           *IN40     ANDEQ'1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
| 04-C           #Z05      IFNE 'Y'
| 05-C           DOPT      IFEQ 2
|   C           DOPT      OREQ 4
|   C                     MOVEL'UGL0001' ERRID
|   C                     MOVEL'GLMF'    ERRF
|   C                     SETON                     6099
| 05-C                     END
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
| 06-C*          DOPT      IFEQ 1                          *ADD
|   C*          K##C      ANDNE*BLANK                     *COPY
|   C*          KEY##C    CHAIN##0                  40
| 07-C*          *IN40     IFEQ '1'
|   C*                    MOVEL'UPT0060' ERRID
|   C*                    MOVEL'PTMF'    ERRF
|   C*                    SETON                     6299
| 07-C*                    END
| 06-C*                    END
|    C*  99                GOTO E1B00
|    C*
04===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
05===C           R1C00     BEGSR
|    C*
| 01-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     SETON                     31    *CAN INPUT
| 01*C                     ELSE                            *DEL, INQ
|   C                     SETOF                     31    *INP PROTECT
| 01-C                     END
|    C*
| 02-C           DOPT      IFEQ 1                          *ADD
|   C**         K##C      ANDEQ*BLANK                     *NO COPY
|   C                     EXSR R1C10
| 02*C                     ELSE                            *U/D/I
|   C                     EXSR R1C20
| 02-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)----------------------------------*
| 03-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 5                          *INQ
|   C           *LOVAL    SETLL#Z0
| 03-C                     END
|    C*---------------------------------------------------------------*
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1C10 ....'SC02' FIELD INIT : ADD (NO COPY)
     C*==============================================================*
06===C           R1C10     BEGSR
|    C*
|    C                     MOVEL*BLANKS   D#Z02
|    C                     Z-ADD0         D#Z03
|    C                     Z-ADD0         D#Z04
|    C*                    TIME           #Z97
|    C*                    Z-ADD$CHYMD    #Z98
|    C*                    MOVEL$USER     #Z99
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*  R1C20 ....'SC02' FIELD INIT : UPD, DEL, INQ, (ADD & COPY)
     C*==============================================================*
07===C           R1C20     BEGSR
|    C*
|    C                     MOVEL#Z02      D#Z02
|    C                     Z-ADD#Z03      D#Z03
|    C                     Z-ADD#Z04      D#Z04
|    C*
| 01-C*          DOPT      IFEQ 1                          *ADD & COPY
|   C*                    TIME           #Z97
|   C*                    Z-ADD$CHYMD    #Z98
|   C*                    MOVEL$USER     #Z99
| 01-C*                    END
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
08===C           R2000     BEGSR
|    C*
|    C                     EXFMTDSPD2
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C* RELEAS FILE --------------------------------------------------*
|   C           *LOVAL    SETLL#Z0
|   C*---------------------------------------------------------------*
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 03-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     EXSR R2B00                      *SC02 CHK
| 03-C                     END
|    C*
| 04-C           *IN99     IFEQ '0'                        *CHK OK
|   C                     EXSR R2D00                      *FILE UPD
|   C                     MOVE 'SC01'    SCID
| 04-C                     END
|    C*
08===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....'SC02' CHECK
     C*==============================================================*
09===C           R2B00     BEGSR
|    C*
| 01-C           D#Z02     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO E2B00
|    C*
| 02-C           D#Z03     IFLT 1
|   C           D#Z03     ORGT 10
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 02-C                     END
|    C   99                GOTO E2B00
|    C*
| 03-C           D#Z04     IFLT 1
|   C           D#Z04     ORGT 32
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 03-C                     END
|    C   99                GOTO E2B00
|    C*
09===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
10===C           R2D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D20
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*==============================================================*
11===C           R2D10     BEGSR
|    C*
|    C                     EXSR R2D11
|    C                     WRITE#Z0
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
12===C           R2D11     BEGSR
|    C*
|    C                     MOVELD#Z01     #Z01
|    C                     MOVELD#Z02     #Z02
|    C                     Z-ADDD#Z03     #Z03
|    C                     Z-ADDD#Z04     #Z04
| 01-C           DOPT      IFEQ 1
|   C                     MOVEL'Y'       #Z05
| 01-C                     END
|    C                     TIME           #Z97
|    C                     Z-ADD$A8YMD    #Z98
|    C                     MOVEL$USER     #Z99
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R2D20 ....FILE PROCESS : UPD
     C*==============================================================*
13===C           R2D20     BEGSR
|    C*
|    C                     EXSR R2D11
|    C                     UPDAT#Z0
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS
     C*==============================================================*
14===C           R2D30     BEGSR
|    C*
|    C                     DELET#Z0
|    C*
14===C                     ENDSR
     C*==============================================================*
