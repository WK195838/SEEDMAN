     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI110                                     *
     A*    PROGRAM NAME :全民健保資料查詢                          *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  :                                            *
     A*    UPDATE DATE  : 91/06/21 A1336 KLAUS                       *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     FPYE8PF  IF  E           K        DISK
     FPYEMPF  IF  E           K        DISK
     FPYI110D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    S#01       35  7               部門代號
     E*---------------------------------------------------------------*
     I*DATA AREA
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      203 2080DDATE
     I                                      152 161 $USERN
     I                                      203 2080DATE
     I*DEFIND STRAT KEY
     I           IDS
     I                                        1  14 @KE1O
     I           IDS
     I                                        1  14 @KE1N
     I                                        1   8 KE802
     I                                        9  140KE803
     I*           DS
     I*                                       1  14 @KE1N
     I*                                       1   8 DE802
     I*                                       1   8 E802
     I*                                       9  140DE803
     I            DS
     I                                        1   7 EM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     I            DS
     I                                        1 245 S#01
     I                                        1 245 CM202O
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C           *LIKE     DEFN E803      WE803
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DE801     PARM           P110I1  2         公司別
     C           KE802     PARM           P110I2  8         員工　
     C           KE803     PARM           P110I3  60        生效　
     C                     PARM           @RTNC   2          FUN KEY
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C*          KEYE1     KLIST                           *MAIN FILE
     C*                    KFLD           DE101
     C*                    KFLD           DE102
     C*                    KFLD           WE103
     C           KEYE8     KLIST                           *MAIN FILE
     C                     KFLD           DE801
     C                     KFLD           KE802
     C                     KFLD           WE803
     C           KEYEM     KLIST                           *MAIN FILE
     C                     KFLD           E801
     C                     KFLD           E802
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
  01-C           @RTNC     DOWEQ*BLANK
    C                     EXSR R1000                      *SCR PROCESS
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C*
|    C                     MOVEL*BLANK    @RTNC
|    C                     Z-ADD14        @PRCD            .PAGE SIZE
|    C*--------------------------------------------------------------*
|    C*檢核是否對全部的部門有權利並帶出管制的部門
|    C                     CALL 'PYSCM2'
|    C                     PARM DE801     CM201I  4        公司
|    C                     PARM           CM201O  1        權利
|    C                     PARM           CM202O           部門代號
|    C*　　　　　　　
|    C*--------------------------------------------------------------*
|    C                     EXSR R1A00                      .SFL INIT
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....SCREEN  PROCESS
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C**                   Z-ADD*ZERO     #LIN
|    C**                   Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     MOVEL'03'      @RTNC
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C                     MOVEL'12'      @RTNC
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN25     IFEQ '1'                        *NEXT PAGE
|   C                     EXSR R1N00
|   C                     GOTO E1000
| 03-C                     END
|    C*
| 04-C           @KE1O     IFNE @KE1N                      *START KEY
|   C                     EXSR R1A00                      *SFL INIT
|   C                     GOTO E1000
| 04-C                     END
|    C*
|    C                     EXSR R1C00                      *OPTION
|    C*
02===C           E1000     ENDSR
     C*=============================================================*
     C*          R1A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
03===C           R1A00     BEGSR
|    C*
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C                     MOVEL'N'       @BOTOM  1        *BOTTOM
|    C*
|    C                     SETOF                     51    *SFLDSPCTL
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
|    C                     EXSR R1A20                      *SFL HEAD
|    C   99                GOTO E1A00
|    C* <<SFL INITIAITAL DATA ADD>>
|    C                     MOVEL@KE1N     @KE1O            *KEY SAVE
|    C                     MOVE *BLANK    DOPT
|    C*
|    C                     CALL 'P31'
|    C                     PARM KE803     P3101I  80       (7.0)
|    C                     PARM '1'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C                     PARM           P3111O  80
|    C                     Z-ADDP3111O    WE803            (8.0)
|    C*
|    C           KEYE8     SETLLE80
|    C                     EXSR R1A10                      *ADD SFL DTA
|    C*
|    C*
| 01-C           RRN       IFGT 0
|   C                     SETON                     50    *SFLDSP
| 01*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C*
03===C           E1A00     ENDSR
     C*=============================================================*
     C*          R1A10 .... ADD SUBFILE DATA
     C*=============================================================*
04===C           R1A10     BEGSR
|    C*
|    C                     ADD  @PRCD     @DRCD
| 01-C           RRN       DOWLT@DRCD
|   C*
|   C           DE801     READEE80                      46
| 02-C           *IN46     IFEQ '0'
|   C*
|   C                     MOVEL*BLANK    EM03
|   C           KEYEM     CHAINPYEMPF               40
|   C*檢查部門權利
| 03-C           CM201O    IFNE 'Y'
|   C                     Z-ADD1         I       20
|   C           EM04      LOKUPS#01,I                   40
| 04-C           S#01,I    IFEQ *BLANK
|   C                     SETOF                         40
| 04-C                     END
|   C  N40                GOTO TAG1
| 03-C                     END
|   C*
|   C                     EXSR R1A11                      *WRITE SFL
| 02*C                     ELSE                            *EOF
|   C                     Z-ADDRRN       @DRCD
|   C                     MOVE 'Y'       @BOTOM
|   C                     SETON                     9653  *SFLEND
| 02-C                     END
|   C*
|   C           TAG1      TAG
| 01-C                     END
|    C*
|    C                     Z-ADDRRN       DRRN
|    C*
04===C                     ENDSR
     C*=============================================================*
     C*          R1A11 .... WRITE SUBFILE DATA
     C*=============================================================*
05===C           R1A11     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << SUBFILE DATA VALUE SET >>
|    C                     MOVELE802      DE802
|    C*
|    C*                    CALL 'P31'
|    C*                    PARM E803      P3101I  80
|    C*                    PARM '2'       P3102I  1
|    C*                    PARM '1'       P3103I  1
|    C*                    PARM DFMT      P3104I  1
|    C*                    PARM DTYPE     P3105I  1
|    C*          DE803     PARM           P3111O  80
     C                     Z-ADDE803      DE803
|    C*
|    C                     MOVE *BLANK    E804N
|    C*
| 01-C           E804      IFEQ '1'
|   C                     MOVEL'投保'  E804N
| 01*C                     ELSE
| 02-C           E804      IFEQ '2'
|   C                     MOVEL'調整'  E804N
| 02*C                     ELSE
| 03-C           E804      IFEQ '3'
|   C                     MOVEL'停保'  E804N
| 03*C                     ELSE
| 04-C           E804      IFEQ '4'
|   C                     MOVEL'復保'  E804N
| 04*C                     ELSE
| 05-C           E804      IFEQ '9'
|   C                     MOVEL'退保'  E804N
| 05-C                     END
| 04-C                     END
| 03-C                     END
| 02-C                     END
| 01-C                     END
|    C*
| 06-C           E804      IFEQ '5'
|   C                     MOVEL'轉出'  E804N
| 06-C                     END
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     ADD  1         RRN
|    C                     WRITESFLSR
|    C*
05===C                     ENDSR
     C*=============================================================*
     C*          R1A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
06===C           R1A20     BEGSR
|    C*
|    C* << CHACK 公司別NOT *BLANK >>
|    C                     MOVEL*BLANK    DE801N
| 01-C           DE801     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO E1A20
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DE801     CM001I  2         公司代號
|    C           DE801N    PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 02-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 02-C                     END
|    C   99                GOTO E1A20
|    C*
| 03-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 03-C                     END
|    C   99                GOTO E1A20
|    C*
06===C           E1A20     ENDSR
     C*=============================================================*
     C*          R1C00 ....挑選資料
     C*=============================================================*
07===C           R1C00     BEGSR
|    C*
|    C                     READCSFLSR                  4242
| 01-C           *IN42     DOWEQ'0'
|   C*
| 02-C           DOPT      IFNE *BLANK
|   C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
|   C                     MOVELDE801     P110I1
|   C                     MOVELDE802     P110I2
|    C                     CALL 'P31'
|    C                     PARM DE803     P3101I  80
|    C                     PARM '1'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           P110I3    PARM           P3111O  80
|   C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|   C                     MOVEL'00'      @RTNC
|   C                     SETON                     42
| 02-C                     END
|   C*
|   C  N42                READCSFLSR                    42
| 01-C                     END
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
     C**         R1E00     BEGSR
     C*
     C*          #CSR      DIV  256       #LIN             *LINE
     C*                    MVR            #COL             *COLUMN
     C*
     C* SFL POSITION KEEP-------------------------------------------*
     C*                    Z-ADD#DRRN     DRRN
     C*-------------------------------------------------------------*
     C**                   ENDSR
     C*=============================================================*
     C*          R1N00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
08===C           R1N00     BEGSR
|    C*
| 01-C           @BOTOM    IFEQ 'Y'                        *EOF
|   C*                    SETON                         96*LST PAG
|   C                     MOVEL'CPF5203' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
|   C                     Z-ADDRRN       DRRN
| 01*C                     ELSE
|   C                     EXSR R1A10                      *ADD SFL DTA
| 01-C                     END
|    C*
08===C                     ENDSR
     C*==============================================================*
