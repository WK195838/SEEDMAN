     H****************************************************************
     H*            泛太資訊科技開發股份有限公司－版權所有          *
     H*--------------------------------------------------------------*
     H*    PROGRAM NAME : PYA301                                     *
     H*    AUTHOR       : A1046  JOHNSON CHEN                        *
     H*    CREATE DATE  : 79/05/31                                   *
     H*    UPDATE DATE  : 91/06/19 A1336 KLAUS                       *
     H*    SYSTEM       : MIRS                                       *
     H*    SUBSYSTEM    : PY                                         *
     H****************************************************************
     FPYPYPF  UF  E           K        DISK                      A
     FPYPDPF  UF  E           K        DISK                      A
     FPYEMPF  IF  E           K        DISK
     FPYPMPF  IF  E           K        DISK
     FSYBNPF  IF  E           K        DISK
     FSYCMPF  IF  E           K        DISK
     FPYSSLF01IF  E           K        DISK
     FPYA3011DCF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    WFUN    5   5  6               作業方式
     E                    WA         16  2               檢查SFL資料是否重復
     E*---------------------------------------------------------------*
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*
     I            DS
     I                                        1   7 EM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     I            DS
     I                                        1   7 DPY04
     I                                        1   3 DPY04H
     I                                        4   7 DPY04D
     I*
     I           UDS
     I                                      101 110 $USER
     I                                      187 1920DDATE
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I                                      210 2160DATE
     I/COPY PYDDSSRC,PYPMDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN SS09      KSS09            項目屬性
     C           *LIKE     DEFN SS05      KSS05            項目別
     C           *LIKE     DEFN SS18      KSS18            薪資組別
     C           *LIKE     DEFN SS01      KSS01            薪資代碼
     C           *LIKE     DEFN PD03      KPD03            薪資項目
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN RRN       @LRRN            LAST REC NUM
     C           *LIKE     DEFN RRN       @LPAGE           LAST PAGE
     C           *LIKE     DEFN RRN       @PRRN            PROMPT USE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           DPY01            公司別
     C                     KFLD           DPY02            員工編號
     C           KEYPM     KLIST
     C                     KFLD           PM01             薪資類別
     C                     KFLD           DPY01            公司別
     C           KEYSS     KLIST                           薪資結構
     C                     KFLD           KSS09            項目屬性
     C                     KFLD           KSS05            項目別
     C                     KFLD           KSS18            薪資組別
     C                     KFLD           KSS01            薪資代碼
     C           KEYSS1    KLIST                           *PYSSPF
     C                     KFLD           KSS09            項目屬性
     C                     KFLD           KSS05            項目別
     C                     KFLD           KSS18            薪資組別
     C           KEYPY     KLIST                           *PYPYPF
     C                     KFLD           DPY01            公司別
     C                     KFLD           DPY02            員工別
     C           KEYPD     KLIST                           *PYPDPF
     C                     KFLD           DPY01            公司別
     C                     KFLD           DPY02            員工別
     C                     KFLD           KPD03            薪資項目
     C           KEYBN     KLIST                           *PYBNPF
     C                     KFLD           DPY04H           總行
     C                     KFLD           DPY04D           分行
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     MOVEL'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C                     MOVEL'1'       KSS09
|    C                     MOVEL'1'       KSS05
|    C*
|    C                     MOVEL*BLANK    DPY02
|    C                     Z-ADD1         DOPT
|    C*
|    C                     MOVEL$COR      DPY01
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'
|   C                     EXSR R1E00
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 03-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 04-C           *IN99     IFEQ '0'
|   C                     EXSR R1C00
|   C                     MOVEL'SC02'    SCID             *SCR2
| 04-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1A00     BEGSR
|    C* AUT CHECK
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM 'PYI301'  S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C   99                GOTO E1A00
|    C*
|    C                     CALL 'PYI301'
|    C*----------------------------------------------------------------
|    C                     PARM DPY01     I301I1  2         公司編號
|    C           DPY02     PARM DPY02     I301I2  8         員工編號
|    C*----------------------------------------------------------------
|    C                     PARM           @RTNC   2          FUN KEY
|    C*
| 02-C           @RTNC     IFEQ '03'
|   C                     SETON                     03
| 02-C                     END
|    C*
03===C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 ....SCREEN CHECK
     C*==============================================================*
04===C           R1B00     BEGSR
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C* << CHACK 公司別NOT *BLANK >>
|    C                     MOVEL*BLANK    DPY01N
| 02-C           DPY01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DPY01     CM001I  2         公司代號
|    C           DPY01N    PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 03-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
| 04-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*+<<DATA CHECK>>+++++++++++++++++++++++++++++++++++++++++++++++++
|    C*帶薪資組別
|    C           DPY01     CHAINCM0                  40
|    C                     MOVELCM13      KSS18
|    C*上期薪資處理設Ｙ
|    C                     MOVEL'01'      PM01
|    C                     MOVEL*BLANK    PM207
|    C           KEYPM     CHAINPM0                  40
|    C   40                MOVEL'Y'       PM207
|    C*員工檢核
| 05-C           DPY02     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 05-C                     END
|    C   99                GOTO E1B00
|    C*員工檢核
|    C           KEYEM     CHAINEM0                  40
| 06-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 06-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*部門別
|    C                     CALL 'PYSCM1'
|    C                     PARM EM01      CM101I  2         公司代號
|    C                     PARM EM04      CM102I  7         部門代號
|    C                     PARM           CM103O 14         部門名稱
|    C                     PARM           CM199O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 07-C           CM199O    IFEQ 'N'
|   C                     SETON                     6199
|   C                     MOVEL'US#0098' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 07-C                     END
|    C   99                GOTO E1B00
|    C*
| 08-C           CM199O    IFEQ *BLANK
|   C                     SETON                     606199
|   C                     MOVEL'US#1111' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 08-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*是否離職
| 09-C           DOPT      IFEQ 1
|   C           EM11      ANDEQ'Y'
|   C                     MOVEL'UMI0400' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     616099
| 09-C                     END
|    C   99                GOTO E1B00
|    C*----------------------------------------------------------------
|    C           KEYPY     CHAINPY0                  40    *NF
| 10-C           DOPT      IFEQ 1                          *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 10-C                     END
|    C   99                GOTO E1B00
|    C*
| 11-C           DOPT      IFNE 1                          *UPD,DEL,INQ
|   C           *IN40     ANDEQ'1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 11-C                     END
|    C   99                GOTO E1B00
|    C*
04===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....SCREEN 2, 3 PREPARE
     C*==============================================================*
05===C           R1C00     BEGSR
|    C*
|    C                     MOVE WFUN,DOPT DFUN
|    C                     EXSR R1CC0                      *INIT SFL
|    C                     SETOF                     31
|    C* ADD
| 01-C           DOPT      IFEQ 1
|   C                     SETON                     31
|   C                     EXSR R1CA0                      *INIT HEAD
|   C                     EXSR R1CD0                      *INIT DETAIL
| 01-C                     END
|    C* UPD
| 02-C           DOPT      IFEQ 2
|   C                     SETON                     31
|   C                     EXSR R1CB0                      *HEADING
|   C                     EXSR R1CE0                      *READ DETAIL
|   C                     EXSR R1CD0                      *INIT DETAIL
| 02-C                     END
|    C* DEL & INQ
| 03-C           DOPT      IFEQ 4
|   C           DOPT      OREQ 5
|   C                     EXSR R1CB0                      *HEADING
|   C                     EXSR R1CE0                      *READ DETAIL
|   C                     SETON                     53    *SFLEND
| 03-C                     END
|    C*SFLDSP
| 04-C           @LRRN     IFGT 0
|   C                     Z-ADD1         DRRN
|   C                     SETON                     50    *SFLDSP
| 04*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)++++++++++++++++++++++++++++++++++*
| 05-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 5
|   C           *LOVAL    SETLLPY0
|   C           *LOVAL    SETLLPD0
| 05-C                     END
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1CA0 ....SFL HEAD INIT : ADD (NO COPY)
     C*==============================================================*
06===C           R1CA0     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << ADD : SUBFILE HEAD DATA INITAILIZE>>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD*ZERO     DPY03
|    C                     MOVEL*BLANK    DPY04
|    C                     MOVEL*BLANK    BN04
|    C                     MOVEL*BLANK    DPY06
|    C                     Z-ADD*ZERO     DPY07
|    C                     Z-ADD*ZERO     DPY08
|    C                     Z-ADD*ZERO     DSUM
|    C                     MOVEL'1'       DPY09
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R1CB0 ....SFL HEAD INIT : U/D/I/A-COPY
     C*==============================================================*
07===C           R1CB0     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << U/D/I AND ADD-COPY : SUBFILE HEAD DATA INITAILIZE>>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
|    C                     Z-ADDPY03      DPY03
|    C                     Z-ADDPY03      DSUM
|    C                     MOVELPY04      DPY04H
|    C                     MOVELPY05      DPY04D
|    C           KEYBN     CHAINBN0                  40
|    C                     MOVELPY06      DPY06
|    C                     Z-ADDPY07      DPY07
|    C                     Z-ADDPY08      DPY08
|    C                     MOVELPY09      DPY09
|    C*
07===C                     ENDSR
     C*=============================================================*
     C*          R1CC0 ....SUBFILE INIT
     C*=============================================================*
08===C           R1CC0     BEGSR
|    C* SFL SETTING ++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD14        @PRCD            *PGE RCD
|    C                     Z-ADD12        @SFTOP  20       *SFL TOP LIN
|    C                     Z-ADD18        @SFBOT  20       *SFL BOT LIN
|    C                     Z-ADD1         @LNUM   20       *明細行數
|    C* ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @LRRN            *LAST RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C*
|    C                     SETOF                     51    *SFLDSPCTL
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
08===C                     ENDSR
     C*==============================================================*
     C*          R1CD0 ....SFL DATA INIT
     C*==============================================================*
09===C           R1CD0     BEGSR
|    C*
|    C                     SETOF                     54    *54 FOR BLANK LOOP
|    C                     Z-ADD@LRRN     RRN
|    C*
|    C           @LRRN     DIV  @PRCD     @LPAGE
|    C                     ADD  1         @LPAGE
|    C           @LPAGE    MULT @PRCD     @DRCD
|    C*新增時由薪資結構中自動帶出資料
| 01-C           DOPT      IFEQ 1
|   C                     MOVEL*BLANK    KSS01
|   C           KEYSS     SETLLSS0
|   C           KEYSS1    READESS0                      42
| 01*C                     ELSE
|   C                     SETON                     42
| 01-C                     END
|    C*
| 02-C           RRN       DOWLT@DRCD
|   C                     EXSR R1CD1                      *INIT SFLDTA
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
|   C  N42      KEYSS1    READESS0                      42
| 02-C                     END
|    C                     SETON                     5053
|    C*
|    C                     Z-ADDRRN       @LRRN
|    C                     Z-ADDRRN       DRRN
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*          R1CD1 ....SFL INIT DATA ADD
     C*==============================================================*
10===C           R1CD1     BEGSR
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
| 01-C           *IN42     IFEQ '0'                        .SS OK
|   C                     SETON                     54    *54 FOR BLANK LOOP
| 02-C           SS05      IFEQ '1'
|   C           SS06      ANDEQ'A'
|   C*固定項目固定金額之薪資，只能顯示，不可修改。
|   C                     SETOF                     31                K LOOP
|   C                     MOVELSS01      DPD03
|   C                     Z-ADDSS07      DPD04
| 02*C                     ELSE
|   C                     SETON                     31                K LOOP
|   C                     MOVELSS01      DPD03
|   C                     Z-ADDSS07      DPD04
| 02-C                     END
|   C*
| 01*C                     ELSE
|   C                     SETOF                     54    *54 FOR BLANK LOOP
|   C                     SETON                     31                K LOOP
|   C                     MOVEL*BLANK    DPD03
|   C                     MOVEL*BLANK    SS02
|   C                     MOVEL*BLANKS   SS06
|   C                     Z-ADD*ZERO     DPD04
| 01-C                     END
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          R1CE0 ....DETAIL DATA TO SFL
     C*==============================================================*
11===C           R1CE0     BEGSR
|    C*
|    C                     SETON                     54    *54
|    C*
|    C                     MOVEL*BLANK    KPD03
|    C           KEYPD     SETLLPD0
|    C           KEYPY     READEPD0                      44*NF/EOF
|    C*
| 01-C           *IN44     DOWEQ'0'
|   C                     EXSR R1CE1
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
|   C           KEYPY     READEPD0                      44*NF/EOF
| 01-C                     END
|    C*
|    C                     SETON                     53
|    C                     Z-ADDRRN       @LRRN
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          R1CE1 ....DETAIL DATA TO SFL
     C*==============================================================*
12===C           R1CE1     BEGSR
|    C*
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
|   C                     SETON                     31
|    C                     MOVELPD03      DPD03
|    C                     MOVEL*BLANK    SS02
|    C                     MOVEL*BLANK    SS06
|    C                     Z-ADDPD04      DPD04
|    C                     MOVELDPD03     KSS01
|    C           KEYSS     CHAINSS0                  40
| 01-C  N40      SS04      IFEQ 'B'
|   C*薪資總額不扣除固定項目扣款金額
| 02-C           SS05      IFNE '1'
|   C                     SUB  PD04      DSUM
| 02-C                     END
| 01*C                     ELSE
| 03-C           SS05      IFEQ '1'
|   C           SS06      ANDEQ'A'
|   C                     SETOF                     31
|   C                     ADD  PD04      DSUM
| 03-C                     END
| 01-C                     END
|    C*
|    C* << OTHER FILE FIELD TO SFLSR >>
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
|    0*                    MOVELDPD03     KSS01
|    C*          KEYSS     CHAINSS0                  40
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R1E00 ....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
13===C           R1E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*員工編號DPY02
| 01-C           #LIN      IFEQ 5
|   C           #COL      ANDGE13
|   C           #COL      ANDLE17
|   C                     CALL 'PYI101'
|   C                     PARM DPY01     P101I1  2         公司別
|   C           DPY02     PARM DPY02     P101I2  8         員工　
|   C                     PARM           @RTNC   2          FUN KEY
| 01-C                     END
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          R2000 ....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
14===C           R2000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'
| 03-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C                     EXSR R2E00                      *CHK SCR
| 03-C                     END
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 04-C           *IN12     IFEQ '1'
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 04-C                     END
|    C*
| 05-C           *IN05     IFEQ '1'
|   C                     EXSR R2B00                      *CHK SCR
|   C                     GOTO E2000
| 05-C                     END
|    C*
| 06-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C                     EXSR R2B00                      *CHK SCR
| 06-C                     END
|    C*
| 07-C           *IN99     IFEQ '0'
|   C**         *IN13     ANDEQ'1'                         SAVE
|   C                     EXSR R2D00                      *FILE UPD
|   C                     MOVE 'SC01'    SCID
| 07-C                     END
|    C*
14===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....CHECK 'SC02' --- SFLSR
     C*==============================================================*
15===C           R2B00     BEGSR
|    C*
|    C*CHECK HEADING
|    C                     MOVEL*BLANK    BN04
| 01-C           DPY04     IFNE *BLANK
|   C           KEYBN     CHAINBN0                  40
| 02-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 02-C                     END
| 01-C                     END
|    C   99                GOTO E2B00
|    C*
| 03-C           DPY04     IFNE *BLANK
|   C           DPY06     ANDEQ*BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 03-C                     END
|    C   99                GOTO E2B00
|    C*CHG BY JADI BEGIN
|    C*          DPY03     IFEQ *ZERO
|    C*                    MOVEL'UPT0010' ERRID
|    C*                    MOVEL'PTMF'    ERRF
|    C*                    SETON                     6499
|    C*                    END
|    C*  99                GOTO E2B00
|    C*
| 04-C           DPY07     IFNE 99
| 05-C           DPY07     IFLT 1
|   C           DPY07     ORGT 30
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6599
| 05-C                     END
| 04-C                     END
|    C   99                GOTO E2B00
|    C*
| 06-C           PM207     IFEQ 'Y'
|   C           DPY08     ANDEQ*ZERO
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6699
| 06-C                     END
|    C   99                GOTO E2B00
|    C*
| 07-C           DPY08     IFNE 99
|   C           DPY08     ANDNE0
| 08-C           DPY08     IFLT 1
|   C           DPY08     ORGT 30
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6699
| 08-C                     END
| 07-C                     END
|    C   99                GOTO E2B00
|    C*
|    C                     Z-ADDDPY03     DSUM
|    C*CHK DETAIL
|    C                     MOVEL*BLANK    WA
|    C*
|    C                     SETON                     54
|    C                     READCSFLSR                  5757
|    C*
| 09-C           *IN57     DOWEQ'0'
|   C           *IN99     ANDEQ'0'
|   C                     EXSR R2B10
|   C                     UPDATSFLSR
|   C*
| 10-C           *IN99     IFEQ '1'
|   C                     Z-ADDRRN       DRRN
| 10*C                     ELSE
|   C                     READCSFLSR                    57
| 10-C                     END
|   C*
| 09-C                     END
|    C   99                GOTO E2B00
|    C*
15===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2B10 ....'SC02' CHECK - SFL DETAIL
     C*==============================================================*
16===C           R2B10     BEGSR
|    C*
|    C                     SETOF                     6061
|    C*
| 01-C           DPD03     IFEQ *BLANK
|   C                     MOVEL*BLANK    SS02
|   C                     MOVEL*BLANK    SS06
| 02-C           DPD04     IFNE *ZERO
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 02-C                     END
|   C                     GOTO E2B10
| 01-C                     END
|    C*
|    C                     MOVELDPD03     KSS01
|    C           KEYSS     CHAINSS0                  40
| 03-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 03-C                     END
|    C   99                GOTO E2B10
|    C*
|    C                     Z-ADD1         WI      20
|    C           DPD03     LOKUPWA,WI                    40
| 04-C           *IN40     IFEQ '1'
|   C                     MOVEL'UMI0012' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6099
| 04-C                     END
|    C   99                GOTO E2B10
|    C*
|    C                     Z-ADD1         WI      20
|    C           *BLANK    LOKUPWA,WI                    40
|    C   40                MOVELDPD03     WA,WI
|    C*
|    C*          DPD04     IFEQ *ZERO
|    C*CHG BY JADI
|    C*          SS06      IFEQ 'A'
|    C*                    Z-ADDSS07      DPD04
|    C*                    END
|    C*          SS06      IFEQ 'B'
|    C*          DPY03     MULT SS08      DPD04     H
|    C*                    END
|    C*                    END
|    C*
|    C*          DPD04     IFEQ *ZERO
|    C*                    MOVEL'UPT0010' ERRID
|    C*                    MOVEL'PTMF'    ERRF
|    C*                    SETON                     6199
|    C*                    END
|    C*  99                GOTO E2B10
|    C*END CHG
|    C*                    ADD  DPD04     DSUM
|    C                     MOVELDPD03     KSS01
|    C           KEYSS     CHAINSS0                  40
| 05-C  N40      SS04      IFEQ 'B'
|   C*薪資總額不扣除固定項目扣款金額
| 06-C           SS05      IFNE '1'
|   C                     SUB  DPD04     DSUM
| 06-C                     END
| 05*C                     ELSE
|   C                     ADD  DPD04     DSUM
| 05-C                     END
|    C*
16===C           E2B10     ENDSR
     C*==============================================================*
     C*          R2E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
17===C           R2E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* <<SFL HEAD FIELD>>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*銀行代號
| 01-C           #LIN      IFEQ 4
|   C           #COL      ANDGE55
|   C           #COL      ANDLE61
|   C                     CALL 'PYI516'
|   C           DPY04     PARM DPY04     I516I1  7         銀行代碼
|   C                     PARM           @RTNC   2
|   C                     GOTO E2E00
| 01-C                     END
|    C*
|    C* SFL DETAIL
| 02-C           #LIN      IFLT @SFTOP
|   C           #LIN      ORGT @SFBOT
|   C                     GOTO E2E00
| 02-C                     END
|    C* GET SFL REC
|    C           #LIN      SUB  @SFTOP    @PRRN
|    C           @PRRN     DIV  @LNUM     @PRRN
|    C                     MVR            @LINE   20
|    C*
| 03-C           #COL      IFGE 47
|   C           #COL      ANDLE58
|   C                     ADD  7         @PRRN
| 03-C                     END
|    C*
|    C                     ADD  1         @LINE            .明細行
|    C                     ADD  #DRRN     @PRRN            .PROMPT RRN
|    C           @PRRN     CHAINSFLSR                40
|    C   40                GOTO E2E00
|    C*
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << SFLSR FIELD >>
| 04-C           @LINE     IFEQ 1
|   C           #COL      ANDGE6
|   C           #COL      ANDLE7
|   C                     CALL 'PYI503'
|   C                     PARM KSS18     I503I1  2         薪資組別
|   C           DPD03     PARM DPD03     I503I2  2         薪資代碼
|   C                     PARM           @RTNC   2
| 04-C                     END
|    C*
| 05-C           @LINE     IFEQ 1
|   C           #COL      ANDGE47
|   C           #COL      ANDLE48
|   C                     CALL 'PYI503'
|   C                     PARM KSS18     I503I1  2         薪資組別
|   C           DPD03     PARM DPD03     I503I2  2         薪資代碼
|   C                     PARM           @RTNC   2
| 05-C                     END
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* UPD SFL
|    C                     SETON                     54
|    C                     UPDATSFLSR
|    C*
17===C           E2E00     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
18===C           R2D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D20
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|    C*
18===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*==============================================================*
19===C           R2D10     BEGSR
|    C* DETAIL
|    C                     READCSFLSR                    57
|    C*
| 01-C           *IN57     DOWEQ'0'
| 02-C           DPD03     IFNE *BLANK
|   C                     EXSR R2D12
|   C                     WRITEPD0
| 02-C                     END
|   C                     READCSFLSR                    57
| 01-C                     END
|    C* HEAD
|    C                     EXSR R2D11
|    C                     WRITEPY0
|    C*
19===C                     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
20===C           R2D11     BEGSR
|    C*
|    C                     MOVELDPY01     PY01
|    C                     MOVELDPY02     PY02
|    C                     Z-ADDDPY03     PY03
|    C                     MOVELDPY04H    PY04
|    C                     MOVELDPY04D    PY05
|    C                     MOVELDPY06     PY06
|    C                     Z-ADDDPY07     PY07
|    C                     Z-ADDDPY08     PY08
|    C                     MOVELDPY09     PY09
|    C**
|    C                     Z-ADD$A8YMD    PY97
|    C                     TIME           PY98
|    C                     MOVEL$USER     PY99
|    C*
20===C                     ENDSR
     C*==============================================================*
     C*          R2D12 ....FILE PROCESS : ADD  (ADD NEW)
     C*==============================================================*
21===C           R2D12     BEGSR
|    C*
|    C                     MOVELDPY01     PD01
|    C                     MOVELDPY02     PD02
|    C                     MOVELDPD03     PD03
|    C                     Z-ADDDPD04     PD04
|    C*
21===C                     ENDSR
     C*==============================================================*
     C*          R2D20 ....FILE PROCESS : UPD
     C*==============================================================*
22===C           R2D20     BEGSR
|    C* DETAIL
|    C                     EXSR R2D31
|    C*
|    C                     READCSFLSR                  5757
| 01-C           *IN57     DOWEQ'0'
|   C*
| 02-C           DPD03     IFNE *BLANK
|   C                     EXSR R2D12
|   C                     WRITEPD0
| 02-C                     END
|   C*
|   C                     READCSFLSR                    57
| 01-C                     END
|    C*
|    C                     EXSR R2D11
|    C                     UPDATPY0
|    C*
22===C                     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS
     C*==============================================================*
23===C           R2D30     BEGSR
|    C* DETAIL
|    C                     EXSR R2D31
|    C*
|    C                     DELETPY0
|    C*
23===C                     ENDSR
     C*==============================================================*
     C*          R2D31 ....FILE PROCESS
     C*==============================================================*
24===C           R2D31     BEGSR
|    C* DETAIL
|    C                     MOVEL*BLANK    KPD03
|    C           KEYPD     SETLLPD0
|    C           KEYPY     READEPD0                      40
| 01-C           *IN40     DOWEQ'0'
|   C                     DELETPD0
|   C           KEYPY     READEPD0                      40
| 01-C                     END
|    C*
24===C                     ENDSR
     C*==============================================================*
**
新增修改      刪除查詢
