     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    PROGRAM-ID   : PYA223                                     *
     H*    PROGRAM NAME :員工獎懲資料建立                          *
     H*    AUTHOR       : A1336 KLAUS                                *
     H*    CREATE DATE  : 91/07/15                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :人事薪資系統                              *
     H****************************************************************
     FPYXXPF  UF  E           K        DISK                      A
     FPYEMPF  IF  E           K        DISK
     FPYDPPF  IF  E           K        DISK
     FPYA223D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    WFUN    5   5  6
     E*---------------------------------------------------------------*
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I           UDS
     I                                      101 110 $USER
     I                                      211 2160DDATE
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I                                      201 2040YEAR
     I*
     I            DS
     I                                        1   7 XX02
     I                                        1   1 XX021
     I                                        2   2 XX022
     I                                        3   3 XX023
     I                                        4   7 XX024
     I            DS
     I                                        1   7 EM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     I/COPY PYDDSSRC,PYPMDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C*          *LIKE     DEFN EM86      WEM86
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           DXX01
     C                     KFLD           DXX04
     C           KEYXX     KLIST                           *PYEMPF
     C                     KFLD           DXX01
     C                     KFLD           DXX04
     C                     KFLD           WXX05
     C                     KFLD           DXX06
     C           KEYDP     KLIST
     C                     KFLD           EM041
     C                     KFLD           EM042
     C                     KFLD           EM043
     C                     KFLD           EM044
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     MOVE 'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     MOVEL*BLANKS   DXX01
|    C                     MOVEL*BLANKS   DXX04
|    C                     Z-ADDDDATE     DXX05
|    C                     Z-ADD0         WXX05
|    C                     MOVEL*BLANK    DXX06
|    C                     MOVEL*BLANK    XX06N
|    C                     Z-ADD1         DOPT
|    C*------------------------------------------------------------****
|    C*公司別
|    C                     MOVEL$COR      DXX01
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'
|   C                     EXSR R1E00
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 03-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 04-C           *IN99     IFEQ '0'
|   C                     EXSR R1C00
|   C                     MOVEL'SC02'    SCID             *SCR2
| 04-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1A00     BEGSR
|    C*
|    C* AUT CHECK
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM 'PYI223'  S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C   99                GOTO E1A00
|    C*
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* CALL SUB-PROGRAM
|    C                     CALL 'PYI223'
|    C           DXX01     PARM DXX01     I223I1  2         公司編號
|    C           DXX04     PARM DXX04     I223I2  8         員工編號
|    C           DXX05     PARM DXX05     I223I3  70        獎懲日期
|    C           DXX06     PARM DXX06     I223I4  2         獎懲代碼
|    C           XX06N     PARM XX06N     I223I5  9         獎懲名稱
|    C                     PARM           @RTNC   2
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* RETURN CODE CHECK
| 02-C           @RTNC     IFEQ '03'                       *PF3
|   C                     SETON                     03
| 02-C                     END
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
03===C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 ....SCREEN CHECK
     C*==============================================================*
04===C           R1B00     BEGSR
|    C*
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*公司別
| 02-C           DXX01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DXX01     CM001I  2         公司代號
|    C           XX01N     PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C                     PARM           CM004O  1         日期格式
|    C                     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C                     MOVE CM004O    DFMT    1
|    C                     MOVE CM005O    DTYPE   1
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 03-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
| 04-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*員工編號
| 05-C           DXX04     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 05-C                     END
|    C   99                GOTO E1B00
|    C*
|    C           KEYEM     CHAINEM0                  40    *NF
| 06-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 06*C                     ELSE
|   C                     MOVELEM03      XX04N
| 06-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*生效日期
| 07-C           DXX05     IFEQ 0
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 07*C                     ELSE
|   C                     CALL 'P30'
|   C                     PARM DXX05     P3001I  80
|   C                     PARM DFMT      P3002I  1
|   C                     PARM DTYPE     P3003I  1
|   C                     PARM           P3011O  1
| 08-C           P3011O    IFEQ 'N'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 08-C                     END
| 07-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'P31'
|    C                     PARM DXX05     P3101I  80
|    C                     PARM DFMT      P3102I  1
|    C                     PARM DTYPE     P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C                     PARM           P3101O  80
|    C                     Z-ADDP3101O    WXX05   80
|    C*獎懲代碼
| 09-C           DXX06     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 09-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*---------------------------------------------------------------
|    C           KEYXX     CHAINXX0                  40    *NF
| 10-C           DOPT      IFEQ 1                          *ADD
| 11-C           *IN40     IFEQ '0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     6399
| 11-C                     END
| 10-C                     END
|    C   99                GOTO E1B00
|    C*
| 12-C           DOPT      IFNE 1                          *UPD,DEL,INQ
| 13-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     6399
| 13-C                     END
| 12-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*
04===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....MOVE  SCREEN TO SCID
     C*==============================================================*
05===C           R1C00     BEGSR
|    C*
|    C                     MOVE WFUN,DOPT DFUN
| 01-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C                     SETON                     31    *IN CONTROL
| 01*C                     ELSE
|   C                     SETOF                     31    *IN CONTROL
| 01-C                     END
|    C*
| 02-C           DOPT      IFEQ 1
|   C                     EXSR R1C10
| 02*C                     ELSE
|   C                     EXSR R1C20
| 02-C                     END
|    C* RELEASE FILE (ADD & INQUIRY)++++++++++++++++++++++++++++++++++*
| 03-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 5                          *INQ
|   C           *LOVAL    SETLLXX0                        *HEAD FILE
| 03-C                     END
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1C10 ....CLEAR SCREEN VARIABLES
     C*==============================================================*
06===C           R1C10     BEGSR
|    C*
|    C                     MOVEL*BLANK    DXX09
|    C                     Z-ADD*ZERO     DXX08
|    C                     MOVEL*BLANK    DXX07
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R1C20 ....MOVE DATA TO SCREEN
     C*==============================================================*
07===C           R1C20     BEGSR
|    C*
|    C                     MOVELXX09      DXX09
|    C                     Z-ADDXX08      DXX08
|    C                     MOVELXX07      DXX07
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
08===C           R1E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*--------------------------------------------------------------**
|    C*員工編號
| 01-C           #LIN      IFEQ 5
|   C           #COL      ANDGE13
|   C           #COL      ANDLE20
|   C                     CALL 'PYI101'
|   C                     PARM DXX01     P101I1  2         公司別
|   C           DXX04     PARM DXX04     P101I2  8         員工　
|   C                     PARM           @RTNC   2          FUN KEY
| 01-C                     END
|    C*--------------------------------------------------------------**
|    C*獎懲代碼DXX06
| 02-C           #FLD      IFEQ 'DXX06'
|   C                     CALL 'TRW001'
|   C                     PARM 'XX06'    TR101I 10        調動類別
|   C           DXX06     PARM DXX06     TR102I 10
|   C                     PARM           TR103I 32
|   C                     PARM           TR104I  2
|   C*
|   C                     MOVEL*BLANK    XX06N  10 P
| 03-C           TR103I    IFNE *BLANK
|   C                     CALL 'P65'
|   C                     PARM TR103I    P6501I 80
|   C                     PARM 10        P6502I  30
|   C           XX06N     PARM           P6511O 80        類別說明
|   C                     PARM           P6512O 80
| 03-C                     END
| 02-C                     END
|    C*
08===C           E1E00     ENDSR
     C*==============================================================*
     C*          R2000  ...'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
09===C           R2000     BEGSR
|    C*
|    C                     EXFMTDSPD2
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 03-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C                     EXSR R2B00                      *SCR CHK
| 03-C                     END
|    C*
| 04-C           *IN99     IFEQ '0'
|   C                     EXSR R2D00                      *PROCESS
|   C                     MOVEL'SC01'    SCID             *SCR2
| 04-C                     END
|    C*
09===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....CHECK 'SC02' SCREEN
     C*==============================================================*
10===C           R2B00     BEGSR
|    C*獎懲次數
| 01-C           DXX08     IFEQ *ZERO
|   C                     MOVEL'UPT0012' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6599
| 01-C                     END
|    C   99                GOTO E2B00
|    C*
10===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
11===C           R2D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D20
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*==============================================================*
12===C           R2D10     BEGSR
|    C*MOVE  DATA
|    C                     EXSR R2D11
|    C                     WRITEXX0
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE (XX0)
     C*==============================================================*
13===C           R2D11     BEGSR
|    C*
|    C                     MOVELDXX01     XX01
|    C                     MOVELEM04      XX02
|    C           KEYDP     CHAINDP0                  40
|    C  N40                MOVELDP02      XX03
|    C                     MOVELDXX04     XX04
|    C                     Z-ADDWXX05     XX05
|    C                     MOVELDXX06     XX06
|    C                     MOVELDXX07     XX07
|    C                     Z-ADDDXX08     XX08
|    C                     MOVELDXX09     XX09
|    C*
|    C                     Z-ADD$A8YMD    XX97
|    C                     TIME           XX98
|    C                     MOVEL$USER     XX99
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          R2D20 ....FILE PROCESS : UPD
     C*==============================================================*
14===C           R2D20     BEGSR
|    C*
|    C                     EXSR R2D11
|    C                     UPDATXX0
|    C*
14===C                     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS
     C*==============================================================*
15===C           R2D30     BEGSR
|    C*
|    C                     DELETXX0
|    C*
15===C                     ENDSR
     C*==============================================================*
**
新增修改      刪除查詢
