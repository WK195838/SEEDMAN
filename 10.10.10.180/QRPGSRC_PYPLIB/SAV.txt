     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    PROGRAM-ID   : PYA3335                                    *
     H*    PROGRAM NAME :住宿費試算作業－員工                      *
     H*    AUTHOR       : A1336 KLAUS                                *
     H*    CREATE DATE  : 91/07/22                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :人事薪資系統                              *
     H****************************************************************
     FPYLIPF  IP  E           K        DISK
     FPYPWPF  UF  E           K        DISK                      A
     FPYWDPF  UF  E           K        DISK                      A
     FPYPHPF  IF  E           K        DISK
     FPYPYPF  IF  E           K        DISK
     FPYPDPF  IF  E           K        DISK
     FPYODPF  IF  E           K        DISK
     FPYSHPF  IF  E           K        DISK
     FPYSDPF  IF  E           K        DISK
     FPYOSPF  IF  E           K        DISK
     FPYSSPF  IF  E           K        DISK
     FPYPMPF  IF  E           K        DISK
     FPYOVLF01IF  E           K        DISK
     FPYLVLF02IF  E           K        DISK
     FPYTAPF  IF  E           K        DISK
     FPYLNPF  IF  E           K        DISK
     FPYJTPF  IF  E           K        DISK
     FPYLCPF  IF  E           K        DISK
     FPYT5LF01IF  E           K        DISK
     FPYDPPF  IF  E           K        DISK
     FPYTRPF  IF  E           K        DISK
     FPYE1PF  IF  E           K        DISK
     FPYE2PF  IF  E           K        DISK
     FPYE4PF  IF  E           K        DISK
     FPYL2PF  IF  E           K        DISK
     FSYCMPF  IF  E           K        DISK
     FPT#YPF  IF  E           K        DISK
     FPYWHPF  IF  E           K        DISK
     FPYX1PF  IF  E           K        DISK
     FPYX9PF  IF  E           K        DISK
     E*---------------------------------------------------------------
     E                    A1         50  2               .薪資項目
     E                    A2         50  7 0             .項目金額
     E*
     E                    B1         50  2               .固定項目
     E                    B2         50  7 0             .調薪前
     E                    B3         50  7 0             .調薪後
     E                    B4         50  7 0             .項目金額
     E*---------------------------------------------------------------
     I/COPY PYDDSSRC,PYPMDS01
     I/COPY PYRPGSRC,PYC02I
     I/COPY PYRPGSRC,PYC03I
     I*
     I           UDS
     I                                      101 110 $USER
     I                                      187 1920DDATE
     I                                      201 2080$A8YMD
     I*
     I                                      501 502 DPW01
     I*                                     503 5060DPW03
     I*                                     503 5040DPW03Y
     I*                                     505 5060DPW03M
     I                                      507 507 DPW04
     I                                      511 5160WPW03
     I                                      511 5140WPW03Y
     I                                      601 601 DOPT
     I                                      515 5160WPW03M
     I                                      603 6070DPW03
     I                                      603 6050DPW03Y
     I                                      606 6070DPW03M
     I*
     IPYDA01      DS                            256
     I                                       50  50 PYD01B
     I                                       52  52 PYD01C
     I*
     I            DS
     I                                        1   80WNYMD
     I                                        1   60WNYM
     I                                        1   40WNY
     I                                        5   60WNM
     I                                        7   80WND
     I*
     I            DS
     I                                        1   80EM13
     I                                        1   60EM13YM
     I                                        7   80EM13D
     I*
     I            DS
     I                                        1   80EM19
     I                                        1   60EM19YM
     I                                        7   80EM19D
     I*
     I            DS
     I                                        1   80EM17
     I                                        1   60EM17YM
     I                                        7   80EM17D
     I*
     I            DS
     I                                        1   80EM18
     I                                        1   60EM18YM
     I                                        7   80EM18D
     I*
     I            DS
     I                                        1   80EM14
     I                                        1   60EM14YM
     I                                        7   80EM14D
     I*
     I            DS
     I                                        1   80EM16
     I                                        1   60EM16YM
     I                                        7   80EM16D
     I*
     I            DS
     I                                        1   80EM951
     I                                        1   60EM951M
     I                                        7   80EM951D
     I*
     I            DS
     I                                        1   80EM952
     I                                        1   60EM952M
     I                                        7   80EM952D
     I            DS
     I                                        1   2 EM06
     I                                        1   20EM061
     I*
     I            DS
     I                                        1   80SH03
     I                                        7   80SH03D
     I*
     I            DS
     I                                        1   80WLV03
     I                                        1   40WLV03Y
     I                                        5   60WLV03M
8503AI            DS
 .   I                                        1   3 K#Y03
 .   I                                        1   10K#Y031
8503AI                                        3   30K#Y032
     I*
     I            DS
     I                                        1   80WYMD
     I                                        1   60WYM
     I                                        5   60WMM
     I                                        7   80WDD
     I            DS
     I                                        1   80LI05
     I                                        1   40LI05Y
     I                                        5   60LI05M
     I                                        7   80LI05D
     I            DS
     I                                        1   80LI06
     I                                        1   40LI06Y
     I                                        5   60LI06M
     I                                        7   80LI06D
     I*
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN SH03      KSH03
     C           *LIKE     DEFN SH03      SH03M
     C           *LIKE     DEFN SH03      WSH03
     C           *LIKE     DEFN LV03      KLV03
     C           *LIKE     DEFN LV04      KLV04
     C           *LIKE     DEFN LV96      KLV96
     C           *LIKE     DEFN LV04      OLV04
     C           *LIKE     DEFN OV96      KOV96
     C           *LIKE     DEFN OV07      SOV07
     C           *LIKE     DEFN OV08      SOV08
     C           *LIKE     DEFN OV09      SOV09
     C           *LIKE     DEFN OV10      SOV10
     C           *LIKE     DEFN OV11      SOV11
     C           *LIKE     DEFN OV21      SOV21
     C           *LIKE     DEFN OV22      SOV22
     C           *LIKE     DEFN OV23      SOV23
     C           *LIKE     DEFN OV24      SOV24
     C           *LIKE     DEFN OV25      SOV25
     C           *LIKE     DEFN OV27      SOV27
     C           *LIKE     DEFN OV28      SOV28
     C           *LIKE     DEFN OV29      SOV29
     C           *LIKE     DEFN OV30      SOV30
     C           *LIKE     DEFN PW05      PW05M
     C           *LIKE     DEFN WD05      #WD05
     C           *LIKE     DEFN WD06      #WD06
     C           *LIKE     DEFN TR04      KTR04
     C           *LIKE     DEFN TR03      KTR03
     C           *LIKE     DEFN EM10      #EM10
     C           *LIKE     DEFN EM05      #EM05
     C           *LIKE     DEFN EM041     #EM041
     C           *LIKE     DEFN EM042     #EM042
     C           *LIKE     DEFN EM043     #EM043
     C           *LIKE     DEFN EM044     #EM044
     C           *LIKE     DEFN LC05      WLC05
     C           *LIKE     DEFN LC05      WLC051
     C           *LIKE     DEFN T502      KT502
     C           *LIKE     DEFN T507      KT507
     C           *LIKE     DEFN PW07      PW07M
     C           *LIKE     DEFN OS05      KOS05
     C*
     C           *LIKE     DEFN CM14      DFMT
     C           *LIKE     DEFN CM15      DTYPE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYPM     KLIST
     C                     KFLD           PM01
     C                     KFLD           DPW01
     C           KEYPY     KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C*PYPWPF
     C           KEYPW     KLIST
     C                     KFLD           EM01
     C                     KFLD           WPW03
     C                     KFLD           DPW04
     C                     KFLD           EM02
     C*PYPDPF
     C           KEYPD     KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C           KEYPD1    KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           PD03P   2
     C*
     C           KEYSH     KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           KSH03
     C           KEYSD     KLIST
     C                     KFLD           SH01
     C                     KFLD           SH02
     C                     KFLD           SH03
     C*PYLVLF02
     C           KEYLV1    KLIST
     C                     KFLD           EM01             公司
     C                     KFLD           EM02             員工
     C                     KFLD           WPW03            計薪年月
     C           KEYLV2    KLIST
     C                     KFLD           EM01             公司
     C                     KFLD           EM02             員工
     C                     KFLD           WPW03            計薪年月
     C                     KFLD           KLV04            假別
     C*PYOVLF01
     C           KEYOV     KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           WPW03            計薪年月
     C*PYWDPF
     C           KEYWD     KLIST
     C                     KFLD           KWD01
     C                     KFLD           KWD03
     C                     KFLD           KWD04
     C                     KFLD           KWD02
     C                     KFLD           KWD05
     C           KEYWD2    KLIST
     C                     KFLD           PW01
     C                     KFLD           WPW03
     C                     KFLD           KWD04
     C                     KFLD           EM02
     C                     KFLD           KWD05
     C*
     C           KEYOS     KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           WPW03
     C                     KFLD           DPW04
     C           KEYOS1    KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           WPW03
     C                     KFLD           DPW04
     C                     KFLD           KOS05
     C           KEYTR     KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           KTR04
     C                     KFLD           KTR03
     C           KEYE1     KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           WTYMD
     C           KEYPH     KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           WPW03
     C                     KFLD           PH04
     C           KEYDP     KLIST
     C                     KFLD           #EM041
     C                     KFLD           #EM042
     C                     KFLD           #EM043
     C                     KFLD           #EM044
     C           KEYE2     KLIST
     C                     KFLD           E101
     C                     KFLD           E102
     C                     KFLD           E103
     C*PYSSPF
     C           KEYSS     KLIST
     C                     KFLD           CM13
     C                     KFLD           #WD05
     C           KEYSS1    KLIST
     C                     KFLD           CM13
     C                     KFLD           CODE    2
     C           KEYWSS    KLIST
     C                     KFLD           CM13
     C                     KFLD           KWD05
     C*
     C           KEYT5     KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           KT507
     C                     KFLD           KT502
     C           KEY#Y     KLIST
     C                     KFLD           #Y01
     C                     KFLD           #Y02
     C           KEYWH     KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           WPW03
     C*PYX1PF
     C           KEYX1A    KLIST
     C                     KFLD           EM01             公司
     C                     KFLD           EM02             員工
     C                     KFLD           WTYMD            生效日期
     C           KEYX1B    KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C*PYX9PF
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR RTN010                     .INIT
     C*
     C                     EXSR RTN020                     .DATA SEL.
     C*
     C                     Z-ADD0         I1      20       .INDEX-PAY
     C                     MOVEL*BLANK    A1
     C                     MOVEL*ZERO     A2
     C*
  01-C           SWEM      IFEQ 'Y'
    C                     EXSR RTN025                     .CLEAR PW
    C                     EXSR RTN030                     .EM DATA
    C*
    C           KEYPY     CHAINPY0                  40
  02-C           *IN40     IFEQ '0'
    C*
    C           KEYPW     CHAINPW0                  48
  03-C           EM100     IFEQ '1'
    C                     EXSR RTN040                     .MAIN
  03*C                     ELSE
    C                     EXSR RTN070                     計算時薪
  03-C                     END
  02-C                     END
  01-C                     END
     C*
     C*==============================================================*
     C*          RTN010... INITIAL PROCESS
     C*==============================================================*
01===C           RTN010    BEGSR
|    C*
|    C                     SETON                     10
|    C                     Z-ADD0         WLV03
|    C*
|    C*
|    C*GET PAYROLL PARAMETER
|    C                     MOVEL'01'      PM01
|    C           KEYPM     CHAINPM0                  40
|    C*
|    C*ADD BY JADI LEE
|    C   40                MOVE *ZERO     PM03
|    C*
|    C*GET PAYROLL PARAMETER
|    C           DPW01     CHAINCM0                  40
|    C                     MOVE CM14      DFMT
|    C                     MOVE CM15      DTYPE
|    C*
|    C*GET THIS YEAR AND YEAR FROM-TO DATE (#C03O1, #C03O2, #C03O3)
|    C                     Z-ADDWPW03     #C03I1  60
|    C*                    EXSR #C0300
|    C*
|    C*GET THIS MONTH FIRST DATE (WFYMD)  YYYYMM01
|    C                     MOVE '02'      WFYMD
|    C                     MOVELWPW03     WFYMD   80
|    C*
|    C*GET THIS MONTH LAST DATE (WTYMD)
|    C                     MOVELWPW03     WTYMD   80
|    C**********
|    C*CALL P22   ( Z-ADDDPW03     P2201I  40  )
|    C**                   CALL 'P34'
|    C**                   PARM WPW03     P3401I  60
|    C**                   PARM '2'       P3403I  1
|    C**                   PARM '1'       P3404I  1
|    C**                   PARM           P3411O  1
|    C**                   PARM           P3412O  20
|    C**                   MOVE P3412O    WTYMD
|    C**********
|    C*每月工作天數一律以３０天計算                      *YH USE ONLY
|    C                     MOVE 30        WTYMD
|    C*
|    C*二月份未滿30天日數計算
|    C                     MOVE WTYMD     WM2A    20
| 01-C           WM2A      IFLT 30
|   C           30        SUB  WM2A      WM2A
| 01*C                     ELSE
|   C                     Z-ADD*ZERO     WM2A
| 01-C                     END
|    C*
|    C*GET NEXT MONTH FIRST DATE(WNYMD)
|    C                     Z-ADDWPW03     WNYM
|    C                     ADD  1         WNM
| 02-C           WNM       IFGT 12
|   C                     ADD  1         WNY
|   C                     SUB  12        WNM
| 02-C                     END
|    C                     Z-ADD01        WND
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          RTN025... CLEAR PW FIELD
     C*==============================================================*
03===C           RTN025    BEGSR
|    C*
|    C                     MOVELEM01      PW01
|    C                     MOVELEM02      PW02
|    C                     Z-ADDWPW03     PW03
|    C                     MOVELDPW04     PW04
|    C                     Z-ADD*ZERO     PW05
|    C                     Z-ADD*ZERO     PW07
|    C                     Z-ADD*ZERO     PW14
|    C                     Z-ADD*ZERO     PW15
|    C                     Z-ADD*ZERO     PW16
|    C                     Z-ADD*ZERO     PW17
|    C                     Z-ADD*ZERO     PW18
|    C                     Z-ADD*ZERO     PW19
|    C                     MOVELEM11      PW20
|    C                     Z-ADD*ZERO     PW22
|    C                     Z-ADD*ZERO     PW24
|    C                     Z-ADD*ZERO     PW26
|    C                     Z-ADD*ZERO     PW27
|    C                     Z-ADD*ZERO     PW28
|    C                     Z-ADD*ZERO     PW29
|    C                     Z-ADD*ZERO     PW30
|    C                     Z-ADD*ZERO     PW31
|    C                     Z-ADD*ZERO     PW32
|    C                     Z-ADD*ZERO     PW33
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          RTN030... GET EMPLOYEE DATA
     C*==============================================================*
04===C           RTN030    BEGSR
|    C*
|    C*計算已到職月日數
|    C                     Z-ADD0         WJOBM   70
|    C                     Z-ADD0         WJOBD   20
|    C                     CALL 'P33'
|    C                     PARM EM13      P3301I  80
|    C                     PARM WFYMD     P3302I  80
|    C                     PARM '2'       P3303I  1
|    C                     PARM '1'       P3304I  1
|    C           WJOBM     PARM           P3311O  70
|    C           WJOBD     PARM           P3312O  20
|    C*
04===C           END030    ENDSR
     C*==============================================================*
     C*          RTN040... MAIN PROCESS
     C*==============================================================*
05===C           RTN040    BEGSR
|    C*
|    C*GET工作日數＆計算實領本薪--PW07&PM301
|    C                     EXSR RTN115
|    C*
|    C*福利金:PM334 (有加入福委會者才扣)
| 02-C           EM24      IFEQ 'Y'
|   C                     EXSR RTN150
| 02-C                     END
|    C*
|    C*請假扣款金額
|    C                     EXSR RTN112
|    C*健保費-PM319, PW24, PW25
|    C*
|    C*先計算健保費：因需GET健保本人補助碼
|    C                     EXSR RTN147
|    C*
|    C*勞保費-PM310, PW19, PW22
|    C                     EXSR RTN145
|    C*
|    C*其它扣款--3.工會費:PM332 (有加入工會者才扣）
| 03-C           EM25      IFEQ 'Y'
|   C                     EXSR RTN180
| 03-C                     END
|    C*
|    C                     Z-ADD$A8YMD    PW97
|    C                     TIME           WTIME  120
|    C                     MOVELWTIME     PW98
|    C                     MOVEL$USER     PW99
|    C*
|    C                     Z-ADDWPW03     PW03
|    C*
| 04-C           *IN48     IFEQ '1'
|   C                     WRITEPW0
| 04*C                     ELSE
|   C                     UPDATPW0
| 04-C                     END
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*   RTN112... GET 住宿天數＆金額
     C*==============================================================*
08===C           RTN112    BEGSR
|    C*
| 02-C           LI05      IFNE 0
| 02-C           LI06      ANDEQ0
|   C                     ADD  PMXXXX    WHR              住宿費
|    C                     END
|    C*
| 02-C           LI05      IFNE 0
| 02-C           LI06      ANDNE0                          已退宿
|   C                     ADD  PMXXXX    WHR              住宿費
|    C                     END
|    C*
|    C                     Z-ADD0         WHR
|    C*------------------------------------------------------------***
|    C*事假
|    C                     Z-ADD*ZERO     WHR     41
|    C                     Z-ADD*ZERO     WHR1    41
|    C                     Z-ADD*ZERO     #WD06
|    C*
|   C*
| 02-C           LI05      IFLT #YMDT
|   C                     ADD  LV08      WHR
|   C           LV09      MULT PM101     WHR1
|   C                     ADD  WHR1      WHR
| 02-C                     END
|   C*
|   C           KEYLV2    READELV0                      42
| 01-C                     END
|    C*
| 03-C           WHR       IFNE 0
|   C           WHR       MULT WBASE     #WD06
|   C*
|   C           PM104     CHAINLC0                  41
| 04-C           *IN41     IFEQ '0'
|   C                     MULT LC05      #WD06     H
| 04-C                     END
|   C*
|   C                     MOVELPM327     #WD05
|   C                     EXSR RTN250
| 03-C                     END
|    C*
|    C*------------------------------------------------------------***
|    C*曠職
|    C                     Z-ADD*ZERO     WHR     41
|    C                     Z-ADD*ZERO     WHR1    41
|    C                     Z-ADD*ZERO     #WD06
|    C*
|    C                     MOVE PM330     KLV04            曠職代碼
|    C           KEYLV2    SETLLLV0
|    C           KEYLV2    READELV0                      42
| 05-C           *IN42     DOWEQ'0'
|   C*
| 06-C           LV05      IFLT #YMDT
|   C                     ADD  LV08      WHR
|   C           LV09      MULT PM101     WHR1
|   C                     ADD  WHR1      WHR
| 06-C                     END
|   C*
|   C           KEYLV2    READELV0                      42
| 05-C                     END
|    C*
| 07-C           WHR       IFNE 0
|   C           WHR       MULT WBASE     #WD06
|   C*
|   C           PM127     CHAINLC0                  41
| 08-C           *IN41     IFEQ '0'
|   C                     MULT LC05      #WD06     H
| 08-C                     END
|   C*
|   C                     MOVELPM330     #WD05
|   C                     EXSR RTN250
| 07-C                     END
|    C*
|    C*------------------------------------------------------------***
|    C*病假
|    C                     Z-ADD0         WHR     41
|    C                     Z-ADD0         WHR1    41
|    C                     Z-ADD0         #WD06
|    C*
|    C                     MOVE PM105     KLV04            .病假代碼
|    C           KEYLV2    SETLLLV0
|    C           KEYLV2    READELV0                      42
| 09-C           *IN42     DOWEQ'0'
|   C*
| 10-C           LV05      IFLT #YMDT
|   C                     ADD  LV08      WHR
|   C           LV09      MULT PM101     WHR1
|   C                     ADD  WHR1      WHR
| 10-C                     END
|   C*
|   C           KEYLV2    READELV0                      42
| 09-C                     END
|    C*
| 11-C           WHR       IFNE 0
|   C           WHR       MULT WBASE     #WD06
|   C*
|   C           PM105     CHAINLC0                  41
| 12-C           *IN41     IFEQ '0'
|   C                     MULT LC05      #WD06     H
| 12-C                     END
|   C*
|   C                     MOVELPM328     #WD05
|   C                     EXSR RTN250
| 11-C                     END
|    C*
|    C*------------------------------------------------------------***
|    C*
08===C           END112    ENDSR
     C*==============================================================*
     C*   RTN115... 工作日數，實領本薪計算PW07,PM301
     C*==============================================================*
09===C           RTN115    BEGSR
|    C*
|    C*工作天數
|    C                     EXSR RTN240                     .工作天數
|    C*
| 01-C           PW07      IFLT *ZERO
|   C                     Z-ADD*ZERO     PW07
| 01-C                     END
|    C*
|    C*          PW14      DIV  PM101     WDAYX   62H      .請假
|    C*                    SUB  WDAYX     PW07      H
|    C*
|    C*實領本薪
|    C                     ADD  1         I1
|    C                     MOVELPM301     A1,I1            .項目
| 02-C           PW07      IFEQ PM102
|   C                     Z-ADDPW05M     A2,I1
| 02*C                     ELSE
|   C           PW05M     MULT PW07      WAMT   218H
|   C           WAMT      DIV  PM102     A2,I1     H
| 02-C                     END
|    C*
|    C*本月調薪金額追加
| 03-C           SH03M     IFNE *ZERO
|   C           SH03M     ANDLT#YMDT                      .計薪止日
|   C*
|   C                     CALL 'P32'
|   C                     PARM SH03M     P3201I  80
|   C                     PARM #YMDT     P3202I  80
|   C                     PARM '2'       P3203I  1
|   C                     PARM '1'       P3204I  1
|   C                     PARM           P3211O  90
|   C*
|   C           PW05      SUB  PW05M     WAMT
|   C           WAMT      DIV  PM102     WAMT      H
|   C                     MULT P3211O    WAMT
|   C                     ADD  WAMT      A2,I1     H
| 03-C                     END
|    C*
|    C                     EXSR RTN280
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*   RTN145... GET 勞保費-PM310
     C*==============================================================*
17===C           RTN145    BEGSR
|    C*未投保
| 01-C           EM14      IFEQ *ZERO
|   C           EM14      ORGT WTYMD
|   C                     GOTO END145
| 01-C                     END
|    C*
|    C                     Z-ADDWFYMD     #YMDF   80
|    C*本月新進人員
| 02-C           EM13YM    IFEQ WPW03
|   C                     Z-ADDEM13      #YMDF
| 02-C                     END
|    C*
| 03-C           EM14      IFGT #YMDF
|   C                     Z-ADDEM14      #YMDF
| 03-C                     END
|    C*
|    C                     Z-ADDWNYMD     #YMDT   80       .計算止日
|    C*離職
| 04-C           EM19      IFNE *ZERO
|   C           EM19      ANDLT#YMDT
|   C                     Z-ADDEM19      #YMDT
| 04-C                     END
|    C*
| 05-C           EM16      IFNE *ZERO
|   C           EM16      ANDLT#YMDT
|   C                     Z-ADDEM16      #YMDT
| 05-C                     END
|    C*
| 06-C           #YMDT     IFLE #YMDF
|   C                     GOTO END145
| 06-C                     END
|    C*
|    C*勞保費
|    C           EM23      MULT L201      WAMT
|    C                     Z-ADDPM102     PW22
|    C*
| 07-C           #YMDF     IFNE WFYMD
|   C           #YMDT     ORNE WNYMD
|   C*
|   C                     CALL 'P32'
|   C                     PARM #YMDF     P3201I  80
|   C                     PARM #YMDT     P3202I  80
|   C                     PARM '2'       P3203I  1
|   C                     PARM '1'       P3204I  1
|   C                     PARM           P3211O  90
|   C*
|   C*功學社：離職天數不含當天日期EX: 1/15離職則計薪天數共有14天
|   C*
|   C                     Z-ADDP3211O    PW22
|   C                     MULT P3211O    WAMT
|   C                     DIV  PM102     WAMT
| 07-C                     END
|    C*
|    C                     Z-ADDWAMT      PW19      H
|    C*
| 08-C           PW19      IFGT *ZERO
|   C                     ADD  1         I1
|   C                     MOVELPM310     A1,I1
|   C*
|   C                     Z-ADD1         WAMT
|   C           WAMT      MULT L202      WAMT
|   C           PW19      MULT WAMT      A2,I1     H
| 08-C                     END
|    C*
17===C           END145    ENDSR
     C*==============================================================*
     C*   RTN147... GET 健保費-PM319
     C*==============================================================*
18===C           RTN147    BEGSR
|    C*
|    C*GET到本月底之投保狀況
|    C           KEYE1     SETGTE10
|    C           KEYPY     REDPEE10                      40
|    C   40                GOTO END147                     無投保
|    C*
| 01-C           E106      IFEQ 0                          全部停保
|   C                     GOTO END147
| 01-C                     END
|    C*
|    C                     Z-ADDWFYMD     #YMDF
|    C                     Z-ADDWNYMD     #YMDT   80       .計算止日
|    C*離職，當月不計
| 02-C           EM19      IFNE *ZERO
|   C           EM19      ANDLT#YMDT
|   C                     GOTO END147
| 02-C                     END
|    C*
|    C*退保，當月不計
| 03-C           E104      IFEQ '9'                        退保
|   C           E103      ANDLT#YMDT
|   C                     GOTO END147
| 03-C                     END
|    C*
|    C*計算健保補助款－本人
|    C                     MOVEL*BLANK    M#Y03   3        本人－補助
|    C                     Z-ADD*ZERO     WE108   54       本人－補助
|    C                     Z-ADD0         WINSR   54       補助總額
|    C*
| 04-C           E108      IFNE *BLANK                     有補助
|   C                     MOVEL'INSR'    #Y01      P
|   C                     MOVELE108      #Y02      P
|   C           KEY#Y     CHAIN#Y0                  40
| 05-C           *IN40     IFEQ '0'
|   C                     MOVEL#Y03      M#Y03            本人
|   C                     MOVEL#Y03      K#Y03
|   C*
| 06-C           K#Y03     IFNE 'ALL'                      顧主
|   C           E107      ANDEQ*BLANK                     未停保
|   C*
| 07-C           K#Y03     IFNE '0/0'                      員工自付
|   C           K#Y032    ANDNE0
|   C           K#Y031    DIV  K#Y032    WE108   54H
|   C                     Z-ADDWE108     WINSR   54H
| 07-C                     END
|   C*
| 08-C           K#Y03     IFEQ '0/0'                      員工自付
|   C                     Z-ADD*ZERO     WINSR   54H
| 08-C                     END
| 06-C                     END
|   C*
| 05-C                     END
| 04-C                     END
|    C*
|    C*計算健保補助款－眷屬
| 09-C           M#Y03     IFNE 'ALL'                      顧主
|   C           KEYE2     SETLLE20
|   C           KEYE2     READEE20                      46
| 10-C           *IN46     DOWEQ'0'
| 11-C           E211      IFNE *BLANK                     補助
|   C           E205      ANDEQ*BLANK                     未停保
|   C                     MOVEL'INSR'    #Y01      P
|   C                     MOVELE211      #Y02      P
|   C           KEY#Y     CHAIN#Y0                  40
| 12-C           *IN40     IFEQ '0'
|   C                     MOVEL#Y03      K#Y03
| 13-C           K#Y03     IFNE 'ALL'                      非顧主
|   C           K#Y032    ANDNE0
|   C           K#Y031    DIV  K#Y032    WE211   54
|   C                     ADD  WE211     WINSR   54H
| 13-C                     END
| 12-C                     END
| 11-C                     END
|   C           KEYE2     READEE20                      46
| 10-C                     END
| 09-C                     END
|    C*
|    C*健保費＝投保額Ｘ費率
|    C           E105      MULT E401      KPW24   94H
|    C                     Z-ADDKPW24     PW24
|    C                     Z-ADDE106      PW26
|    C*
|    C*最多六人投保
| 14-C           E106      IFGT E405
|   C                     Z-ADDE405      PW26
| 14-C                     END
|    C*
| 15-C           PW24      IFGT *ZERO
|   C                     ADD  1         I1
|   C                     MOVELPM319     A1,I1
|   C*
| 16-C           M#Y03     IFEQ '0/0'                      員工自付
|   C                     Z-ADDPW24      A2,I1     H
|   C                     GOTO END147
| 16-C                     END
|   C*
| 17-C           M#Y03     IFNE 'ALL'                      顧主
|   C           KPW24     MULT E402      WAMTX   70H
| 17*C                     ELSE
|   C                     Z-ADDKPW24     WAMTX
| 17-C                     END
|   C*補助扣減
|   C           PW26      SUB  WINSR     WAMT
| 18-C           WAMT      IFLT 0
|   C                     Z-ADD0         WAMT
| 18-C                     END
|   C*
|   C           WAMTX     MULT WAMT      A2,I1     H
| 15-C                     END
|    C*
18===C           END147    ENDSR
     C*==============================================================*
     C*          RTN150...福利金PM334   *YH USE ONLY
     C*==============================================================*
19===C           RTN150    BEGSR
|    C*
|    C*加入福委會者才需繳交福利金
|    C*福利金金額＝PY03本俸＊0.005
|    C                     Z-ADD*ZERO     WAMT
|    C*
|    C           PY03      MULT 0.005     WAMT      H
|    C*
|    C*部分員工屬於扣定額福利金
| 01-C           EM02      IFEQ '00041'
|   C                     Z-ADD1182      WAMT
| 01-C                     END
|    C*
| 02-C           EM02      IFEQ '38507'
|   C                     Z-ADD961       WAMT
| 02-C                     END
|    C*
| 03-C           EM02      IFEQ '37110'
|   C                     Z-ADD590       WAMT
| 03-C                     END
|    C*
| 04-C           EM02      IFEQ '37135'
|   C                     Z-ADD551       WAMT
| 04-C                     END
|    C*
| 05-C           EM02      IFEQ '38242'
|   C                     Z-ADD821       WAMT
| 05-C                     END
|    C*
| 06-C           EM02      IFEQ '38531'
|   C                     Z-ADD578       WAMT
| 06-C                     END
|    C*
| 07-C           EM02      IFEQ '38556'
|   C                     Z-ADD603       WAMT
| 07-C                     END
|    C*
|    C*福利金：PM334
| 08-C           WAMT      IFNE *ZERO
|   C                     ADD  1         I1
|   C                     MOVELPM334     A1,I1
|   C                     Z-ADDWAMT      A2,I1     H
| 08-C                     END
|    C*
19===C           END150    ENDSR
     C*==============================================================*
     C*   RTN160... GET 扣項    -PM131:貸款
     C*==============================================================*
20===C           RTN160    BEGSR
|    C*
|    C*貸款
|    C           KEYPY     SETLLLN0
|    C           KEYPY     READELN0                      42
|    C*
|    C                     Z-ADD*ZERO     WPM313
|    C*
| 01-C           *IN42     DOWEQ'0'
|   C*
|   C                     Z-ADD*ZERO     WAMT
| 02-C           WPW03     IFEQ LN09                       .最後一期
|   C                     Z-ADDLN10      WAMT
| 02*C                     ELSE
| 03-C           WPW03     IFEQ LN06                       .第一期
|   C                     Z-ADDLN07      WAMT
| 03*C                     ELSE
| 04-C           WPW03     IFGT LN06                       .每一期
|   C           WPW03     ANDLTLN09
|   C                     Z-ADDLN08      WAMT
| 04-C                     END
| 03-C                     END
| 02-C                     END
|   C*
| 05-C           WAMT      IFNE *ZERO
|   C                     ADD  WAMT      WPM313  70H
| 05-C                     END
|   C*
|   C           KEYPY     READELN0                      42
| 01-C                     END
|    C*
| 06-C           WPM313    IFNE *ZERO
|   C                     ADD  1         I1
|   C                     MOVELPM313     A1,I1
|   C                     Z-ADDWPM313    A2,I1
| 06-C                     END
|    C*
20===C                     ENDSR
     C*==============================================================*
     C*   RTN165... GET 薪資固定項目
     C*==============================================================*
21===C           RTN165    BEGSR
|    C*
|    C*固定項目
|    C                     Z-ADD1         K1      20
| 01-C           K1        DOWLEIB
|   C*
| 02-C           B1,K1     IFNE *BLANK
|   C           B4,K1     ANDNE*ZERO
|   C                     MOVELB1,K1     #WD05
|   C                     Z-ADDB4,K1     #WD06
|   C                     EXSR RTN250
| 02-C                     END
|   C*
|   C                     ADD  1         K1
| 01-C                     END
|    C*
|    C*變動項目
|    C           KEYOS     SETLLOS0
|    C           KEYOS     READEOS0                      42
|    C*
| 03-C           *IN42     DOWEQ'0'
|   C*
|   C                     MOVELOS05      #WD05
| 04-C           OS06      IFNE *ZERO
|   C                     Z-ADDOS06      #WD06
| 04*C                     ELSE
|   C           OS07      MULT OS08      #WD06
| 04-C                     END
|   C                     EXSR RTN250
|   C*
|   C           KEYOS     READEOS0                      42
| 03-C                     END
|    C*
|    C*系統設定
|    C                     Z-ADD1         K1      20
| 05-C           K1        DOWLEI1
|   C*
| 06-C           A1,K1     IFNE *BLANK
|   C           A2,K1     ANDNE*ZERO
|   C                     MOVELA1,K1     #WD05
|   C                     Z-ADDA2,K1     #WD06
|   C                     EXSR RTN250
| 06-C                     END
|   C*
|   C                     ADD  1         K1
| 05-C                     END
|    C*
21===C                     ENDSR
     C*==============================================================*
     C*   RTN170... GET 固定薪資所得稅 & PYPWPF
     C*==============================================================*
22===C           RTN170    BEGSR
|    C*
|    C*本期應稅＋上期應稅
|    C*ADD CHG BY JADI
|    C           PW17      SUB  PW28      WAMT
|    C*
|    C*ADD CHG BY JADI
| 01-C           WAMT      IFLE *ZERO
|   C                     GOTO END170
| 01-C                     END
|    C*
|    C*END CHG
| 02-C           EM101     IFEQ '1'
|   C                     Z-ADDWAMT      PI1     80
|   C                     MOVELEM34      PI2     1
|   C                     Z-ADDEM35      PI3     20
|   C                     CALL 'PYS306'                   .TAX
|   C                     PARM           PI1
|   C                     PARM           PI2
|   C                     PARM           PI3
|   C                     PARM           PO1     70
|   C                     PARM           PO2     53
| 02-C                     END
|    C*
| 03-C           EM101     IFEQ '2'
|   C                     Z-ADD0.1       PO2
|   C           WAMT      MULT PO2       PO1       H
| 03-C                     END
|    C*
| 04-C           EM101     IFEQ '3'
|   C                     Z-ADD0.2       PO2
|   C           WAMT      MULT PO2       PO1       H
| 04-C                     END
|    C*
| 05-C           EM101     IFEQ '4'
|   C                     Z-ADD*ZERO     PO2
|   C                     Z-ADDEM102     PO1       H
| 05-C                     END
|    C*
| 06-C           PO1       IFNE *ZERO
|   C                     MOVELPM318     #WD05
|   C                     Z-ADDPO1       #WD06
|   C                     EXSR RTN250
| 06-C                     END
|    C*--------
|    C                     Z-ADDPO2       PW15
|    C*
22===C           END170    ENDSR
     C*==============================================================*
     C*   RTN175... 計算獎金所得稅 & PYPWPF
     C*==============================================================*
23===C           RTN175    BEGSR
|    C*
|    C           PW27      SUB  PW29      WAMT
| 01-C           WAMT      IFLE *ZERO
|   C                     GOTO END175
| 01-C                     END
|    C*
|    C*獎金稅=非固定薪資所得X獎金稅率
|    C*不足最低稅額不扣
|    C           WAMT      MULT TA07      #WD06     H
|    C*          #WD06     IFLT TA08
|    C*                    Z-ADD*ZERO     #WD06
|    C*                    END
|    C*
| 02-C           #WD06     IFNE *ZERO
|   C                     MOVELPM311     #WD05
|   C                     EXSR RTN250
| 02-C                     END
|    C*
23===C           END175    ENDSR
     C*==============================================================*
     C*   RTN180... GET 扣項    -PM332:工會費
     C*==============================================================*
24===C           RTN180    BEGSR
|    C*
|    C                     ADD  1         I1
|    C                     MOVELPM332     A1,I1
|    C                     Z-ADDPM129     A2,I1
|    C*
24===C                     ENDSR
     C*==============================================================*
     C*   RTN201... GET 某一時間點薪制，職務-#EM10, #EM05
     C*==============================================================*
25===C           RTN201    BEGSR
|    C*
|    C                     MOVELEM041     #EM041
|    C                     MOVELEM042     #EM042
|    C                     MOVELEM043     #EM043
|    C                     MOVELEM044     #EM044
|    C                     MOVELEM05      #EM05
|    C                     MOVELEM10      #EM10
|    C                     MOVELWTYMD     KTR04
|    C                     MOVEL*HIVAL    KTR03
|    C           KEYTR     SETGTTR0
|    C           KEYPY     READETR0                      40
|    C*
| 01-C           *IN40     IFEQ '0'
|   C                     MOVELTR051     #EM041
|   C                     MOVELTR052     #EM042
|   C                     MOVELTR053     #EM043
|   C                     MOVELTR054     #EM044
|   C                     MOVELTR07      #EM05
|   C                     MOVELTR11      #EM10
| 01-C                     END
|    C*全勤獎金有無
|    C           #EM05     CHAINJT0                  40
|    C   40                MOVEL'Y'       JT07
|    C*駐外津貼，房屋津貼，加班計算
|    C           KEYDP     CHAINDP0                  40
| 02-C           *IN40     IFEQ '1'
|   C                     Z-ADD0         DP03
|   C                     Z-ADD0         DP05
|   C                     MOVEL'1'       DP07
| 02-C                     END
|    C*
25===C                     ENDSR
     C*==============================================================*
     C*   RTN210... 假別扣薪請假時數累計--- WPW14
     C*==============================================================*
26===C           RTN210    BEGSR
|    C*
|    C*只處理本月資料
|    C*                    MOVELLV03      LV03YM  60
|    C*          LV03YM    IFNE WPW03
|    C*                    GOTO END210
|    C*                    END
|    C*
|    C                     ADD  LV08      WPW14
|    C           LV09      MULT PM101     WHOUR  149
|    C                     ADD  WHOUR     WPW14     H
|    C*
|    C                     ADD  LV11      SLV11
|    C                     ADD  LV12      SLV12
|    C*
| 01-C           LV04      IFEQ PM104
|   C           LV04      OREQ PM110
|   C           LV04      OREQ PM127
|   C                     ADD  WPW14     W309
| 01-C                     END
|    C*
26===C           END210    ENDSR
     C*==============================================================*
     C*   RTN220... 假別扣薪請假時數判別--- WPW14
     C*             假別本年度已休時數累計-- WPW14A
     C*==============================================================*
27===C           RTN220    BEGSR
|    C*
|    C           OLV04     CHAINLC0                  40
| 01-C           *IN40     IFEQ '1'
|   C                     MOVEL'1'       LC03
|   C                     Z-ADD999       LC04
|   C                     Z-ADD0         LC08
|   C                     Z-ADD0         LC09
|   C                     MOVEL'1'       LC10
|   C                     Z-ADD999       LC11
|   C                     GOTO END220
| 01-C                     END
|    C*
|    C*有薪且不限天數者不計算
| 02-C           LC03      IFEQ '1'
|   C           LC04      ANDEQ999
|   C                     GOTO END220
| 02-C                     END
|    C*
|    C*無薪
| 03-C           LC03      IFEQ '2'
|   C           LC09      ANDEQ0                          無超扣
|   C                     GOTO END220
| 03-C                     END
|    C*
|    C*有部份計薪－計算本年度已請時數(WPW14A)
|    C                     MOVELPYLVT     PYLVTD
|    C*
|    C                     Z-ADDLV03      WLV03
| 04-C           WLV03M    IFGT PM103
|   C           WLV03Y    ADD  1         #C02I1  40
| 04*C                     ELSE
|   C                     Z-ADDWLV03Y    #C02I1
| 04-C                     END
|    C*
|    C                     EXSR #C0200                     在職月數
|    C                     MOVEL#C02T     WC02TD  80
|    C                     MOVE '31'      WC02TD
|    C*
|    C                     MOVELOLV04     KLV04
|    C                     Z-ADD*ZERO     WPW14A  62
|    C           KEYLV2    SETLLLV0
|    C           KEYLV2    READELV0                      40
| 05-C           *IN40     DOWEQ'0'
|   C*
| 06-C           LV96      IFNE *ZERO
|   C                     ADD  LV08      WPW14A
|   C           LV09      MULT PM101     WHOUR
|   C                     ADD  WHOUR     WPW14A    H
| 06-C                     END
|   C*
|   C           KEYLV2    READELV0                      40
| 05-C                     END
|    C*
| 07-C           SWSH      IFEQ 'Y'
|   C                     ADD  C2,J      WPW14A
| 07-C                     END
|    C*
|    C                     MOVELPYLVTD    PYLVT
|    C*
27===C           END220    ENDSR
     C*==============================================================*
     C*   RTN230... 扣薪請假時數累計--- SPW14
     C*==============================================================*
28===C           RTN230    BEGSR
|    C*
| 01-C           LV04      IFEQ PM127
|   C                     MOVEL'Y'       WSTOP
| 01-C                     END
|    C*
|    C*當月有請無全勤假者'Y'-> WSTOP無全勤獎金
| 02-C           LC07      IFEQ 'N'                        .無全勤
|   C           WPW14     ANDNE*ZERO
| 02-C                     END
|    C*
|    C*無薪
| 03-C           LC03      IFEQ '2'
| 04-C           LC09      IFEQ 0                          .無超扣
|   C                     ADD  WPW14     SPW14
|   C*
| 04*C                     ELSE                            .有超扣
|   C*不需超扣的天數計算
|   C           LC08      MULT PM101     WHOUR            .計薪時數
|   C                     MULT #C02M1    WHOUR            .新進比率
|   C                     DIV  12        WHOUR     H
|   C*
| 05-C           WHOUR     IFGT WPW14A
|   C                     SUB  WPW14A    WHOUR            .剩下不扣
| 05*C                     ELSE
|   C                     Z-ADD*ZERO     WHOUR
| 05-C                     END
|   C*
| 06-C           WPW14     IFLE WHOUR
|   C                     ADD  WPW14     SPW14
| 06*C                     ELSE
|   C                     ADD  WHOUR     SPW14
|   C           WPW14     SUB  WHOUR     WHOUR            .超扣時數
|   C           WHOUR     MULT LC09      WHOUR            .X超比
|   C                     ADD  WHOUR     SPW14
| 06-C                     END
|   C*
| 04-C                     END
| 03-C                     END
|    C*
|    C*有部份計薪
| 07-C           LC03      IFEQ '1'
|   C           LC04      ANDNE999
|   C*
|   C           LC04      MULT PM101     WHOUR            .計薪時數
|   C                     MULT #C02M1    WHOUR            .新進比率
|   C                     DIV  12        WHOUR     H
|   C*
|   C           LC10      MULT PM101     WHOUR1           .計薪時數
|   C                     MULT #C02M1    WHOUR1           .新進比率
|   C                     DIV  12        WHOUR1 149H
|   C*
|   C           1         SUB  LC05      WLC05            .扣薪比率
|   C           1         SUB  LC11      WLC051           .扣薪比率
|   C*
|   C           WPW14     ADD  WPW14A    WPW14B 149H
|   C*
| 08-C                     SELEC
|   C           WPW14A    WHLE WHOUR
| 09-C           WPW14B    IFLE WHOUR
|   C           WPW14     MULT WLC05     WAMT      H
|   C                     ADD  WAMT      SPW14     H      .扣部份薪
| 09*C                     ELSE
| 10-C           WPW14B    IFLE WHOUR1
|   C           WHOUR     SUB  WPW14A    WAMT      H
|   C           WAMT      MULT WLC05     WAMT      H
|   C                     ADD  WAMT      SPW14     H      .扣部份薪
|   C           WPW14B    SUB  WHOUR     WHOUR     H
|   C           WHOUR     MULT WLC051    WAMT      H
|   C                     ADD  WAMT      SPW14     H      .扣全薪
| 10*C                     ELSE
|   C           WHOUR     SUB  WPW14A    WAMT      H
|   C           WAMT      MULT WLC05     WAMT      H
|   C                     ADD  WAMT      SPW14     H      .扣部份薪
|   C           WHOUR1    SUB  WHOUR     WAMT      H
|   C           WAMT      MULT WLC051    WAMT      H
|   C                     ADD  WAMT      SPW14     H      .扣部份薪
|   C           WPW14B    SUB  WHOUR1    WHOUR1    H
|   C                     ADD  WHOUR1    SPW14     H      .扣全薪
| 10-C                     END
| 09-C                     END
|   C*
|   C           WPW14A    WHGT WHOUR
|   C           WPW14A    ANDLEWHOUR1
| 11-C           WPW14B    IFLE WHOUR1
|   C           WPW14     MULT WLC051    WAMT      H
|   C                     ADD  WAMT      SPW14     H      .扣部份薪
| 11*C                     ELSE
|   C           WHOUR1    SUB  WPW14A    WAMT      H
|   C           WAMT      MULT WLC051    WAMT      H
|   C                     ADD  WAMT      SPW14     H      .扣部份薪
|   C           WPW14B    SUB  WHOUR1    WHOUR1    H
|   C                     ADD  WHOUR1    SPW14     H      .扣全薪
| 11-C                     END
|   C*
|   C                     OTHER
|   C                     ADD  WPW14     SPW14     H
| 08-C                     END
| 07-C                     END
|    C* TEMP FIELD CLEAR
|    C                     Z-ADD*ZERO     WPW14
|    C*
28===C           END230    ENDSR
     C*==============================================================*
     C*   RTN240... 工作日數計算PW07
     C*==============================================================*
29===C           RTN240    BEGSR
|    C*
|    C                     Z-ADDWFYMD     #YMDF            .計薪起日
|    C                     Z-ADDWNYMD     #YMDT            .計薪止日
|    C*
|    C*本月工作日數(30 DAYS)
|    C                     Z-ADDPM102     PW07
|    C*
|    C*本月新進人員も工作天數－到職日至月底日も
| 01-C           EM13      IFGT WFYMD
|   C*
|   C                     CALL 'P32'
|   C                     PARM EM13      P3201I  80
|   C                     PARM WTYMD     P3202I  80
|   C                     PARM '2'       P3203I  1
|   C                     PARM '1'       P3204I  1
|   C                     PARM           P3211O  90
|   C           P3211O    ADD  1         PW07
|   C                     Z-ADDEM13      #YMDF
| 01-C                     END
|    C*
|    C*離職人員も工作日數計算－計薪起日至離職日間天數も
| 02-C           EM19      IFNE *ZERO
|   C           EM19YM    ANDLEWPW03
|   C*                    MOVEL'Y'       WSTOP            .無全勤
|   C*離職日＜計薪起日，工作日數＝０
| 03-C           EM19      IFLT #YMDF
|   C                     Z-ADD*ZERO     PW07
|   C                     Z-ADDEM19      #YMDT
|   C                     GOTO END240
| 03*C                     ELSE
|   C                     CALL 'P32'
|   C                     PARM #YMDF     P3201I  80
|   C                     PARM EM19      P3202I  80
|   C                     PARM '2'       P3203I  1
|   C                     PARM '1'       P3204I  1
|   C                     PARM           P3211O  90
|   C                     ADD  1         P3211O
|   C                     Z-ADDP3211O    PW07
|   C                     Z-ADDEM19      #YMDT
| 03-C                     END
| 02-C                     END
|    C*
|    C*本期間留職人員も工作日數計算－計薪起日至留職日間天數も
| 04-C           EM11      IFEQ 'R'
|   C           EM17YM    ANDLEWPW03
|   C*                    MOVEL'Y'       WSTOP            .無全勤
|   C*留職日＜計薪起日，工作日數＝０
| 05-C           EM17      IFLT #YMDF
|   C                     Z-ADD*ZERO     PW07
|   C                     Z-ADDEM17      #YMDT
|   C                     GOTO END240
| 05*C                     ELSE
|   C                     CALL 'P32'
|   C                     PARM #YMDF     P3201I  80
|   C                     PARM EM17      P3202I  80
|   C                     PARM '2'       P3203I  1
|   C                     PARM '1'       P3204I  1
|   C                     PARM           P3211O  90
|   C                     ADD  1         P3211O
|   C                     Z-ADDP3211O    PW07
|   C                     Z-ADDEM17      #YMDT
| 05-C                     END
| 04-C                     END
|    C*
|    C*本期間長病人員も工作日數計算－計薪起日至留職日間天數も
| 06-C           EM11      IFEQ 'S'
|   C           EM951M    ANDLEWPW03
|   C*                    MOVEL'Y'       WSTOP            .無全勤
|   C*留職日＜計薪起日，工作日數＝０
| 07-C           EM951     IFLT #YMDF
|   C                     Z-ADD*ZERO     PW07
|   C                     Z-ADDEM951     #YMDT
|   C                     GOTO END240
| 07*C                     ELSE
|   C                     CALL 'P32'
|   C                     PARM #YMDF     P3201I  80
|   C                     PARM EM951     P3202I  80
|   C                     PARM '2'       P3203I  1
|   C                     PARM '1'       P3204I  1
|   C                     PARM           P3211O  90
|   C                     ADD  1         P3211O
|   C                     Z-ADDP3211O    PW07
|   C                     Z-ADDEM951     #YMDT
| 07-C                     END
| 06-C                     END
|    C*
|    C*本期間留職停薪日數扣除
| 08-C           EM11      IFNE 'R'
|   C           EM18      ANDGT#YMDF
|   C           EM17      ANDLT#YMDT
|   C*
| 09-C           EM17      IFGE #YMDF
|   C                     Z-ADDEM17      P3201I
| 09*C                     ELSE
|   C                     Z-ADD#YMDF     P3201I
| 09-C                     END
| 10-C           EM18      IFLT #YMDT
|   C                     Z-ADDEM18      P3202I
| 10*C                     ELSE
|   C                     Z-ADD#YMDT     P3202I
| 10-C                     END
|   C*
|   C                     CALL 'P32'
|   C                     PARM           P3201I  80
|   C                     PARM           P3202I  80
|   C                     PARM '2'       P3203I  1
|   C                     PARM '1'       P3204I  1
|   C                     PARM           P3211O  90
|   C*
|   C                     SUB  P3211O    PW07
| 08-C                     END
|    C*
|    C*本期間長病停薪日數扣除
| 11-C           EM11      IFNE 'S'
|   C           EM952     ANDGT#YMDF
|   C           EM951     ANDLT#YMDT
|   C*                    MOVEL'Y'       WSTOP            .無全勤
|   C*
| 12-C           EM951     IFGE #YMDF
|   C                     Z-ADDEM951     P3201I
| 12*C                     ELSE
|   C                     Z-ADD#YMDF     P3201I
| 12-C                     END
| 13-C           EM952     IFLT #YMDT
|   C                     Z-ADDEM952     P3202I
| 13*C                     ELSE
|   C                     Z-ADD#YMDT     P3202I
| 13-C                     END
|   C                     CALL 'P32'
|   C                     PARM           P3201I  80
|   C                     PARM           P3202I  80
|   C                     PARM '2'       P3203I  1
|   C                     PARM '1'       P3204I  1
|   C                     PARM           P3211O  90
|   C*
|   C                     SUB  P3211O    PW07
| 11-C                     END
|    C*
29===C           END240    ENDSR
     C*==============================================================*
     C*          RTN250... 薪資項目－一
     C*==============================================================*
30===C           RTN250    BEGSR
|    C*
| 01-C           #WD06     IFEQ *ZERO
|   C                     GOTO END250
| 01-C                     END
|    C*
|    C           KEYSS     CHAINSS0                  40
| 02-C           *IN40     IFEQ '0'
|   C*
| 03-C           SS04      IFEQ 'B'                        .減項
|   C*
| 04-C           SS03      IFEQ 'N'                        .免稅
|   C                     ADD  #WD06     PW18
| 04*C                     ELSE                            .應稅
| 05-C           SS11      IFEQ '1'                        .固定
|   C                     ADD  #WD06     PW28
| 05*C                     ELSE                            .非固定
|   C                     ADD  #WD06     PW29
| 05-C                     END
| 04-C                     END
|   C*
| 03*C                     ELSE                            .加項
|   C*
| 06-C           SS03      IFEQ 'N'                        .免稅
|   C                     ADD  #WD06     PW16
| 06*C                     ELSE                            .應稅
| 07-C           SS11      IFEQ '1'                        .固定
|   C                     ADD  #WD06     PW17
| 07*C                     ELSE                            .非固定
|   C                     ADD  #WD06     PW27
| 07-C                     END
| 06-C                     END
|   C*
| 03-C                     END
| 02-C                     END
|    C*
|    C                     MOVELPW01      KWD01   2
|    C                     MOVELPW02      KWD02   8
|    C                     Z-ADDWPW03     KWD03   60
|    C                     MOVELPW04      KWD04   1
|    C                     MOVEL#WD05     KWD05   2
|    C           KEYWD     CHAINWD0                  40
|    C*
| 08-C           *IN40     IFEQ '0'
|   C                     ADD  #WD06     WD06
|   C                     UPDATWD0
| 08*C                     ELSE
|   C                     MOVELPW01      WD01
|   C                     MOVELPW02      WD02
|   C                     Z-ADDWPW03     WD03
|   C                     MOVELPW04      WD04
|   C                     MOVEL#WD05     WD05
|   C                     Z-ADD#WD06     WD06
|   C                     WRITEWD0
| 08-C                     END
|    C*
30===C           END250    ENDSR
     C*==============================================================*
     C*          RTN251.. 薪資項目－二
     C*==============================================================*
31===C           RTN251    BEGSR
|    C*
| 01-C           WD06      IFEQ *ZERO
|   C                     GOTO END251
| 01-C                     END
|    C*
|    C           KEYWSS    CHAINSS0                  42
| 02-C           *IN42     IFEQ '0'
|   C*
| 03-C           SS04      IFEQ 'B'                        .減項
|   C*
| 04-C           SS03      IFEQ 'N'                        .免稅
|   C                     ADD  WD06      PW18
| 04*C                     ELSE                            .應稅
| 05-C           SS11      IFEQ '1'                        .固定
|   C                     ADD  WD06      PW28
| 05*C                     ELSE                            .非固定
|   C                     ADD  WD06      PW29
| 05-C                     END
| 04-C                     END
|   C*
| 03*C                     ELSE                            .加項
|   C*
| 06-C           SS03      IFEQ 'N'                        .免稅
|   C                     ADD  WD06      PW16
| 06*C                     ELSE                            .應稅
| 07-C           SS11      IFEQ '1'                        .固定
|   C                     ADD  WD06      PW17
| 07*C                     ELSE                            .非固定
|   C                     ADD  WD06      PW27
| 07-C                     END
| 06-C                     END
|   C*
| 03-C                     END
| 02-C                     END
|    C*
31===C           END251    ENDSR
     C*==============================================================*
     C*          RTN260... 薪資固定項目－本月底薪資
     C*==============================================================*
32===C           RTN260    BEGSR
|    C*
|    C                     MOVEL*BLANK    B1
|    C                     MOVEL*ZERO     B2
|    C                     MOVEL*ZERO     B3
|    C                     MOVEL*ZERO     B4
|    C                     Z-ADD*ZERO     IB      20       .固定數
|    C*
| 01-C           SWSH      IFEQ 'N'                        .未調薪
|   C*
|   C           KEYPY     SETLLPD0
|   C           KEYPY     READEPD0                      42
| 02-C           *IN42     DOWEQ'0'
|   C                     ADD  1         IB
|   C                     MOVELPD03      B1,IB
|   C                     Z-ADDPD04      B3,IB
|   C           KEYPY     READEPD0                      42
| 02-C                     END
|   C*
| 01*C                     ELSE                            .有調薪
|   C           KEYSD     SETLLSD0
|   C           KEYSD     READESD0                      42
| 03-C           *IN42     DOWEQ'0'
|   C*
| 04-C           SD05      IFNE *ZERO
|   C                     ADD  1         IB
|   C                     MOVELSD04      B1,IB
|   C                     Z-ADDSD05      B3,IB
| 04-C                     END
|   C*
|   C           KEYSD     READESD0                      42
| 03-C                     END
|   C*
| 01-C                     END
|    C*
32===C                     ENDSR
     C*==============================================================*
     C*   RTN270... 薪資固定項目－調薪前薪資
     C*==============================================================*
33===C           RTN270    BEGSR
|    C*
|    C           KEYSD     SETLLSD0
|    C           KEYSD     READESD0                      42
|    C*
| 01-C           *IN42     DOWEQ'0'
|   C*
| 02-C           SD05      IFNE *ZERO
|   C                     Z-ADD1         I2      20
|   C           SD04      LOKUPB1,I2                    40
|   C*
| 03-C           *IN40     IFEQ '0'
|   C                     ADD  1         IB
|   C                     MOVELSD04      B1,IB
|   C                     Z-ADDSD05      B2,IB
| 03*C                     ELSE
|   C                     Z-ADDSD05      B2,I2
| 03-C                     END
|   C*
| 02-C                     END
|   C           KEYSD     READESD0                      42
| 01-C                     END
|    C*
33===C                     ENDSR
     C*==============================================================*
     C*   RTN280... 薪資固定項目－本月應給付
     C*==============================================================*
34===C           RTN280    BEGSR
|    C*
|    C                     Z-ADD1         I2
| 01-C           I2        DOWLEIB
|   C*
| 02-C           SH03M     IFEQ *ZERO                      .未調薪
|   C                     Z-ADDB3,I2     B2,I2
| 02-C                     END
|   C*
| 03-C           PW07      IFEQ PM102
|   C                     Z-ADDB2,I2     B4,I2
| 03*C                     ELSE
|   C           B2,I2     MULT PW07      WAMT
|   C           WAMT      DIV  PM102     B4,I2     H
| 03-C                     END
|   C*
|   C*本月調薪金額追加
| 04-C           SH03M     IFNE *ZERO                      .破月調薪
|   C           SH03M     ANDLT#YMDT                      .計薪止日
|   C           B3,I2     SUB  B2,I2     WAMT
|   C           WAMT      DIV  PM102     WAMT      H
|   C                     MULT P3211O    WAMT
|   C                     ADD  WAMT      B4,I2     H
| 04-C                     END
|   C*
|   C                     ADD  1         I2
| 01-C                     END
|    C*
34===C                     ENDSR
     C*==============================================================*
     C*          RTN060... 時薪制MAIN PROCESS
     C*==============================================================*
35===C           RTN060    BEGSR
|    C*
|    C*GET本薪與調薪前薪資(PW05,PW05M,SH03M)
|    C                     EXSR RTN105
|    C*
|    C*GET工作時數＆計算實領本薪--PW07&PM301
|    C                     EXSR RTN061
|    C*
|    C*健保費-PM319, PW24, PW25
|    C*先計算健保費：因需GET健保本人補助碼
|    C                     EXSR RTN147
|    C*
|    C*勞保費-PM310, PW19, PW22
|    C                     EXSR RTN145
|    C*
|    C*薪定項目---> PYWDPF
|    C                     EXSR RTN063
|    C*固定薪資所得稅(PW15, PM318)
|    C                     EXSR RTN170
|    C*
|    C                     Z-ADD$A8YMD    PW97
|    C                     TIME           WTIME  120
|    C                     MOVELWTIME     PW98
|    C                     MOVEL$USER     PW99
|    C*ADD BY JADI
|    C                     Z-ADDWPW03     PW03
|    C                     WRITEPW0
|    C*
35===C           END060    ENDSR
     C*==============================================================*
     C*   RTN061... 工作時數，實領本薪計算PW07,PM301
     C*==============================================================*
36===C           RTN061    BEGSR
|    C*
|    C*工作時數
|    C                     EXSR RTN062                     .工作時數
|    C*實領本薪
|    C                     ADD  1         I1
|    C                     MOVELPM301     A1,I1            .項目
|    C           PW07      MULT PW05M     A2,I1
|    C*本月調薪金額追加
| 01-C           SH03M     IFNE *ZERO
|   C           SH03M     ANDLT#YMDT                      .計薪止日
|   C*
|   C           PW05      SUB  PW05M     WAMT
|   C           WAMT      MULT PW07M     WAMT
|   C                     ADD  WAMT      A2,I1     H
| 01-C                     END
|    C*
|    C                     EXSR RTN280
|    C*
36===C                     ENDSR
     C*==============================================================*
     C*   RTN062      ... 工作時數計算PW07
     C*==============================================================*
37===C           RTN062    BEGSR
|    C*
|    C                     Z-ADDWFYMD     #YMDF            .計薪起日
|    C                     Z-ADDWNYMD     #YMDT            .計薪止日
|    C*----------------------------------------------------------------
|    C*實際刷卡時間
|    C*----------------------------------------------------------------
|    C                     SETOF                     818283
|    C                     Z-ADD#YMDF     KT502
|    C                     Z-ADD0         T504T   60
|    C* GET上班資料
| 01-C           KT502     DOWLE#YMDT
|   C                     Z-ADD0         T503A   20
|   C                     Z-ADD0         T503B   20
|   C                     Z-ADD0         T503C   20
|   C                     Z-ADD0         T504A   40
|   C                     Z-ADD0         T504B   40
|   C                     Z-ADD0         T504C   40
|   C                     MOVELPYD01B    KT507
|   C           KEYT5     SETLLT50
|   C           KEYT5     READET50                      40
|   C*
| 02-C           *IN40     IFEQ '0'
|   C                     Z-ADDT503      T503A
|   C                     Z-ADDT504      T504A
|   C                     SETON                     81
| 02-C                     END
|   C* GET下班資料
|   C                     MOVELPYD01C    KT507
|   C*若無區分上下班，則下班須讀第二筆資料(SETON 50)
|   C                     SETOF                     50
| 03-C           KT507     IFEQ *BLANKS
|   C                     SETON                     50
| 03-C                     END
|   C*
|   C           KEYT5     SETLLT50
|   C           KEYT5     READET50                      43
|   C   50      KEYT5     READET50                      43
| 04-C           *IN43     IFEQ '0'
|   C                     Z-ADDT503      T503B
|   C                     Z-ADDT504      T504B
|   C                     SETON                     82
| 04-C                     END
|   C*日期往後一天
|   C                     CALL 'P37'
|   C                     PARM KT502     P3701I  80
|   C                     PARM '2'       P3702I  1
|   C                     PARM '1'       P3703I  1
|   C                     PARM 1         P3704I  40
|   C           KT502     PARM           P3711O  80
|   C*累計時數
| 05-C           *IN81     IFEQ '1'
|   C           *IN82     ANDEQ'1'
|   C           T503B     SUB  T503A     T503C
|   C           T503C     MULT 60        T504C
|   C                     ADD  T504B     T504C
|   C                     SUB  T504A     T504C
| 05-C                     END
|   C                     ADD  T504C     T504T            累計分
| 01-C                     END
|    C*計算總工時
|    C           T504T     DIV  60        T504B            工作總時數
|    C                     MVR            T504C
|    C                     Z-ADDT504B     PW07
| 06-C           T504C     IFGE 30
|   C                     ADD  0.5       PW07
| 06-C                     END
|    C*有調薪
| 07-C           SWSH      IFEQ 'Y'
|   C                     Z-ADD0         T504T
|   C                     Z-ADDSH03M     KT502
|   C* GET上班資料
| 08-C           KT502     DOWLE#YMDT
|   C                     Z-ADD0         T503A
|   C                     Z-ADD0         T503B
|   C                     Z-ADD0         T503C
|   C                     Z-ADD0         T504A
|   C                     Z-ADD0         T504B
|   C                     Z-ADD0         T504C
|   C                     MOVELPYD01B    KT507
|   C           KEYT5     SETLLT50
|   C           KEYT5     READET50                      40
|   C*
| 09-C           *IN40     IFEQ '0'
|   C                     Z-ADDT503      T503A
|   C                     Z-ADDT504      T504A
|   C                     SETON                     81
| 09-C                     END
|   C* GET下班資料
|   C                     MOVELPYD01C    KT507
|   C*若無區分上下班，則下班須讀第二筆資料(SETON 50)
|   C                     SETOF                     50
| 10-C           KT507     IFEQ *BLANKS
|   C                     SETON                     50
| 10-C                     END
|   C*
|   C           KEYT5     SETLLT50
|   C           KEYT5     READET50                      43
|   C   50      KEYT5     READET50                      43
| 11-C           *IN43     IFEQ '0'
|   C                     Z-ADDT503      T503B
|   C                     Z-ADDT504      T504B
|   C                     SETON                     82
| 11-C                     END
|   C*日期往後一天
|   C                     CALL 'P37'
|   C                     PARM KT502     P3701I  80
|   C                     PARM '2'       P3702I  1
|   C                     PARM '1'       P3703I  1
|   C                     PARM 1         P3704I  40
|   C           KT502     PARM           P3711O  80
|   C*累計時數
| 12-C           *IN81     IFEQ '1'
|   C           *IN82     ANDEQ'1'
|   C           T503B     SUB  T503A     T503C
|   C           T503C     MULT 60        T504C
|   C                     ADD  T504B     T504C
|   C                     SUB  T504A     T504C
| 12-C                     END
|   C                     ADD  T504C     T504T            累計分
| 08-C                     ENDDO
|   C*計算總工時                                        調薪後
|   C           T504T     DIV  60        T504B            工作總時數
|   C                     MVR            T504C
|   C                     Z-ADDT504B     PW07M
| 13-C           T504C     IFGE 30
|   C                     ADD  0.5       PW07M
| 13-C                     END
| 07-C                     END
|    C*
37===C                     ENDSR
     C*==============================================================*
     C*   RTN063... 薪定項目
     C*==============================================================*
38===C           RTN063    BEGSR
|    C*系統設定
|    C                     Z-ADD1         K1      20
| 01-C           K1        DOWLEI1
|   C*
| 02-C           A1,K1     IFNE *BLANK
|   C           A2,K1     ANDNE*ZERO
|   C                     MOVELA1,K1     #WD05
|   C                     Z-ADDA2,K1     #WD06
|   C                     EXSR RTN250
| 02-C                     END
|   C*
|   C                     ADD  1         K1
| 01-C                     END
38===C                     ENDSR
     C*==============================================================*
     C*          RTN070... 時薪制MAIN PROCESS   *OF USE ONLY
     C*==============================================================*
39===C           RTN070    BEGSR
|    C*
|    C*GET時薪資料(PW05)
|    C*每小時時薪計算：*YH USE ONLY
|    C*（PY03０１本俸＋０３職務加給＋０２主管津貼＋１１午餐津貼＋
|    C*　１２交通津貼一＋１３交通津貼二）
|    C                     Z-ADD*ZERO     WBASE   70       時薪基準
|    C*
|    C                     Z-ADDPY03      WBASE            本俸
|    C                     Z-ADDPY03      PW05             底薪
|    C*
|    C           KEYPD     SETLLPD0
|    C           KEYPD     READEPD0                      46
| 01-C           *IN46     DOWEQ'0'
|   C*
| 02-C           PD03      IFEQ '02'
|   C           PD03      OREQ '03'
|   C           PD03      OREQ '11'
|   C           PD03      OREQ '12'
|   C           PD03      OREQ '13'
|   C                     ADD  PD04      WBASE
| 02-C                     END
|   C*
|   C           KEYPD     READEPD0                      46
| 01-C                     END
|    C*
|    C*以時薪計算者狀況有二：*YH USE ONLY
|    C*狀況一：按規定工作天數及時數換算出的時薪
|    C*狀況二：38572丁淑麗的時薪＝WBASE/(7.5*30)
| 03-C           EM02      IFEQ '38572   '
|   C           WBASE     DIV  7.5       WBASE
|   C                     DIV  30        WBASE     H
| 03*C                     ELSE
|   C           WBASE     DIV  PM101     WBASE
|   C                     DIV  PM102     WBASE     H
| 03-C                     END
|    C*
|    C*GET工作時數＆計算實領本薪--PW07
|    C           KEYWH     CHAINWH0                  40
| 04-C           *IN40     IFEQ '0'
|   C                     Z-ADDWH04      PW07             工作時數
| 04*C                     ELSE
|   C                     Z-ADD*ZERO     PW07
| 04-C                     END
|    C*
|    C                     Z-ADD*ZERO     PTIME   70
|    C           WBASE     MULT PW07      PTIME            　　
|    C                     Z-ADDPTIME     PW17    70       實領本薪
|    C*
|    C*團保費繳款金額
|    C                     EXSR RTN120
|    C*
|    C*健保費-PM319, PW24, PW25
|    C*先計算健保費：因需GET健保本人補助碼
|    C                     EXSR RTN147
|    C*
|    C*勞保費-PM310, PW19, PW22
|    C                     EXSR RTN145
|    C                     EXSR RTN063
|    C*
|    C*薪定項目---> PYWDPF
|    C                     MOVEL'01'      KWD05     P      薪資
|    C           KEYWD2    CHAINWD0                  40
| 05-C           *IN40     IFEQ '0'
|   C                     ADD  PTIME     WD06             薪資金額
|   C                     UPDATWD0
| 05*C                     ELSE
|   C                     MOVELPW01      WD01
|   C                     MOVELPW02      WD02
|   C                     Z-ADDWPW03     WD03
|   C                     MOVELPW04      WD04
|   C                     MOVEL'01'      WD05
|   C                     Z-ADDPTIME     WD06
|   C                     WRITEWD0
| 05-C                     END
|    C*
|    C*固定薪資所得稅(PW15, PM318)
|    C                     EXSR RTN170
|    C*
|    C                     Z-ADD$A8YMD    PW97
|    C                     TIME           WTIME  120
|    C                     MOVELWTIME     PW98
|    C                     MOVEL$USER     PW99
|    C*ADD BY JADI
|    C                     Z-ADDWPW03     PW03
|    C*
| 06-C           *IN48     IFEQ '1'
|   C                     WRITEPW0
| 06*C                     ELSE
|   C                     UPDATPW0
| 06-C                     END
|    C*
39===C           END070    ENDSR
     C*==============================================================*
     I/COPY PYRPGSRC,PYC02
     I/COPY PYRPGSRC,PYC03
