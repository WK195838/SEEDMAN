     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PY610                                      *
     A*    PROGRAM NAME :綜合福利金發放作業全頁查詢                *
     A*    AUTHOR       : A1274 EVA                                  *
     A*    CREATE DATE  : 90/04/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資管理系統                          *
     A****************************************************************
     FPYGDLF01IF  E           K        DISK
     FPYEMPF  IF  E           K        DISK
     FPT#YPF  IF  E           K        DISK
     FPYI610D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    S#01       35  7               部門代號
     E*---------------------------------------------------------------*
     I*DATA AREA
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      187 1920DDATE
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I                                      152 161 $USERN
     I*DEFIND STRAT KEY
     I            DS
     I                                        1   8 @KEYO
     I            DS
     I                                        1   8 @KEYN
     I                                        1   8 KGD02
     I            DS
     I                                        1 245 S#01
     I                                        1 245 CM202O
     I            DS
     I                                        1   7 EM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DGD01     PARM           I610I1  2        *公司別
     C           DGD04     PARM           I610I2  30       *福利年度
     C           KGD02     PARM           I610I3  8        *員工　
     C                     PARM           @RTNC   2          FUN KEY
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYGD     KLIST                           *MAIN FILE
     C                     KFLD           DGD01            *公司別
     C                     KFLD           KGD04   40       *申請年度
     C                     KFLD           KGD02            *員工　
     C           KEYGD1    KLIST                           *MAIN FILE
     C                     KFLD           DGD01            *公司別
     C                     KFLD           KGD04   40       *申請年度
     C           KEYGDW    KLIST                           *MAIN FILE
     C                     KFLD           DGD01            *公司別
     C                     KFLD           KGD04   40       *申請年度
     C*
     C                     MOVEL*BLANK    W#Y01  10
     C                     MOVEL*BLANK    W#Y02  10
     C           KEY#Y     KLIST                           *PT#YPF
     C                     KFLD           W#Y01
     C                     KFLD           W#Y02
     C*
     C           KEYEM     KLIST                           *MAIN FILE
     C                     KFLD           DGD01            *公司別
     C                     KFLD           GD02             *員工　
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
  01-C           @RTNC     DOWEQ*BLANK
    C                     EXSR R1000                      *SCR PROCESS
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     MOVEL*BLANK    @RTNC
|    C                     Z-ADD14        @PRCD            .PAGE SIZE
|    C*福利年度DGD04
|    C                     CALL 'P82'
|    C                     PARM DGD04     P8201I  40
|    C                     PARM '2'       P8202I  1
|    C                     PARM '2'       P8203I  1
|    C                     PARM '2'       P8204I  1
|    C                     PARM '1'       P8205I  1
|    C                     PARM           P8211O  40
|    C                     Z-ADDP8211O    KGD04
|    C*--------------------------------------------------------------*
|    C*檢核是否對全部的部門有權利並帶出管制的部門
|    C                     CALL 'PYSCM2'
|    C                     PARM DGD01     CM201I  4        公司
|    C                     PARM           CM201O  1        權利
|    C                     PARM           CM202O           部門代號
|    C*--------------------------------------------------------------*
|    C                     EXSR R1A00                      .SFL INIT
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....SCREEN  PROCESS
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     MOVEL'03'      @RTNC
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C                     MOVEL'12'      @RTNC
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN25     IFEQ '1'                        *NEXT PAGE
|   C                     EXSR R1N00
|   C                     GOTO E1000
| 03-C                     END
|    C*
| 04-C           @KEYO     IFNE @KEYN                      *START KEY
|   C                     EXSR R1A00                      *SFL INIT
|   C                     GOTO E1000
| 04-C                     END
|    C*
|    C                     EXSR R1C00                      *OPTION
|    C*
02===C           E1000     ENDSR
     C*=============================================================*
     C*          R1A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
03===C           R1A00     BEGSR
|    C*
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C                     MOVEL'N'       @BOTOM  1        *BOTTOM
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
|    C                     EXSR R1A20                      *SFL HEAD
|    C   99                GOTO E1A00
|    C*
|    C                     MOVEL@KEYN     @KEYO            *KEY SAVE
|    C                     MOVE *BLANK    DOPT
|    C*
| 01-C           KGD02     IFNE *BLANKS
|   C           KEYGD     SETLLGD0
| 01*C                     ELSE
|   C           KEYGD1    SETLLGD0
| 01-C                     END
|    C*
|    C                     EXSR R1A10                      *ADD SFL DTA
|    C*
| 02-C           RRN       IFGT 0
|   C                     SETON                     50    *SFLDSP
| 02*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 02-C                     END
|    C*
03===C           E1A00     ENDSR
     C*=============================================================*
     C*          R1A10 .... ADD SUBFILE DATA
     C*=============================================================*
04===C           R1A10     BEGSR
|    C*
|    C                     ADD  @PRCD     @DRCD
| 01-C           RRN       DOWLT@DRCD
|   C*
| 02-C           KGD02     IFNE *BLANKS
|   C           KEYGDW    READEGD0                      46
| 02*C                     ELSE
|   C           KEYGDW    READEGD0                      46
| 02-C                     END
|   C*
| 03-C           *IN46     IFEQ '0'
|   C*檢查部門權利
| 04-C           CM201O    IFNE 'Y'
|   C                     Z-ADD1         I       20
|   C           EM04      LOKUPS#01,I                   40
| 05-C           S#01,I    IFEQ *BLANK
|   C                     SETOF                         40
| 05-C                     END
|   C  N40                GOTO TAG1
| 04-C                     END
|   C*
|   C                     EXSR R1A11                      *WRITE SFL
| 03*C                     ELSE                            *EOF
|   C                     Z-ADDRRN       @DRCD
|   C                     MOVE 'Y'       @BOTOM
|   C                     SETON                     9653  *SFLEND
| 03-C                     END
|   C           TAG1      TAG
|   C*
| 01-C                     END
|    C*
|    C                     Z-ADDRRN       DRRN
|    C*
04===C                     ENDSR
     C*=============================================================*
     C*          R1A11 .... WRITE SUBFILE DATA
     C*=============================================================*
05===C           R1A11     BEGSR
|    C*
|    C* << SUBFILE DATA VALUE SET >>
|    C*申請項目GD07
|    C                     MOVEL'GRANT'   W#Y01     P
|    C                     MOVELGD07      W#Y02     P
|    C           KEY#Y     CHAIN#Y0                  40
| 01-C           *IN40     IFEQ '1'
|   C                     MOVEL*BLANK    GD07N
| 01*C                     ELSE
|   C                     MOVEL#Y03      GD07N
| 01-C                     END
|    C*
|    C*員工姓名
|    C           KEYEM     CHAINEM0                  40
| 02-C           *IN40     IFEQ '0'
|   C                     MOVELEM03      GD021     P
| 02*C                     ELSE
|   C                     MOVEL*BLANK    GD021
| 02-C                     END
|    C*
|    C*核發日期
|    C                     CALL 'P31'
|    C                     PARM GD06      P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM DFMT      P3104I  1
|    C                     PARM DTYPE     P3105I  1
|    C                     PARM           P3111O  80
|    C                     Z-ADDP3111O    DGD06
|    C*申請年月GD09
|    C                     CALL 'P39'
|    C                     PARM GD09      P3901I  60
|    C                     PARM '2'       P3902I  1
|    C                     PARM '1'       P3903I  1
|    C                     PARM DFMT      P3904I  1
|    C                     PARM DTYPE     P3905I  1
|    C                     PARM           P3911O  60
|    C                     Z-ADDP3911O    DGD09            *計薪年月
|    C*
|    C                     ADD  1         RRN
|    C                     WRITESFLSR
|    C*
05===C                     ENDSR
     C*=============================================================*
     C*          R1A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
06===C           R1A20     BEGSR
|    C*
|    C*公司檢核
| 01-C           DGD01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO E1A20
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DGD01     CM001I  2         公司代號
|    C           DGD01N    PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 02-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 02-C                     END
|    C   99                GOTO E1A20
|    C*
| 03-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 03-C                     END
|    C   99                GOTO E1A20
|    C*
06===C           E1A20     ENDSR
     C*=============================================================*
     C*          R1C00 ....挑選資料
     C*=============================================================*
07===C           R1C00     BEGSR
|    C*
|    C                     READCSFLSR                  4242
| 01-C           *IN42     DOWEQ'0'
|   C*
| 02-C           DOPT      IFNE *BLANK
|   C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
|   C                     MOVELGD01      I610I1
|   C                     MOVELGD04      I610I2
|   C                     MOVELGD02      I610I3
|   C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|   C                     MOVEL'00'      @RTNC
|   C                     SETON                     42
| 02-C                     END
|   C*
|   C  N42                READCSFLSR                    42
| 01-C                     END
|    C*
07===C                     ENDSR
     C*=============================================================*
     C*          R1N00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
08===C           R1N00     BEGSR
|    C*
| 01-C           @BOTOM    IFEQ 'Y'                        *EOF
|   C*                    SETON                         96*LST PAG
|   C                     MOVEL'CPF5203' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
|   C                     Z-ADDRRN       DRRN
| 01*C                     ELSE
|   C                     EXSR R1A10                      *ADD SFL DTA
| 01-C                     END
|    C*
08===C                     ENDSR
     C*==============================================================*
