     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     A*    PROGRAM-ID   :PYI223                                      *
     A*    PROGRAM NAME :獎懲資料查詢　　                          *
     A*    AUTHOR       :A1336 KLAUS                                 *
     A*    CREATE DATE  :91/07/26                                    *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     F****************************************************************
     FPYXXPF  IF  E           K        DISK
     FPYEMPF  IF  E           K        DISK
     FPT#YPF  IF  E           K        DISK
     FPYI223D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    S#01       35  7               部門代號
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      152 161 $USERN
     I                                      211 2160DDATE
     I                                      201 2080$A8YMD
     I*DEFIND STRAT KEY
     I           IDS
     I                                        1  17 @KXXO
     I           IDS
     I                                        1  17 @KXXN
     I                                        1   8 DXX04
     I                                        9  150DXX05
     I                                       16  17 DXX06
     I            DS
     I                                        1   7 XX02
     I                                        1   1 XX021
     I                                        2   2 XX022
     I                                        3   3 XX023
     I                                        4   7 XX024
     I            DS
     I                                        1 245 S#01
     I                                        1 245 CM202O
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C           *LIKE     DEFN XX05      WXX05
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DXX01     PARM           I223I1  2         公司編號
     C           DXX04     PARM           I223I2  8         員工編號
     C           DXX05     PARM           I223I3  70        獎懲日期
     C           DXX06     PARM           I223I4  2         獎懲代碼
     C           XX06N     PARM           I223I5  9         獎懲代碼
     C                     PARM           @RTNC   2          FUN KEY
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C*
     C           KEYXX     KLIST                           *PYLVPF
     C                     KFLD           DXX01
     C                     KFLD           DXX04
     C                     KFLD           WXX05
     C                     KFLD           DXX06
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           XX01
     C                     KFLD           XX04
     C           KEY#Y     KLIST                           *PYEMPF
     C                     KFLD           #Y01
     C                     KFLD           #Y02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
     C           @RTNC     DOWEQ*BLANK
     C                     EXSR R1000                      *SCR PROCESS
     C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C* INIT VALUE SET *---------------------------------------------*
     C                     MOVEL*BLANK    @RTNC
     C                     Z-ADD14        @PRCD            .PAGE SIZE
|    C*--------------------------------------------------------------*
     C*檢核是否對全部的部門有權利並帶出管制的部門
     C                     CALL 'PYSCM2'
     C                     PARM DXX01     CM201I  4        公司
     C                     PARM           CM201O  1        權利
     C                     PARM           CM202O           部門代號
     C*　　　　　　　
     C*--------------------------------------------------------------*
     C                     EXSR R1A00                      .SFL INIT
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1000 ....SCREEN  PROCESS
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C                     WRITEDSPC1
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C                     Z-ADD*ZERO     #LIN
     C                     Z-ADD*ZERO     #COL
     C*
     C           *IN03     IFEQ '1'                        *END
     C                     MOVEL'03'      @RTNC
     C                     GOTO E1000
     C                     END
     C*
     C           *IN12     IFEQ '1'                        *PREVIOUS
     C                     MOVEL'12'      @RTNC
     C                     GOTO E1000
     C                     END
     C*
     C           *IN25     IFEQ '1'                        *NEXT PAGE
     C                     EXSR R1N00
     C                     GOTO E1000
     C                     END
     C*
     C           @KXXO     IFNE @KXXN                      *START KEY
     C                     EXSR R1A00                      *SFL INIT
     C                     GOTO E1000
     C                     END
     C*
     C                     EXSR R1C00                      *OPTION
     C*
     C           E1000     ENDSR
     C*=============================================================*
     C*          R1A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
     C           R1A00     BEGSR
     C*
     C                     Z-ADD0         DRRN
     C                     Z-ADD0         RRN
     C                     Z-ADD0         @DRCD            *DSP RCD
     C                     MOVEL'N'       @BOTOM  1        *BOTTOM
     C*
     C                     SETOF                     51    *SFLDSPCTL
     C                     SETON                     52    *SFLCLR
     C                     WRITESFLCR
     C                     SETOF                     52
     C*
     C                     SETON                     51    *SFLDSPCTL
     C                     SETOF                     5053  *SFLDSP,END
     C*
     C                     EXSR R1A20                      *SFL HEAD
     C   99                GOTO E1A00
     C* <<SFL INITIAITAL DATA ADD>>
     C                     MOVEL@KXXN     @KXXO            *KEY SAVE
     C*
     C           KEYXX     SETLLXX0
     C                     EXSR R1A10                      *ADD SFL DTA
     C*
     C*
     C           RRN       IFGT 0
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     99    *NO DTA
     C                     MOVEL'UPT2050' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C           E1A00     ENDSR
     C*=============================================================*
     C*          R1A10 .... ADD SUBFILE DATA
     C*=============================================================*
     C           R1A10     BEGSR
     C*
     C                     ADD  @PRCD     @DRCD
     C           RRN       DOWLT@DRCD
     C*
     C           DXX01     READEXX0                      46
     C           *IN46     IFEQ '0'
     C*
     C                     MOVEL*BLANK    EM03
|   C           KEYEM     CHAINEM0                  40
     C*檢查部門權利
     C           CM201O    IFNE 'Y'
     C                     Z-ADD1         I       20
     C           XX02      LOKUPS#01,I                   40
     C           S#01,I    IFEQ *BLANK
     C                     SETOF                         40
     C                     END
     C  N40                GOTO TAG1
     C                     END
|   C*
     C                     EXSR R1A11                      *WRITE SFL
     C                     ELSE                            *EOF
     C                     Z-ADDRRN       @DRCD
     C                     MOVE 'Y'       @BOTOM
     C                     SETON                     9653  *SFLEND
     C                     END
     C*
     C           TAG1      TAG
     C                     END
     C*
     C                     Z-ADDRRN       DRRN
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1A11 .... WRITE SUBFILE DATA
     C*=============================================================*
     C           R1A11     BEGSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* << SUBFILE DATA VALUE SET >>
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVEL*BLANK    XX06N
     C                     MOVEL'XX06'    #Y01
     C                     MOVELXX06      #Y02
|   C           KEY#Y     CHAIN#Y0                  40
|   C           *IN40     IFEQ '0'
|    C                     CALL 'P65'
|    C                     PARM #Y03      P6501I 80
|    C                     PARM 9         P6502I  30
|    C           XX06N     PARM           P6511O 80
|    C                     PARM           P6512O 80
|   C                     END
     C*
     C                     ADD  1         RRN
     C*
     C                     CALL 'P31'
     C                     PARM XX05      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM DFMT      P3104I  1
     C                     PARM DTYPE     P3105I  1
     C           XX05D     PARM           P3101O  80
     C*
     C                     WRITESFLSR
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
     C           R1A20     BEGSR
     C*
     C* << CHACK 公司別NOT *BLANK >>
     C                     MOVEL*BLANK    XX01N
     C           DXX01     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6099
     C                     END
     C   99                GOTO E1A20
     C*
     C                     CALL 'PYSCM0'
     C                     PARM DXX01     CM001I  2         公司代號
     C           XX01N     PARM           CM002O 22         公司名稱
     C                     PARM           CM003O 32         代碼說明
     C           DFMT      PARM           CM004O  1         日期格式
     C           DTYPE     PARM           CM005O  1         日期曆別
     C                     PARM           CM098O 50         備用欄
     C                     PARM           CM099O  1         存在否？
     C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
     C           CM099O    IFEQ 'N'
     C                     SETON                     6099
     C                     MOVEL'US#0099' ERRID
     C                     MOVEL'S#MF'    ERRF
     C                     END
     C   99                GOTO E1A20
     C*
     C           CM099O    IFEQ *BLANK
     C                     SETON                     6099
     C                     MOVEL'UPT2010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C   99                GOTO E1A20
|    C*
     C                     CALL 'P31'
     C                     PARM DXX05     P3101I  80
     C                     PARM DFMT      P3102I  1
     C                     PARM DTYPE     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           WXX05     PARM           P3101O  80
|    C*
     C           E1A20     ENDSR
     C*=============================================================*
     C*          R1C00 ....挑選資料
     C*=============================================================*
     C           R1C00     BEGSR
     C*
     C                     READCSFLSR                  4242
     C           *IN42     DOWEQ'0'
     C*
     C           DOPT      IFNE *BLANK
     C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVELXX01      I223I1
     C                     MOVELXX04      I223I2
     C                     MOVELXX05D     I223I3
     C                     MOVELXX06      I223I4
     C                     MOVELXX06N     I223I5
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVEL'00'      @RTNC
     C                     SETON                     42
     C                     END
     C*
     C  N42                READCSFLSR                    42
     C                     END
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1N00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
     C           R1N00     BEGSR
     C*
     C           @BOTOM    IFEQ 'Y'                        *EOF
     C                     SETON                         96*LST PAG
     C                     Z-ADDRRN       DRRN
     C                     ELSE
     C                     EXSR R1A10                      *ADD SFL DTA
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
