     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     A*    PROGRAM-ID   :PYI202                                      *
     A*    PROGRAM NAME :請假資料查詢　　                          *
     A*    AUTHOR       :A1232 ERIC                                  *
     A*    CREATE DATE  :86/01/20                                    *
     A*    UPDATE DATE  :91/06/19 A1336 KLAUS                        *
     A*    SYSTEM       :人事薪資                                  *
     F****************************************************************
     FPYLVPF  IF  E           K        DISK
     FPYLVLF08IF  E           K        DISK
     F            LV0                               KRENAMELVL8
     FPYLVLF09IF  E           K        DISK
     F            LV0                               KRENAMELVL9
     FPYEMPF  IF  E           K        DISK
     FPYLCPF  IF  E           K        DISK
     FPYI202D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    S#01       35  7               部門代號
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      152 161 $USERN
     I                                      187 1920DDATE
     I                                      201 2080$A8YMD
     I*DEFIND STRAT KEY
     I           IDS
     I                                        1  16 @KLVO
     I           IDS
     I                                        1  16 @KLVN
     I                                        1   8 DLV02
     I                                        9  150DLV03
     I                                       16  16 DLV04
     I            DS
     I                                        1   7 EM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     I            DS
     I                                        1 245 S#01
     I                                        1 245 CM202O
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C           *LIKE     DEFN LV03      WLV03
     C           *LIKE     DEFN LV05      WLV05
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DLV01     PARM           I202I1  2         公司編號
     C           DLV02     PARM           I202I2  8         員工編號
     C           DLV03     PARM           I202I3  70        起始日期
     C           DLV04     PARM           I202I4  1         假別　　
     C                     PARM           @RTNC   2          FUN KEY
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C*
     C           KEYLV     KLIST                           *PYLVPF
     C                     KFLD           DLV01            公司編號
     C                     KFLD           DLV02            員工編號
     C                     KFLD           WLV03            起始日期
     C                     KFLD           DLV04            假別
     C           KEYLV8    KLIST                           *PYLVPF
     C                     KFLD           DLV01            公司編號
     C                     KFLD           DLV04            假別　　
     C           KEYLV9    KLIST                           *PYLVPF
     C                     KFLD           DLV01            公司編號
     C                     KFLD           WLV03            起始日期
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           LV01
     C                     KFLD           LV02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
     C           @RTNC     DOWEQ*BLANK
     C                     EXSR R1000                      *SCR PROCESS
     C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C* INIT VALUE SET *---------------------------------------------*
     C                     MOVEL*BLANK    @RTNC
     C                     Z-ADD14        @PRCD            .PAGE SIZE
|    C*--------------------------------------------------------------*
     C*檢核是否對全部的部門有權利並帶出管制的部門
     C                     CALL 'PYSCM2'
     C                     PARM DLV01     CM201I  4        公司
     C                     PARM           CM201O  1        權利
     C                     PARM           CM202O           部門代號
     C*　　　　　　　
     C*--------------------------------------------------------------*
     C                     EXSR R1A00                      .SFL INIT
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1000 ....SCREEN  PROCESS
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C                     WRITEDSPC1
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C                     Z-ADD*ZERO     #LIN
     C                     Z-ADD*ZERO     #COL
     C*
     C           *IN03     IFEQ '1'                        *END
     C                     MOVEL'03'      @RTNC
     C                     GOTO E1000
     C                     END
     C*
     C           *IN12     IFEQ '1'                        *PREVIOUS
     C                     MOVEL'12'      @RTNC
     C                     GOTO E1000
     C                     END
     C*
     C           *IN25     IFEQ '1'                        *NEXT PAGE
     C                     EXSR R1N00
     C                     GOTO E1000
     C                     END
     C*
     C           @KLVO     IFNE @KLVN                      *START KEY
     C                     EXSR R1A00                      *SFL INIT
     C                     GOTO E1000
     C                     END
     C*
     C                     EXSR R1C00                      *OPTION
     C*
     C           E1000     ENDSR
     C*=============================================================*
     C*          R1A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
     C           R1A00     BEGSR
     C*
     C                     Z-ADD0         DRRN
     C                     Z-ADD0         RRN
     C                     Z-ADD0         @DRCD            *DSP RCD
     C                     MOVEL'N'       @BOTOM  1        *BOTTOM
     C*
     C                     SETOF                     51    *SFLDSPCTL
     C                     SETON                     52    *SFLCLR
     C                     WRITESFLCR
     C                     SETOF                     52
     C*
     C                     SETON                     51    *SFLDSPCTL
     C                     SETOF                     5053  *SFLDSP,END
     C*
     C                     EXSR R1A20                      *SFL HEAD
     C   99                GOTO E1A00
     C* <<SFL INITIAITAL DATA ADD>>
     C                     MOVEL@KLVN     @KLVO            *KEY SAVE
     C*
| 01-C           DLV02     IFEQ *BLANK                     員工編號
| 02-C           DLV03     IFEQ *ZERO                      起始日期
| 03-C           DLV04     IFEQ *BLANK                     假別
|   C           *LOVAL    SETLLLV0
| 03*C                     ELSE
|   C           KEYLV8    SETLLLVL8
| 03-C                     END
     C*
| 02*C                     ELSE
|   C           KEYLV9    SETLLLVL9
| 02-C                     END
     C*
| 04*C                     ELSE
     C           KEYLV     SETLLLV0
| 01-C                     END
     C                     EXSR R1A10                      *ADD SFL DTA
     C*
     C           RRN       IFGT 0
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     99    *NO DTA
     C                     MOVEL'UPT2050' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C           E1A00     ENDSR
     C*=============================================================*
     C*          R1A10 .... ADD SUBFILE DATA
     C*=============================================================*
     C           R1A10     BEGSR
     C*
     C                     ADD  @PRCD     @DRCD
     C           RRN       DOWLT@DRCD
     C*
| 02-C           DLV02     IFEQ *BLANK                     員工編號
| 03-C           DLV03     IFEQ *ZERO                      起始日期
| 04-C           DLV04     IFEQ *BLANK                     假別
|   C           DLV01     READELV0                      46
| 04*C                     ELSE
|   C           DLV01     READELVL8                     46
| 04-C                     END
|   C*
| 03*C                     ELSE
|   C           DLV01     READELVL9                     46
| 03-C                     END
|   C*
| 02*C                     ELSE
     C           DLV01     READELV0                      46
| 02-C                     END
|   C*
|   C*
     C           *IN46     IFEQ '0'
     C*
     C                     MOVEL*BLANK    EM03
|   C           KEYEM     CHAINEM0                  40
     C*檢查部門權利
     C           CM201O    IFNE 'Y'
     C                     Z-ADD1         I       20
     C           EM04      LOKUPS#01,I                   40
     C           S#01,I    IFEQ *BLANK
     C                     SETOF                         40
     C                     END
     C  N40                GOTO TAG1
     C                     END
|   C*
     C                     EXSR R1A11                      *WRITE SFL
     C                     ELSE                            *EOF
     C                     Z-ADDRRN       @DRCD
     C                     MOVE 'Y'       @BOTOM
     C                     SETON                     9653  *SFLEND
     C                     END
     C*
     C           TAG1      TAG
     C                     END
     C*
     C                     Z-ADDRRN       DRRN
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1A11 .... WRITE SUBFILE DATA
     C*=============================================================*
     C           R1A11     BEGSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* << SUBFILE DATA VALUE SET >>
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVEL*BLANK    LC02
|   C           LV04      CHAINLC0                  40
     C                     ADD  1         RRN
     C                     CALL 'P31'
     C                     PARM LV03      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM DFMT      P3104I  1
     C                     PARM DTYPE     P3105I  1
     C           LV03D     PARM           P3101O  80
     C*
     C                     CALL 'P31'
     C                     PARM LV05      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM DFMT      P3104I  1
     C                     PARM DTYPE     P3105I  1
     C           LV05D     PARM           P3101O  80
     C                     WRITESFLSR
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
     C           R1A20     BEGSR
     C*
     C* << CHACK 公司別NOT *BLANK >>
     C                     MOVEL*BLANK    DLV01N
     C           DLV01     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6099
     C                     END
     C   99                GOTO E1A20
     C*
     C                     CALL 'PYSCM0'
     C                     PARM DLV01     CM001I  2         公司代號
     C           DLV01N    PARM           CM002O 22         公司名稱
     C                     PARM           CM003O 32         代碼說明
     C           DFMT      PARM           CM004O  1         日期格式
     C           DTYPE     PARM           CM005O  1         日期曆別
     C                     PARM           CM098O 50         備用欄
     C                     PARM           CM099O  1         存在否？
     C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
     C           CM099O    IFEQ 'N'
     C                     SETON                     6099
     C                     MOVEL'US#0099' ERRID
     C                     MOVEL'S#MF'    ERRF
     C                     END
     C   99                GOTO E1A20
     C*
     C           CM099O    IFEQ *BLANK
     C                     SETON                     6099
     C                     MOVEL'UPT2010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C   99                GOTO E1A20
|    C*
     C                     CALL 'P31'
     C                     PARM DLV03     P3101I  80
     C                     PARM DFMT      P3102I  1
     C                     PARM DTYPE     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           WLV03     PARM           P3101O  80
|    C*
     C           E1A20     ENDSR
     C*=============================================================*
     C*          R1C00 ....挑選資料
     C*=============================================================*
     C           R1C00     BEGSR
     C*
     C                     READCSFLSR                  4242
     C           *IN42     DOWEQ'0'
     C*
     C           DOPT      IFNE *BLANK
     C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVELLV01      I202I1
     C                     MOVELLV02      I202I2
     C                     MOVELLV03D     I202I3
     C                     MOVELLV04      I202I4
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVEL'00'      @RTNC
     C                     SETON                     42
     C                     END
     C*
     C  N42                READCSFLSR                    42
     C                     END
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1N00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
     C           R1N00     BEGSR
     C*
     C           @BOTOM    IFEQ 'Y'                        *EOF
     C                     SETON                         96*LST PAG
     C                     Z-ADDRRN       DRRN
     C                     ELSE
     C                     EXSR R1A10                      *ADD SFL DTA
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
