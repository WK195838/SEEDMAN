     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    PROGRAM ID   : PYE901                                     *
     A*    AUTHOR       : EASON                                      *
     A*    DATE WRITTEN : 97/04/01                                   *
     A*    SYSTEM       :人事薪資                                  *
     A*    SUBSYSTEM    :主檔資料維護子系統                        *
     A*    PROCEDURE    :健保補助資料維護 (A/U/D/I)                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     FPYE9PF  UF  E           K        DISK                      A
     F                                              KINFSR ERRSR
     FPYE901D CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
     F                                              KINFDS DSPFDS
     E*: FUNCTION DATA :
     E                    WFUN    5   5  6
     E*: A/U/D/I AUT
     E                    WA01        5  1
     I*
     ISDS        SDS
     I                                     *PROGRAM APPPGM
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      152 161 $USERN
     I                                      111 1160$CHYMD
     I                                      187 1920$EGYMD
 @   I                                      135 135 $ADD
 @   I                                      136 136 $UPD
 @   I                                      137 137 $DLT
 @   I                                      138 138 $INQ
 @   I                                      139 139 $RMK01
M001AI                                      201 2080$EG8
     I                                      598 5990$LPRT
     I                                      501 505 $WRHUS
     I                                      506 506 $PURID
     I                                      507 508 $TRNID
     I                                      509 513 $APAUT
     I                                      514 521 $#101
     I            DS
     I                                        1   1 DOPT1
     I                                        1   10WNUM
     I* FOR F4 HELP -- GET CURSOR POSITION
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     C*==============================================================*
     C*                    DEFINE ROUTINE
     C*==============================================================*
     C                     Z-ADD0         HRRN    40
     C                     MOVE *BLANKS   APPSCR  6
     C                     MOVE *BLANK    PARMI1 10
     C                     MOVE *BLANK    PARMO1 10
     C           *LIKE     DEFN E901      OBGN1            *START KEY
     C*  FOR SUBFILE (SYS+PGM CONTROL)
     C                     MOVE *BLANK    WBOTOM  1
     C           *LIKE     DEFN RRN       WRRN
     C           *LIKE     DEFN RRN       WPRCD
     C           *LIKE     DEFN RRN       DSPRCD
     C*==============================================================*
     C*                    KLIST
     C*==============================================================*
     C           KEYE9     KLIST
     C                     KFLD           E901
     C           KEYE91    KLIST
     C                     KFLD           DBGN1
     C*==============================================================*
     C*                    MGIN ROUTINE
     C*==============================================================*
     C                     EXSR RTN010                     *INIT ENVIRMENT
     C                     EXSR RTN191                     *INIT SFL
     C*
     C                     MOVE *ALL'0'   *IN,60
     C                     EXSR RTN192                     *READ NEXT PAGE
     C                     EXSR RTN195                     *EXFMT
     C*
  01-C           *IN03     DOWEQ'0'                        *03    OFF
    C*
  02-C           *IN06     CASEQ'1'       RTN120
    C           *IN25     CASEQ'1'       RTN192
    C                     CAS            RTN170
  02-C                     END
    C  N03                EXSR RTN195
    C*
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL VALUE
     C*==============================================================*
01===C           RTN010    BEGSR
|    C*
|    C                     MOVE $ADD      WA01,1
|    C                     MOVE $UPD      WA01,2
|    C                     MOVE $ADD      WA01,3
|    C                     MOVE $DLT      WA01,4
|    C                     MOVE $INQ      WA01,5
|    C                     MOVE 'SCR001'  APPSCR
|    C*
01===C                     ENDSR
      *==============================================================*
      *          RTN120....F6(ADD)
      *==============================================================*
02===C           RTN120    BEGSR
|    C*
|    C                     MOVE '1'       DOPT1
| 01-C           WA01,WNUM IFNE 'Y'                         WNUM = DOPT
|   C                     SETON                     9099
| 01-C                     END
|    C  N99                EXSR RTN200
| 02-C           DRRN      IFGT 0
|   C           DRRN      SUB  1         HRRN
| 02-C                     END
|    C                     EXSR RTN191
|    C                     EXSR RTN192
|    C                     SETOF                     12
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTN170....ENTER
      *==============================================================*
03===C           RTN170    BEGSR
|    C*
| 01-C           DBGN1     IFNE OBGN1
|   C                     Z-ADD0         HRRN
|   C                     EXSR RTN191
|   C                     EXSR RTN192
|   C                     MOVE DBGN1     OBGN1
| 01*C                     ELSE
|   C                     EXSR RTN171
| 02-C           *IN99     IFEQ '0'
|   C                     Z-ADDRRN       HRRN
|   C                     EXSR RTN191
|   C                     EXSR RTN192
| 02-C                     END
| 01-C                     END
|    C*
03===C                     ENDSR
      *==============================================================*
      *          RTN171....READC SFL
      *==============================================================*
04===C           RTN171    BEGSR
|    C*
|    C                     SETON                     54
|    C*
|    C                     READCSFLSR                  4040*NF
| 01-C           *IN40     DOWEQ'0'                        *40 LOOP
|   C*
|   C                     SETOF                     60    *DOP *IND
| 02-C           *IN99     IFEQ '0'
|   C*
| 03-C           DOPT1     IFNE *BLANK
|   C                     EXSR RTN180                     *CHK
| 04-C           *IN99     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C                     Z-ADDRRN       DRRN
|   C                     EXSR RTN200
|   C                     MOVE *BLANKS   DOPT1
| 04-C                     END
| 03-C                     END
| 02-C                     END                             *IN99
|   C*
|   C           UPD       TAG
|   C*
|   C                     UPDATSFLSR
|   C                     READCSFLSR                    40*NF
|   C*
| 01-C                     END                             *40 END
| 05-C           *IN99     IFEQ '0'
|   C                     SETOF                     5412
|   C                     SETON                     98
| 05-C                     END
|    C*
04===C                     ENDSR
      *==============================================================*
      *          RTN180....CHECK 'SC01' SCREEN
      *==============================================================*
05===CSR         RTN180    BEGSR
|    C*
|    C* CHK AUT *
| 01-C           WA01,WNUM IFNE 'Y'                         WNUM = DOPT
|   C                     SETON                     609099
| 01-C                     END
|    C   99                GOTO END180
|    C*
05===C           END180    ENDSR
      *=========================================================*
      *          RTN191....INITIAL                              *
      *=========================================================*
06===C           RTN191    BEGSR
|    C*
|    C                     MOVE *BLANKS   DOPT1
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         WRRN
|    C                     Z-ADD10        WPRCD
|    C                     Z-ADD0         DSPRCD
|    C           HRRN      DIV  WPRCD     DSPRCD
|    C                     MULT WPRCD     DSPRCD
|    C                     MOVE 'N'       WBOTOM
|    C                     SETOF                     53
|    C                     SETON                     5254
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C           KEYE91    SETLLE90
|    C*
06===C                     ENDSR
      *=========================================================*
      *          RTN192 .. ROLLUP MGIN ROUTINE
      *=========================================================*
07===C           RTN192    BEGSR
|    C*
| 01-C           WBOTOM    IFEQ 'Y'
|   C                     SETON                     97    *LST PAG
|   C                     Z-ADDRRN       DRRN
| 01*C                     ELSE
|   C                     EXSR RTN193
| 01-C                     END
|    C*
07===C                     ENDSR
      *=========================================================*
      *          RTN193 .. ADD SUBFILE DATA
      *=========================================================*
08===C           RTN193    BEGSR
|    C*
|    C                     MOVE *BLANK    DOPT1
|    C                     SETOF                     54
|    C                     Z-ADDWRRN      RRN
|    C                     ADD  WPRCD     DSPRCD
| 01-C           RRN       DOWLTDSPRCD
|   C                     READ E90                      46
|   C*
| 02-C           *IN46     IFEQ '0'
|   C                     ADD  1         RRN
|   C*
|   C*
|   C                     WRITESFLSR
| 02*C                     ELSE
|   C                     Z-ADDRRN       DSPRCD
|   C                     MOVE 'Y'       WBOTOM
|   C                     SETON                     53
| 02-C                     END
|   C*
| 01-C                     ENDDO
      *預讀一筆在將指標帶回
|   C                     READ E90                      46
|   C           E901      SETLLE90
|    C*
|    C           *IN46     IFEQ '1'
|   C                     SETON                     53
|    C                     ENDIF
|    C*
|    C                     Z-ADDRRN       DRRN
|    C                     Z-ADDRRN       WRRN
|    C*
08===C                     ENDSR
      *==============================================================*
      *          RTN195... EXFMT SFL                                 *
      *==============================================================*
09===C           RTN195    BEGSR
|    C*
| 01-C           RRN       IFGT *ZEROS
|   C                     SETON                     50    *SFLDSP
| 01*C                     ELSE
|   C                     SETON                     96    *NODATA
| 01-C                     END
|    C*
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     SETOF                     5051
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
09===C                     ENDSR
      *==============================================================*
      *          RTN200....'SC02' SCREEN MGIN ROUTINE
      *==============================================================*
10===C           RTN200    BEGSR
|    C*
|    C                     MOVE 'SCR002'  APPSCR
|    C                     MOVE WFUN,WNUM DFUN
| 01-C           DOPT1     CASEQ'1'       RTN210           *ADD
|   C           DOPT1     CASEQ'2'       RTN220           *UPD
|   C           DOPT1     CASEQ'3'       RTN225           *CPY
|   C           DOPT1     CASEQ'4'       RTN230           *DEL
|   C           DOPT1     CASEQ'5'       RTN240           *INQ
| 01-C                     END
|    C           *LOVAL    SETLLE90
|    C*
10===C                     ENDSR
      *==============================================================*
      *          RTN210....ADD DATA
      *==============================================================*
11===C           RTN210    BEGSR
|    C*
|    C                     SETON                     3031  *ADD MSG
|    C                     EXSR RTN251                     *CLR FIELD
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           *IN12     ANDEQ'0'
|   C           APPSCR    ANDEQ'SCR002'
|   C                     EXFMTDSPD2
|   C                     MOVEA*ALL'0'   *IN,60
|   C                     Z-ADD0         D#ROW
|   C                     Z-ADD0         D#COL
| 02-C           *IN03     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C*
| 03-C           *IN04     CASEQ'1'       RTNF4
|   C                     CAS            RTN280
| 03-C                     END
|   C*
| 04-C           *IN99     IFEQ '0'
|   C           *IN05     ANDEQ'0'
|   C           *IN04     ANDEQ'0'
|   C                     EXSR RTN253
|   C                     WRITEE90
|   C                     MOVE 'SCR001'  APPSCR
| 04-C                     END
|   C*
| 02-C                     END
|   C*
| 01-C                     END
|    C                     SETON                     98
|    C*
11===C                     ENDSR
      *==============================================================*
      *          RTN220....UPD DATA
      *==============================================================*
12===C           RTN220    BEGSR
|    C*
|    C           KEYE9     CHAINE90                  40
|    C                     EXSR RTN252                     *FIEL->SCRN
| 01-C           *IN03     DOWEQ'0'
|   C           *IN12     ANDEQ'0'
|   C           APPSCR    ANDEQ'SCR002'
|   C*
|   C                     SETOF                     30
|   C                     SETON                     31    *CAN INPUT
|   C                     MOVELE901      WE901   1
|   C                     EXFMTDSPD2
|   C                     MOVEA*ALL'0'   *IN,60
|   C                     Z-ADD0         D#ROW
|   C                     Z-ADD0         D#COL
|   C*
| 02-C           *IN03     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C*
| 03-C           *IN04     CASEQ'1'       RTNF4
|   C                     CAS            RTN280
| 03-C                     END
|   C*
| 04-C           *IN99     IFEQ '0'
|   C           *IN05     ANDEQ'0'
|   C           *IN04     ANDEQ'0'
|   C                     EXSR RTN253
|   C                     UPDATE90
|   C                     MOVE 'SCR001'  APPSCR
| 04-C                     END
| 02-C                     END
|   C*
| 01-C                     END
|    C*
12===C                     ENDSR
      *==============================================================*
      *          RTN225....COPY ADD DATA
      *==============================================================*
13===C           RTN225    BEGSR
|    C*
|    C                     SETON                     3031  *ADD MSG
|    C           KEYE9     CHAINE90                  40
|    C                     EXSR RTN254                     *FIEL->SCRN
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           *IN12     ANDEQ'0'
|   C           APPSCR    ANDEQ'SCR002'
|   C                     EXFMTDSPD2
|   C                     MOVEA*ALL'0'   *IN,60
|   C                     Z-ADD0         D#ROW
|   C                     Z-ADD0         D#COL
| 02-C           *IN03     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C*
| 03-C           *IN04     CASEQ'1'       RTNF4
|   C                     CAS            RTN280
| 03-C                     END
|   C*
| 04-C           *IN99     IFEQ '0'
|   C           *IN05     ANDEQ'0'
|   C           *IN04     ANDEQ'0'
|   C                     EXSR RTN253
|   C                     WRITEE90
|   C                     MOVE 'SCR001'  APPSCR
| 04-C                     END
|   C*
| 02-C                     END
|   C*
| 01-C                     END
|    C*
13===C                     ENDSR
      *==============================================================*
      *          RTN240....INQ DATA
      *==============================================================*
14===C           RTN240    BEGSR
|    C*
|    C           KEYE9     CHAINE90                  40
|    C                     EXSR RTN252                     *FIEL->SCRN
|    C                     SETOF                     3031  *NO INPUT
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           *IN12     ANDEQ'0'
|   C           APPSCR    ANDEQ'SCR002'
|   C*
|   C                     EXFMTDSPD2
|   C                     Z-ADD0         D#ROW
|   C                     Z-ADD0         D#COL
|   C                     MOVEA*ALL'0'   *IN,60
|   C*
| 02-C           *IN03     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C           *IN04     ANDEQ'0'
|   C                     MOVE 'SCR001'  APPSCR
|   C                     Z-ADDRRN       DRRN
| 02-C                     END
|   C*
| 01-C                     END
|    C*
14===C                     ENDSR
      *==============================================================*
      *          RTN230....DELETE DATA
      *==============================================================*
15===C           RTN230    BEGSR
|    C*
|    C           KEYE9     CHAINE90                  40
|    C                     EXSR RTN252                     *FIEL->SCRN
|    C                     SETOF                     3031  *NO INPUT
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           *IN12     ANDEQ'0'
|   C           APPSCR    ANDEQ'SCR002'
|   C                     EXFMTDSPD2
|   C                     Z-ADD0         D#ROW
|   C                     Z-ADD0         D#COL
|   C                     MOVEA*ALL'0'   *IN,60
|   C*
| 02-C           *IN03     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C           *IN04     ANDEQ'0'
|   C                     CALL 'WDS000'
|   C                     PARM 'N'       DLTFLG  1
| 03-C           DLTFLG    IFEQ 'Y'
|   C           KEYE9     CHAINE90                  40
|   C                     DELETE90
|   C                     MOVE 'SCR001'  APPSCR
|   C                     Z-ADDRRN       DRRN
| 03-C                     END
|   C*
| 02-C                     END
| 01-C                     END
|    C*
15===C                     ENDSR
      *==============================================================*
      *          RTN251....CLEAR SCREEN DATA
      *==============================================================*
16===CSR         RTN251    BEGSR
|    C*
|    C                     MOVE *BLANK    DE901
|    C                     MOVE *BLANK    DE902
|    C                     MOVE *BLANK    DE903
|    C                     MOVE *BLANK    DE904
|    C*
16===CSR                   ENDSR
      *==============================================================*
      *          RTN252....MOVE DATA TO SCREEN
      *==============================================================*
17===CSR         RTN252    BEGSR
|    C*
|    C                     MOVE E901      DE901
|    C                     MOVE E902      DE902
|    C                     MOVE E903      DE903
|    C                     MOVELE904      DE904
|    C*
17===CSR                   ENDSR
      *==============================================================*
      *          RTN253....MOVE SCREEN DATA TO FILE
      *==============================================================*
18===CSR         RTN253    BEGSR
|    C*
|    C                     MOVE DE901     E901
|    C                     MOVE DE902     E902
|    C                     MOVE DE903     E903
|    C                     MOVELDE904     E904
|    C**
M001AC                     Z-ADD$EG8      E997
|    C**
B2000C                     TIME           E998
|    C                     MOVE $USER     E999
|    C*
18===CSR                   ENDSR
      *==============================================================*
      *          RTN254....FOR COPY
      *==============================================================*
19===CSR         RTN254    BEGSR
|    C*
|    C                     MOVE *BLANKS   DE901
|    C                     MOVE E902      DE902
|    C                     MOVE E903      DE903
|    C                     MOVE E904      DE904
|    C**
19===CSR                   ENDSR
     C*==============================================================*
     C*          RTN280....CHECK 'SC02' SCREEN
     C*==============================================================*
20===CSR         RTN280    BEGSR
|    C*
| 01-C           DOPT1     IFEQ '1'
|   C           DOPT1     OREQ '3'
|   C*
| 02-C           DE901     IFEQ *BLANK
|   C                     SETON                     609899
| 02-C                     END
|   C   99                GOTO END280
|   C*
|   C           DE901     CHAINE90                  40
| 03-C           *IN40     IFEQ '0'
|   C                     SETON                     609799
| 03-C                     END
|   C   99                GOTO END280
|   C*
| 01-C                     END
|    C*
| 07-C           DOPT1     IFEQ '2'
|   C           WE901     ANDNEDE901
|   C           DE901     CHAINE90                  42
| 08-C           *IN42     IFEQ '0'
|   C                     SETON                     619799
| 08-C                     END
| 07-C                     END
|    C   99                GOTO END280
|    C*
20===CSR         END280    ENDSR
      *===============================================================*
      *          RTNF4 ....F4 HELP
      *===============================================================*
21===C           RTNF4     BEGSR
|    C*
|    C                     SETON                     99
|    C*
|    C********************
|    C*  GET ROW,COL     *
|    C********************
|    C           #CSR      DIV  256       D#ROW            *ROW
|    C                     MVR            D#COL            *COL
|    C** 游標位置即為(D#ROW,D#COL)
|    C*
|    C********************
|    C*  FOR  SCR002     *
|    C********************
| 01-C           APPSCR    IFEQ 'SCR002'
|   C**
|   C*
| 03-C*          D#ROW     IFEQ 10
|   C*          D#COL     ANDGE14
|   C*          D#COL     ANDLE15
|   C*                    CALL 'WDS012'
|   C*                    PARM 'SHOPDEPT'PARMI1
|   C*          DMG03     PARM DMG03     PARMO1
|   C*                    GOTO ENDF4
| 03-C*                    END
|   C*
| 01-C                     END
|    C*
21===C           ENDF4     ENDSR
     C*==============================================================*
     C*          RTN990....AUT CHK (CALL SUBPGM)
     C*==============================================================*
22===C           RTN990    BEGSR
|    C*
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM           S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     9099
| 01-C                     END
|    C*
22===C                     ENDSR
     C*==============================================================*
     C*          ERRSR ....DATA LOCK ROUTINE
     C*==============================================================*
23===C           ERRSR     BEGSR
|    C*
|    C                     CALL 'P50'
|    C           DMSG      PARM           P5001O 78
|    C                     SETON                     2899
|    C*
23===C                     ENDSR'*DETC'
M001AC/COPY HBP2CVTR
**
新增修改新增刪除查詢
