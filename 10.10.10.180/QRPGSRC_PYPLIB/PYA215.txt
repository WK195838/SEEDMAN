     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : PYA215                                     *
     H*    PROGRAM NAME :員工出勤異常資料維護                      *
     H*    AUTHOR       : A1184                                      *
     H*    CREATE DATE  : 85/03/16                                   *
     H*    UPDATE DATE  : 88/12/20 A1274 EVA                         *
     H*    SYSTEM       :                                            *
     F****************************************************************
     FPYT6PF  UF  E           K        DISK                      A
     FPYT2PF  IF  E           K        DISK
     FPYPMPF  IF  E           K        DISK
     FPYEMPF  IF  E           K        DISK
     FPYLCPF  IF  E           K        DISK
     FPYA215D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    @T25       31  2               刷卡別
     E*---------------------------------------------------------------*
     I              '無上班刷卡資料'    C         FLD01
     I              '無排班表      '    C         FLD02
     I              '早　　退      '    C         FLD03
     I              '無下班刷卡資料'    C         FLD04
     I              '遲　　到      '    C         FLD05
     I              '忘刷卡    '        C         FLD06
     I              '簽　到    '        C         FLD07
     I              '遲　到    '        C         FLD08
     I              '不處理    '        C         FLD09
     I              '公　出    '        C         FLD10
     I              '早　退    '        C         FLD11
     I              '多筆刷卡異常　'    C         FLD12
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      187 1920DDATE
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I            DS
     I                                        1   80WT601
     I                                        1   60KYYMM
     I                                        7   80KDD
     I            DS
     I                                        1  62 @T25
     I                                        1  62 T205
     I            DS
     I                                        1   40DT614
     I                                        1   20DT102
     I                                        3   40DT103
     I            DS
     I                                        1   40DT615
     I                                        1   20DT112
     I                                        3   40DT113
     I            DS
     I                                        1   40DT612
     I                                        1   20T503A
     I                                        3   40T504A
     I            DS
     I                                        1   40DT613
     I                                        1   20T503B
     I                                        3   40T504B
     I/COPY PYDDSSRC,PYPMDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYT6     KLIST                           *PYT6PF
     C                     KFLD           WT601
     C                     KFLD           DT602
     C                     KFLD           DT603
     C                     KFLD           DT604
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           DT602
     C                     KFLD           DT603
     C           KEYPM     KLIST                           *PYPMPF
     C                     KFLD           PM01
     C                     KFLD           DT602
     C           KEYT2     KLIST                           *PYT2PF
     C                     KFLD           DT602
     C                     KFLD           DT603
     C                     KFLD           KYYMM
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N99                EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C*
|    C                     MOVE 'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     Z-ADD1         DOPT
|    C                     Z-ADD0         DT601
|    C                     MOVEL*BLANK    DT602
|    C                     MOVEL*BLANK    DT603
|    C                     MOVEL*BLANK    DT604
|    C*--------------------------------------------------------------*
|    C*公司別
|    C                     MOVEL$COR      DT602
| 03-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 03*C                     ELSE
|   C                     SETON                     32
| 03-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'                        *PROMPT
|   C                     EXSR R1E00
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 03-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 04-C           *IN99     IFEQ '0'                        *SCR CHK OK
|   C                     EXSR R1C00                      *PRE-SC02
|   C                     MOVEL'SC02'    SCID
| 04-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1A00     BEGSR
|    C*
|    C* AUT CHECK
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM 'PYI215'  S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C   99                GOTO E1A00
|    C*
|    C* CALL SUB-PROGRAM
|    C                     CALL 'PYI215'
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C           DT602     PARM DT602     P000I1  2
|    C           DT603     PARM DT603     P000I2  8
|    C           DT601     PARM DT601     P000I3  60
|    C           DT604     PARM DT604     P000I4  1
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     PARM           @RTNC   2
|    C* RETURN CODE CHECK
| 02-C           @RTNC     IFEQ '03'                       *PF3
|   C                     SETON                     03
| 02-C                     END
|    C*
03===C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
04===C           R1B00     BEGSR
|    C*
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*公司別
| 02-C           DT602     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DT602     CM001I  2         公司代號
|    C           DT602N    PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 03-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
| 04-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     MOVEL'01'      PM01
|    C           KEYPM     CHAINPM0                  40
| 05-C           *IN40     IFEQ '1'
|   C                     MOVEL'UMO0440' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6099
| 05-C                     END
|    C   99                GOTO E1B00
|    C*員工編號
| 06-C           DT603     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 06-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     MOVEL*BLANKS   EM03D
|    C           KEYEM     CHAINEM0                  40
| 07-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 07-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     MOVELEM03      EM03D
|    C*員工必為需刷卡
| 08-C           EM103     IFNE 'Y'
|   C                     MOVEL'UPY0012' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6199
| 08-C                     END
|    C   99                GOTO E1B00
|    C*刷卡日期
| 09-C           DT601     IFEQ *ZERO
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 09-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'P30'
|    C                     PARM DT601     P3001I  80
|    C                     PARM DFMT      P3002I  1
|    C                     PARM DTYPE     P3003I  1
|    C                     PARM           P3011O  1
| 10-C           P3011O    IFEQ 'N'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 10-C                     END
|    C*
|    C                     CALL 'P31'
|    C                     PARM DT601     P3101I  80
|    C                     PARM DFMT      P3102I  1
|    C                     PARM DTYPE     P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           WT601     PARM           P3101O  80
|    C*
| 11-C           WT601     IFLT EM13
|   C                     MOVEL'UMI0360' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6299
| 11-C                     END
|    C   99                GOTO E1B00
|    C*刷卡異常
| 12-C           DT604     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 12-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     SELEC
|    C           DT604     WHEQ '*'
|    C                     MOVELFLD01     DT604D
|    C           DT604     WHEQ 'N'
|    C                     MOVELFLD02     DT604D
|    C           DT604     WHEQ 'E'
|    C                     MOVELFLD03     DT604D
|    C           DT604     WHEQ '#'
|    C                     MOVELFLD04     DT604D
|    C           DT604     WHEQ 'L'
|    C                     MOVELFLD05     DT604D
|    C           DT604     WHEQ '!'
|    C                     MOVELFLD12     DT604D
|    C                     OTHER
|    C           DT604     CHAINLC0                  40
| 13-C           *IN40     IFEQ '0'
|   C                     MOVELLC02      DT604D
| 13*C                     ELSE
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 13-C                     END
|    C   99                GOTO E1B00
| ERRC                     END
|    C*
|    C* DATA CHECK *--------------------------------------------------*
|    C           KEYT6     CHAINT60                  40
|    C*
| 14-C           DOPT      IFEQ 1                          *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     6399
| 14-C                     END
|    C   99                GOTO E1B00
|    C*
| 15-C           DOPT      IFNE 1                          *UPD,DEL,INQ
|   C           *IN40     ANDEQ'1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     6399
| 15-C                     END
|    C   99                GOTO E1B00
|    C*巳作異常確認之資料不可修改或刪除
| 16-C           DOPT      IFEQ 2
|   C           DOPT      OREQ 4
| 17-C           T696      IFNE 0
|   C                     MOVEL'UMI0740' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     6399
| 17-C                     END
|   C   99                GOTO E1B00
| 16-C                     END
|    C*
04===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
05===C           R1C00     BEGSR
|    C*
| 01-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     SETON                     31    *CAN INPUT
| 01*C                     ELSE                            *DEL, INQ
|   C                     SETOF                     31    *INP PROTECT
| 01-C                     END
|    C*
| 02-C           DOPT      IFEQ 1                          *ADD
|   C                     EXSR R1C10
| 02*C                     ELSE                            *U/D/I
|   C                     EXSR R1C20
| 02-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)----------------------------------*
| 03-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 5                          *INQ
|   C           *LOVAL    SETLLT60
| 03-C                     END
|    C*---------------------------------------------------------------*
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1C10 ....'SC02' FIELD INIT : ADD (NO COPY)
     C*==============================================================*
06===C           R1C10     BEGSR
|    C*上／下班時間
|    C                     Z-ADD*ZERO     DT612
|    C                     Z-ADD*ZERO     DT613
|    C                     Z-ADD*ZERO     DT614
|    C                     Z-ADD*ZERO     DT615
|    C*差異時間
|    C                     Z-ADD0         DT605
|    C                     Z-ADD0         DT606
|    C*更正狀態
|    C                     MOVEL*BLANKS   DT607
|    C                     MOVEL*BLANK    DT607D
|    C*系統處理
|    C                     MOVEL*BLANKS   DT608
|    C                     MOVEL*BLANK    DT608D
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*  R1C20 ....'SC02' FIELD INIT : UPD, DEL, INQ, (ADD & COPY)
     C*==============================================================*
07===C           R1C20     BEGSR
|    C*上／下班時間
|    C                     Z-ADDT612      DT612
|    C                     Z-ADDT613      DT613
|    C                     Z-ADDT614      DT614
|    C                     Z-ADDT615      DT615
|    C*差異時間
|    C                     Z-ADDT605      DT605
|    C                     Z-ADDT606      DT606
|    C*更正狀態
|    C                     MOVELT607      DT607
|    C                     MOVEL*BLANK    DT607D
|    C                     SELEC
|    C           DT607     WHEQ 'F'
|    C                     MOVELFLD06     DT607D
|    C           DT607     WHEQ 'T'
|    C                     MOVELFLD07     DT607D
|    C           DT607     WHEQ 'L'
|    C                     MOVELFLD08     DT607D
|    C           DT607     WHEQ 'D'
|    C                     MOVELFLD09     DT607D
|    C           DT607     WHEQ 'C'
|    C                     MOVELFLD10     DT607D
|    C                     OTHER
|    C           DT607     CHAINLC0                  40
| 01-C           *IN40     IFEQ '0'
|   C                     MOVELLC02      DT607D
| 01*C                     ELSE
|   C                     MOVEL*BLANK    DT607D
| 01-C                     END
| ERRC                     END
|    C*系統處理
|    C                     MOVELT608      DT608
|    C                     MOVEL*BLANK    DT608D
|    C                     SELEC
|    C           DT608     WHEQ 'F'
|    C                     MOVELFLD06     DT608D
|    C           DT608     WHEQ 'T'
|    C                     MOVELFLD07     DT608D
|    C           DT608     WHEQ 'L'
|    C                     MOVELFLD08     DT608D
|    C           DT608     WHEQ 'D'
|    C                     MOVELFLD09     DT608D
|    C           DT608     WHEQ 'C'
|    C                     MOVELFLD10     DT608D
|    C           DT608     WHEQ 'E'
|    C                     MOVELFLD11     DT608D
|    C                     OTHER
|    C           DT608     CHAINLC0                  40
| 02-C           *IN40     IFEQ '0'
|   C                     MOVELLC02      DT608D
| 02*C                     ELSE
|   C                     MOVEL*BLANK    DT608D
| 02-C                     END
| ERRC                     END
|    C*
07===C           E1C20     ENDSR
     C*==============================================================*
     C*          R1E00 .... 'SC01' PROMPT
     C*==============================================================*
08===C           R1E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*員工編號
| 01-C           #LIN      IFEQ 5
|   C           #COL      ANDGE13
|   C           #COL      ANDLE17
|   C                     CALL 'PYI101'
|   C           DT602     PARM DT602     P101I1  2        公司別
|   C           DT603     PARM DT603     P101I2  8        員工號
|   C                     PARM           @RTNC   2        FUN KEY
| 01-C                     END
|    C*
08===C                     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
09===C           R2000     BEGSR
|    C*
|    C                     EXFMTDSPD2
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C* RELEAS FILE --------------------------------------------------*
|   C           *LOVAL    SETLLT60
|   C*---------------------------------------------------------------*
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 03-C           *IN04     IFEQ '1'                        *PREVIOUS
| 04-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     EXSR R2E00
|   C                     GOTO E2000
| 04-C                     END
| 03-C                     END
|    C*
| 05-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     EXSR R2B00                      *SC02 CHK
| 05-C                     END
|    C*
| 06-C           *IN05     IFEQ '1'                        *CHECK ONLY
|   C                     GOTO E2000
| 06-C                     END
|    C*
| 07-C           *IN99     IFEQ '0'                        *CHK OK
|   C                     EXSR R2D00                      *FILE UPD
|   C                     MOVE 'SC01'    SCID
| 07-C                     END
|    C*
09===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....'SC02' CHECK
     C*==============================================================*
10===C           R2B00     BEGSR
|    C*差異時間
| 01-C           DT605     IFEQ 0
|   C           DT606     ANDEQ0
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 01-C                     END
|    C   99                GOTO E2B00
|    C*
| 02-C           DT605     IFGT PM101
|   C                     MOVEL'UMI0730' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E2B00
|    C*
| 03-C           DT605     IFEQ PM101
|   C           DT606     ANDGT0
|   C                     MOVEL'UMI0730' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6199
| 03-C                     END
|    C   99                GOTO E2B00
|    C*
| 04-C           DT606     IFGT 59
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 04-C                     END
|    C   99                GOTO E2B00
|    C*更正狀態
| 05-C           DT607     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 05-C                     END
|    C   99                GOTO E2B00
|    C*
|    C                     SELEC
|    C           DT607     WHEQ 'F'
|    C                     MOVELFLD06     DT607D
|    C           DT607     WHEQ 'T'
|    C                     MOVELFLD07     DT607D
|    C           DT607     WHEQ 'L'
|    C                     MOVELFLD08     DT607D
|    C           DT607     WHEQ 'D'
|    C                     MOVELFLD09     DT607D
|    C           DT607     WHEQ 'C'
|    C                     MOVELFLD10     DT607D
|    C           DT607     WHEQ 'E'
|    C                     MOVELFLD11     DT607D
|    C                     OTHER
|    C           DT607     CHAINLC0                  40
| 06-C           *IN40     IFEQ '0'
|   C                     MOVELLC02      DT607D
| 06*C                     ELSE
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 06-C                     END
|    C   99                GOTO E2B00
| ERRC                     END
|    C*
10===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
11===C           R2D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D20
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*==============================================================*
12===C           R2D10     BEGSR
|    C*
|    C                     EXSR R2D11
|    C                     WRITET60
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
13===C           R2D11     BEGSR
|    C*
|    C                     Z-ADDWT601     T601
|    C                     MOVELDT602     T602
|    C                     MOVELDT603     T603
|    C                     MOVELDT604     T604
|    C                     Z-ADDDT605     T605
|    C                     Z-ADDDT606     T606
|    C                     MOVELDT607     T607
|    C                     MOVELDT608     T608
|    C           KEYT2     CHAINT20                  40
|    C                     MOVEL@T25,KDD  T611
|    C                     Z-ADDDT612     T612
|    C                     Z-ADDDT613     T613
|    C                     Z-ADDDT614     T614
|    C                     Z-ADDDT615     T615
|    C                     Z-ADD*ZERO     T696
|    C                     Z-ADD$A8YMD    T697
|    C                     TIME           T698
|    C                     MOVEL$USER     T699
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          R2D20 ....FILE PROCESS : UPD
     C*==============================================================*
14===C           R2D20     BEGSR
|    C*
|    C           KEYT6     CHAINT60                  40
|    C                     EXSR R2D11
|    C                     UPDATT60
|    C*
14===C                     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS
     C*==============================================================*
15===C           R2D30     BEGSR
|    C*
|    C           KEYT6     CHAINT60                  40
|    C                     DELETT60
|    C*
15===C                     ENDSR
     C*==============================================================*
     C*          R2E00 .... 'SC02' PROMPT
     C*==============================================================*
16===C           R2E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*假別
| 01-C           #LIN      IFEQ 15
|   C           #COL      ANDGE59
|   C           #COL      ANDLE59
|   C                     CALL 'PYI502'
|   C           DT607     PARM DT607     P502I1  1         假別　
|   C                     PARM           @RTNC   2        FUN KEY
| 01-C                     END
|    C*
16===C                     ENDSR
     C*==============================================================*
