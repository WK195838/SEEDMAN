     A****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    PROGRAM-ID   : PYH2001                                    *
     H*    PROGRAM NAME :產生本月申報檔作業                        *
     H*    AUTHOR       :                                            *
     H*    CREATE DATE  : 94/05/17                                   *
     H*    UPDATE DATE  :                                            *
     H*    UPDATE AUTHOR: MAX PENG 95/05/17                          *
     H****************************************************************
     FPYH6PF  IP  E           K        DISK
     FPYH6LF01IF  E           K        DISK
     F            H60                               KRENAMEH60X
     FPYH9PF  UF  E           K        DISK                      A
     FPYH9LF01UF  E           K        DISK
     F            H90                               KRENAMEH90X
     I*================================================================
     IH60X
     I              H601                            X601
     I              H602                            X602
     I              H603                            X603
     I              H604                            X604
     I              H605                            X605
     I              H606                            X606
     I              H607                            X607
     I              H608                            X608
     I              H609                            X609
     I              H697                            X697
     I              H698                            X698
     I              H699                            X699
     IH90X
     I              H901                            X901
     I              H903                            X903
     I*================================================================
     I           UDS
     I                                      101 110 $USER
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I                                      201 2060$A8YM
     I                                      201 2040$A8Y
     I                                      211 2160DDATE
     I                                      201 2040DSYY
     I*
     I                                      601 602 UD01
     I                                      603 6080UDYYMM
     I*
     C*==============================================================*
     C*                    KLIST
     C*==============================================================*
     C           KEYH9     KLIST
     C                     KFLD           H901             公司編號
     C                     KFLD           H902             員工編號
     C                     KFLD           H903             調整月份
     C           KEYH9X    KLIST
     C                     KFLD           X901             公司編號
     C                     KFLD           X903             調整月份
     C           KEYH6     KLIST
     C                     KFLD           X601             公司編號
     C                     KFLD           X602             員工編號
     C                     KFLD           X603             月份
     C           KEYH6E    KLIST
     C                     KFLD           X601             公司編號
     C                     KFLD           X602             員工編號
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR R0100
     C*
     C                     SETON                     10
     C                     CLEARH60X
     C                     MOVE H601      X601
     C                     MOVE H602      X602
     C                     Z-ADDH603      X603
     C           KEYH6     SETLLPYH6LF01
     C                     READPPYH6LF01                 40
     C   40                DO
     C                     Z-ADD0         TP605   60
     C                     Z-ADD0         TP606   60
     C                     END
     C           H601      IFEQ X601
     C           H602      ANDEQX602
     C                     Z-ADDX605      TP605
     C                     Z-ADDX606      TP606
     C                     ELSE
     C                     Z-ADD0         TP605
     C                     Z-ADD0         TP606
     C                     END
     C*
     C*
     C           TP605     IFNE H605
     C           TP606     ORNE H606
     C                     CLEARH90
     C                     MOVE H601      H901
     C                     MOVE H602      H902
     C                     MOVE H603      H903
     C           KEYH9     CHAINPYH9PF               40
     C           TP605     IFNE H605
     C                     Z-ADDTP605     H904
     C                     Z-ADDH605      H906
     C                     ELSE
     C                     Z-ADD0         H904
     C                     Z-ADD0         H906
     C                     END
     C           TP606     IFNE H606
     C                     Z-ADDTP606     H905
     C                     Z-ADDH606      H907
     C                     ELSE
     C                     Z-ADD0         H905
     C                     Z-ADD0         H907
     C                     END
     C   40                WRITEH90
     C  N40                UPDATH90
     C                     END
     C*==============================================================*
     C*          R0100 ... INITIAL PROCESS
     C*==============================================================*
     C           R0100     BEGSR
     C                     MOVE H601      X901
     C                     Z-ADDH603      X903
     C           KEYH9X    SETLLPYH9LF01
     C           LOOP      TAG
     C           KEYH9X    READEPYH9LF01                 40
     C   40                GOTO NEXT
     C                     DELETH90X
     C                     GOTO LOOP
     C           NEXT      TAG
     C                     ENDSR
