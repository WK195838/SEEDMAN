     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYI101                                     *
     A*    PROGRAM NAME :人事基本資料查詢                          *
     A*    AUTHOR       : A1226 PETER                                *
     A*    CREATE DATE  : 85/06/11                                   *
     A*    UPDATE DATE  : 91/06/17 A1336 KLAUS                       *
     A*    SYSTEM       :人事薪資系統                              *
     A****************************************************************
     FPYEMPF  IF  E           K        DISK
     FPYEMLF03IF  E           K        DISK
     F            EM0                               KRENAMEEML3
     FPYEMLF04IF  E           K        DISK
     F            EM0                               KRENAMEEML4
     FPYEMLF07IF  E           K        DISK
     F            EM0                               KRENAMEEML7
     FPYEMLF08IF  E           K        DISK
     F            EM0                               KRENAMEEML8
     FPYDPPF  IF  E           K        DISK
     FPYI101D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    S#01       36  7               部門代號
     E*---------------------------------------------------------------*
     I*DATA AREA
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     IPTDA01      DS
     I                                        1   1 DFMT
     I                                        2   2 DTYPE
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      203 2080DDATE
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I                                      152 161 $USERN
     I*DEFIND STRAT KEY
     I            DS
     I                                        1  45 @KEMO
     I            DS
     I                                        1  45 @KEMN
     I                                        1   8 DEM02
     I                                        9  22 DEM03
     I                                       23  29 DEM04
     I                                       30  39 DEM31
     I                                       40  450EM13D
     I            DS
     I                                        1   7 EM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     I            DS
     I                                        1   7 WEM04
     I                                        1   1 DEM041
     I                                        2   2 DEM042
     I                                        3   3 DEM043
     I                                        4   7 DEM044
     I            DS
     I                                        1 252 S#01
     I                                        1 252 CM202O
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01           TRAN DATE
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C**         *LIKE     DEFN CM004O    DFMT
     C**         *LIKE     DEFN CM005O    DTYPE
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DEM01     PARM           P101I1  2         公司別
     C           DEM02     PARM           P101I2  8         員工　
     C                     PARM           @RTNC   2          FUN KEY
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYEM     KLIST                           *MAIN FILE
     C                     KFLD           DEM01
     C                     KFLD           DEM02
     C           KEYEM1    KLIST                           *MAIN FILE
     C                     KFLD           DEM01
     C                     KFLD           DEM041
     C                     KFLD           DEM042
     C                     KFLD           DEM043
     C                     KFLD           DEM044
     C           KEYEM2    KLIST                           *MAIN FILE
     C                     KFLD           DEM01
     C                     KFLD           DEM03
     C           KEYEM3    KLIST                           *MAIN FILE
     C                     KFLD           DEM01
     C                     KFLD           DEM31
     C           KEYEM8    KLIST                           *MAIN FILE
     C                     KFLD           DEM01
     C                     KFLD           EM13W
     C           KEYDP     KLIST                           *MAIN FILE
     C                     KFLD           EM041
     C                     KFLD           EM042
     C                     KFLD           EM043
     C                     KFLD           EM044
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
  01-C           @RTNC     DOWEQ*BLANK
    C                     EXSR R1000                      *SCR PROCESS
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C*--------------------------------------------------------------
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C*
|    C                     MOVEL*BLANK    DEM31
|    C                     MOVEL*BLANK    DEM04
|    C                     MOVEL*BLANK    DEM03
|    C                     Z-ADD*ZERO     EM13D
|    C                     MOVEL*BLANK    @RTNC
|    C                     Z-ADD14        @PRCD            .PAGE SIZE
|    C*--------------------------------------------------------------*
|    C*檢核是否對全部的部門有權利並帶出管制的部門
|    C*                    CALL 'PYSCM2'
|    C*                    PARM DEM01     CM201I  4        公司
|    C*                    PARM           CM201O  1        權利
|    C*                    PARM           CM202O           部門代號
|    C*　　　　　　　
|    C*--------------------------------------------------------------*
|    C                     EXSR R1A00                      .SFL INIT
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....SCREEN  PROCESS
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     MOVEL'03'      @RTNC
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C                     MOVEL'12'      @RTNC
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN25     IFEQ '1'                        *NEXT PAGE
|   C                     EXSR R1N00
|   C                     GOTO E1000
| 03-C                     END
|    C*
| 04-C           @KEMO     IFNE @KEMN                      *START KEY
|   C                     EXSR R1A00                      *SFL INIT
|   C                     GOTO E1000
| 04-C                     END
|    C*
|    C                     EXSR R1C00                      *OPTION
|    C*
02===C           E1000     ENDSR
     C*=============================================================*
     C*          R1A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
03===C           R1A00     BEGSR
|    C*
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C                     MOVEL'N'       @BOTOM  1        *BOTTOM
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
|    C                     EXSR R1A20                      *SFL HEAD
|    C   99                GOTO E1A00
|    C* <<SFL INITIAITAL DATA ADD>>
|    C                     MOVEL@KEMN     @KEMO            *KEY SAVE
|    C                     MOVELDEM04     WEM04            *KEY SAVE
|    C                     MOVE *BLANK    DOPT
|    C*
| 01-C           EM13D     IFNE *ZERO
|   C                     CALL 'P31'
|   C                     PARM EM13D     P3101I  80
|   C                     PARM '1'       P3102I  1
|   C                     PARM '1'       P3103I  1
|   C                     PARM '2'       P3104I  1
|   C                     PARM '1'       P3105I  1
|   C                     PARM           P3111O  80
|   C                     Z-ADDP3111O    EM13W   80
| 01-C                     END
|    C*
| 02-C           DEM02     IFEQ *BLANKS
| 03-C           DEM03     IFEQ *BLANKS
| 04-C           EM13D     IFEQ *ZERO
| 05-C           DEM31     IFEQ *BLANKS
|   C           KEYEM1    SETLLEML7
| 05*C                     ELSE
|   C           KEYEM3    SETLLEML4
| 05-C                     END
|   C*
| 04*C                     ELSE
|   C           KEYEM8    SETLLEML8
| 04-C                     END
|   C*
| 03*C                     ELSE
|   C           KEYEM2    SETLLEML3
| 03-C                     END
|   C*
| 02*C                     ELSE
|   C           KEYEM     SETLLEM0
| 02-C                     END
|    C*
|    C                     EXSR R1A10                      *ADD SFL DTA
|    C*
| 06-C           RRN       IFGT 0
|   C                     SETON                     50    *SFLDSP
| 06*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 06-C                     END
|    C*
03===C           E1A00     ENDSR
     C*=============================================================*
     C*          R1A10 .... ADD SUBFILE DATA
     C*=============================================================*
04===C           R1A10     BEGSR
|    C*
|    C                     ADD  @PRCD     @DRCD
| 01-C           RRN       DOWLT@DRCD
|   C*
| 02-C           DEM02     IFEQ *BLANKS
| 03-C           DEM03     IFEQ *BLANKS
| 04-C           EM13D     IFEQ *ZERO
| 05-C           DEM31     IFEQ *BLANKS
|   C           DEM01     READEEML7                     46
| 05*C                     ELSE
|   C           DEM01     READEEML4                     46
| 05-C                     END
|   C*
| 04*C                     ELSE
|   C           DEM01     READEEML8                     46
| 04-C                     END
|   C*
| 03*C                     ELSE
|   C           DEM01     READEEML3                     46
| 03-C                     END
|   C*
| 02*C                     ELSE
|   C           DEM01     READEEM0                      46
| 02-C                     END
|   C*
|   C*
| 06-C           *IN46     IFEQ '0'
|   C*檢查部門權利
| 07-C*          CM201O    IFNE 'Y'
|   C*                    Z-ADD1         I       20
|   C*          EM04      LOKUPS#01,I                   40
| 08-C*          S#01,I    IFEQ *BLANK
|   C*                    SETOF                         40
| 08-C*                    END
|   C* N40                GOTO TAG1
| 07-C*                    END
|   C*
|   C                     EXSR R1A11                      *WRITE SFL
| 06*C                     ELSE                            *EOF
|   C                     Z-ADDRRN       @DRCD
|   C                     MOVE 'Y'       @BOTOM
|   C                     SETON                     9653  *SFLEND
| 06-C                     END
|   C           TAG1      TAG
|   C*
| 01-C                     END
|    C*
|    C                     Z-ADDRRN       DRRN
|    C*
04===C                     ENDSR
     C*=============================================================*
     C*          R1A11 .... WRITE SUBFILE DATA
     C*=============================================================*
05===C           R1A11     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << SUBFILE DATA VALUE SET >>
|    C                     MOVEL*BLANKS   DP02
|    C*到職日
|    C*                    CALL 'P31'
|    C*                    PARM EM13      P3101I  80
|    C*                    PARM '2'       P3102I  1
|    C*                    PARM '1'       P3103I  1
|    C*                    PARM DFMT      P3104I  1
|    C*                    PARM DTYPE     P3105I  1
|    C*                    PARM           P3111O  80
|    C*                    Z-ADDP3111O    DEM13
     C                     Z-ADDEM13      DEM13
|    C*
|    C           KEYDP     CHAINPYDPPF               40
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     ADD  1         RRN
|    C                     WRITESFLSR
|    C*
05===C                     ENDSR
     C*=============================================================*
     C*          R1A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
06===C           R1A20     BEGSR
|    C*
|    C*公司檢核
| 01-C           DEM01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO E1A20
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DEM01     CM001I  2         公司代號
|    C           DEM01N    PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 02-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 02-C                     END
|    C   99                GOTO E1A20
|    C*
| 03-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 03-C                     END
|    C   99                GOTO E1A20
|    C*
06===C           E1A20     ENDSR
     C*=============================================================*
     C*          R1C00 ....挑選資料
     C*=============================================================*
07===C           R1C00     BEGSR
|    C*
|    C                     READCSFLSR                  4242
| 01-C           *IN42     DOWEQ'0'
|   C*
| 02-C           DOPT      IFNE *BLANK
|   C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
|   C                     MOVELEM01      P101I1
|   C                     MOVELEM02      P101I2
|   C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|   C                     MOVEL'00'      @RTNC
|   C                     SETON                     42
| 02-C                     END
|   C*
|   C  N42                READCSFLSR                    42
| 01-C                     END
|    C*
07===C                     ENDSR
     C*=============================================================*
     C*          R1N00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
08===C           R1N00     BEGSR
|    C*
| 01-C           @BOTOM    IFEQ 'Y'                        *EOF
|   C*                    SETON                         96*LST PAG
|   C                     MOVEL'CPF5203' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
|   C                     Z-ADDRRN       DRRN
| 01*C                     ELSE
|   C                     EXSR R1A10                      *ADD SFL DTA
| 01-C                     END
|    C*
08===C                     ENDSR
     C*==============================================================*
