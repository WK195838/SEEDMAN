     H****************************************************************
     H*            泛太資訊科技開發股份有限公司－版權所有          *
     H*--------------------------------------------------------------*
     H*    PROGRAM NAME : PYA302                                     *
     H*    AUTHOR       : A1413 CLOUD                                *
     H*    CREATE DATE  : 96/06/01                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       : MIRS                                       *
     H*    SUBSYSTEM    : PY                                         *
     H****************************************************************
     F*調薪主檔 HEADING
     FPYSHPF  UF  E           K        DISK                      A
     F*調薪主檔 DETAIL
     FPYSDPF  UF  E           K        DISK                      A
     FPYEMPF  IF  E           K        DISK
     FPYDPPF  IF  E           K        DISK
     FPYPYPF  UF  E           K        DISK
     FPYPDPF  UF  E           K        DISK
     FPT#YPF  IF  E           K        DISK
     FPYSSPF  IF  E           K        DISK
     FPYA302D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    WFUN    5   5  6
     E                    WA         16  2
     E*---------------------------------------------------------------*
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*
     I           UDS
     I                                      101 110 $USER
     I                                      211 2160DDATE
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I*
     I            DS
     I                                        1   7 DEM04
     I                                        1   7 EM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN SS09      KSS09
     C           *LIKE     DEFN SS05      KSS05
     C           *LIKE     DEFN SS18      KSS18
     C           *LIKE     DEFN SS01      KSS01
     C           *LIKE     DEFN SH03      KSH03
     C           *LIKE     DEFN SD04      KSD04
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN RRN       @LRRN            LAST REC NUM
     C           *LIKE     DEFN RRN       @LPAGE           LAST PAGE
     C           *LIKE     DEFN RRN       @PRRN            PROMPT USE
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C           *LIKE     DEFN SH03      WSH03
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           DSH01
     C                     KFLD           DSH02
     C           KEYSS     KLIST                           *PYSSPF
     C                     KFLD           KSS18
     C                     KFLD           KSS01
     C                     MOVEL'SC'      KSS18
     C           KEYSH     KLIST                           *PYSHPF
     C                     KFLD           DSH01
     C                     KFLD           DSH02
     C                     KFLD           WSH03
     C           KEYSH1    KLIST                           *PYSHPF
     C                     KFLD           DSH01
     C                     KFLD           DSH02
     C                     KFLD           KSH03
     C           KEYSH2    KLIST                           *PYSHPF
     C                     KFLD           DSH01
     C                     KFLD           DSH02
     C           KEYSD     KLIST                           *PYSDPF
     C                     KFLD           DSH01
     C                     KFLD           DSH02
     C                     KFLD           WSH03
     C                     KFLD           KSD04
     C           KEYPY     KLIST                           *PYPYPF
     C                     KFLD           DSH01
     C                     KFLD           DSH02
     C           KEYPD     KLIST                           *PYPDPF
     C                     KFLD           DSH01
     C                     KFLD           DSH02
     C                     KFLD           PD03
     C           KEYDP     KLIST                           *PYDPPF
     C                     KFLD           EM041
     C                     KFLD           EM042
     C                     KFLD           EM043
     C                     KFLD           EM044
     C           KEY#Y     KLIST                           *DETIL FILE
     C                     KFLD           #Y01
     C                     KFLD           #Y02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     MOVEL'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C                     MOVEL'1'       KSS09            項目屬性
|    C                     MOVEL'1'       KSS05            項目別
|    C*
|    C                     MOVEL*BLANK    DSH02            員工編號
|    C                     Z-ADD*ZERO     DSH03            生效日期
|    C                     Z-ADD1         DOPT
|    C*
|    C                     MOVEL$COR      DSH01            公司編號
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'
|   C                     EXSR R1E00
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 03-C                     END
     C*
     C**         *IN25     IFEQ '1'
     C**                   EXSR R1N00
     C**                   GOTO E1000
     C**                   END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 04-C           *IN99     IFEQ '0'
|   C                     EXSR R1C00
|   C                     MOVEL'SC02'    SCID             *SCR2
| 04-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1A00     BEGSR
|    C* AUT CHECK
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM 'PYI302'  S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C   99                GOTO E1A00
|    C*
|    C                     CALL 'PYI302'
|    C*----------------------------------------------------------------
|    C                     PARM DSH01     I302I1  2         公司編號
|    C           DSH02     PARM DSH02     I302I2  8         員工編號
|    C           DSH03     PARM DSH03     I302I3  70        生效日期
|    C*----------------------------------------------------------------
|    C                     PARM           @RTNC   2          FUN KEY
|    C*
| 02-C           @RTNC     IFEQ '03'
|   C                     SETON                     03
| 02-C                     END
|    C*
03===C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 ....SCREEN CHECK
     C*==============================================================*
04===C           R1B00     BEGSR
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C* << CHACK 公司別NOT *BLANK >>
|    C                     MOVEL*BLANK    DSH01N
| 02-C           DSH01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DSH01     CM001I  2         公司代號
|    C           DSH01N    PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 03-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
| 04-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*+<<DATA CHECK>>+++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
|    C*          DSH01     CHAINCM0                  40
|    C*                    MOVELCM13      KSS18
|    C*員工編號
| 05-C           DSH02     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 05*C                     ELSE
|   C           KEYEM     CHAINEM0                  40
| 06-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 06-C                     END
| 05-C                     END
|    C   99                GOTO E1B00
|    C*薪資基本資料
|    C           KEYPY     CHAINPY0                  40    *NF
| 07-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     616099
| 07-C                     END
|    C   99                GOTO E1B00
|    C*
| 08-C           DOPT      IFEQ 1
|   C           EM11      ANDEQ'Y'                        離職碼
|   C                     MOVEL'UMI0400' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     616099
| 08-C                     END
|    C   99                GOTO E1B00
|    C*生效日期
| 09-C           DSH03     IFEQ 0
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 09*C                     ELSE
|   C                     CALL 'P30'
|   C                     PARM DSH03     P3001I  80
|   C                     PARM '1'       P3002I  1
|   C                     PARM DTYPE     P3003I  1
|   C                     PARM           P3011O  1
|   C*
| 10-C           P3011O    IFEQ 'N'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 10-C                     END
|   C   99                GOTO E1B00
|   C*
|   C                     CALL 'P31'
|   C                     PARM DSH03     P3101I  80
|   C                     PARM '1'       P3102I  1
|   C                     PARM DTYPE     P3103I  1
|   C                     PARM '2'       P3104I  1
|   C                     PARM '1'       P3105I  1
|   C           WSH03     PARM           P3101O  80
| 09-C                     END
|    C   99                GOTO E1B00
|    C*生效日不可小於到職日期
| 11-C           DOPT      IFEQ 1
|   C           WSH03     ANDLTEM13
|   C                     MOVEL'UMI0078' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6299
| 11-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     MOVEL*HIVAL    KSH03
|    C           KEYSH1    SETLLPYSHPF
|    C           KEYSH2    REDPEPYSHPF                   40
|    C           *LOVAL    SETLLSH0
|    C*
| 12-C           DOPT      IFEQ 1
|   C           *IN40     ANDEQ'0'
| 13-C           WSH03     IFLE SH03
|   C                     MOVEL'UMI0300' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6299
| 13-C                     END
|   C   99                GOTO E1B00
|   C*          SH06      IFEQ *ZERO
|   C*                    MOVEL'UMI0520' ERRID
|   C*                    MOVEL'CEMF'    ERRF
|   C*                    SETON                     6299
|   C*                    END
| 12-C                     END
|    C   99                GOTO E1B00
|    C*
| 14-C           DOPT      IFEQ 2
|   C           DOPT      OREQ 4
| 15-C           *IN40     IFEQ '0'
|   C           WSH03     ANDNESH03
|   C                     MOVEL'UMI0310' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6299
| 15-C                     END
| 14-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C           KEYSH     CHAINSH0                  40    *NF
| 16-C           DOPT      IFEQ 1                          *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
|   C                     SETON                     62
| 16-C                     END
|    C   99                GOTO E1B00
|    C*
| 17-C           DOPT      IFNE 1                          *UPD,DEL,INQ
| 18-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
|   C                     SETON                     62
| 18*C                     ELSE
|   C*          SH06      IFNE 0
|   C*          DOPT      ANDNE5
|   C*                    MOVEL'UMI0510' ERRID
|   C*                    MOVEL'CEMF'    ERRF
|   C*                    SETON                     606162
|   C*                    SETON                     99
|   C*                    END
| 18-C                     END
| 17-C                     END
|    C   99                GOTO E1B00
|    C*
04===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....SCREEN 2, 3 PREPARE
     C*==============================================================*
05===C           R1C00     BEGSR
|    C*
|    C                     MOVE WFUN,DOPT DFUN
|    C                     EXSR R1CC0                      *INIT SFL
|    C                     SETOF                     3132
|    C* ADD
| 01-C           DOPT      IFEQ 1
|   C                     SETON                     313233
|   C                     EXSR R1CA0                      *INIT HEAD
|   C                     EXSR R1CD0                      *INIT DETAIL
| 01-C                     END
|    C* UPD
| 02-C           DOPT      IFEQ 2
|   C                     SETON                     313233
|   C                     EXSR R1CB0                      *HEADING
|   C                     EXSR R1CE0                      *READ DETAIL
|   C                     EXSR R1CD0                      *INIT DETAIL
| 02-C                     END
|    C* DEL & INQ
| 03-C           DOPT      IFEQ 4
|   C           DOPT      OREQ 5
|   C                     EXSR R1CB0                      *HEADING
|   C                     EXSR R1CE0                      *READ DETAIL
|   C                     EXSR R1CD0                      *INIT DETAIL
|   C                     SETON                     53    *SFLEND
     C                     SETOF                     313233
| 03-C                     END
|    C*SFLDSP
|    C*          @LRRN     IFGT 0
|    C*                    Z-ADD1         DRRN
|    C*                    SETON                     50    *SFLDSP
|    C*                    ELSE
|    C*                    SETON                     99    *NO DTA
|    C*                    MOVEL'UPT2050' ERRID
|    C*                    MOVEL'PTMF'    ERRF
|    C*                    END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)++++++++++++++++++++++++++++++++++*
| 04-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 5
|   C           *LOVAL    SETLLSH0
|   C           *LOVAL    SETLLSD0
| 04-C                     END
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1CA0 ....SFL HEAD INIT : ADD (NO COPY)
     C*==============================================================*
06===C           R1CA0     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << ADD : SUBFILE HEAD DATA INITAILIZE>>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     MOVEL*BLANK    DP02
|    C           KEYDP     CHAINDP0                  40
|    C*
|    C*                    MOVEL*BLANK    JT02
|    C*          EM05      CHAINJT0                  40
|    C*
|    C                     MOVEL*BLANK    DSH04
|    C                     MOVEL*BLANK    DSH05
|    C                     Z-ADDPY03      DSH07
|    C                     Z-ADDPY03      DSH08
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R1CB0 ....SFL HEAD INIT : U/D/I/A-COPY
     C*==============================================================*
07===C           R1CB0     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << U/D/I AND ADD-COPY : SUBFILE HEAD DATA INITAILIZE>>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
|    C                     MOVEL*BLANK    DP02
|    C           KEYDP     CHAINDP0                  40
|    C*
|    C*                    MOVEL*BLANK    JT02
|    C*          EM05      CHAINJT0                  40
|    C*
|    C                     MOVELSH04      DSH04
|    C                     MOVELSH05      DSH05
|    C                     Z-ADDSH07      DSH07
|    C                     Z-ADDSH08      DSH08
|    C*
07===C                     ENDSR
     C*=============================================================*
     C*          R1CC0 ....SUBFILE INIT
     C*=============================================================*
08===C           R1CC0     BEGSR
|    C* SFL SETTING ++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD8         @PRCD            *PGE RCD
|    C                     Z-ADD11        @SFTOP  20       *SFL TOP LIN
|    C                     Z-ADD18        @SFBOT  20       *SFL BOT LIN
|    C                     Z-ADD1         @LNUM   20       *明細行數
|    C                     MOVEL'N'       @BOTOM  1        *BOTTOM
|    C* ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @LRRN            *LAST RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C*
|    C                     SETOF                     51    *SFLDSPCTL
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
08===C                     ENDSR
     C*==============================================================*
     C*          R1CD0 ....SFL DATA INIT
     C*==============================================================*
09===C           R1CD0     BEGSR
|    C*
|    C                     SETOF                     54    *54 FOR BLANK LOOP
|    C                     Z-ADD@LRRN     RRN
|    C*
|    C           @LRRN     DIV  @PRCD     @LPAGE
|    C                     ADD  1         @LPAGE
|    C           @LPAGE    MULT @PRCD     @DRCD
|    C*新增時由薪資結構中帶出資料
| 01-C           DOPT      IFEQ 1
|   C                     MOVEL'01'      PD03
|   C           KEYPD     SETLLPD0
|   C           KEYPY     READEPD0                      42
| 01*C                     ELSE
|   C                     SETON                     42
| 01-C                     END
|    C*
| 02-C           RRN       DOWLT@DRCD
|   C                     EXSR R1CD1                      *INIT SFLDTA
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
|   C  N42      KEYPY     READEPD0                      42
| 02-C                     END
|    C                     SETON                     5053
|    C*
|    C                     Z-ADDRRN       @LRRN
|    C                     Z-ADDRRN       DRRN
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*          R1CD1 ....SFL INIT DATA ADD
     C*==============================================================*
10===C           R1CD1     BEGSR
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
| 01-C           *IN42     IFEQ '1'
|   C                     SETOF                     54
|   C*                    SETON                     32
|   C*                    MOVEL'Y'       DSEL
|   C                     MOVEL*BLANK    DSD04
|   C                     MOVEL*BLANK    SS02
|   C                     MOVEL*BLANK    SS06
|   C                     Z-ADD*ZERO     DSD05
|   C                     Z-ADD*ZERO     DSD06
|   C                     GOTO E1CD1
| 01-C                     END
|    C*
|    C                     SETON                     54    *54 FOR BLANK LOOP
|    C*                    SETOF                     32
|    C*                    MOVEL'N'       DSEL
|    C                     MOVELPD03      DSD04            調薪項目
|    C                     Z-ADDPD04      DSD05            調薪前金額
|    C                     Z-ADDPD04      DSD06            調薪後金額
|    C*                    MOVEL*BLANK    SS02
|    C*                    MOVEL*BLANK    SS06
|    C*
|    C                     MOVELPD03      KSS01            調薪項目
|    C           KEYSS     CHAINSS0                  40
| 02-C           *IN40     IFEQ '1'
|   C                     MOVEL*BLANK    SS02
|   C                     MOVEL*BLANK    SS06
| 02-C                     ENDIF
|    C*********
|    C*                    MOVELDSD04     KSS01
|    C*
|    C*          KEYSS     CHAINSS0                  40
|    C*          SS06      IFEQ 'A'
|    C*                    Z-ADDSS07      DSD06
|    C*                    END
|    C*          SS06      IFEQ 'B'
|    C*          PY03      MULT SS08      DSD06     H
|    C*                    END
|    C**********
10===C           E1CD1     ENDSR
     C*==============================================================*
     C*          R1CE0 ....DETAIL DATA TO SFL
     C*==============================================================*
11===C           R1CE0     BEGSR
|    C*
|    C                     SETON                     54    *54
|    C*
|    C                     MOVEL*BLANK    KSD04
|    C           KEYSD     SETLLSD0
|    C           KEYSH     READESD0                      44*NF/EOF
|    C*
| 01-C           *IN44     DOWEQ'0'
|   C                     EXSR R1CE1
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
|   C           KEYSH     READESD0                      44*NF/EOF
| 01-C                     END
|    C*
|    C                     SETON                     53
|    C                     Z-ADDRRN       @LRRN
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          R1CE1 ....DETAIL DATA TO SFL
     C*==============================================================*
12===C           R1CE1     BEGSR
|    C*
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
|    C                     MOVELSD04      DSD04
|    C                     MOVEL*BLANK    SS02
|    C                     MOVEL*BLANK    SS06
|    C                     Z-ADDSD05      DSD05
|    C                     Z-ADDSD06      DSD06
|    C*
|    C                     MOVELDSD04     KSS01
|    C           KEYSS     CHAINSS0                  40
| 01-C           *IN40     IFEQ '1'
|   C                     MOVEL*BLANK    SS02
|   C                     MOVEL*BLANK    SS06
| 01-C                     ENDIF
|    C*
|    C                     SETOF                     32
|    C*                    MOVE 'N'       DSEL
| 02-C           DOPT      IFEQ 2
|   C           DSD05     ANDEQ*ZERO
|   C                     SETON                     32
|   C*                    MOVE 'Y'       DSEL
| 02-C                     END
|    C*
|    C* << OTHER FILE FIELD TO SFLSR >>
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R1E00 ....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
13===C           R1E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*員工編號DSH02
| 01-C           #LIN      IFEQ 5
|   C           #COL      ANDGE13
|   C           #COL      ANDLE17
|   C                     CALL 'PYI101'
|   C                     PARM DSH01     P101I1  2         公司別
|   C           DSH02     PARM DSH02     P101I2  8         員工　
|   C                     PARM           @RTNC   2          FUN KEY
| 01-C                     END
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          R2000 ....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
14===C           R2000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'
| 03-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C                     EXSR R2E00                      *CHK SCR
| 03-C                     END
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 04-C           *IN12     IFEQ '1'
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 04-C                     END
|    C*
| 05-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C                     EXSR R2B00                      *CHK SCR
| 05-C                     END
|    C*
| 06-C           *IN99     IFEQ '0'
|   C           *IN13     ANDEQ'1'
|   C                     EXSR R2D00                      *FILE UPD
|   C                     MOVE 'SC01'    SCID
| 06-C                     END
|    C*
14===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....CHECK 'SC02' --- SFLSR
     C*==============================================================*
15===C           R2B00     BEGSR
|    C*
|    C*CHECK HEADING
|    C*調薪原因
|    C                     MOVEL*BLANKS   DSH05
| 01-C           DSH04     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 01-C                     END
|    C   99                GOTO E2B00
|    C*
|    C                     MOVEL'SH04'    #Y01
|    C                     MOVELDSH04     #Y02
|    C           KEY#Y     CHAIN#Y0                  40
|    C  N40                MOVEL#Y03      DSH05
|    C*
| 02-C           DSH08     IFEQ *ZERO
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6599
| 02-C                     END
|    C   99                GOTO E2B00
|    C*
|    C*CHK DETAIL
|    C                     MOVEL*BLANK    WA
|    C*
|    C                     SETON                     54
|    C                     READCSFLSR                  5757
|    C*
| 03-C           *IN57     DOWEQ'0'
|   C           *IN99     ANDEQ'0'
|   C                     EXSR R2B10
|   C                     UPDATSFLSR
|   C*
| 04-C           *IN99     IFEQ '1'
|   C                     Z-ADDRRN       DRRN
| 04*C                     ELSE
|   C                     READCSFLSR                    57
| 04-C                     END
|   C*
| 03-C                     END
|    C   99                GOTO E2B00
|    C*
15===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2B10 ....'SC02' CHECK - SFL DETAIL
     C*==============================================================*
16===C           R2B10     BEGSR
|    C*
|    C                     SETOF                     606132
| 01-C*          DSEL      IFEQ 'Y'
|   C*                    SETON                     32
| 01-C*                    END
|    C*
| 02-C           DSD04     IFEQ *BLANK
|   C                     MOVEL*BLANK    SS02
|   C                     MOVEL*BLANK    SS06
| 03-C           DSD06     IFNE *ZERO
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 03-C                     END
|   C   99                GOTO E2B10
| 02-C                     END
|    C*
| 04-C           DSD04     IFNE *BLANK
|   C*                    MOVELDSD04     KSS01
|   C*          KEYSS     CHAINSS0                  40
|   C*          *IN40     IFEQ '1'
|   C*                    MOVEL'UPT2010' ERRID
|   C*                    MOVEL'PTMF'    ERRF
|   C*                    SETON                     6099
|   C*                    END
|   C*  99                GOTO E2B10
|   C*
|   C                     Z-ADD1         WI      20
|   C           DSD04     LOKUPWA,WI                    40
| 05-C           *IN40     IFEQ '1'
|   C                     MOVEL'UMI0012' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6099
| 05-C                     END
|   C   99                GOTO E2B10
|   C*
|   C                     Z-ADD1         WI      20
|   C           *BLANK    LOKUPWA,WI                    40
|   C   40                MOVELDSD04     WA,WI
|   C*
| 04-C                     END
|    C*
| 06-C*          DSEL      IFNE 'Y'
|   C*                    GOTO E2B10
| 06-C*                    END
|    C*NEW RECORD CHECK ONLY
| 07-C           DSD06     IFEQ *ZERO
| 08-C           SS06      IFEQ 'A'
|   C                     Z-ADDSS07      DSD06
| 08-C                     END
| 09-C           SS06      IFEQ 'B'
|   C           DSH08     MULT SS08      DSD06     H
| 09-C                     END
| 07-C                     END
|    C********* A1274 EVA
|    C*          DSD06     IFEQ *ZERO
|    C*                    MOVEL'UPT0010' ERRID
|    C*                    MOVEL'PTMF'    ERRF
|    C*                    SETON                     6199
|    C*                    END
|    C*  99                GOTO E2B10
|    C********* A1274 EVA
16===C           E2B10     ENDSR
     C*==============================================================*
     C*          R2E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
17===C           R2E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*
|    C*調薪原因
| 01-C           #LIN      IFEQ 6
|   C           #COL      ANDGE14
|   C           #COL      ANDLE14
|   C                     CALL 'PTI121'
|   C                     PARM 'SH04'    P120I1 10
|   C           DSH04     PARM DSH04     P120O1 10
| 01-C                     END
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* <<SFL HEAD FIELD>>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* SFL DETAIL
| 02-C           #LIN      IFLT @SFTOP
|   C           #LIN      ORGT @SFBOT
|   C           #COL      ORLT 16
|   C           #COL      ORGT 17
|   C                     GOTO E2E00
| 02-C                     END
|    C* GET SFL REC
|    C           #LIN      SUB  @SFTOP    @PRRN
|    C           @PRRN     DIV  @LNUM     @PRRN
|    C                     MVR            @LINE   20
|    C*
|    C                     ADD  1         @LINE            .明細行
|    C                     ADD  #DRRN     @PRRN            .PROMPT RRN
|    C           @PRRN     CHAINSFLSR                40
|    C   40                GOTO E2E00
| 03-C*          DSEL      IFNE 'Y'
|   C*                    GOTO E2E00
| 03-C*                    END
|    C*
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << SFLSR FIELD >>
| 04-C           @LINE     IFEQ 1
|   C           #COL      ANDGE16
|   C           #COL      ANDLE17
|   C                     CALL 'PYI503'
|   C                     PARM KSS18     I503I1  2         薪資組別
|   C           DSD04     PARM DSD04     I503I2  2         薪資代碼
|   C                     PARM           @RTNC   2
| 04-C                     END
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* UPD SFL
|    C                     SETON                     54
|    C                     UPDATSFLSR
|    C                     Z-ADDRRN       DRRN
|    C*
17===C           E2E00     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
18===C           R2D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D20
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|    C*
18===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*==============================================================*
19===C           R2D10     BEGSR
|    C* DETAIL
|    C                     READCSFLSR                    57
|    C*
| 01-C           *IN57     DOWEQ'0'
| 02-C           DSD04     IFNE *BLANK
|   C                     EXSR R2D12
|   C                     WRITESD0
|   C*回寫PD0
|   C           KEYPD     CHAINPD0                  40
|   C  N40                Z-ADDSD06      PD04
|   C  N40                UPDATPD0
|   C*
| 02-C                     END
|   C                     READCSFLSR                    57
| 01-C                     END
|    C* HEAD
|    C                     EXSR R2D11
|    C                     WRITESH0
|    C*回寫PY0
|    C           KEYPY     CHAINPY0                  40
|    C  N40                Z-ADDSH08      PY03
|    C  N40                UPDATPY0
|    C*
19===C                     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
20===C           R2D11     BEGSR
|    C*
|    C                     MOVELDSH01     SH01
|    C                     MOVELDSH02     SH02
|    C                     Z-ADDWSH03     SH03
|    C                     MOVELDSH04     SH04
|    C                     MOVELDSH05     SH05
|    C                     Z-ADD*ZERO     SH06
|    C                     Z-ADDDSH07     SH07
|    C                     Z-ADDDSH08     SH08
|    C**
|    C                     Z-ADD$A8YMD    SH97
|    C                     TIME           SH98
|    C                     MOVEL$USER     SH99
|    C*
|    C*
20===C                     ENDSR
     C*==============================================================*
     C*          R2D12 ....FILE PROCESS : ADD  (ADD NEW)
     C*==============================================================*
21===C           R2D12     BEGSR
|    C*
|    C                     MOVELDSH01     SD01
|    C                     MOVELDSH02     SD02
|    C                     Z-ADDWSH03     SD03
|    C                     MOVELDSD04     SD04
|    C                     Z-ADDDSD05     SD05
|    C                     Z-ADDDSD06     SD06
|    C*
|    C                     MOVELDSD04     PD03
|    C*
21===C                     ENDSR
     C*==============================================================*
     C*          R2D20 ....FILE PROCESS : UPD
     C*==============================================================*
22===C           R2D20     BEGSR
|    C* DETAIL
|    C                     EXSR R2D31
|    C*
|    C                     READCSFLSR                  5757
| 01-C           *IN57     DOWEQ'0'
|   C*
| 02-C           DSD04     IFNE *BLANK
|   C                     EXSR R2D12
|   C                     WRITESD0
|   C*回寫PD0
|   C           KEYPD     CHAINPD0                  40
|   C  N40                Z-ADDSD06      PD04
|   C  N40                UPDATPD0
|   C*
| 02-C                     END
|   C*
|   C                     READCSFLSR                    57
| 01-C                     END
|    C*
|    C                     EXSR R2D11
|    C                     UPDATSH0
|    C*回寫PY0
|    C           KEYPY     CHAINPY0                  40
|    C  N40                Z-ADDSH08      PY03
|    C  N40                UPDATPY0
|    C*
22===C                     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS
     C*==============================================================*
23===C           R2D30     BEGSR
|    C* DETAIL
|    C                     EXSR R2D31
|    C*
|    C                     DELETSH0
|    C*
|    C           KEYPY     CHAINPY0                  40
|    C  N40                Z-ADDSH07      PY03
|    C  N40                UPDATPY0
|    C*
23===C                     ENDSR
     C*==============================================================*
     C*          R2D31 ....FILE PROCESS
     C*==============================================================*
24===C           R2D31     BEGSR
|    C* DETAIL
|    C                     MOVEL*BLANK    KSD04
|    C           KEYSD     SETLLSD0
|    C           KEYSH     READESD0                      40
| 01-C           *IN40     DOWEQ'0'
     C*
     C                     MOVELSD04      PD03
     C           KEYPD     CHAINPD0                  41
     C  N41                Z-ADDSD05      PD04
     C  N41                UPDATPD0
     C*
|   C                     DELETSD0
     C*
|   C           KEYSH     READESD0                      40
| 01-C                     END
|    C*
24===C                     ENDSR
     C*==============================================================*
**
新增修改      刪除查詢
