     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    PROGRAM-ID   : PYA611                                     *
     H*    PROGRAM NAME :福委會獎金轉檔作業　　　　                *
     H*    AUTHOR       : A1274 EVA                                  *
     H*    CREATE DATE  : 90/02/21                                   *
     H*    UPDATE DATE  : 90/04/16 A1274 EVA
     H*    SYSTEM       :人事薪資系統                              *
     H****************************************************************
     FSYCMPF  IF  E           K        DISK
     FPYSSLF03IF  E           K        DISK
     FPYPBLF02IF  E           K        DISK
     FPYERLF02UF  E           K        DISK
     FPYA611D CF  E                    WORKSTN
     I*-------------------------------------------------------------**
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     ILDA        UDS
     I                                      101 110 $USER
     I                                      211 2160DDATE
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I                                      203 2060$A8YM
     I*
     I                                      501 502 DER01
     I                                      503 5060WPB03
     I                                      507 5120WPB05
     I                                      513 514 DPB04
     I            DS
     I                                        1   80ER03
     I                                        1   60ER03YM
     I            DS
     I                                        1   80WER03S
     I                                        1   60W03YM
     I                                        7   80W03DD
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN *LDA      LDA
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C*SYCMPF
     C                     MOVEL*BLANK    WCM01   2
     C           KEYCM     KLIST
     C                     KFLD           WCM01
     C*PYPBLF02
     C           KEYPB     KLIST
     C                     KFLD           DER01
     C                     KFLD           WPB03
     C                     KFLD           DPB04
     C                     KFLD           WPB05
     C*PYSSLF03
     C                     MOVEL'5'       KSS09   1
     C           KEYSS     KLIST
     C                     KFLD           KSS09            .項目屬性
     C                     KFLD           KCM13   2        .薪資組別
     C                     KFLD           DPB04            .獎金類別
     C*PYERLF02
     C                     MOVEL'1'       WER04   1        福委會支出
     C           KEYER     KLIST
     C                     KFLD           DER01
     C                     KFLD           WPB03
     C                     KFLD           WER04
     C                     KFLD           DPB04
     C                     KFLD           WER03S
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INI. RTN.
     C*
  01-C           *IN03     DOWEQ'0'
    C                     EXSR R1000                      *MAIN
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL ROUTINE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   *NAMVAR
|    C                     IN   LDA
|    C*
|    C*-------------------------------------------------------------***
|    C                     MOVEL*BLANK    DER01
|    C                     MOVEL*BLANK    DER01N
|    C                     Z-ADD*ZERO     DPB03
|    C                     Z-ADD*ZERO     WPB03
|    C                     Z-ADD*ZERO     DPB05
|    C                     Z-ADD*ZERO     WPB05
|    C                     MOVEL*BLANK    DPB04
|    C                     MOVEL*BLANK    DPB04N
|    C*-------------------------------------------------------------***
|    C                     MOVEL$COR      DER01
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 .... MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     SETON                     97
|    C                     EXFMTDSPC1
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'
|   C                     EXSR R1E00
|   C                     GOTO E1000
| 02-C                     END
|    C*
|    C                     EXSR R1B00                      *CHK SCREEN1
|    C*
| 03-C           *IN13     IFEQ '1'
|   C           *IN99     ANDEQ'0'
|   C                     EXSR R1C00                      *PROCESS
| 03-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1B00 ....CHECK SCREEN1
     C*==============================================================*
03===C           R1B00     BEGSR
|    C*
|    C*------------------------------------------------------------***
|    C*福委會
| 01-C           DER01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DER01     CM001I  2         公司代號
|    C                     PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 02-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
| 03-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     MOVEL*BLANK    DER01N
| 04-C           DER01     IFNE *BLANK
|   C                     MOVELDER01     WCM01
|   C           KEYCM     CHAINCM0                  40
| 05-C           *IN40     IFEQ '0'
|   C                     MOVELCM02      DER01N
|   C                     MOVELCM13      KCM13            .薪資組別
| 05*C                     ELSE
|   C                     MOVEL*BLANK    DER01N
|   C                     MOVEL*BLANK    KCM13
| 05-C                     END
| 04-C                     END
|    C*------------------------------------------------------------***
|    C*獎金年度
| 06-C           DPB03     IFEQ *ZERO
|   C                     Z-ADD2000      WPB03
| 06*C                     ELSE
|   C                     CALL 'P82'
|   C                     PARM DPB03     P8201I  40
|   C                     PARM DFMT      P8202I  1
|   C                     PARM DTYPE     P8203I  1
|   C                     PARM '2'       P8204I  1
|   C                     PARM '1'       P8205I  1
|   C                     PARM           P8211O  40
|   C                     MOVE P8211O    WPB03   40       .YYYY
| 06-C                     END
|    C*------------------------------------------------------------***
|    C*獎金發放年月
|    C                     Z-ADD*ZERO     WPB05
|    C*
| 07-C           DPB05     IFEQ *ZERO
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 07-C                     END
|    C   99                GOTO E1B00
|    C*
| 08-C           DPB05     IFNE *ZERO
|   C                     CALL 'P38'
|   C                     PARM DPB05     P3801I  60
|   C                     PARM DFMT      P3802I  1
|   C                     PARM DTYPE     P3803I  1
|   C                     PARM           P3801O  60
|   C                     PARM           P3811O  1
| 09-C           P3811O    IFEQ 'N'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 09-C                     END
|   C   99                GOTO E1B00
|   C*
|   C                     CALL 'P39'
|   C                     PARM DPB05     P3901I  60
|   C                     PARM DFMT      P3902I  1
|   C                     PARM DTYPE     P3903I  1
|   C                     PARM '2'       P3904I  1
|   C                     PARM '1'       P3905I  1
|   C                     PARM           P3911O  60
|   C                     Z-ADDP3911O    WPB05
| 08-C                     END
|    C*------------------------------------------------------------***
|    C*獎金類別：為薪資代碼．但性質為獎金型態
| 10-C           DPB04     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 10-C                     END
|    C   99                GOTO E1B00
|    C*
| 11-C           DPB04     IFNE *BLANK
|   C           KEYSS     CHAINSS0                  42
| 12-C           *IN42     IFEQ '0'
|   C                     MOVELSS02      DPB04N
| 12*C                     ELSE
|   C                     MOVEL*BLANK    DPB04N
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 12-C                     END
|   C   99                GOTO E1B00
| 11-C                     END
|    C*------------------------------------------------------------***
|    C*所篩選的資料必存在於獎金檔中．且需已經核定過PB96<>0
|    C           KEYPB     CHAINPB0                  40
| 13-C           *IN40     IFEQ '0'
| 14-C           PB96      IFEQ *ZERO                      核定日期
|   C                     MOVEL'UTR0015' ERRID
|   C                     MOVEL'TRMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     6399
| 14-C                     END
| 13*C                     ELSE
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     6399
| 13-C                     END
|    C   99                GOTO E1B00
|    C*------------------------------------------------------------***
|    C*每次重新寫入相關資料：先刪後寫
|    C                     Z-ADD*ZERO     WER03S
|    C                     Z-ADDWPB05     W03YM
|    C                     Z-ADD*ZERO     W03DD
|    C*
|    C           KEYER     SETLLER0
|    C           KEYER     READEER0                      46
| 15-C           *IN46     DOWEQ'0'
|   C*
|   C                     DELETER0
|   C*
|   C           KEYER     READEER0                      46
| 15-C                     END
|    C*------------------------------------------------------------***
|    C*
03===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 .... PROCESS
     C*==============================================================*
04===C           R1C00     BEGSR
|    C*
|    C                     SETON                     96
|    C                     WRITEDSPC1
|    C*
|    C                     OUT  *NAMVAR
|    C                     CALL 'PYA6111C'
|    C*
|    C                     MOVEL'UPT2142' ERRID
|    C                     MOVEL'PTMF'    ERRF
|    C                     SETON                     99
|    C                     SETOF                     28
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R1E00 ...欄位查詢
     C*==============================================================*
05===C           R1E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*------------------------------------------------------------***
|    C*獎金類別DPB04
| 01-C           #FLD      IFEQ 'DPB04'
|   C                     CALL 'TRW001'
|   C                     PARM 'GIFT'    TR101I 10        *代碼欄位
|   C           DPB04     PARM DPB04     TR102I 10        *代碼內容
|   C           DPB04N    PARM           TR103I 32        *代碼說明
|   C                     PARM           TR104I  2          FUN KEY
| 01-C                     END
|    C*------------------------------------------------------------***
|    C*
05===C                     ENDSR
