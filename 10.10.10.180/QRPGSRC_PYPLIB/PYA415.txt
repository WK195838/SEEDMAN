     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    PROGRAM-ID   : PYA415                                     *
     H*    PROGRAM NAME :特休遞延多筆資料建立                      *
     H*    AUTHOR       : A1336 KLAUS                                *
     H*    CREATE DATE  : 92/02/13                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :人事薪資系統                              *
     H****************************************************************
     FPYH3PF  UF  E           K        DISK                      A
     FPYEMPF  IF  E           K        DISK
     FPYPMPF  IF  E           K        DISK
     FPYA415D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    WFUN    5   5  6
     E                    TCHK     2000  8               員工編號
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      211 2160DDATE
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      152 161 $USERN
     I                                      173 174 $COR
     I                                      201 2080$A8YMD
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN RRN       @LRRN            LAST REC NUM
     C           *LIKE     DEFN RRN       @LPAGE           LAST PAGE
     C           *LIKE     DEFN RRN       @PRRN            PROMPT USE
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           DH301            公司
     C                     KFLD           DH303            員工
     C*
     C           KEYH3     KLIST
     C                     KFLD           DH301
     C                     KFLD           WH302   40       年度
     C                     KFLD           DH303
     C*
     C           KEYH3A    KLIST
     C                     KFLD           DH301
     C                     KFLD           WH302
     C*
     C           KEYPM     KLIST
     C                     KFLD           PM01
     C                     KFLD           DH301
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     MOVE 'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     Z-ADD1         DOPT
|    C                     Z-ADD*ZERO     DH302            *年度
|    C                     Z-ADD*ZERO     WH302            *年度
|    C                     MOVEL*BLANK    DH303            *員工編號
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....INIT 'SC01'
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E1000
| 01-C                     END
|    C*
|    C**         *IN18     IFEQ '1'                        *BROWSE
|    C**                   EXSR R1A00
|    C**                   GOTO E1000
|    C**                   END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 02-C           *IN99     IFEQ '0'                        *SCR CHK OK
|   C                     EXSR R1C00                      *PRE-SC02
|   C                     MOVEL'SC02'    SCID
| 02-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
     C**         R1A00     BEGSR
     C*
     C* AUT CHECK
     C*
     C**         E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
03===C           R1B00     BEGSR
|    C*
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C* DATA CHECK *--------------------------------------------------*
|    C*公司編號
| 02-C           DH301     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DH301     CM001I  2         公司代號
|    C                     PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 03-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
| 04-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     MOVEL'01'      PM01
|    C           KEYPM     CHAINPM0                  40
| 05-C           *IN40     IFEQ '1'
|   C                     MOVEL'UMI0440' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6099
| 05-C                     END
|    C   99                GOTO E1B00
|    C* DATA CHECK *--------------------------------------------------*
|    C*年度不可為零
| 06-C           DH302     IFEQ *ZERO
|   C                     MOVEL'UPT0012' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     619899
| 06-C                     END
|    C   99                GOTO E1B00
|    C*
|    C           DH302     ADD  1911      WH302
|    C*
|    C           KEYH3A    CHAINH30                 N40    *NF
|    C*
| 07-C           DOPT      IFEQ 1                          *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 07-C                     END
|    C   99                GOTO E1B00
|    C*
| 08-C           DOPT      IFNE 1                          *UPD,DEL,INQ
|   C           *IN40     ANDEQ'1'
|   C                     SETON                     606199
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 08-C                     END
|    C   99                GOTO E1B00
|    C*
03===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
04===C           R1C00     BEGSR
|    C*
|    C                     SETOF                     3132  *IN CONTROL
|    C*
|    C                     MOVELWFUN,DOPT DFUN             *作業說明
|    C                     EXSR R1CC0                      *INIT SFL
|    C*ADD
| 01-C           DOPT      IFEQ 1                          *ADD
|   C                     SETON                     31    *CAN INPUT
|   C                     EXSR R1CA0                      *INIT HEAD
|   C                     EXSR R1CD0                      *INIT DETAIL
| 01-C                     END
|    C* UPD
| 02-C           DOPT      IFEQ 2
|   C                     SETON                     31
|   C                     EXSR R1CB0                      *HEADING
|   C                     EXSR R1CD0                      *INIT DETAIL
| 02-C                     END
|    C*
|    C* DEL & INQ
| 03-C           DOPT      IFEQ 4
|   C           DOPT      OREQ 5
|   C                     EXSR R1CB0                      *HEADING
|   C                     EXSR R1CD0                      *READ DETAIL
|   C                     SETON                     53    *SFLEND
| 03-C                     END
|    C*SFLDSP
| 04-C           RRN       IFGT 0
|   C                     Z-ADD1         DRRN
|   C                     SETON                     50    *SFLDSP
| 04*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)----------------------------------*
| 05-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 5                          *INQ
|   C           *LOVAL    SETLLH30
| 05-C                     END
|    C*---------------------------------------------------------------*
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R1CA0 ....SFL HEAD INIT : ADD (NO COPY)
     C*==============================================================*
05===C           R1CA0     BEGSR
|    C*
|    C* << ADD : SUBFILE HEAD DATA INITAILIZE>>
|    C*
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1CB0 ....SFL HEAD INIT : U/D/I/A-COPY
     C*==============================================================*
06===C           R1CB0     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << U/D/I AND ADD-COPY : SUBFILE HEAD DATA INITAILIZE>>
|    C*
|    C           KEYH3A    SETLLH30
|    C           KEYH3A    READEH30                      46
| 01-C           *IN46     DOWEQ'0'
|   C                     SETON                     54    *54 FOR BLANK LOOP
|   C*
|   C                     MOVELH303      DH303            員工
|   C           KEYEM     CHAINEM0                  40
|   C   40                MOVEL*BLANK    DH304     P
|   C  N40                MOVELEM03      DH304     P
|   C*
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
|   C           KEYH3A    READEH30                      46
| 01-C                     END
|    C*
06===C                     ENDSR
     C*=============================================================*
     C*          R1CC0 ....SUBFILE INIT
     C*=============================================================*
07===C           R1CC0     BEGSR
|    C* SFL SETTING ++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD2000      @DRCD            *PGE RCD
|    C                     Z-ADD8         @SFTOP  20       *SFL TOP LIN
|    C                     Z-ADD19        @SFBOT  20       *SFL BOT LIN
|    C                     Z-ADD1         @LNUM   20       *明細行數
|    C* ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD1         DRRN
|    C                     Z-ADD0         RRN
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R1CD0 ....SFL DATA INIT
     C*==============================================================*
08===C           R1CD0     BEGSR
|    C*
| 01-C           RRN       DOWLT@DRCD
|   C                     SETOF                     54
|   C                     MOVEL*BLANK    DH303
|   C                     MOVEL*BLANK    DH304
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
| 01-C                     END
|    C*
08===C                     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
09===C           R2000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C                     Z-ADD#DRRN     DRRN
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'
| 03-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C                     EXSR R2E00                      *CHK SCR
| 03-C                     END
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 04-C           *IN12     IFEQ '1'                        *END
|   C                     MOVE 'SC01'    SCID
|   C                     GOTO E2000
| 04-C                     END
|    C*
| 05-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     EXSR R2B00                      *SC02 CHK
| 05-C                     END
|    C*
| 06-C           *IN99     IFEQ '0'                        *CHK OK
|   C           *IN13     ANDEQ'1'                        *CHK OK
|   C                     EXSR R2D00                      *FILE UPD
|   C                     MOVE 'SC01'    SCID
| 06-C                     END
|    C*
09===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....'SC02' CHECK
     C*==============================================================*
10===C           R2B00     BEGSR
|    C*
|    C* CHECK HEADING (SFLCR) ++++++++++++++++++++++++++++++++++++++++
|    C*
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* CHECK DETAIL (SFLSR)
|    C                     MOVEL*BLANK    TCHK
|    C                     Z-ADD0         I       40
|    C* CHECK DETAIL (SFLSR)
|    C                     SETON                     54
|    C                     READCSFLSR                  5757
|    C*
| 01-C           *IN57     DOWEQ'0'
|   C                     SETOF                     6162
| 02-C           *IN99     IFEQ '0'
|   C                     EXSR R2B10
|   C   99                Z-ADDRRN       DRRN
| 02-C                     END
|   C                     UPDATSFLSR
|   C*
|   C                     READCSFLSR                    57
| 01-C                     END
|    C*
10===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2B10 ....'SC02' CHECK - SFL DETAIL
     C*==============================================================*
11===C           R2B10     BEGSR
|    C*
|    C                     MOVEL*BLANK    DH304
| 01-C           DH303     IFNE *BLANK
|   C           KEYEM     CHAINEM0                  39
| 02-C           *IN39     IFEQ '1'
|   C                     SETON                     6299
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 02*C                     ELSE
|   C                     MOVELEM03      DH304
| 02-C                     END
|   C   99                GOTO E2B10
|   C*
|   C                     ADD  1         I
|   C                     MOVELDH303     TCHK,I
| 03-C           I         IFGT 1
|   C                     Z-ADD1         J       40
| 04-C           J         DOWLTI
| 05-C           TCHK,I    IFEQ TCHK,J
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 05-C                     END
|   C                     ADD  1         J
| 04-C                     END
| 03-C                     END
| 01-C                     END
|    C*
11===C           E2B10     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
12===C           R2D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D10
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS
     C*==============================================================*
13===C           R2D10     BEGSR
|    C*
|    C*DELETE RECOORD
|    C           KEYH3     SETLLH30
|    C           KEYH3     READEH30                      46
| 01-C           *IN46     DOWEQ'0'
|   C                     DELETH30
|   C           KEYH3     READEH30                      46
| 01-C                     END
|    C* DETAIL<明細檔>
|    C                     SETON                     54
|    C                     READCSFLSR                  5757
|    C*
| 02-C           *IN57     DOWEQ'0'
|   C*
| 03-C           DH303     IFNE *BLANK
|   C           KEYH3     CHAINH30                  41
| 04-C           *IN41     IFEQ '1'
|   C*
|   C                     EXSR R2D11
|   C                     WRITEH30
| 04-C                     END                             *MRC CHANGE
| 03-C                     END                             *MRC CHANGE
|   C                     READCSFLSR                    57*MRC CHANGE
| 02-C                     END
|    C*
13===C           E2D10     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
14===C           R2D11     BEGSR
|    C*
|    C                     MOVELDH301     H301             公司
|    C                     Z-ADDWH302     H302             年度
|    C                     MOVELDH303     H303             工號
|    C                     MOVELDH304     H304             姓名
|    C                     Z-ADD$A8YMD    H397
|    C                     TIME           H398
|    C                     MOVEL$USER     H399
|    C*
14===C                     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS
     C*==============================================================*
15===C           R2D30     BEGSR
|    C* DETAIL
|    C           KEYH3     SETLLH30
|    C           KEYH3     READEH30                      46
|    C*
| 01-C           *IN46     DOWEQ'0'
|   C                     DELETH30
|   C           KEYH3     READEH30                      46
| 01-C                     END
|    C*
15===C                     ENDSR
     C*==============================================================*
     C*          R2E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
16===C           R2E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C* SFL DETAIL
| 01-C           #LIN      IFLT @SFTOP
|   C           #LIN      ORGT @SFBOT
|   C                     GOTO E2E00
| 01-C                     END
|    C* GET SFL REC
|    C           #LIN      SUB  @SFTOP    @PRRN
|    C           @PRRN     DIV  @LNUM     @PRRN
|    C                     MVR            @LINE   20
|    C*
|    C                     ADD  1         @LINE            .明細行
|    C                     ADD  #DRRN     @PRRN            .PROMPT RRN
|    C           @PRRN     CHAINSFLSR                40
|    C   40                GOTO E2E00
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << SFLSR FIELD >>
| 02-C           @LINE     IFEQ 0
| 03-C           #COL      IFGE 2
|   C           #COL      ANDLE9
|   C*員工號碼
|   C                     CALL 'PYI101'
|   C           DH301     PARM DH301     P101I1  2         公司別
|   C           DH303     PARM DH303     P101I2  8         員工　
|   C                     PARM           @RTNC   2
| 03-C                     END
| 02-C                     END
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* UPD SFL
|    C                     SETON                     54
|    C                     UPDATSFLSR
|    C*
16===C           E2E00     ENDSR
     C*==============================================================*
**
新增修改      刪除查詢
