     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    PROGRAM-ID   : PYA3312                                    *
     H*    PROGRAM NAME :年度調薪處理作業　　                      *
     H*    AUTHOR       : A1274 EVA                                  *
     H*    CREATE DATE  : 89/04/14                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :人事薪資                                  *
     H****************************************************************
     FPYEMPF  IP  E           K        DISK
     FPYSHPF  UF  E           K        DISK                      A
     FPT#YPF  IF  E           K        DISK
     FPYSDLF01UF  E           K        DISK                      A
     FPYPYPF  UF  E           K        DISK
     FPYPSPF  UF  E           K        DISK
     E*---------------------------------------------------------------
     I           UDS
     I                                      101 110 $USER
     I                                      210 2160DDATE
     I                                      201 2080$A8YMD
     I*
     I                                      501 502 DSH01
     I                                      509 5160WSH06
     I                                      517 5223DRATE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C*PYPYPF
     C           KEYPY     KLIST
     C                     KFLD           DSH01            公司
     C                     KFLD           EM02             員工編號
     C*PYSHPF
     C           KEYSH     KLIST
     C                     KFLD           DSH01            公司
     C                     KFLD           EM02             員工
     C                     KFLD           WSH06            生效日期
     C*PT#YPF
     C           KEY#Y     KLIST                           *DETIL FILE
     C                     KFLD           KY01   10        代碼欄位
     C                     KFLD           KY02   10        代碼編號
     C*PYSDPF
     C           KEYSD1    KLIST
     C                     KFLD           DSH01            公司
     C                     KFLD           EM02             員工編號
     C                     KFLD           WSH06            生效日期
     C*
     C           KEYSD2    KLIST
     C                     KFLD           DSH01            公司
     C                     KFLD           EM02             員工編號
     C*
     C           KEYSD3    KLIST
     C                     KFLD           DSH01            公司
     C                     KFLD           EM02             員工編號
     C                     KFLD           KSD03   80       生效日期
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR R1000
     C*
     C                     EXSR R2000                      .DETAIL
     C*
     CLR 10                EXSR R8900                      .PYPSPF
     C*==============================================================*
     C*          R1000 ... INITIAL PROCESS
     C*==============================================================*
01===C           R1000     BEGSR
|    C*
|    C                     SETON                     10
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R2000 ... DETAIL
     C*==============================================================*
02===C           R2000     BEGSR
|    C*
|    C                     Z-ADD*ZERO     WPY03   70
|    C                     Z-ADD*ZERO     SALARY  70
|    C*
|    C           KEYPY     CHAINPY0                  40
| 01-C           *IN40     IFEQ '0'
|   C*薪資主檔HEADING
|   C                     EXSR R2001                      .PYPYPF
|   C*調薪主檔HEADING
|   C                     EXSR R2002                      .PYSHPF
|   C*調薪主檔DETAIL
|   C                     EXSR R2003                      .PYSDPF
| 01-C                     END
|    C*
02===C           E2000     ENDSR
     C*==============================================================*
     C*          R2001 ... DETAIL 1
     C*==============================================================*
03===C           R2001     BEGSR
|    C*
|    C*薪資主檔PYPYPF
|    C                     Z-ADDPY03      WPY03
|    C           WPY03     MULT DRATE     SALARY    H
|    C                     Z-ADDSALARY    PY03             .底薪
|    C                     Z-ADD$A8YMD    PY97
|    C                     TIME           WTIME  120
|    C                     MOVELWTIME     PY98
|    C                     MOVEL$USER     PY99
|    C                     UPDATPY0
|    C*
03===C           E2001     ENDSR
     C*==============================================================*
     C*          R2002 ... DETAIL 2
     C*==============================================================*
04===C           R2002     BEGSR
|    C*
|    C*調薪主檔PYSHPF: HEADING
|    C           KEYSH     CHAINSH0                  40
| 01-C           *IN40     IFEQ '0'
|   C                     MOVEL'2'       SH04
|   C                     Z-ADDWSH06     SH06
|   C                     Z-ADDWPY03     SH07
|   C                     Z-ADDSALARY    SH08
|   C                     Z-ADD$A8YMD    SH97
|   C                     TIME           WTIME  120
|   C                     MOVELWTIME     SH98
|   C                     MOVEL$USER     SH99
|   C*
|   C                     MOVEL'SH04'    KY01
|   C                     MOVEL'2'       KY02
|   C           KEY#Y     CHAIN#Y0                  40
|   C  N40                MOVEL#Y03      SH05
|   C*
|   C                     UPDATSH0
|   C*
| 01*C                     ELSE
|   C                     MOVELDSH01     SH01             .公司
|   C                     MOVELEM02      SH02             .員工編號
|   C                     Z-ADDWSH06     SH03
|   C                     MOVEL'2'       SH04
|   C                     Z-ADDWSH06     SH06
|   C                     Z-ADDWPY03     SH07
|   C                     Z-ADDSALARY    SH08
|   C                     Z-ADD$A8YMD    SH97
|   C                     TIME           WTIME  120
|   C                     MOVELWTIME     SH98
|   C                     MOVEL$USER     SH99
|   C*
|   C                     MOVEL'SH04'    KY01
|   C                     MOVEL'2'       KY02
|   C           KEY#Y     CHAIN#Y0                  40
|   C  N40                MOVEL#Y03      SH05
|   C*
|   C                     WRITESH0
| 01-C                     END
|    C*
04===C           E2002     ENDSR
     C*==============================================================*
     C*          R2003 ... DETAIL 3
     C*==============================================================*
05===C           R2003     BEGSR
|    C*
|    C*調薪主檔PYSDPF: DETAIL
|    C                     Z-ADD*ZERO     KSD03   80
|    C                     SETON                     50
|    C*
|    C           KEYSD1    SETLLSD0
|    C           KEYSD2    READESD0                      44
|    C*
| 01-C           *IN44     DOWEQ'0'
|   C   50                MOVE SD03      KSD03
|   C                     SETOF                     50
|   C*
| 02-C           SD02      IFEQ EM02
|   C           SD03      ANDEQKSD03
|   C                     MOVELSD04      KSD04   2 P      .調薪項目
|   C                     Z-ADDSD05      KSD05   70       .調薪前＄
|   C                     Z-ADDSD06      KSD06   70       .調薪後＄
|   C*
| 03-C           SD03      IFNE WSH06
|   C                     MOVELDSH01     SD01             .公司
|   C                     MOVELEM02      SD02             .員工編號
|   C                     Z-ADDWSH06     SD03
|   C                     MOVELKSD04     SD04
|   C                     Z-ADDKSD05     SD05
|   C                     Z-ADDKSD06     SD06
|   C                     WRITESD0
| 03-C                     END
| 02-C                     END
|   C*
|   C           KEYSD3    READESD0                      44
| 01-C                     END
|    C*
05===C           E2003     ENDSR
     C*==============================================================*
     C*          R8900 ... UPDATE PYPSPF
     C*==============================================================*
06===C           R8900     BEGSR
|    C*
|    C* UPDATE核薪標準資料檔
|    C           *LOVAL    SETLLPS0
|    C                     READ PS0                      45
| 01-C           *IN45     DOWEQ'0'
|   C*
|   C           PS02      MULT DRATE     PS02      H      核薪下限
|   C           PS03      MULT DRATE     PS03      H      核薪上限
|   C                     Z-ADD$A8YMD    PS97
|   C                     TIME           WTIME  120
|   C                     MOVELWTIME     PS98
|   C                     MOVEL$USER     PS99
|   C*
|   C                     UPDATPS0
|   C*
|   C                     READ PS0                      45
| 01-C                     END
|    C*
06===C                     ENDSR
