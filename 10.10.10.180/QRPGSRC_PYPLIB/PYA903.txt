     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : PYA903                                     *
     H*    PROGRAM NAME :勞健保及勞退保額調整維護                  *
     H*    AUTHOR       : A1445 HONDA                                *
     H*    CREATE DATE  : 94/12/13                                   *
     H*    UPDATE DATE  : 95/07/07                                   *
     H*    SYSTEM       :人事薪資系統                              *
     H****************************************************************
     FPYLOLF02UF  E           K        DISK
     FPYEMPF  UF  E           K        DISK
     FPYE1PF  UF  E           K        DISK                      A
     FPYH4PF  UF  E           K        DISK
     FPYL3PF  UF  E           K        DISK                      A
     FPYH3PF  UF  E           K        DISK                      A
     FPYE8PF  UF  E           K        DISK                      A
     FPYH9PF  UF  E           K        DISK                      A
     FSYCMPF  IF  E           K        DISK
     FPYDPPF  IF  E           K        DISK
     FPYL1LF01IF  E           K        DISK
     FPYE3LF01IF  E           K        DISK
     FPYH7LF01IF  E           K        DISK
     FPYA903D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    WFUN    5   5  6
     E*---------------------------------------------------------------*
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I           UDS
     I                                      101 110 $USER
     I                                      203 2080DDATE
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I            DS
     I                                        1   7 LO05
     I                                        1   1 DP1
     I                                        2   2 DP2
     I                                        3   3 DP3
     I                                        4   7 DP4
     I            DS
     I                                        1   70WLO04
     I                                        1   30LOYR
     I                                        4   70LOMD
     I            DS
     I                                        1   80DSDATE
     I                                        1   60DSYM
     I            DS
     I                                        1   40DSDYM
     I                                        1   20DSDYY
     I                                        3   40DSDMM
     I           IDS
     I                                        1   80LOYMD
     I                                        1   60DLOYM
     I                                        7   80DLODD
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN RRN       @LRRN            LAST REC NUM
     C           *LIKE     DEFN RRN       @LPAGE           LAST PAGE
     C           *LIKE     DEFN RRN       @PRRN            PROMPT USE
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C*刪生效月1日的資料用
     C           KEYDEL    KLIST
     C                     KFLD           LO01             公司
     C                     KFLD           LO02             員工編號
     C                     KFLD           LOYMD            生效月一日
     C*刪生效月1日的資料用
     C           KEYDE2    KLIST
     C                     KFLD           LO01             公司
     C                     KFLD           LO02             員工編號
     C                     KFLD           DLOYM            生效月一日
     C*刪生效月1日的資料用
     C           KEYSF     KLIST                           *SFL=>LOPF
     C                     KFLD           LO01             公司
     C                     KFLD           DLOYM            年月
     C                     KFLD           LO02             員工編號
     C           KEYLO     KLIST                           *PYLOPF
     C                     KFLD           DLO01S           公司
     C                     KFLD           DLOYM            年月
     C           KEYDP     KLIST                           *PYDPPF
     C                     KFLD           DP1
     C                     KFLD           DP2
     C                     KFLD           DP3
     C                     KFLD           DP4
     C*異動勞保
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           LO01             公司
     C                     KFLD           LO02             員工編號
     C*異動健保
     C           KEYE1     KLIST                           *PYE1PF
     C                     KFLD           LO01             公司
     C                     KFLD           LO02             員工編號
     C*異動勞退
     C           KEYH4     KLIST                           *PYH4PF
     C                     KFLD           LO01             公司
     C                     KFLD           LO02             員工編號
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
    C           SCID      CASEQ'SC03'    R3000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C                     MOVE 'SC01'    SCID    4
|    C                     Z-ADD5         DO      10
|    C                     Z-ADD2         DOPT    10
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C*
|    C                     MOVEL'SC'      DLO01S
|    C                     MOVEL'SC'      DLO01E
|    C                     MOVEL*BLANK    DLO02S
|    C                     MOVEL*ALL'9'   DLO02E
|    C                     MOVEL'Y'       DLO13
|    C*---------------------------------------------------------------
|    C                     Z-ADD$A8YMD    DSDATE
|    C                     Z-ADDDSYM      DLO04
|    C                     ADD  1         DLO04
|    C                     Z-ADD1         DLODD
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*公司別
|    C*                    MOVEL$COR      DLO01S
|    C*                    MOVEL$COR      DLO01E
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN04     IFEQ '1'
|   C                     EXSR R1E00                      .PROMPT
|   C                     GOTO E1000
| 03-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 04-C           *IN99     IFEQ *OFF
|   C                     EXSR R1C00
|   C                     MOVE 'SC02'    SCID
| 04-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1A00     BEGSR
|    C*
|    C* AUT CHECK
|    C*                    CALL 'S#S01'
|    C*                    PARM $USER     S0101I 10
|    C*                    PARM 'PYI204'  S0102I 10
|    C*                    PARM           S0101O  1
|    C*          S0101O    IFEQ 'N'
|    C*                    SETON                     99
|    C*                    MOVEL'UPT2150' ERRID
|    C*                    MOVEL'PTMF'    ERRF
|    C*                    END
|    C*  99                GOTO E1A00
|    C*
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* CALL SUB-PROGRAM
|    C*                    Z-ADDDLO04     WLO04
|    C*                    CALL 'PYA206I'
|    C*          DLO01     PARM DLO01     I206I1  2
|    C*          DLO02     PARM DLO02     I206I2 10
|    C*          LOYR      PARM LOYR      I206I3  30
|    C*          LOMD      PARM LOMD      I206I4  40
|    C*                    PARM           @RTNC   2
|    C*                    Z-ADDWLO04     DLO04
|    C* RETURN CODE CHECK
|    C*          @RTNC     IFEQ '03'                       *PF3
|    C*                    SETON                     03
|    C*                    END
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
03===C           E1A00     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
04===C           R1E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*
|    C*員工編號DOV02
|    C*          #LIN      IFEQ 6
|    C*          #COL      ANDGE13
|    C*          #COL      ANDLE22
|    C*                    CALL 'PYI101'
|    C*                    PARM DLO01     P101I1  2         公司別
|    C*          DLO02     PARM DLO02     P101I2 10         員工　
|    C*                    PARM           P101I3 10         員工　
|    C*                    PARM           @RTNC   2          FUN KEY
|    C*                    END
|    C*
04===C           E1E00     ENDSR
     C*==============================================================*
     C*          R1B00 ....SCREEN CHECK
     C*==============================================================*
05===C           R1B00     BEGSR
|    C* CHK FUN AUT
|    C*          @A01,DOPT IFNE 'Y'
|    C*                    MOVEL'UPT2150' ERRID
|    C*                    MOVEL'PTMF'    ERRF
|    C*                    SETON                     99
|    C*                    END
|    C*  99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*公司別
|    C*                    MOVEL*BLANK    DLO01N 22
| 01-C           DLO01S    IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO E1B00
| 02-C           DLO01E    IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 02-C                     END
|    C   99                GOTO E1B00
| 03-C           DLO01S    IFGT DLO01E
|   C                     MOVEL'UPT1011' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 03-C                     END
|    C   99                GOTO E1B00
|    C*-------------------------------------------------------------
|    C*公司中文
|    C*                    MOVEL*BLANK    DCM03
|    C*          DLO01     CHAINCM0                  40
|    C*
|    C* N40                MOVELCM03      DCM03
|    C*--------------------------------------------------------------
|    C                     CALL 'PYSCM0'
|    C                     PARM DLO01S    CM001I  2         公司代號
|    C                     PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
|    C*
| 04-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
| 05-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 05-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*部門編號
|    C*          DLO02S    IFEQ *BLANK
|    C*                    MOVEL'UPT0010' ERRID
|    C*                    MOVEL'PTMF'    ERRF
|    C*                    SETON                     6299
|    C*                    END
|    C*  99                GOTO E1B00
| 06-C           DLO02E    IFEQ *BLANK
|   C                     MOVELDLO02S    DLO02E
|   C*                    MOVEL'UPT0010' ERRID
|   C*                    MOVEL'PTMF'    ERRF
|   C*                    SETON                     6399
| 06-C                     END
|    C   99                GOTO E1B00
| 07-C           DLO02S    IFGT DLO02E
|   C                     MOVEL'UPT1011' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     626399
| 07-C                     END
|    C   99                GOTO E1B00
|    C*-------------------------------------------------------------
|    C*年月
| 08-C           DLO04     IFEQ *ZERO
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6499
| 08-C                     END
|    C   99                GOTO E1B00
|    C* 搬  年月至DS作切割年月比對用
|    C                     Z-ADDDLO04     DSDYM
|    C*----------------------------------------------------------------
|    C* 檢查發薪年月格式 中國年月5Y 0
|    C*----------------------------------------------------------------
| 09-C           DSDYY     IFLT 1
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6499
| 09-C                     ENDIF
|    C   99                GOTO E1B00
|    C*
| 10-C           DSDMM     IFGT 12
|   C           DSDMM     ORLT 1
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6499
| 10-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*年月轉成西元年月
|    C           DLO04     ADD  200000    DLOYM   60
|    C*
05===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....MOVE DATA TO 'SC02' SCREEN
     C*==============================================================*
06===C           R1C00     BEGSR
|    C*
|    C                     MOVELWFUN,DO   DFUN
|    C*
|    C*                    SETOF                     313233*IN CONTROL
|    C*
|    C                     EXSR R1CC0                      *INIT SFL
|    C*
|    C*          DOPT      IFEQ 1
|    C*                    EXSR R1CA0
|    C*                    ELSE
|    C*                    EXSR R1CB0
|    C*                    END
|    C*
|    C*          DOPT      IFEQ 1
|    C*          DOPT      OREQ 2
|    C*                    SETON                     31
|    C*                    ELSE
|    C*                    SETOF                     31
|    C*                    END
|    C*
| 01-C           DO        IFEQ 5
|   C                     EXSR R1CB0                      *HEADING
|   C                     SETON                     53    *SFLEND
| 01-C                     END
|    C*---------------------------------------------------------------
|    C*SAVE LAST RRN ===> @LRRN
|    C                     Z-ADDRRN       @LRRN
|    C*---------------------------------------------------------------
|    C*SFLDSP
| 02-C           RRN       IFGT 0
|   C                     Z-ADD1         DRRN
|   C                     SETON                     50    *SFLDSP
| 02*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 02-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)----------------------------------*
|    C*          DOPT      IFEQ 1                          *ADD
| 03-C           DO        IFEQ 5                          *INQ
|   C           *LOVAL    SETLLLO0
| 03-C                     END
|    C*---------------------------------------------------------------*
|    C*
06===C                     ENDSR
     C*=============================================================*
     C*          R1CC0 ....SUBFILE INIT
     C*=============================================================*
07===C           R1CC0     BEGSR
|    C* SFL SETTING ++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD99        @DRCD            *PGE RCD
|    C                     Z-ADD9         @SFTOP  20       *SFL TOP LIN
|    C                     Z-ADD20        @SFBOT  20       *SFL BOT LIN
|    C                     Z-ADD1         @LNUM   20       *明細行數
|    C* ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD1         DRRN
|    C                     Z-ADD0         RRN
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R1CA0 ....CLEAR FIELD WHEN ADD
     C*==============================================================*
08===C           R1CA0     BEGSR
|    C*
|    C* << ADD : SUBFILE HEAD DATA INITAILIZE>>
|    C*
|    C*
08===C           E1CA0     ENDSR
     C*==============================================================*
     C*          R1CB0 ....MOVE DATA TO 'SC02' SCREEN
     C*==============================================================*
09===C           R1CB0     BEGSR
|    C*
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << U/D/I AND ADD-COPY : SUBFILE HEAD DATA INITAILIZE>>
|    C           KEYLO     SETLLLO0
|    C           KEYLO     READELO0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
|   C                     EXSR RSFLCK
|   C*
|   C           KEYLO     READELO0                      44
| 01-C                     END
|    C*
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*          RSFLCK....判斷符合條件才寫入SFL
     C*==============================================================*
10===C           RSFLCK    BEGSR
|    C*
|    C*
| 01-C           LO01      IFGE DLO01S
|   C           LO01      ANDLEDLO01E
|   C           LO02      ANDGEDLO02S
|   C           LO02      ANDLEDLO02E
|   C*
| 02-C           DLO13     IFEQ 'A'
|   C*
| 03-C           LO13      IFEQ 'Y'
|   C                     SETON                     30
| 03-C                     END
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
|   C                     SETOF                     30
|   C*
| 02*C                     ELSE
|   C*
| 04-C           LO13      IFEQ DLO13
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
| 04-C                     END
|   C*
| 02-C                     END
|   C*
| 01-C                     END
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          R2000 ....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
11===C           R2000     BEGSR
|    C*
|    C                     WRITEDSPC2
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'
|   C                     MOVE 'SC01'    SCID
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 03-C           *IN25     IFEQ '1'
|   C                     Z-ADD@LRRN     DRRN
|   C                     SETON                     96
|   C                     GOTO E2000
| 03-C                     END
|    C*
|    C                     EXSR R2CCC
|    C*
| 04-C           *IN23     IFEQ '1'                        *FUN KEY F23
|   C           *IN99     ANDEQ*OFF
|   C                     EXSR R2SFL                      抓變更別Y
|   C                     MOVE 'SC01'    SCID
| 04-C                     END
|    C*
11===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....CHECK 'SC02' SCREEN
     C*==============================================================*
12===C           R2B00     BEGSR
|    C*
12===C           E2B00     ENDSR
     C*=============================================================*
     C*          R2CCC ....挑選資料
     C*=============================================================*
13===C           R2CCC     BEGSR
|    C*
|    C                     READCSFLSR                  5757
| 01-C           *IN57     DOWEQ'0'
|   C                     Z-ADDRRN       DRRN
|   C*
| 02-C           OPT       IFNE *BLANK                     輸入任意值
|   C                     EXSR R2C00                      SCREEN 3
| 03-C           *IN99     IFEQ *OFF                       *SFL DATA OK
|   C                     MOVEL'SC03'    SCID
| 04-C           *IN03     DOUEQ*ON                        DO
|   C           *IN12     OREQ *ON
|   C           *IN98     OREQ *ON
|   C*
|   C                     EXSR R3000
|   C*
| 04-C                     ENDDO                           *END DOU
|   C*
|   C                     MOVEL*BLANK    OPT
| 05-C           DLO13     IFEQ 'A'                        IF   'A'
|   C           DLO13     OREQ 'N'
|   C*
| 06-C           LO13      IFEQ 'Y'                        IF   'Y'
|   C                     SETON                     30
| 06-C                     END                             *END 'Y'
|   C*
| 05-C                     END                             *END 'A'
|   C                     UPDATSFLSR
|   C                     SETOF                     30
|   C*
| 07-C           *IN03     IFEQ *ON                        IF   *IN03
|   C                     LEAVE
| 07-C                     END                             *END *IN03
|   C*
| 08-C           *IN12     IFEQ *ON                        IF   *IN12
|   C                     SETOF                     12
|   C                     MOVELWFUN,DO   DFUN
|   C                     WRITEDSPC2
|   C                     EXFMTSFLCR
| 08-C                     END                             *END *IN12
|   C*----------------------------------------------------------------
| 09-C           *IN03     IFEQ *ON                        IF   *IN03
|   C           *IN12     OREQ *ON
|   C                     MOVEL'SC01'    SCID
|   C                     LEAVE
| 09-C                     END                             *END *IN03
|   C*----------------------------------------------------------------
|   C*
| 03*C                     ELSE
|   C*
|   C                     MOVEL'SC02'    SCID
|   C                     Z-ADDRRN       DRRN
|   C                     UPDATSFLSR
|   C*
| 03-C                     END                             *END *IN99
| 02-C                     END                             *END BLANK
|   C*
|   C                     READCSFLSR                    57
| 01-C                     ENDDO                           *END DOWEQ
|    C*---------------------------------------------------------------
|    C*SET LAST RRN TO DRRN
|    C                     Z-ADDRRN       DRRN
|    C*---------------------------------------------------------------
|    C*
13===C           E2CCC     ENDSR
     C*==============================================================*
     C*          R2SFL ....CHECK SFL中 變動別 Y的資料
     C*==============================================================*
14===C           R2SFL     BEGSR
|    C*
|    C                     Z-ADD1         CK      40
| 01-C           CK        DOWLE@LRRN
|   C*
|   C           CK        CHAINSFLSR                40
| 02-C           *IN40     IFEQ *OFF
|   C*
|   C*ADD BY HONDA
|   C*刪生效月1日的異動紀錄H3 L3 E1
|   C                     EXSR R2DEL                      刪異動資料
|   C*ADD BY HONDA
| 03-C           LO13      IFEQ 'Y'
|   C                     EXSR R2D00
| 03-C                     END
|   C*
| 02-C                     END
|   C*
|   C                     ADD  1         CK
| 01-C                     ENDDO
|    C*
14===C                     ENDSR
     C*==============================================================*
     C*          R2DEL .刪除生效月一日的資料20060401 E1 H3 L3 H9 E8
     C*==============================================================*
15===C           R2DEL     BEGSR
|    C*
|    C           KEYDEL    CHAINPYE1PF               40
|    C  N40                DELETE10
|    C           KEYDEL    CHAINPYL3PF               40
|    C  N40                DELETL30
|    C           KEYDEL    CHAINPYH3PF               40
|    C  N40                DELETH30
|    C           KEYDE2    CHAINPYH9PF               40
|    C  N40                DELETH90
|    C           KEYDEL    CHAINPYE8PF               40
|    C  N40                DELETE80
|    C*
15===C                     ENDSR
     C*==============================================================*
     C*          R2D00 ....挑SFL中Y的資料...異動到主檔
     C*==============================================================*
16===C           R2D00     BEGSR
|    C*
|    C*異動類別'A2'
| 01-C           LO07      IFNE LO08
|   C                     EXSR R2DEM                      PYEMPF勞保
|   C                     EXSR R2DL3                      PYL3PF勞保
| 01-C                     END
|    C*
|    C*異動類別'2'
| 02-C           LO09      IFNE LO10
|   C                     EXSR R2DE1                      PYE1PF健保
|   C                     EXSR R2DE8                      PYE8PF健保
| 02-C                     END
|    C*
|    C*異動類別'AA'
| 03-C           LO11      IFNE LO12
|   C                     EXSR R2DH9                      PYH9PF勞退
|   C                     EXSR R2DH4                      PYH4PF勞退
|   C                     EXSR R2DH5                      PYH3PF勞退
| 03-C                     END
|    C*
16===C                     ENDSR
     C*==============================================================*
     C*          R2DEM ....異動PYEMPF的 EM23 勞保額
     C*==============================================================*
17===C           R2DEM     BEGSR
|    C*
|    C           KEYEM     CHAINPYEMPF               40
| 01-C           *IN40     IFEQ *OFF
|   C           LO08      ANDNEEM23
|   C                     Z-ADDLO08      EM23             新勞保額
|   C                     Z-ADDLOYMD     EM15             調整日
|   C                     UPDATEM0
| 01-C                     END
|    C*
17===C                     ENDSR
     C*==============================================================*
     C*          R2DL3 ....異動PYL3PF的 L304 勞保薪資
     C*==============================================================*
18===C           R2DL3     BEGSR
|    C*
|    C           KEYDEL    SETGTL30
|    C           KEYEM     REDPEL30                      45
| 01-C           *IN45     IFEQ *OFF
|   C*寫一筆生效月1日的資料
|   C                     Z-ADDLOYMD     L303
|   C                     MOVEL'A2'      L307             異動類別
|   C*寫一筆生效月1日的資料
|   C                     Z-ADDLO08      L304             投保薪資
|   C                     Z-ADD$A8YMD    L397
|   C                     TIME           L398
|   C                     MOVEL$USER     L399
|   C                     WRITEL30
| 01-C                     END
|    C*
18===C                     ENDSR
     C*==============================================================*
     C*          R2DE1 ....異動PYE1PF的 E105 健保額
     C*==============================================================*
19===C           R2DE1     BEGSR
|    C*
|    C           KEYDEL    SETGTE10
|    C           KEYE1     REDPEE10                      45
| 01-C           *IN45     IFEQ *OFF
|   C*寫一筆生效月1日的資料
|   C                     Z-ADDLOYMD     E103
|   C                     MOVEL'2'       E104             異動類別
|   C*寫一筆生效月1日的資料
|   C                     Z-ADDLO10      E105             新健保額
|   C                     Z-ADD$A8YMD    E197
|   C                     TIME           E198
|   C                     MOVEL$USER     E199
|   C                     WRITEE10
| 01-C                     END
|    C*
19===C                     ENDSR
     C*==============================================================*
     C*          R2DE8 ....異動PYE8PF的 E805 健保額
     C*==============================================================*
19===C           R2DE8     BEGSR
|    C*
|    C           KEYDEL    SETGTE80
|    C           KEYEM     REDPEE80                      45
| 01-C           *IN45     IFEQ *OFF
|   C*寫一筆生效月1日的資料
|   C                     Z-ADDLOYMD     E803
|   C                     MOVEL'2'       E804             異動類別
|   C*寫一筆生效月1日的資料
|   C                     Z-ADDLO10      E805             新健保額
|   C                     Z-ADD$A8YMD    E897
|   C                     TIME           E898
|   C                     MOVEL$USER     E899
|   C                     WRITEE80
| 01-C                     END
|    C*
19===C                     ENDSR
     C*==============================================================*
     C*          R2DH4 ....異動PYH4PF的 H409 勞退額
     C*==============================================================*
20===C           R2DH4     BEGSR
|    C*
|    C           KEYH4     CHAINPYH4PF               40
| 01-C           *IN40     IFEQ *OFF
|   C           LO12      ANDNEH409
|   C                     Z-ADDLO12      H409             新提繳金額
|   C                     Z-ADDLOYMD     H497
|   C                     TIME           H498
|   C                     MOVEL$USER     H499
|   C                     UPDATH40
| 01-C                     END
|    C*
20===C                     ENDSR
     C*==============================================================*
     C*          R2DH5 ....異動PYH3PF的 H311 提繳工資
     C*==============================================================*
21===C           R2DH5     BEGSR
|    C*
|    C           KEYDEL    SETGTH30
|    C           KEYEM     REDPEH30                      45
| 01-C           *IN45     IFEQ *OFF
|   C*寫一筆生效月1日的資料
|   C                     Z-ADDLOYMD     H303
|   C*寫一筆生效月1日的資料
|   C                     Z-ADDLO12      H311             新勞保額
|   C                     Z-ADD$A8YMD    H397
|   C                     TIME           H398
|   C                     MOVEL$USER     H399
|   C                     WRITEH30
| 01-C                     END
|    C*
21===C                     ENDSR
     C*==============================================================*
     C*          R2DH9 ....異動PYH9PF的 H906 提繳工資
     C*==============================================================*
21===C           R2DH9     BEGSR
|    C*
|    C           KEYDE2    SETGTH90
|    C           KEYEM     REDPEH90                      45
| 01-C           *IN45     IFEQ *OFF
|   C*寫一筆生效月1日的資料
|   C                     Z-ADDLOYMD     H903
|   C                     MOVEL'AA'      H904             異動別
|   C*寫一筆生效月1日的資料
|   C                     Z-ADDH907      H908             原勞保額
|   C                     Z-ADDLO12      H907             新勞保額
|   C                     Z-ADD$A8YMD    H997
|   C                     TIME           H998
|   C                     MOVEL$USER     H999
|   C                     WRITEH90
| 01-C                     END
|    C*
21===C                     ENDSR
     C*==============================================================*
     C*          R2C00 ....INIT 'SC03'
     C*==============================================================*
22===C           R2C00     BEGSR
|    C*
|    C*  DFUN 取作業項目的中文
|    C                     MOVELWFUN,DOPT DFUN
|    C*
|    C*          DOPT      IFEQ 1                          *ADD
|    C*          DOPT      IFEQ 2                          *UPD
|    C*                    SETON                     31    *CAN INPUT
|    C*                    ELSE                            *DEL, INQ
|    C*                    SETOF                     31    *INP PROTECT
|    C*                    END
|    C*
|    C                     SETON                     31    *INPUT
| 01-C           DOPT      IFNE 1                          *U/D/I
|   C                     EXSR R2CB0
| 01-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)----------------------------------*
|    C*          DOPT      IFEQ 1                          *ADD
|    C*          DOPT      OREQ 5                          *INQ
|    C*          *LOVAL    SETLLLO0
|    C*                    END
|    C*---------------------------------------------------------------*
|    C*
22===C                     ENDSR
     C*==============================================================*
     C*  R2C20 ....'SC03' FIELD INIT : UPD, DEL, INQ, (ADD & COPY)
     C*==============================================================*
23===C           R2CB0     BEGSR
|    C*
|    C                     MOVEL*BLANK    CM03
|    C                     MOVEL*BLANK    DP08
|    C           LO01      CHAINSYCMPF               40
|    C           KEYDP     CHAINPYDPPF               40
|    C*
|    C                     Z-ADDLO08      DLO08
|    C                     Z-ADDLO10      DLO10
|    C                     Z-ADDLO12      DLO12
|    C                     Z-ADDLO15      DLO15
|    C*
23===C                     ENDSR
     C*==============================================================*
     C*          R3000 .... 'SC03' SCREEN MAIN ROUTINE
     C*==============================================================*
24===C           R3000     BEGSR
|    C*
|    C                     EXFMTDSPD3
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E3000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C                     MOVEL'SC02'    SCID
|   C                     GOTO E3000
| 02-C                     END
|    C*
|    C                     EXSR R3B00                      *SC03 CHK
|    C*
| 03-C           *IN99     IFEQ '0'                        *CHK OK
|   C                     EXSR R3D00                      *FILE UPD
|   C                     SETON                     98
|   C                     MOVE 'SC02'    SCID
| 03-C                     END
|    C*
24===C           E3000     ENDSR
     C*==============================================================*
     C*          R3B00 ....CHECK 'SC03' SCREEN
     C*==============================================================*
25===C           R3B00     BEGSR
|    C*
|    C*---------------------------------------------------------------
| 01-C           DLO12     IFGE 15840
| 02-C           DLO12     IFGE 131700
|   C                     Z-ADD131700    DLO10
|   C                     Z-ADD42000     DLO08
|   C                     GOTO CHK
| 02-C                     END
| 03-C           DLO12     IFGE 15840
|   C           DLO12     ANDLE131700
|   C                     Z-ADDDLO12     DLO10
|   C                     Z-ADD42000     DLO08
|   C                     GOTO CHK
| 03-C                     END
| 01-C                     END
|    C*---------------------------------------------------------------
|    C*
|    C           CHK       TAG
|    C*新勞保
|    C*
| 04-C           DLO08     IFEQ *ZERO
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6699
|   C*
| 04*C                     ELSE
|   C*
|   C           DLO08     CHAINPYL1LF01             40
| 05-C           *IN40     IFEQ *ON
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6699
| 05-C                     END
|   C*
| 04-C                     END
|    C*---------------------------------------------------------------
|    C*新健保
|    C   99                GOTO E3B00
| 06-C           DLO10     IFEQ *ZERO
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6799
|   C*
| 06*C                     ELSE
|   C*
|   C           DLO10     CHAINPYE3LF01             40
| 07-C           *IN40     IFEQ *ON
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6799
| 07-C                     END
|   C*
| 06-C                     END
|    C   99                GOTO E3B00
|    C*---------------------------------------------------------------
|    C*新勞退
| 08-C           DLO12     IFEQ *ZERO
|   C                     MOVEL'UPT1011' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6899
|   C*
| 08*C                     ELSE
|   C*
|   C           DLO12     CHAINPYH7LF01             40
| 09-C           *IN40     IFEQ *ON
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6899
| 09-C                     END
|   C*
| 08-C                     END
|    C   99                GOTO E3B00
|    C*-------------------------------------------------------------
|    C*
25===C           E3B00     ENDSR
     C*==============================================================*
     C*          R3D00 ....FILE PROCESS
     C*==============================================================*
26===C           R3D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ2         R3D10
| 01-C                     END
|    C*
26===C                     ENDSR
     C*==============================================================*
     C*          R3D10 ....FILE PROCESS : UPD
     C*==============================================================*
27===C           R3D10     BEGSR
|    C*
|    C           KEYSF     CHAINLO0                  40
|    C*
| 01-C           *IN40     IFEQ *OFF
|   C                     Z-ADDDLO08     LO08
|   C                     Z-ADDDLO10     LO10
|   C                     Z-ADDDLO12     LO12
| 02-C           LO07      IFNE LO08                        有變動時
|   C           LO09      ORNE LO10
|   C           LO11      ORNE LO12
|   C                     MOVE 'Y'       LO13              變動Y
| 02*C                     ELSE
|   C                     MOVE 'N'       LO13              變動N
| 02-C                     END
|   C                     Z-ADD$A8YMD    LO97
|   C                     TIME           LO98
|   C                     MOVEL$USER     LO99
|   C                     UPDATLO0
| 01-C                     END
|    C*
27===C                     ENDSR
     C*==============================================================*
**
新增修改      刪除查詢
