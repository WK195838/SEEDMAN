     H****************************************************************
     H*   PROGRAM NAME : PYR3322                                     *
     H*   AUTHER       : A1084 CHARLES                               *
     H*   CREATE DATE  : 93/12/31 A1386 SHARON                       *
     H*   UPDATE DATE  :                                             *
     H*   SYSTEM       :                                             *
     H*   SUBSYSTEM    :人事薪資系統                               *
     H****************************************************************
     FPYPHJF04IP  E           K        DISK
     FPYEMPF  IF  E           K        DISK
     FSYCMPF  IF  E           K        DISK
     FPYDPPF  IF  E           K        DISK
     FPYSSLF03IF  E           K        DISK
     FPYHDPF  IF  E           K        DISK
     FPYPMPF  IF  E           K        DISK
     FPYLVLF02IF  E           K        DISK
     FPYLCPF  IF  E           K        DISK
     FPYLTLF01IF  E           K        DISK
     FPYOVLF01IF  E           K        DISK
     FPYSHPF  IF  E           K        DISK
     FPYSDPF  IF  E           K        DISK
     FPYHAPF  UF  E           K        DISK                      A
     C*勞退新增部份
     FPYH4PF  IF  E           K        DISK
     FPYH6PF  IF  E           K        DISK
     FPYR332P O   E             39     PRINTER
     E*---------------------------------------------------------------
     E                    A1         48  2               薪資別
     E                    A2         48 14               薪資名
     E                    A3         48  7 0             薪資
     E*
     E                    LV          6  1               假別編號
     E                    LC          6  6               假別名
     E                    HOUR        6  4 2             請假時數
     E*---------------------------------------------------------------
     IPYDA04      DS
     I                                        1  44 FLD05
     I*
     I            DS
     I                                        1   60PH03W
     I                                        2   40PH03Y1
     I                                        5   60PH03M1
     I            DS
     I                                        1   60LT96
     I                                        1   40LT96Y
     I                                        5   60LT96M
     I            DS
     I                                        1   60WYM
     I                                        1   40WY
     I                                        5   60WM
     I            DS
     I                                        1  36 PD20
     I                                        1   6 PD21
     I                                        7  12 PD22
     I                                       13  18 PD23
     I                                       19  24 PD24
     I                                       25  30 PD25
     I                                       31  36 PD26
     I            DS
     I                                        1  240PD30
     I                                        1   41PD31
     I                                        5   81PD32
     I                                        9  121PD33
     I                                       13  161PD34
     I                                       17  201PD35
     I                                       21  241PD36
     ILDA        UDS
     I                                      101 110 $USER
     I                                      187 1920DDATE
     I                                      125 1270$CPY
     I                                      201 2080$A8YMD
     I                                      217 218 $PRTCD
     I                                      219 219 $PENV
     I                                      500 500 $DATA
     I*
     I                                      553 554 DEM01
     I                                      623 629 DEM041
     I                                      630 636 DEM042
     I                                      531 538 DEM021
     I                                      539 546 DEM022
     I                                      547 5520WDYM
     I                                      555 555 DPH04
     I                                      560 5660DYMD
     I                                      720 7270DYMX1
     I                                      728 7350DYMX2
     I                                      740 779 RXMARK
     I/COPY PYDDSSRC,PYPMDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C           *NAMVAR   DEFN *LDA      LDA
     C           *NAMVAR   DEFN           PYDA04
     C           *LIKE     DEFN SS09      WSS09
     C           *LIKE     DEFN HD05      KHD05
     C           *LIKE     DEFN PH01      KPH01
     C           *LIKE     DEFN PH02      KPH02
     C           *LIKE     DEFN PH03      KPH03
     C           *LIKE     DEFN SH03      KSH03
     C*==============================================================*
     C*                    KLIST
     C*==============================================================*
     C           KEYPM     KLIST
     C                     KFLD           PM01
     C                     KFLD           DEM01
     C           KEYDP     KLIST
     C                     KFLD           EM041
     C                     KFLD           EM042
     C                     KFLD           EM043
     C                     KFLD           EM044
     C           KEYHD     KLIST
     C                     KFLD           PH01
     C                     KFLD           PH02
     C                     KFLD           PH03
     C                     KFLD           PH04
     C           KEYLV     KLIST
     C                     KFLD           EM01
     C                     KFLD           EM02
     C                     KFLD           WDYM
     C           KEYSH     KLIST
     C                     KFLD           PH01
     C                     KFLD           PH02
     C                     KFLD           KSH03
     C           KEYSH1    KLIST
     C                     KFLD           PH01
     C                     KFLD           PH02
     C*
     C                     MOVEL'1'       WSS09
     C           KEYSS     KLIST
     C                     KFLD           WSS09            薪資項目
     C                     KFLD           CM13             薪資組別
     C*
     C           KEYSS1    KLIST
     C                     KFLD           WSS09            薪資項目
     C                     KFLD           CM13             薪資組別
     C                     KFLD           HD05             薪資代碼
     C*勞退新增部份
     C           KEYHA     KLIST
     C                     KFLD           HA01             公司別
     C                     KFLD           HA02             薪資年月
     C           KEYH4     KLIST
     C                     KFLD           H401             公司別
     C                     KFLD           H402             工號
     C           KEYH6     KLIST
     C                     KFLD           H601             公司別
     C                     KFLD           H602             工號
     C                     KFLD           H603             發新年月
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C*
     C  N10                EXSR RTN010
     C*
     C                     EXSR RTN020
     CLRN10                EXSR RTN899
     C*==============================================================*
     C*          RTN010... INITIAL GET HEADING
     C*==============================================================*
01===C           RTN010    BEGSR
|    C*勞退新增部份
|    C                     MOVE PH01      HA01
|    C                     Z-ADDWDYM      HA02
|    C           KEYHA     CHAINPYHAPF               40
|    C                     Z-ADDDYMX1     HA03
|    C                     Z-ADDDYMX2     HA04
|    C                     MOVELRXMARK    HA05
|    C                     Z-ADD$A8YMD    EM97
|    C                     TIME           EM98
|    C                     MOVEL$USER     EM99
|    C   40                WRITEHA0
|    C  N40                UPDATHA0
|    C*
|     *
|    C                     IN   *NAMVAR
|    C                     SETON                     10
|    C*
|    C                     MOVEL'Y'       $DATA
|    C                     SETON                     20
|    C*GET PAYROLL PARAMETER
|    C                     MOVEL'01'      PM01
|    C           KEYPM     CHAINPM0                  40
|    C*-------------------------------------------------------
|    C           DEM01     CHAINCM0                  40
|    C*-------------------------------------------------------
|    C*薪資項目
|    C                     MOVEL*BLANKS   A1
|    C                     MOVEL*BLANKS   A2
|    C                     Z-ADD*ZERO     PD07
|    C                     Z-ADD*ZERO     PD08
|    C                     Z-ADD*ZERO     PD10
|    C                     Z-ADD*ZERO     PD17
|    C                     MOVEL*BLANKS   PD20
|    C                     Z-ADD*ZERO     PD30
|    C*
|    C           KEYSS     SETLLSS0
|    C           KEYSS     READESS0                      46
| 01-C           *IN46     DOWEQ'0'
|   C*
| 02-C           DPH04     IFEQ '2'
|   C                     Z-ADDSS16      WI      20
| 02*C                     ELSE
|   C                     Z-ADDSS13      WI
| 02-C                     END
|   C*
| 03-C           WI        IFNE *ZERO
|   C                     MOVELSS01      A1,WI
|   C                     MOVELSS02      A2,WI
| 03-C                     END
|   C*
|   C           KEYSS     READESS0                      46
| 01-C                     END
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DEM01     CM001I  2         公司代號
|    C                     PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*
|    C           WDYM      SETGTLT0
|    C                     READPLT0                      46
| 04-C           *IN46     IFEQ '0'
|   C           LT96      ANDNE*ZERO
|   C                     Z-ADDLT96Y     WY
|   C           LT96M     ADD  1         WM
|   C*
| 05-C           WM        IFGT 12
|   C                     ADD  1         WY
|   C                     Z-ADD1         WM
| 05-C                     END
|   C*
| 04-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          RTN020... MAIN PROCESS
     C*==============================================================*
02===C           RTN020    BEGSR
|    C*
|    C           KEYDP     CHAINDP0                  40
|    C   40                MOVEL*BLANK    DP02
|    C*
|    C*發薪年月以民國年表示
|    C                     CALL 'P39'
|    C                     PARM PH03      P3901I  60
|    C                     PARM '2'       P3902I  1
|    C                     PARM '1'       P3903I  1
|    C                     PARM '2'       P3904I  1
|    C                     PARM '2'       P3905I  1
|    C           PH03W     PARM           P3911O  60
|    C*
|    C                     MOVELEM03      EM03X
|    C                     WRITEPH1
|    C*GET DATA
|    C                     EXSR RTN100
|    C*WRITE DETAIL
|    C                     EXSR RTN200
|    C*WRITE BOTTOM
|    C                     EXSR RTN300
|    C*
02===C                     ENDSR
     C*==============================================================*
     C*          RTN100... GET DATA
     C*==============================================================*
03===C           RTN100    BEGSR
|    C*
|    C                     Z-ADD0         A3
|    C                     Z-ADD*ZERO     PADD
|    C                     Z-ADD*ZERO     PSUB
|    C*
|    C           KEYHD     SETLLHD0
|    C           KEYHD     READEHD0                      46
| 01-C           *IN46     DOWEQ'0'
|   C*
|   C                     Z-ADD1         WI
|   C           HD05      LOKUPA1,WI                    40
| 02-C           *IN40     IFEQ '1'
|   C                     Z-ADDHD06      A3,WI
|   C*
|   C           KEYSS1    CHAINSS0                  41
| 03-C           *IN41     IFEQ '0'
|   C*
| 04-C           SS04      IFEQ 'A'                        加項薪資
|   C                     ADD  HD06      PADD
| 04-C                     END
|   C*
| 05-C           SS04      IFEQ 'B'                        減項薪資
|   C                     ADD  HD06      PSUB
| 05-C                     END
|   C*
| 03-C                     END
|   C*
| 02-C                     END
|   C*
|   C           KEYHD     READEHD0                      46
| 01-C                     END
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          RTN200... WRITE DETAIL
     C*==============================================================*
04===C           RTN200    BEGSR
|    C*
|    C                     Z-ADD0         WI
| 01-C           WI        DOWLT33
|   C*
|   C                     ADD  1         WI
|   C                     MOVELA2,WI     ARR1
|   C                     Z-ADDA3,WI     AMT1
|   C                     ADD  1         WI
|   C                     MOVELA2,WI     ARR2
|   C                     Z-ADDA3,WI     AMT2
|   C                     ADD  1         WI
|   C                     MOVELA2,WI     ARR3
|   C                     Z-ADDA3,WI     AMT3
|   C*
|   C           KEYSH1    CHAINEM0                  40
|   C                     WRITEPD1
| 01-C                     END
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          RTN300... WRITE BOTTOM
     C*==============================================================*
05===C           RTN300    BEGSR
|    C*調薪資料
|    C                     SETOF                     25
|    C                     MOVELPH03      KSH03
|    C                     MOVE '01'      KSH03
|    C           KEYSH     SETLLSH0
|    C           KEYSH1    READESH0                      40
| 01-C           *IN40     IFEQ '0'
|   C                     MOVELSH03      SH03Y   60
| 02-C           SH03Y     IFEQ WDYM
|   C                     SETOF                     25
|   C                     Z-ADDSH07      SH071
|   C                     Z-ADDSH08      SH081
|   C*
|   C           KEYSH     SETLLSD0
|   C           KEYSH     READESD0                      41
| 03-C           *IN41     DOWEQ'0'
|   C                     ADD  SD05      SH071
|   C                     ADD  SD06      SH081
|   C           KEYSH1    READESD0                      41
| 03-C                     END
| 02-C                     END
| 01-C                     END
| 04-C           DPH04     IFEQ '1'
|   C*請假資料
|   C                     MOVEL*BLANK    LV
|   C                     MOVEL*BLANK    LC
|   C                     Z-ADD0         HOUR
| 05-C           *IN20     IFEQ '1'
|   C                     Z-ADD0         I       10
|   C           KEYLV     SETLLLV0
|   C           KEYLV     READELV0                      46
| 06-C           *IN46     DOWEQ'0'
|   C                     Z-ADDLV08      WHR     52
|   C           LV09      MULT PM101     WHOUR   52H
|   C                     ADD  WHOUR     WHR
|   C                     Z-ADD1         J       10
|   C           LV04      LOKUPLV,J                     59
| 07-C           *IN59     IFEQ '1'
|   C                     ADD  WHR       HOUR,J
| 07*C                     ELSE
|   C                     ADD  1         I
|   C           LV04      CHAINLC0                  40
| 08-C           *IN40     IFEQ '0'
|   C                     MOVELLV04      LV,I
|   C                     MOVELLC02      LC,I
|   C                     Z-ADDWHR       HOUR,I
| 08-C                     END
| 07-C                     END
|   C           KEYLV     READELV0                      46
| 06-C                     END
|    *假別
|   C                     MOVELLC,1      PD21
|   C                     MOVELLC,2      PD22
|   C                     MOVELLC,3      PD23
|   C                     MOVELLC,4      PD24
|   C                     MOVELLC,5      PD25
|   C                     MOVELLC,6      PD26
|    *時數
|   C                     Z-ADDHOUR,1    PD31
|   C                     Z-ADDHOUR,2    PD32
|   C                     Z-ADDHOUR,3    PD33
|   C                     Z-ADDHOUR,4    PD34
|   C                     Z-ADDHOUR,5    PD35
|   C                     Z-ADDHOUR,6    PD36
| 05-C                     END
|    *加班資料
|   C                     Z-ADD0         PD07
|   C                     Z-ADD0         PD08
|   C                     Z-ADD0         PD10
|   C                     Z-ADD0         PD17
| 09-C           *IN20     IFEQ '1'
|   C           KEYLV     SETLLOV0
|   C           KEYLV     READEOV0                      46
| 10-C           *IN46     DOWEQ'0'
|   C                     ADD  OV07      PD07
|   C                     ADD  OV08      PD08
|   C                     ADD  OV10      PD10
|   C                     ADD  OV17      PD17
|   C           KEYLV     READEOV0                      46
| 10-C                     END
| 09-C                     END
| 04-C                     END
|    C*應扣金額
|    C                     Z-ADD*ZERO     PHSUB   70
|    C           PH28      ADD  PH29      PHSUB
|    C                     ADD  PH18      PHSUB
|    C*實領金額
|    C           PH17      SUB  PHSUB     PHTOT
|    C                     ADD  PH27      PHTOT
|    C                     ADD  PH16      PHTOT
|    C*
|    C                     CALL 'P31'
|    C                     PARM SH03      P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM DFMT      P3104I  1
|    C                     PARM DTYPE     P3105I  1
|    C           SH03W     PARM           P3101O  80
|    C*
|    C                     WRITEPE1
|    C*
|    C*勞退增加部份
|    C           DYMX1     SUB  19110000  DYMD1
|    C           DYMX2     SUB  19110000  DYMD2
|    C                     MOVELRXMARK    REMARK
|    C                     MOVE PH01      H401
|    C                     MOVE PH02      H402
|    C           KEYH4     CHAINPYH4PF               40
|    C  N40                DO
|    C                     Z-ADDH412      BMT4
|    C                     Z-ADDH413      BMT3
|    C                     END
|    C   40                DO
|    C                     Z-ADD0         BMT4
|    C                     Z-ADD0         BMT3
|    C                     END
|    C                     MOVE PH01      H601
|    C                     MOVE PH02      H602
|    C                     Z-ADDPH03      H603
|    C           KEYH6     CHAINPYH6PF               40
|    C  N40                DO
|    C                     Z-ADDH605      BMT2
|    C                     Z-ADDH606      BMT1
|    C                     END
|    C   40                DO
|    C                     Z-ADD0         BMT2
|    C                     Z-ADD0         BMT1
|    C                     END
|    C                     WRITEPZ1
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          RTN899... LEVEL BREAK LR --- NO DATA
     C*==============================================================*
06===C           RTN899    BEGSR
|    C*
| 01-C           $PENV     IFNE '2'
| 01*C                     ELSE
|   C                     WRITEPE9
| 01-C                     END
|    C*
06===C                     ENDSR
     C*==============================================================*
