     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    PROGRAM-ID   : PYA606                                     *
     H*    PROGRAM NAME :綜合福利金年度餘額計算                    *
     H*    AUTHOR       : A1274 EVA                                  *
     H*    CREATE DATE  : 90/04/17                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :人事薪資系統                              *
     H****************************************************************
     FPYGHPF  IF  E           K        DISK
     FPYGDLF01IF  E           K        DISK
     FPYEMPF  IF  E           K        DISK
     FPYA606D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    WFUN    5   5  6
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      211 2160DDATE
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 174 $COR
     I                                      201 2080$A8YMD
     I                                      201 2040$A8YY
     I                                      205 2080$A8MD
     I            DS
     I                                        1   80WYMDS
     I                                        1   40SYY
     I                                        5   80SMD
     I            DS
     I                                        1   80WYMDE
     I                                        1   40EYY
     I                                        5   80EMD
     I            DS
     I                                        1   70DGH042
     I                                        6   70WGH042
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C*==============================================================*
     C*                    KLIST
     C*==============================================================*
     C*PYEMPF
     C           KEYEM     KLIST
     C                     KFLD           DGH01
     C                     KFLD           DGH02
     C*PYGHPF
     C           KEYGH     KLIST
     C                     KFLD           DGH01
     C                     KFLD           DGH02
     C                     KFLD           WGH03
     C*PYGDPF
     C           KEYGD     KLIST
     C                     KFLD           DGH01
     C                     KFLD           WGH03
     C                     KFLD           DGH02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N99                EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     MOVE 'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     Z-ADD5         DOPT
|    C                     MOVEL*BLANK    DGH01
|    C                     MOVEL*BLANK    DGH01N
|    C                     MOVEL*BLANK    DGH02
|    C                     MOVEL*BLANK    DGH02N
|    C*910125S KLAUS       Z-ADD$A8YY     DGH03            福利年度
|    C           $A8MD     IFLE 0331
|    C           $A8YY     SUB  1         WGH03   40
|    C                     ELSE
|    C                     Z-ADD$A8YY     WGH03            福利年度
| 01-C                     END
|    C           WGH03     SUB  1911      DGH03            福利年度
|    C*910125E KLAUS
|    C           $A8YMD    SUB  19110000  DGH07
|    C                     Z-ADD$A8YMD    WGH07   80
|    C                     Z-ADD*ZERO     DGH041
|    C                     Z-ADD*ZERO     DGH042
|    C                     Z-ADD*ZERO     DPAY1
|    C                     Z-ADD*ZERO     DPAY2
|    C                     Z-ADD*ZERO     DRST1
|    C                     Z-ADD*ZERO     DRST2
|    C*--------------------------------------------------------------*
|    C*公司
|    C                     MOVEL$COR      DGH01
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPC1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'                        *END
|   C                     EXSR R1E00
|   C                     GOTO E1000
| 02-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 03-C           *IN99     IFEQ '0'                        *SCR CHK OK
|   C                     EXSR R1C00                      *PRE-SC02
|   C                     MOVEL'SC02'    SCID
| 03-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
03===C           R1B00     BEGSR
|    C*
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*公司別
| 02-C           DGH01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DGH01     CM001I  2         公司代號
|    C           DGH01N    PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 03-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
| 04-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*員工編號
| 05-C           DGH02     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 05-C                     END
|    C   99                GOTO E1B00
|    C*
|    C           KEYEM     CHAINEM0                  40    *NF
| 06-C           *IN40     IFEQ '0'
|   C                     MOVELEM03      DGH02N    P
| 06*C                     ELSE
|   C                     MOVEL*BLANK    DGH02N
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 06-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*福利年度（中國年）
| 07-C           DGH03     IFEQ *ZERO
|   C                     MOVEL'UPT0012' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 07-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*資料查詢：該福利年度的該員工福利資料須存在才可查詢
|    C           KEYGH     CHAINGH0                  40
| 08-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     99
| 08-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*預定離職日期：有兩種狀況
|    C*狀況一：離職日期＝０者．求當年度餘額
|    C*狀況二：離職日期＜＞０者．求當年度餘額及比例計算金額
|    C*
| 09-C*          DGH07     IFNE 0
| 09-C           WGH07     IFNE 0
|   C                     CALL 'P30'
|   C                     PARM WGH07     P3001I  80
|   C                     PARM '2'       P3002I  1
|   C                     PARM '1'       P3003I  1
|   C                     PARM           P3011O  1
| 10-C           P3011O    IFEQ 'N'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 10-C                     END
|   C   99                GOTO E1B00
|   C*
| 09-C                     END
|    C*
|    C*---------------------------------------------------------------
|    C*
03===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
04===C           R1C00     BEGSR
|    C*
|    C*作業 DFUN
|    C                     MOVELWFUN,DOPT DFUN
|    C*
| 01-C           DOPT      IFEQ 5
|   C                     EXSR R1C20                      *U/D/I
| 01-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)----------------------------------*
| 02-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 5                          *INQ
|   C           *LOVAL    SETLLGH0
|   C           *LOVAL    SETLLGD0
| 02-C                     END
|    C*---------------------------------------------------------------*
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*  R1C20 ....'SC02' FIELD INIT : UPD, DEL, INQ, (ADD & COPY)
     C*==============================================================*
05===C           R1C20     BEGSR
|    C*
|    C*---------------------------------------------------------------*
|    C                     Z-ADD*ZERO     DGH041
|    C                     Z-ADD*ZERO     DGH042
|    C                     Z-ADD*ZERO     DPAY1
|    C                     Z-ADD*ZERO     DPAY2
|    C                     Z-ADD*ZERO     DRST1
|    C                     Z-ADD*ZERO     DRST2
|    C*---------------------------------------------------------------*
|    C*年度福利金．個人申請總額．福利金餘額：無預定離職日期
|    C           KEYGH     CHAINGH0                  40
| 01-C           *IN40     IFEQ '0'
|   C                     Z-ADDGH04      DGH041
|   C*
|   C           KEYGD     SETLLGD0
|   C           KEYGD     READEGD0                      46
| 02-C           *IN46     DOWEQ'0'
|   C                     ADD  GD08      DPAY1
|   C           KEYGD     READEGD0                      46
| 02-C                     END
|   C*
|   C           DGH041    SUB  DPAY1     DRST1
|   C*
| 01-C                     END
|    C*---------------------------------------------------------------*
|    C*年度福利金．個人申請總額．福利金餘額：有預定離職日期
|    C*計算比率＝（年度福利金／全年工作天數＊在職天數）
|    C*計算起日：以福利年度４月份起算（０４０１∼離職日期）且含起日
| 03-C**         DGH07     IFNE *ZERO
| 03-C           WGH07     IFNE *ZERO
|   C*
|   C                     Z-ADD*ZERO     DAY1    30
|   C                     Z-ADD*ZERO     DAY2    30
|   C                     Z-ADD*ZERO     WYMDS            年度起日
|   C                     Z-ADD*ZERO     WYMDE            年度迄日
|   C*
|   C*                    Z-ADDDGH03     SYY
|   C                     Z-ADDWGH03     SYY
|   C                     Z-ADD0401      SMD
|   C**         DGH03     ADD  1         EYY
|   C           WGH03     ADD  1         EYY
|   C                     Z-ADD0331      EMD
|   C*
|   C*全年工作天數
|   C                     CALL 'P32'
|   C                     PARM WYMDS     P3201I  80
|   C                     PARM WYMDE     P3202I  80
|   C                     PARM '2'       P3203I  1
|   C                     PARM '1'       P3204I  1
|   C                     PARM           P3211O  90
|   C           P3211O    ADD  1         DAY1
|   C*
|   C*在職天數
|   C                     CALL 'P32'
|   C                     PARM WYMDS     P3201I  80
|   C**                   PARM DGH07     P3202I  80
|   C                     PARM WGH07     P3202I  80
|   C                     PARM '2'       P3203I  1
|   C                     PARM '1'       P3204I  1
|   C                     PARM           P3211O  90
|   C           P3211O    ADD  1         DAY2
|   C*
|   C*年度福利金．個人申請總額．福利金餘額：取至百位數
|   C           DGH041    DIV  DAY1      DGH042
|   C           DGH042    MULT DAY2      DGH042
|   C                     Z-ADD*ZERO     WGH042
|   C                     Z-ADDDPAY1     DPAY2
|   C           DGH042    SUB  DPAY2     DRST2
|   C*
| 03-C                     END
|    C*---------------------------------------------------------------*
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1E00 ....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
06===C           R1E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*-------------------------------------------------------------***
|    C*員工編號DGH02
| 01-C           #FLD      IFEQ 'DGH02'
|   C                     CALL 'PYI101'
|   C                     PARM DGH01     P101I1  2         公司別
|   C           DGH02     PARM DGH02     P101I2  8         員工　
|   C                     PARM           @RTNC   2          *FUN KEY
| 01-C                     END
|    C*-------------------------------------------------------------***
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
07===C           R2000     BEGSR
|    C*
|    C                     EXFMTDSPC2
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C* RELEAS FILE --------------------------------------------------*
|   C           *LOVAL    SETLLGH0
|   C           *LOVAL    SETLLGD0
|   C*---------------------------------------------------------------*
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 03-C           *IN99     IFEQ '0'                        *CHK OK
|   C                     MOVE 'SC01'    SCID
| 03-C                     END
|    C*
07===C           E2000     ENDSR
     C*==============================================================*
**
新增修改      刪除查詢
