     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : PYA106                                     *
     H*    PROGRAM NAME :休假基本資料建立                          *
     H*    AUTHOR       : A1232 ERIC                                 *
     H*    CREATE DATE  : 86/02/21                                   *
     H*    UPDATE DATE  : 88/11/18 A1274 EVA                         *
     H*    SYSTEM       :人事薪資系統                              *
     H****************************************************************
     FPYLUPF  UF  E           K        DISK                      A
     FPYLTPF  UF  E           K        DISK                      A
     FPYEMPF  IF  E           K        DISK
     FPYPMPF  IF  E           K        DISK
     FPYA106D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    TFUN    5   5  6
     E                    TFUN1   4  12  9
     E*---------------------------------------------------------------*
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I           UDS
     I                                      101 110 $USER
     I                                      211 2160DDATE
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      142 142 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I            DS
     I                                        1   7 EM04
     I                                        1   1 EM041
     I                                        2   2 EM042
     I                                        3   3 EM043
     I                                        4   7 EM044
     I            DS
     I                                        1   40WLT03
     I                                        3   40WLT032
     I*
     I/COPY PYDDSSRC,PYPMDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN RRN       @LRRN            LAST REC NUM
     C           *LIKE     DEFN RRN       @LPAGE           LAST PAGE
     C           *LIKE     DEFN RRN       @PRRN            PROMPT USE
     C           *LIKE     DEFN CM004O    DFMT
     C           *LIKE     DEFN CM005O    DTYPE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYEM     KLIST                           *PYEMPF
     C                     KFLD           DLT01
     C                     KFLD           DLT02
     C           KEYLT     KLIST                           *PYLTPF
     C                     KFLD           DLT01
     C                     KFLD           DLT02
     C                     KFLD           WLT03
     C           KEYLU     KLIST                           *PYLUPF
     C                     KFLD           DLT01
     C                     KFLD           DLT02
     C                     KFLD           WLT03
     C                     KFLD           DLU04   20
     C           KEYPM     KLIST
     C                     KFLD           PM01
     C                     KFLD           DLT01
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
    C           SCID      CASEQ'SC03'    R3000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C                     MOVE 'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     MOVEL*BLANKS   DLT02
|    C                     Z-ADD0         DLT03
|    C                     Z-ADD1         DOPT
|    C*公司
|    C                     MOVEL$COR      DLT01
| 01-C           $RMK03    IFNE 'Y'
|   C                     SETOF                     32
| 01*C                     ELSE
|   C                     SETON                     32
| 01-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'
|   C                     EXSR R1E00
|   C                     GOTO E1000
| 02-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 03-C           *IN99     IFEQ '0'
|   C                     EXSR R1C00
|   C                     MOVEL'SC02'    SCID             *SCR2
| 03-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1B00 ....SCREEN CHECK
     C*==============================================================*
03===C           R1B00     BEGSR
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*公司別
|    C                     MOVEL*BLANK    DLT01N
| 02-C           DLT01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DLT01     CM001I  2         公司代號
|    C           DLT01N    PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C           DFMT      PARM           CM004O  1         日期格式
|    C           DTYPE     PARM           CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 03-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
| 04-C           CM099O    IFEQ *BLANK
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     MOVEL'01'      PM01
|    C           KEYPM     CHAINPM0                  40
| 05-C           *IN40     IFEQ '1'
|   C                     MOVEL'UMI0440' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6099
| 05-C                     END
|    C   99                GOTO E1B00
|    C*+<<DATA CHECK>>+++++++++++++++++++++++++++++++++++++++++++++++++
|    C*員工編號
| 06-C           DLT02     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 06-C                     END
|    C   99                GOTO E1B00
|    C*
|    C           KEYEM     CHAINEM0                  40    *NF
| 07-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 07-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*部門別
|    C                     CALL 'PYSCM1'
|    C                     PARM EM01      CM101I  2         公司代號
|    C                     PARM EM04      CM102I  7         部門代號
|    C                     PARM           CM103O 14         部門名稱
|    C                     PARM           CM199O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 08-C           CM199O    IFEQ 'N'
|   C                     SETON                     6199
|   C                     MOVEL'US#0098' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 08-C                     END
|    C   99                GOTO E1B00
|    C*
| 09-C           CM199O    IFEQ *BLANK
|   C                     SETON                     606199
|   C                     MOVEL'US#1111' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 09-C                     END
|    C   99                GOTO E1B00
|    C*---------------------------------------------------------------
|    C*年度
|    C*          DLT03     IFEQ 0
|    C*                    MOVEL'UPT0010' ERRID
|    C*                    MOVEL'PTMF'    ERRF
|    C*                    SETON                     6299
|    C*                    END
|    C*  99                GOTO E1B00
|    C*
|    C*          DLT03     ADD  1911      WLT03
|    C                     CALL 'P82'
|    C                     PARM DLT03     P3101I  40
|    C                     PARM DFMT      P3102I  1
|    C                     PARM DTYPE     P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           WLT03     PARM           P3101O  40
|    C*                    Z-ADDWLV03     WDLV03
|    C*
|    C           KEYLT     CHAINLT0                  40
| 10-C           DOPT      IFEQ 1                          *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     99
| 10-C                     END
|    C   99                GOTO E1B00
|    C*
| 11-C           DOPT      IFNE 1                          *UPD,DEL,INQ
|   C           *IN40     ANDEQ'1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     99
| 11-C                     END
|    C   99                GOTO E1B00
|    C*已結轉不可半變更
| 12-C           DOPT      IFEQ 1
|   C           WLT032    ANDLTPM108
|   C                     MOVEL'UMI0430' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     99
| 12-C                     END
|    C   99                GOTO E1B00
|    C*已結轉不可半變更
| 13-C           DOPT      IFNE 1
|   C           LT96      ANDNE*ZERO
|   C                     MOVEL'UMO0430' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     99
| 13-C                     END
|    C   99                GOTO E1B00
|    C*
03===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....MOVE  SCREEN TO SCID
     C*==============================================================*
04===C           R1C00     BEGSR
|    C*
|    C                     MOVE TFUN,DOPT DFUN
|    C*
| 01-C           DOPT      IFEQ 1
|   C                     EXSR R1CA0
| 01*C                     ELSE
|   C                     EXSR R1CB0
| 01-C                     END
|    C*
| 02-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C                     SETON                     3132
| 02*C                     ELSE
|   C                     SETOF                     3132
| 02-C                     END
|    C*
| 03-C           EM107     IFNE 'Y'
|   C                     SETOF                     32
| 03-C                     END
|    C*
| 04-C           DOPT      IFEQ 5
|   C           *LOVAL    SETLLLT0
| 04-C                     END
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R1CA0 ....CLEAR SCREEN VARIABLES
     C*==============================================================*
05===C           R1CA0     BEGSR
|    C*
|    C                     Z-ADD0         DLT10
|    C                     Z-ADD0         DLT11
|    C                     Z-ADD0         DLT12
|    C                     Z-ADD0         DLT13
|    C                     Z-ADD0         DLT14
|    C                     Z-ADD0         DLT15
|    C                     Z-ADD0         DLT16
|    C                     Z-ADD0         LTT1
|    C                     Z-ADD0         LTT2
|    C                     Z-ADD0         DLT20
|    C                     Z-ADD0         DLT21
|    C                     Z-ADD0         DLT22
|    C                     Z-ADD0         DLT23
|    C                     Z-ADD0         DLT24
|    C                     Z-ADD0         DLT25
|    C                     Z-ADD0         DLT26
|    C                     Z-ADD0         LTT3
|    C                     Z-ADD0         LTT4
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1CB0 ....MOVE DATA TO SCREEN
     C*==============================================================*
06===C           R1CB0     BEGSR
|    C*
|    C                     Z-ADDLT10      DLT10
|    C                     Z-ADDLT11      DLT11
|    C                     Z-ADDLT12      DLT12
|    C                     Z-ADDLT13      DLT13
|    C                     Z-ADDLT14      DLT14
|    C                     Z-ADDLT15      DLT15
|    C                     Z-ADDLT16      DLT16
|    C*計算未休天數時數　
|    C           LT10      ADD  LT13      LTT1
|    C                     ADD  LT15      LTT1
|    C           LTT1      MULT 8         FLDT1   40         計算時數用
|    C                     ADD  LT14      FLDT1
|    C                     ADD  LT16      FLDT1
|    C           LT11      MULT 8         FLDT2   40         計算時數用
|    C                     ADD  LT12      FLDT2
|    C                     SUB  FLDT2     FLDT1
|    C           FLDT1     DIV  8         LTT1
|    C                     MVR            LTT2
|    C*
|    C                     Z-ADDLT20      DLT20
|    C                     Z-ADDLT21      DLT21
|    C                     Z-ADDLT22      DLT22
|    C                     Z-ADDLT23      DLT23
|    C                     Z-ADDLT24      DLT24
|    C                     Z-ADDLT25      DLT25
|    C                     Z-ADDLT26      DLT26
|    C*計算未休天數時數　
|    C           LT20      ADD  LT23      LTT3
|    C           LTT3      MULT 8         FLDT1   40         計算時數用
|    C                     ADD  LT24      FLDT1
|    C           LT21      MULT 8         FLDT2   40         計算時數用
|    C                     ADD  LT22      FLDT2
|    C                     SUB  FLDT2     FLDT1
|    C           FLDT1     DIV  8         LTT3
|    C                     MVR            LTT4
|    C*
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R1E00 ....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
07===C           R1E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*員工編號DPY02
| 01-C           #LIN      IFEQ 5
|   C           #COL      ANDGE13
|   C           #COL      ANDLE17
|   C                     CALL 'PYI101'
|   C                     PARM DLT01     P101I1  2         公司別
|   C           DLT02     PARM DLT02     P101I2  8         員工　
|   C                     PARM           @RTNC   2          FUN KEY
| 01-C                     END
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R2000  ...'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
08===C           R2000     BEGSR
|    C*
|    C                     EXFMTDSPD2
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 03-C           DOPT      IFEQ 1
|   C           DOPT      OREQ 2
|   C                     EXSR R2B00                      *SCR CHK
| 03-C                     END
|    C*
| 04-C           *IN99     IFEQ '0'
|   C                     EXSR R2C00
|   C                     MOVEL'SC03'    SCID             *SCR2
| 04-C                     END
|    C*
08===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....CHECK 'SC02' SCREEN
     C*==============================================================*
09===C           R2B00     BEGSR
|    C*已休時數
| 01-C           DLT12     IFGT 8
|   C                     MOVEL'UMI0420' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO E2B00
|    C*轉休時數
| 02-C           DLT14     IFGT 8
|   C                     MOVEL'UMI0420' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6199
| 02-C                     END
|    C   99                GOTO E2B00
|    C*預借時數
| 03-C           DLT16     IFGT 8
|   C                     MOVEL'UMI0420' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6299
| 03-C                     END
|    C   99                GOTO E2B00
|    C*已休時數
| 04-C           DLT22     IFGT 8
|   C                     MOVEL'UMI0420' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6399
| 04-C                     END
|    C   99                GOTO E2B00
|    C*轉休時數
| 05-C           DLT24     IFGT 8
|   C                     MOVEL'UMI0420' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6499
| 05-C                     END
|    C   99                GOTO E2B00
|    C*預借時數
| 06-C           DLT26     IFGT 8
|   C                     MOVEL'UMI0420' ERRID
|   C                     MOVEL'CEMF'    ERRF
|   C                     SETON                     6599
| 06-C                     END
|    C   99                GOTO E2B00
|    C*
09===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2C00 ....SCREEN 2, 3 PREPARE
     C*==============================================================*
10===C           R2C00     BEGSR
|    C*
|    C                     EXSR R2CC0                      *INIT SFL
|    C                     SETOF                     31
|    C* ADD
| 01-C           DOPT      IFEQ 1
|   C                     SETON                     31
|   C                     EXSR R2CD0                      *INIT DETAIL
| 01-C                     END
|    C* UPD
| 02-C           DOPT      IFEQ 2
|   C                     SETON                     31
|   C                     EXSR R2CE0                      *READ DETAIL
| 02-C                     END
|    C* DEL & INQ
| 03-C           DOPT      IFEQ 4
|   C           DOPT      OREQ 5
|   C                     EXSR R2CE0                      *READ DETAIL
|   C                     SETON                     53    *SFLEND
| 03-C                     END
|    C*SFLDSP
| 04-C           @LRRN     IFGT 0
|   C                     Z-ADD1         DRRN
|   C                     SETON                     50    *SFLDSP
| 04*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)++++++++++++++++++++++++++++++++++*
| 05-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 5
|   C           *LOVAL    SETLLLT0
| 05-C                     END
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          R2CA0 ....SFL HEAD INIT : ADD (NO COPY)
     C*=============================================================*
     C*          R2CC0 ....SUBFILE INIT
     C*=============================================================*
11===C           R2CC0     BEGSR
|    C* SFL SETTING ++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD12        @PRCD            *PGE RCD
|    C                     Z-ADD10        @SFTOP  20       *SFL TOP LIN
|    C                     Z-ADD21        @SFBOT  20       *SFL BOT LIN
|    C                     Z-ADD1         @LNUM   20       *明細行數
|    C* ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD0         TOTAL
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @LRRN            *LAST RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C*
|    C                     SETOF                     51    *SFLDSPCTL
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C*
|    C*第十二月時清空累積時數
| 01-C           RRN       IFEQ 12
|   C                     Z-ADD*ZERO     TOTAL
| 01-C                     END
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          R2CD0 ....SFL DATA INIT
     C*==============================================================*
12===C           R2CD0     BEGSR
|    C*
|    C                     SETON                     54    *54
|    C                     Z-ADD@LRRN     RRN
|    C*
|    C           @LRRN     DIV  @PRCD     @LPAGE
|    C                     ADD  1         @LPAGE
|    C           @LPAGE    MULT @PRCD     @DRCD
|    C*
| 01-C           RRN       DOWLT@DRCD
|   C                     ADD  1         RRN
|   C                     EXSR R2CD1                      *INIT SFLDTA
|   C                     WRITESFLSR
| 01-C                     END
|    C                     SETON                     5053
|    C*
|    C                     Z-ADDRRN       @LRRN
|    C                     Z-ADDRRN       DRRN
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R2CD1 ....SFL INIT DATA ADD
     C*==============================================================*
13===C           R2CD1     BEGSR
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD*ZERO     DLU05
|    C                     Z-ADD*ZERO     DLU06
|    C                     Z-ADD*ZERO     LUT1
|    C                     Z-ADD*ZERO     TOTAL
|    C                     MOVELTFUN1,RRN WFUN
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          R2CE0 ....DETAIL DATA TO SFL
     C*==============================================================*
14===C           R2CE0     BEGSR
|    C*
|    C                     SETON                     54    *54
|    C*
|    C                     Z-ADD0         DLU04
|    C           KEYLU     SETLLLU0
|    C           KEYLT     READELU0                      44*NF/EOF
|    C*
| 01-C           *IN44     DOWEQ'0'
|   C                     ADD  1         RRN
|   C                     EXSR R2CE1
|   C                     WRITESFLSR
|   C           KEYLT     READELU0                      44*NF/EOF
| 01-C                     END
|    C*
|    C                     SETON                     53    *SFLEND
|    C                     Z-ADDRRN       @LRRN
|    C*
14===C                     ENDSR
     C*==============================================================*
     C*          R2CE1 ....DETAIL DATA TO SFL
     C*==============================================================*
15===C           R2CE1     BEGSR
|    C*
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
|    C                     Z-ADDLU05      DLU05
|    C                     Z-ADDLU06      DLU06
|    C           DLU05     SUB  LU06      LUT1
|    C                     ADD  LUT1      TOTAL
|    C                     MOVELTFUN1,RRN WFUN
|    C*
|    C* << OTHER FILE FIELD TO SFLSR >>
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
|    C*
15===C                     ENDSR
     C*==============================================================*
     C*          R3000 ....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
16===C           R3000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E3000
| 01-C                     END
|    C*
|    C*
| 02-C           *IN12     IFEQ '1'
|   C                     MOVEL'SC02'    SCID
|   C                     GOTO E3000
| 02-C                     END
|    C*
| 03-C           *IN99     IFEQ '0'
|   C                     EXSR R3D00                      *FILE UPD
|   C                     MOVE 'SC01'    SCID
| 03-C                     END
|    C*
16===C           E3000     ENDSR
     C*==============================================================*
     C*          R3D00 ....FILE PROCESS
     C*==============================================================*
17===C           R3D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R3D10
|   C           DOPT      CASEQ2         R3D20
|   C           DOPT      CASEQ4         R3D30
| 01-C                     END
|    C*
17===C                     ENDSR
     C*==============================================================*
     C*          R3D10 ....FILE PROCESS : ADD
     C*==============================================================*
18===C           R3D10     BEGSR
|    C* DETAIL
|    C                     READCSFLSR                    57
|    C*
| 01-C           *IN57     DOWEQ'0'
|   C                     EXSR R3D12
|   C                     WRITELU0
|   C                     READCSFLSR                    57
| 01-C                     END
|    C* HEAD
|    C                     EXSR R3D11
|    C                     WRITELT0
|    C*
18===C                     ENDSR
     C*==============================================================*
     C*          R3D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
19===C           R3D11     BEGSR
|    C*
|    C                     MOVELDLT01     LT01
|    C                     MOVELDLT02     LT02
|    C                     Z-ADDWLT03     LT03
|    C                     Z-ADDDLT10     LT10
|    C                     Z-ADDDLT11     LT11
|    C                     Z-ADDDLT12     LT12
|    C                     Z-ADDDLT13     LT13
|    C                     Z-ADDDLT14     LT14
|    C                     Z-ADDDLT15     LT15
|    C                     Z-ADDDLT16     LT16
|    C                     Z-ADDDLT20     LT20
|    C                     Z-ADDDLT21     LT21
|    C                     Z-ADDDLT22     LT22
|    C                     Z-ADDDLT23     LT23
|    C                     Z-ADDDLT24     LT24
|    C                     Z-ADDDLT25     LT25
|    C                     Z-ADDDLT26     LT26
|    C**
|    C                     Z-ADD$A8YMD    LT97
|    C                     TIME           LT98
|    C                     MOVEL$USER     LT99
|    C*
19===C                     ENDSR
     C*==============================================================*
     C*          R3D12 ....FILE PROCESS : ADD  (ADD NEW)
     C*==============================================================*
20===C           R3D12     BEGSR
|    C*
|    C                     MOVELDLT01     LU01
|    C                     MOVELDLT02     LU02
|    C                     Z-ADDWLT03     LU03
|    C                     Z-ADDRRN       LU04
|    C                     Z-ADDDLU05     LU05
|    C                     Z-ADDDLU06     LU06
|    C**
|    C                     Z-ADD$A8YMD    LU97
|    C                     TIME           LU98
|    C                     MOVEL$USER     LU99
|    C*
20===C                     ENDSR
     C*==============================================================*
     C*          R3D20 ....FILE PROCESS : UPD
     C*==============================================================*
21===C           R3D20     BEGSR
|    C* DETAIL
|    C                     EXSR R3D30
|    C*
|    C                     READCSFLSR                  5757
| 01-C           *IN57     DOWEQ'0'
|   C*
|   C                     EXSR R3D12
|   C                     WRITELU0
|   C*
|   C                     READCSFLSR                    57
| 01-C                     END
|    C*
|    C                     EXSR R3D11
|    C                     UPDATLT0
|    C*
21===C                     ENDSR
     C*==============================================================*
     C*          R3D30 ....FILE PROCESS
     C*==============================================================*
22===C           R3D30     BEGSR
|    C* DETAIL
|    C                     Z-ADD0         DLU04
|    C           KEYLU     SETLLLU0
|    C           KEYLT     READELU0                      40
| 01-C           *IN40     DOWEQ'0'
|   C                     DELETLU0
|   C           KEYLT     READELU0                      40
| 01-C                     END
|    C*
| 02-C           DOPT      IFEQ 4
|   C                     DELETLT0
| 02-C                     END
|    C*
22===C                     ENDSR
     C*==============================================================*
**
新增修改      刪除查詢
**
　一月:　二月:　三月:　四月:
　五月:　六月:　七月:　八月:
　九月:　十月:十一月:十二月:
