     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    PROGRAM-ID   : PYA313                                     *
     H*    PROGRAM NAME :　　　　　　　　　　　　                  *
     H*    AUTHOR       : A1276 VINCENT                              *
     H*    CREATE DATE  : 88/02/01                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :　　　　　　　　　　　                    *
     H****************************************************************
     FPYBXPF  IF  E           K        DISK
     FPYSMPF  IF  E           K        DISK
     FPYSMLF02UF  E           K        DISK                      A
     F            SM0                               KRENAMESM2
     F                                              KINFDS QCDS
     FPYA313D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    @S4        99  2               SALARY
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*ERROR  HANDLE
     IQCDS        DS
     I                                     *STATUS  STS1
     I*TRAN DATE
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      201 2080$A8YMD
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      152 161 $USERN
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN RRN       @LRRN            LAST REC NUM
     C           *LIKE     DEFN RRN       @LPAGE           LAST PAGE
     C           *LIKE     DEFN RRN       @PRRN            PROMPT USE
     C           *NAMVAR   DEFN           PTDA01           TRAN DATE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYSM     KLIST                           CHAIN SM0
     C                     KFLD           DSM01            COMPANY 　
     C                     KFLD           DSM03            DATE
     C                     KFLD           DSM021           COST 1
     C                     KFLD           DSM022           COST 2
     C                     KFLD           DSM023           COST 3
     C           KEYBX     KLIST                           CHAIN SM0
     C                     KFLD           DSM01            COMPANY 　
     C                     KFLD           DSM04            SALARY
     C           KSM2H     KLIST                           CHAIN SM0
     C                     KFLD           DSM01            COMPANY 　
     C                     KFLD           DSM03            DATE
     C                     KFLD           DSM021           COST 1
     C                     KFLD           DSM022           COST 2
     C                     KFLD           DSM023           COST 3
     C                     KFLD           HSM04            SALARY
     C                     KFLD           HSM05            MONEY
     C           KDELE     KLIST                           DELETE
     C                     KFLD           DSM01            COMPANY 　
     C                     KFLD           DSM03            DATE
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N99                EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C                     MOVE 'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM $PFMT     P3102I  1
|    C                     PARM $PTYPE    P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           DDATE     PARM           P3101O  80
|    C*
|    C                     Z-ADD1         DOPT
|    C                     Z-ADD*ZERO     DSM03
|    C                     CLEAR@S4
|    C*--------------------------------------------------------------*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'                        *END
|   C                     EXSR R1E00
|   C                     GOTO E1000
| 02-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 03-C           *IN99     IFEQ '0'                        *SCR CHK OK
|   C                     EXSR R1C00                      *PRE-SC02
|   C                     MOVEL'SC02'    SCID
| 03-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
03===C           R1B00     BEGSR
|    C*
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*
|    C* DATA CHECK *--------------------------------------------------*
|    C*
| 02-C           DSM01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'PYSCM0'
|    C                     PARM DSM01     CM001I  2         公司代號
|    C                     PARM           CM002O 22         公司名稱
|    C                     PARM           CM003O 32         代碼說明
|    C                     PARM '2'       CM004O  1         日期格式
|    C                     PARM '1'       CM005O  1         日期曆別
|    C                     PARM           CM098O 50         備用欄
|    C                     PARM           CM099O  1         存在否？
|    C*存在否？(Y-存在有權利N-存在無權利*BLANK-不存在)
| 03-C           CM099O    IFEQ 'N'
|   C                     SETON                     6099
|   C                     MOVEL'US#0099' ERRID
|   C                     MOVEL'S#MF'    ERRF
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*  ---COST CENTER CHECK---
| 04-C           DSM021    IFEQ *BLANK
|   C           DSM022    OREQ *BLANK
|   C           DSM023    OREQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*  ---YERA CHECK---
| 05-C           DSM03     IFEQ *ZERO
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 05-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*  ---CHECK IS YEAR CORRECT---
| 06-C           DSM03     IFNE *ZERO
|   C                     Z-ADDDSM03     KDATE   60
|   C*
|   C                     CALL 'P38'
|   C                     PARM KDATE     P3801I  60
|   C                     PARM '2'       P3802I  1        .YYYYMM
|   C                     PARM '1'       P3803I  1
|   C                     PARM           P3801O  60
|   C                     PARM           P3811O  1        .'Y':OK
|   C*
| 07-C           P3811O    IFEQ 'N'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 07-C                     END
|   C   99                GOTO E1B00
|   C*
| 06-C                     END
|    C*
|    C*  ---IS RECORD EXIST CHECK---
| 08-C           DOPT      IFEQ 1                          *ADD
|   C*
|   C           KEYSM     CHAINSM0                  40
| 09-C           *IN40     IFEQ '0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     99
| 09-C                     END
|   C   99                GOTO E1B00
|   C*
| 08-C                     END
|    C*
| 10-C           DOPT      IFNE 1                          *UP,DEL,INQ
|   C*
|   C           KEYSM     CHAINSM0                  40
| 11-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     99
| 11-C                     END
|   C   99                GOTO E1B00
|   C*
| 10-C                     END
03===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
04===C           R1C00     BEGSR
|    C*
|    C                     SETOF                     313234*IN CONTROL
|    C                     SETOF                     33
|    C                     EXSR R1CC0                      *INIT SFL
|    C* ADD
| 01-C           DOPT      IFEQ 1
|   C                     SETON                     3130
|   C*                    EXSR R1CA0                      *INIT HEAD
|   C                     EXSR R1CD0                      *INIT DETAIL
| 01-C                     END
|    C* UPD
| 02-C           DOPT      IFEQ 2
|   C                     SETON                     3132
|   C                     SETOF                     34
|   C                     EXSR R1CB0                      *HEADING
|   C                     EXSR R1CE0                      *READ DETAIL
|   C                     EXSR R1CD0                      *INIT DETAIL
| 02-C                     END
|    C* DEL & INQ
| 03-C           DOPT      IFEQ 4
|   C           DOPT      OREQ 5
|   C*                    EXSR R1CB0                      *HEADING
|   C                     EXSR R1CE0                      *READ DETAIL
|   C                     SETON                     53    *SFLEND
| 03-C                     END
|    C*SFLDSP
| 04-C           @LRRN     IFGT 0
|   C                     Z-ADD1         DRRN
|   C                     SETON                     50    *SFLDSP
| 04*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)++++++++++++++++++++++++++++++++++*
| 05-C           DOPT      IFEQ 1                          *INQ
|   C           DOPT      OREQ 5
|   C           *LOVAL    SETLLSM2                        *HEAD FILE
| 05-C                     END
|    C*
|    C*          *LOVAL    SETLLSM0                        *HEAD FILE
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
04===C                     ENDSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* << ADD : SUBFILE HEAD DATA INITAILIZE>>
     C*==============================================================*
     C*          R1CB0 ....SFL HEAD INIT : U/D/I/A-COPY
     C*==============================================================*
05===C           R1CB0     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << U/D/I AND ADD-COPY : SUBFILE HEAD DATA INITAILIZE>>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
05===C                     ENDSR
     C*=============================================================*
     C*          R1CC0 ....SUBFILE INIT
     C*=============================================================*
06===C           R1CC0     BEGSR
|    C* SFL SETTING ++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD12        @PRCD            *PGE RCD
|    C                     Z-ADD10        @SFTOP  20       *SFL TOP LIN
|    C                     Z-ADD21        @SFBOT  20       *SFL BOT LIN
|    C                     Z-ADD0         @LNUM   20       *明細行數
|    C* ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @LRRN            *LAST RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C*
|    C                     SETOF                     51    *SFLDSPCTL
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R1CD0 ....SFL DATA INIT
     C*==============================================================*
07===C           R1CD0     BEGSR
|    C*
|    C                     SETOF                     54    *54 FOR BLANK LOOP
|    C                     Z-ADD@LRRN     RRN
|    C*
|    C           @LRRN     DIV  @PRCD     @LPAGE
|    C                     ADD  1         @LPAGE
|    C           @LPAGE    MULT @PRCD     @DRCD
|    C*
|    C                     EXSR R1CD1                      *INIT SFLDTA
| 01-C           RRN       DOWLT@DRCD
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
| 01-C                     END
|    C*
|    C                     Z-ADDRRN       @LRRN
|    C                     Z-ADDRRN       DRRN
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R1CD1 ....SFL INIT DATA ADD
     C*==============================================================*
08===C           R1CD1     BEGSR
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     MOVEL*BLANK    DDEL             .DEL OPT
|    C                     MOVEL'Y'       DNEW             .NEW RECORD
|    C                     MOVEL*BLANK    DSM04
|    C                     MOVEL*BLANK    HSM04
|    C                     MOVEL*BLANK    DNAME
|    C                     Z-ADD0         DSM05
|    C                     Z-ADD0         HSM05
08===C                     ENDSR
     C*==============================================================*
     C*          R1CE0 ....DETAIL DATA TO SFL
     C*==============================================================*
09===C           R1CE0     BEGSR
|    C*
|    C                     SETON                     54    *54
|    C           KEYSM     SETLLSM0
|    C           KEYSM     READESM0                      46
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
| 01-C           *IN46     DOWEQ'0'
|   C                     EXSR R1CE1
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
|   C           KEYSM     READESM0                      46
|   C*
| 01-C                     END
|    C*
|    C                     Z-ADDRRN       @LRRN
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*          R1CE1 ....DETAIL DATA TO SFL
     C*==============================================================*
10===C           R1CE1     BEGSR
|    C*
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     MOVEL*BLANK    DDEL
|    C                     MOVEL*BLANK    DNEW
|    C*
|    C* << OTHER FILE FIELD TO SFLSR >>
|    C                     MOVELSM04      DSM04
|    C                     MOVELSM04      HSM04
|    C*---MOVE SALARY TYPE TO SCREEN---
|    C           KEYBX     CHAINBX0                  40
|    C  N40                MOVELBX03      DNAME
|    C*
|    C                     Z-ADDSM05      DSM05
|    C                     Z-ADDSM05      HSM05
10===C                     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢　　　　　　　　　
     C*==============================================================*
11===C           R1E00     BEGSR
11===C                     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
12===C           R2000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD#DRRN     DRRN
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN25     IFEQ '1'                        *NEXT PAGE
| 03-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPDATE
|   C                     EXSR R1CD0
| 03*C                     ELSE
|   C                     SETON                     98    *EOP
|   C                     MOVEL'UPD2031' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 03-C                     END
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 04-C           *IN26     IFEQ '1'                        *RPEV PAGE
|   C                     SETON                     98    *EOP
|   C                     MOVEL'UPD2031' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     GOTO E2000
| 04-C                     END
|    C*
| 05-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C* RELEAS FILE --------------------------------------------------*
|   C           *LOVAL    SETLLSM2
|   C*---------------------------------------------------------------*
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 05-C                     END
|    C*
| 06-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     EXSR R2B00                      *SC02 CHK
| 06-C                     END
|    C*
| 07-C           *IN99     IFEQ '0'                        *CHK OK
|   C**********           Z-ADD0         RRN
|   C                     EXSR R2D00                      *FILE UPD
|   C                     MOVE 'SC01'    SCID
| 07-C                     END
|    C*
12===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....'SC02' CHECK
     C*==============================================================*
13===C           R2B00     BEGSR
|    C*
|    C* CHECK DETAIL (SFLSR)
|    C                     SETON                     54
|    C                     MOVEL*BLANK    @S4
|    C                     Z-ADD*ZERO     S
|    C                     READCSFLSR                  5757
|    C*
| 01-C           *IN57     DOWEQ'0'
|   C           *IN99     ANDEQ'0'
|   C                     EXSR R2B10
| 02-C           DNEW      IFEQ 'Y'
|   C                     SETON                     33
| 02*C                     ELSE
|   C                     SETOF                     33
| 02-C                     END
|   C                     UPDATSFLSR
| 03-C           *IN99     IFEQ '1'
|   C                     Z-ADDRRN       DRRN
| 03*C                     ELSE
|   C                     READCSFLSR                    57
| 03-C                     END
| 01-C                     END
|    C   99                GOTO E2B00
|    C*
13===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2B10 ....'SC02' CHECK - SFL DETAIL
     C*==============================================================*
14===C           R2B10     BEGSR
|    C*CHECK DDEL
| 01-C           DDEL      IFNE *BLANK
|   C                     GOTO E2B10
| 01-C                     END
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << CHECK DETAIL FIELD >>
|    C* PS : DNEW=' ' --> 不可清除整筆SFLSR,請使用DDEL作DELETE
|    C*                   (( PROGRAM中請作CHECK ))
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* --SALARY & MONEY CAN'T BE EMPTY---
| 02-C           DSM04     IFEQ *BLANK
|   C           DSM05     ANDNE*ZERO
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     7099
| 02-C                     END
|    C   99                GOTO E2B10
|    C*
| 03-C           DSM05     IFEQ *ZERO
|   C           DSM04     ANDNE*BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     7199
| 03-C                     END
|    C   99                GOTO E2B10
|    C*
|    C*CHECK IS SALARY TYPE CORRECT
|    C*
| 04-C           DSM04     IFNE *BLANK
|   C           KEYBX     CHAINBX0                  41
| 05-C           *IN41     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     7099
| 05-C                     END
| 04-C                     END
|    C   99                GOTO E2B10
|    C*
|    C*---EXIST RECORD CAN'T CHANGE SALARY TYPE---
|    C*
| 06-C           DOPT      IFEQ 2
|   C           HSM04     ANDNE*BLANK
| 07-C           DSM04     IFNE HSM04
|   C                     MOVEL'UPT2120' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     7099
|   C                     MOVELHSM04     DSM04
| 07-C                     END
| 06-C                     END
|    C   99                GOTO E2B10
|    C*
|    C*CHECK FOR UPDATE ---SALARY CAN'T REPEAT
|    C*
| 08-C           DSM04     IFNE *BLANK
|   C           DSM04     LOKUP@S4,1                    49
| 09-C           *IN49     IFEQ '1'
|   C                     MOVEL'UPT0140' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     7099
| 09*C                     ELSE
|   C                     ADD  1         S       50
|   C                     MOVELDSM04     @S4,S
| 09-C                     END
| 08-C                     END
|    C   99                GOTO E2B10
|    C*
14===C           E2B10     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
15===C           R2D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D20
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|    C*
15===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*==============================================================*
16===C           R2D10     BEGSR
|    C* DETAIL
|    C                     READCSFLSR                    57
|    C*
| 01-C           *IN57     DOWEQ'0'
| 02-C           DSM04     IFNE *BLANK
|   C                     EXSR R2D11
|   C                     WRITESM2
| 02-C                     END
|   C                     READCSFLSR                    57
| 01-C                     END
|    C*
16===C                     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE (DETAIL)
     C*==============================================================*
17===C           R2D11     BEGSR
|    C*
|    C                     MOVELDSM01     SM01
|    C                     MOVELDSM021    SM021
|    C                     MOVELDSM022    SM022
|    C                     MOVELDSM023    SM023
|    C                     Z-ADDDSM03     SM03
|    C                     MOVELDSM04     SM04
|    C                     Z-ADDDSM05     SM05
|    C                     Z-ADD$A8YMD    SM97
|    C                     TIME           SM98
|    C                     MOVEL$USER     SM99
|    C*
17===C                     ENDSR
     C*==============================================================*
     C*          R2D20 ....FILE PROCESS : UPD
     C*==============================================================*
18===C           R2D20     BEGSR
|    C* DETAIL
|    C                     READCSFLSR                    57
|    C*
| 01-C           *IN57     DOWEQ'0'
|   C*
|   C                     SETON                     40
| 02-C           DNEW      IFEQ *BLANK
|   C           KSM2H     CHAINSM2                  40
| 02-C                     END
|   C*
| 03-C           DDEL      IFNE *BLANK
|   C  N40                DELETSM2
|   C*
| 03*C                     ELSE
| 04-C           DSM04     IFNE *BLANK
|   C                     EXSR R2D11
|   C   40                WRITESM2
|   C  N40                UPDATSM2
| 04-C                     END
| 03-C                     END
|   C*
|   C                     READCSFLSR                    57
| 01-C                     END
18===C                     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS (DEL)
     C*==============================================================*
19===C           R2D30     BEGSR
|    C*
|    C           KDELE     SETLLSM2
|    C           KDELE     READESM2                      46
|    C*
| 01-C           *IN46     DOWEQ'0'
|   C                     DELETSM2
|   C           KDELE     READESM2                      46
| 01-C                     END
|    C*
19===C                     ENDSR
