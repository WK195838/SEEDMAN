     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : S#S01                                      *
     H*    PROGRAM NAME :程式安全管制副程式                        *
     H*    AUTHOR       : A1165 SERMON                               *
     H*    CREATE DATE  : 85/08/09                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :程式安全管制系統                          *
     F****************************************************************
     FS#SDPF  IF  E           K        DISK
     FS#SUPF  IF  E           K        DISK
     E*---------------------------------------------------------------*
     IS#DA01      DS
     I                                        1  10 ADMIN
     ILDA        UDS
     I                                      135 135 SD03
     I                                      136 136 SD04
     I                                      137 137 SD05
     I                                      138 138 SD06
     I                                      139 139 SD07
     I                                      140 140 SD08
     I                                      141 141 SD09
     I                                       91 100 PGMNAM
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           S#DA01
     C           *NAMVAR   DEFN *LDA      LDA
     C*==============================================================*
     C*          IDENTIFY A PARAMETER LIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C                     PARM           S0101I 10        使用者
     C                     PARM           S0102I 10        程式
     C                     PARM           S0101O  1        執行碼
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYSD     KLIST
     C                     KFLD           S0101I           使用者
     C                     KFLD           S0102I           程式
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     IN   S#DA01
     C                     IN   LDA
     C*管制者
     C           S0101I    IFEQ ADMIN
     C                     EXSR R1000
     C*非管制者
     C                     ELSE
     C                     EXSR R2000
     C                     END
     C*
     C                     OUT  LDA
     C                     SETON                     LR
     C                     RETRN
     C*==============================================================*
     C*          R1000 ....授與全部權利
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C                     MOVEL'Y'       SD03
     C                     MOVEL'Y'       SD04
     C                     MOVEL'Y'       SD05
     C                     MOVEL'Y'       SD06
     C                     MOVEL'Y'       SD07
     C                     MOVEL'Y'       SD08
     C                     MOVEL'Y'       SD09
     C*
     C           S0101I    CHAINSU0                  40
     C                     MOVEL'Y'       S0101O           *可執行
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2000 ....讀取權利資料
     C*==============================================================*
     C           R2000     BEGSR
     C*
     C           KEYSD     CHAINS#SDPF               40
     C*
     C           *IN40     IFEQ '0'
     C                     MOVEL'SQ'      SU07
     C                     MOVEL'Y'       SD09
     C           S0101I    CHAINSU0                  40
     C                     MOVEL'Y'       S0101O           *可執行
     C                     ELSE
     C                     MOVEL'N'       S0101O           *不可執行
     C                     END
     C*
     C                     ENDSR
