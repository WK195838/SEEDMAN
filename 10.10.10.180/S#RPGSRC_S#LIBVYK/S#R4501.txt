     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : S#R4501                                    *
     H*    PROGRAM NAME :使用者程式庫內容說明                      *
     H*    AUTHOR       : A1165 SERMON                               *
     H*    CREATE DATE  : 85/11/28                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :程式安全管制系統                          *
     H****************************************************************
     FS#A160F IP  E           K        DISK
     FS#R450P O   E             39     PRINTER
     E*---------------------------------------------------------------
     IQLIDOBJD
     I                                              ODLBNML1
     I*
     I           UDS
     I                                      101 110 $USER
     I                                      111 1160$CHYMD
     I                                      201 2080$A8YMD
     I           IDS
     I                                        1  13 CPD13
     I                                        1   70PD13
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN $A8YMD    FDATE            .TRANS DATE USE
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR R1000
     C*+ LEVEL BREAK INIT PROCESS +++++++++++++++++++++++++++++++++++
     C   L1                EXSR R7100
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     EXSR R2000                      .DETAIL
     C*
     C*+ LEVEL BREAK PROCESS ++++++++++++++++++++++++++++++++++++++++
     CL1 10                EXSR R8100
     CLR 10                EXSR R8900
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     CLRN10                EXSR R8999
     C*==============================================================*
     C*          R1000 ... INITIAL PROCESS
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C                     SETON                     1039
     C*GET表頭公司別++++++++++++++++++++++++++++++++++++++++++++++++
     C*CLEAR TOTAL FIELD
     C                     Z-ADD*ZERO     PT11
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2000 ... DETAIL
     C*==============================================================*
     C           R2000     BEGSR
     C*
     C*+<<DATA SET>>+++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     ADD  ODOBSZ    TEMP
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C* BREAK CONTROL FOR FIELD PRINTED +++++++++++++++++++++++++++++++
     C* ACCUMULATE TOTAL
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R7100 ... LEVEL BREAK 1 :INIT PROCESS
     C*==============================================================*
     C           R7100     BEGSR
     C*
     C                     MOVEL*BLANK    PD11
     C                     MOVEL*BLANK    PD12
     C                     Z-ADD*ZERO     PD13
     C                     Z-ADD*ZERO     PD14
     C                     Z-ADD*ZERO     TEMP   290
     C*
     C                     CALL 'S#A142'
     C           PD11      PARM ODLBNM    A140I1 10
     C           PD12      PARM *BLANK    A140I2 50
     C                     PARM *BLANK    A140I3 13
     C*
     C           A140I3    IFNE *BLANK
     C                     MOVELA140I3    CPD13
     C                     END
     C*最後修改日
     C                     Z-ADDPD13      FDATE
     C                     EXSR R9200
     C                     Z-ADDP3111O    PD13
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R8100 ... LEVEL BREAK 1
     C*==============================================================*
     C           R8100     BEGSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* << TOTAL LINE DATA PREPARE >>
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     DIV  1024      TEMP
     C           TEMP      DIV  1024      PD14      H
     C*
     C           *IN39     IFEQ '1'
     C                     WRITEPH1
     C                     SETOF                     39
     C                     END
     C*
     C                     WRITEPD1
     C* << ACCUMULATE & CLEAR >>+++++++++++++++++++++++++++++++++++++++
     C                     ADD  PD14      PT11
     C                     Z-ADD*ZERO     TEMP
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R8900 ... LEVEL BREAK LR -- HAS DATA
     C*==============================================================*
     C           R8900     BEGSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* << TOTAL LINE DATA PREPARE >>
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     WRITEPT1
     C*
     C* << ACCUMULATE & CLEAR >>+++++++++++++++++++++++++++++++++++++++
     C                     Z-ADD*ZERO     PT11
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R8999 ... LEVEL BREAK LR -- NO DATA
     C*==============================================================*
     C           R8999     BEGSR
     C*
     C                     EXSR R1000
     C*
     C                     WRITEPH1
     C*
     C                     WRITEPE9
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R9200 ....DATE TYPE CHANGE 2.(FILE TO SCREEN)
     C*==============================================================*
     C           R9200     BEGSR
     C*
     C                     CALL 'P31'
     C                     PARM FDATE     P3101I  80
     C                     PARM '1'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '2'       P3105I  1
     C                     PARM           P3111O  80
     C*
     C                     ENDSR
     C*==============================================================*
