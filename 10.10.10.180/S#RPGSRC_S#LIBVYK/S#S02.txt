     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : S#S02                                      *
     H*    PROGRAM NAME :公司安全管制副程式（部門）                *
     H*    AUTHOR       : A1165 SERMON                               *
     H*    CREATE DATE  : 85/08/12                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :程式安全管制系統                          *
     F****************************************************************
     FS#SCPF  IF  E           K        DISK
     E*---------------------------------------------------------------*
     IS#DA01      DS
     I                                        1  10 ADMIN
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           S#DA01
     C*==============================================================*
     C*          IDENTIFY A PARAMETER LIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C                     PARM           S0201I 10        使用者
     C                     PARM           S0202I  4        公司
     C                     PARM           S0203I  7        部門
     C                     PARM           S0201O  1        執行碼
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYSC     KLIST
     C                     KFLD           S0201I           使用者
     C                     KFLD           S0202I           公司
     C                     KFLD           S0203I           部門
     C           KEYSC1    KLIST
     C                     KFLD           S0201I           使用者
     C                     KFLD           S0202I           公司
     C           KEYSC2    KLIST
     C                     KFLD           S0201I           使用者
     C                     KFLD           S0202I           公司
     C                     KFLD           SC03             部門
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     IN   S#DA01
     C*管制者
     C           S0201I    IFEQ ADMIN
     C                     MOVEL'Y'       S0201O           *可執行
     C                     GOTO ENDPGM
     C                     END
     C*部門為空白時，表示對此公司的全部部門都有權利
     C                     MOVEL*BLANK    SC03
     C           KEYSC2    CHAINSC0                  40
     C           *IN40     IFEQ '0'
     C                     MOVEL'Y'       S0201O           *可執行
     C                     GOTO ENDPGM
     C                     END
     C*部門輸入為空白時，只作公司檢核
     C           S0203I    IFNE *BLANK
     C           KEYSC     CHAINSC0                  40
     C                     ELSE
     C           KEYSC1    CHAINSC0                  40
     C                     END
     C*
     C           *IN40     IFEQ '0'
     C                     MOVEL'Y'       S0201O           *可執行
     C                     ELSE
     C                     MOVEL'N'       S0201O           *不可執行
     C                     END
     C*
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
