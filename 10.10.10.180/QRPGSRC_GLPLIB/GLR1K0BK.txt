     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM NAME : GLR1K0                                     *
     H*    PROCSEDURE   : GLR1K0                                     *
     H*    AUTHOR       : A1150                                      *
     H*    CREATE DATE  : 81/11/19                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :泛太總帳                                  *
     H*    REMARK       :單張傳票列印－副程式                      *
     H****************************************************************
     FGLAHLF01IF  E           K        DISK
     FGLAFPF  IF  E           K        DISK
     FGLAKPF  IF  E           K        DISK
     FPT#BPF  IF  E           K        DISK
     FGLR1K0P O   E             39     PRINTER
     E*----------------------------------------------------------------
     IAH0         01
     I*
     I           UDS
     I                                      125 1270$CPY
     I                                      217 218 $PRTCD
     C*==============================================================*
     C*                  *ENTRY LIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C                     PARM           P1K0I1  2        *公司別
     C                     PARM           P1K0I2  9        *傳票號碼
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN AH03      WAH03
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEY#B     KLIST
     C                     KFLD           P1K0I1
     C           KEYAF     KLIST
     C                     KFLD           P1K0I1
     C                     KFLD           AH04
     C           KEYAH     KLIST
     C                     KFLD           P1K0I1
     C                     KFLD           P1K0I2
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C           *IN10     IFEQ '0'
     C                     EXSR RTN010                     計算總頁數
     C                     SETON                     10
     C                     END
     C*
     C                     EXSR RTN100
     C*
     C                     WRITEPE2
     C*列表選擇--
     C                     CALL 'P64'
     C                     PARM 'GLR1K0P' P640I1 10
     C                     PARM '1'       P640I2  1
     C                     PARM $CPY      P640I3  30
     C                     PARM $PRTCD    P640I4  2
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INIT
     C*==============================================================*
     C           RTN010    BEGSR
     C*
     C           KEY#B     CHAIN#B0                  40
     C           *IN40     IFEQ '1'
     C                     MOVE *BLANKS   #B16
     C                     END
     C*計算總頁數----
     C           KEYAH     SETGTAH0
     C                     READPAH0                      47
     C           AH03      ADD  6         WAH03
     C           WAH03     DIV  7         TOTPAG
     C                     Z-ADD1         PAG
     C*
     C           AH20      IFEQ 'V'
     C                     MOVE '作廢'  DDLT
     C                     ELSE
     C                     MOVE *BLANKS   DDLT
     C                     END
     C*
     C           KEY#B     CHAINAK0                  40
     C*製票日期格式轉換F->S
     C                     CALL 'P31'
     C                     PARM AH25      P310I1  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM AK11      P3104I  1
     C                     PARM AK09      P3105I  1
     C           PAH25     PARM           P3101O  80
     C*
     C*表頭日期格式轉換F->中國年／月／日
     C                     CALL 'P31'
     C                     PARM AH10      P310I1  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '2'       P3105I  1
     C           PAH10     PARM           P3101O  80
     C*
     C                     WRITEPH1
     C                     WRITEPH2
     C*
     C                     Z-ADD0         WNUM    10
     C                     Z-ADD0         PAH12T
     C                     Z-ADD0         PAH13T
     C*
     C                     ENDSR
     C*==============================================================*
     C*                    RTN100
     C*==============================================================*
     C           RTN100    BEGSR
     C*GET 資料
     C           KEYAH     SETLLAH0
     C           KEYAH     READEAH0                      44
     C           *IN44     DOWEQ'0'
     C*
     C*已列印滿一頁
     C           WNUM      IFEQ 7
     C                     EXSR RTN800
     C                     END
     C*
     C           KEYAF     CHAINAF0                  40
     C           *IN40     IFEQ '1'
     C                     MOVE *BLANKS   PAF03D
     C                     MOVE *BLANKS   PAF03C
     C                     ELSE
     C           AH12      IFNE 0
     C                     MOVELAF03      PAF03D
     C                     SETON                     30
     C                     ELSE
     C                     MOVE AF03      PAF03C
     C                     SETOF                     30
     C                     END
     C                     END
     C*
     C                     ADD  1         WNUM
     C                     WRITEPD1
     C                     ADD  AH12      PAH12T
     C                     ADD  AH13      PAH13T
     C*
     C           KEYAH     READEAH0                      44
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*                    RTN800
     C*==============================================================*
     C           RTN800    BEGSR
     C*
     C                     WRITEPE1
     C                     ADD  1         PAG
     C                     WRITEPH1
     C                     WRITEPH2
     C                     Z-ADD0         WNUM
     C*
     C                     ENDSR
