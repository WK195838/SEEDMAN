     H****************************************************************
     H*                                                              *
     H*    PROGRAM ID   : PCA0081                                    *
     H*    PROGRAM NAME : 零用金常用傳票登錄                       *
     H*    AUTHOR       : TINA                                       *
     H*    CREATE DATE  : 1994.03.02                                 *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       : 零用金系統                               *
     F****************************************************************
     FPCCAPF  O   E           K        DISK
     FPCCDPF  IF  E           K        DISK
     FPCCEPF  IF  E           K        DISK
     FGLAAPF  IF  E           K        DISK
     FGLAFPF  IF  E           K        DISK
     FGLAKPF  IF  E           K        DISK
     FMTMCPF  IF  E           K        DISK
     FPT#BPF  IF  E           K        DISK
     FPCA0081DCF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     F                                        RRND  KSFILE SFLSRD
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E*---------------------------------------------------------------*
     E*: FUNCTION DATA :
     E*                   WFUN    5   5 14
     E*輸入欄位代碼
     E                    W02         7  2
     E*輸入欄位名稱
     E                    W03         7 20
     E*檢核程式名稱
     E                    W04         7 10
     E*輸入欄位內容
     E                    W05         4 20
     E*輸入欄位內容說明
     E                    W06         7 20
     E*輸入欄位之控制碼
     E                    W07         7  1
     E*傳入參數名稱
     E                    W08         7 10
     E* KEEP RRN VALUES／分攤比率#AQ2
     E                    W13        50  3 0 W14     7 4
     E                    W15        50  7 4D
     E*---------------------------------------------------------------*
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*LOCAL DATA AREA
     ILDA        UDS
     I                                      101 110 $USER
     I                                      111 1160$CHYMD
     I                                      125 1270$CPY
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      139 139 $RMK01
     I                                      152 161 $USERN
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I*                                     211 2160DYMD
     I                                      217 218 $PRTCD
     I                                      219 219 $PENV
     I*檢核程式之參數
     ICHKPGM      DS
     I                                        1   2 WAK01
     I                                        3  22 WPARM1
     I                                       23  32 WPARM2
     I                                       33  33 WCHK
     I            DS
     I                                        1   1 DOPT
     I                                        1   10WNUM
     I            DS
     I                                        1   7 W07
     I                                        1   7 AF18
     I            DS
     I                                        1 140 W03
     I                                        1  20 DAA031
     I                                       21  40 DAA032
     I                                       41  60 DAA033
     I                                       61  80 DAA034
     I                                       81 100 DAA035
     I                                      101 120 DAA036
     I                                      121 140 DAA037
     I            DS
     I                                        1  80 W05
     I                                        1  20 DCA05
     I                                       21  40 DCA06
     I                                       41  60 DCA07
     I                                       61  80 DCA08
     I            DS
     I                                        1 140 W06
     I                                        1  20 DCA051
     I                                       21  40 DCA061
     I                                       41  60 DCA071
     I                                       61  80 DCA081
     I                                       81 100 DCA091
     I                                      101 120 DCA141
     I                                      121 140 DCA151
     I            DS
     I I                                      1   80WCA10
     I                                        3   60WCA10L
     C*============================================ =================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *NAMVAR   DEFN *LDA      LDA
     C*
     C           *LIKE     DEFN RRND      WRRND
     C           *LIKE     DEFN CA03      WNO
     C           *LIKE     DEFN CA02      WCA02
     C*          *LIKE     DEFN CA10      WCA10
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DCA01     PARM           P121I1  2         公司別
     C           DCA17     PARM           P121I2  1         辦事處
     C           DCD02     PARM           P121I3  8         傳票代碼
     C           DCA10     PARM           P121I4  70        傳票日期
     C           DCA28     PARM           P121I5  6         請款人
     C                     PARM WCA02     P121O1  9         傳票號碼
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYAF     KLIST
     C                     KFLD           DCA01
     C                     KFLD           AF02
     C           KEYAA     KLIST
     C                     KFLD           DCA01
     C                     KFLD           W02,WI
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
    C           @RTNC     ANDEQ*BLANK
  02-C           SCID      CASEQ'SC01'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     MOVE 'SC01'    SCID    4
|    C                     MOVE *BLANK    @RTNC   2
|    C                     MOVE *BLANK    WCA02
|    C                     MOVE *BLANK    P121O1
|    C*
|    C                     IN   PTDA01
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           DDATE     PARM           P3101O  80
|    C*
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     MOVEL*BLANK    #B02
|    C           DCA01     CHAIN#B0                  40
|    C           DCA01     CHAINAK0                  40
|    C*
|    C* 日期格式轉換(SCREEN->FILE)
|    C                     CALL 'P31'
|    C                     PARM DCA10     P3101I  80
|    C                     PARM AK11      P3102I  1
|    C                     PARM AK09      P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           WCA10     PARM           P3101O  80
|    C*--------------------------------------------------------------*
     C           DCA28     CHAINMC0                  45
     C   10                MOVEL*BLANK    MC02
|    C*
|    C                     MOVEL*BLANK    DCA03
|    C           DCD02     CHAINCD0                  40
|    C  N40                EXSR R1C00
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC01'
     C*==============================================================*
02===C           R1C00     BEGSR
|    C*
|    C                     Z-ADD0         WNO               *CA03
|    C                     Z-ADD0         RRND
|    C                     Z-ADD0         WRRND
|    C                     Z-ADD0         TOT
|    C                     SETON                     52
|    C                     WRITESFLCRD
|    C                     SETOF                     52
|    C*
|    C           CD02      SETLLCE0
|    C           CD02      READECE0                    9844
| 01-C           *IN44     DOWEQ'0'
| 02-C           *IN98     IFEQ '1'
|   C                     EXSR ERRSR
|   C                     GOTO E1C00
| 02-C                     END
     C*DATA BASE FILE TO SAVE SFL
|   C                     EXSR R1C10
|   C*
| 03-C*          CE12      IFNE 0
|   C                     ADD  1         RRND
|   C                     WRITESFLSRD
|   C                     ADD  CE12      TOT
| 03-C*                    END
|   C*
|   C                     ADD  1         WNO
|   C           CD02      READECE0                    9844
| 01-C                     END
|    C*
|    C                     Z-ADDRRND      WRRND
|    C*
02===C           E1C00     ENDSR
     C*==============================================================*
     C*          R1C10 ....DATA BASE FILE-> SAVE SFL
     C*==============================================================*
03===C           R1C10     BEGSR
|    C*
|    C                     MOVE CE04      AF02
|    C           KEYAF     CHAINAF0                  40
|    C*
|    C                     Z-ADDCE12      DCA12D
|    C                     MOVE CE04      DCA04D
|    C                     MOVE CE05      DCA05D
|    C                     MOVE AF11      DCA05A
|    C                     MOVE CE06      DCA06D
|    C                     MOVE AF12      DCA06A
|    C                     MOVE CE07      DCA07D
|    C                     MOVE AF13      DCA07A
|    C                     MOVE CE08      DCA08D
|    C                     MOVE AF14      DCA08A
|    C                     Z-ADDCE09      DCA09D
|    C                     MOVE AF15      DCA09A
|    C                     Z-ADDCE14      DCA14D
|    C                     MOVE AF16      DCA14A
|    C                     MOVE CE15      DCA15D
|    C                     MOVE AF17      DCA15A
|    C                     MOVE CE16      DCA16D
|    C                     MOVE CE16      DCA16H
|    C                     MOVE 'N'       DLTCOD
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
04===C           R2000     BEGSR
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           @RTNC     ANDEQ*BLANK
|   C           SCID      ANDEQ'SC01'
|   C*
|   C                     EXSR R2A10                      *INIT SFLSR
|   C                     EXSR R2A20                      *EXFMT SFLCR
|   C*
| 02-C           *IN03     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C*
| 03-C           *IN06     CASEQ'1'       R2D00
|   C                     CAS            R2B00            *SC02 CHK
| 03-C                     END
| 02*C                     ELSE
|   C* *IN12 ON | *IN03 ON
|   C                     MOVE '00'      @RTNC
| 02-C                     END
| 01-C                     END                             *ENDDO
|    C*
04===C           E2000     ENDSR
     C*==============================================================*
     C*          R2A10 ....INIT 'SC02'
     C*==============================================================*
05===C           R2A10     BEGSR
|    C* INIT SCR02
|    C                     Z-ADD0         RRN
|    C                     Z-ADD1         DRRN
|    C                     SETON                     5251
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     MOVE *BLANKS   DOPT
|    C*
|    C                     Z-ADD1         WI
|    C           WI        CHAINSFLSRD               4040
| 01-C           *IN40     DOWEQ'0'
| 02-C           DLTCOD    IFEQ 'N'
|   C                     Z-ADDRRND      DNO
|    C*SFLSRD TO SFLSR
|   C                     EXSR R2A30
| 02-C                     END
|   C                     ADD  1         WI
|   C           WI        CHAINSFLSRD               40
| 01-C                     END
|    C*
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R2A20 ....EXFMT SFLSR
     C*==============================================================*
06===C           R2A20     BEGSR
|    C*
| 01-C           RRN       IFGT 0
|   C                     SETON                     50
| 01*C                     ELSE
|   C                     SETON                     96
|   C                     SETOF                     50
| 01-C                     END
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,67
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R2A30 ....MOVE
     C*==============================================================*
07===C           R2A30     BEGSR
|    C*
|    C*GET AF03
|    C                     MOVE *BLANK    DOPT
|    C                     MOVE *BLANKS   DAF031
|    C                     MOVE DCA04D    DCA041
|    C                     Z-ADDDCA12D    DCA121
|    C                     MOVE DCA16D    DCA161
|    C                     MOVE DCA04D    AF02
|    C           KEYAF     CHAINAF0                  40
|    C  N40                MOVELAF03      DAF031
|    C   40                MOVEL*BLANKS   DAF031
|    C*
|    C                     ADD  1         RRN
|    C                     Z-ADDRRN       DCA031
|    C                     WRITESFLSR
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R2B00 ....'SC02' CHECK
     C*==============================================================*
08===C           R2B00     BEGSR
|    C*
|    C                     READCSFLSR                  5757
| 01-C           *IN57     DOWEQ'0'
| 02-C           DOPT      IFNE *BLANKS
|   C                     EXSR R2B10                      *SFL-->SCR
| 03-C           DOPT      CASLE'2'       R3000        3131
|   C           DOPT      CASGE'4'       R4000
| 03-C                     END
| 02-C                     END
|   C                     READCSFLSR                  5757
| 01-C  N03N12             END
|    C*
08===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2B10 ....
     C*==============================================================*
09===C           R2B10     BEGSR
|    C*
|    C           DNO       CHAINSFLSRD               40
|    C*
|    C                     Z-ADDRRN       DCA03
|    C                     MOVE DCA04D    DCA04
|    C                     Z-ADDDCA12D    DCA12
|    C                     MOVE DCA05D    DCA05
|    C                     MOVE DCA05A    W02,1
|    C                     MOVE DCA06D    DCA06
|    C                     MOVE DCA06A    W02,2
|    C                     MOVE DCA07D    DCA07
|    C                     MOVE DCA07A    W02,3
|    C                     MOVE DCA08D    DCA08
|    C                     MOVE DCA08A    W02,4
|    C                     Z-ADDDCA09D    DCA09
|    C                     MOVE DCA09A    W02,5
|    C                     Z-ADDDCA14D    DCA14
|    C                     MOVE DCA14A    W02,6
|    C                     MOVE DCA15D    DCA15
|    C                     MOVE DCA15A    W02,7
|    C*GET AF03
|    C                     MOVE *BLANKS   DAF03
|    C                     MOVE DCA04D    AF02
|    C           KEYAF     CHAINAF0                  40
|    C  N40                MOVELAF03      DAF03
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*          R2D00 ....CHK & SAVE (SFL-->DBF)
     C*==============================================================*
10===C           R2D00     BEGSR
|    C*
|    C                     EXSR R2D10                      *CHK
|    C  N99                EXSR R2D30                      *SAVE
| 01-C           *IN98     IFEQ '0'
|   C           *IN99     ANDEQ'0'
|   C                     MOVEA*ALL'0'   *IN,60
|   C                     MOVE '00'      @RTNC
| 01-C                     END
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....CHECK SC02 (PF06 ON)
     C*==============================================================*
11===C           R2D10     BEGSR
|    C*
|    C* CHK須有分錄存在
|    C*
| 01-C           WNO       IFEQ 0
|   C                     MOVEL'UGL0012' ERRID
|   C                     MOVEL'GLMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E2D10
|    C***
|    C                     Z-ADDWRRND     WKRND   30
|    C*金額合計不可大於3000
| 02-C           TOT       IFGT 3000
|   C                     MOVEL'UPC0002' ERRID
|   C                     MOVEL'PCMF'    ERRF
|   C                     SETON                     99
| 02-C                     END
|    C   99                GOTO E2D10
|    C*
|    C*金額合計不可為0
| 02-C           TOT       IFLE 0
|   C                     MOVEL'UPC0003' ERRID
|   C                     MOVEL'PCMF'    ERRF
|   C                     SETON                     99
| 02-C                     END
|    C   99                GOTO E2D10
     C*
|    C                     Z-ADD1         WI      30
|    C           WI        CHAINSFLSRD               40
| 02-C           WI        DOWLEWKRND
|   C           *IN40     ANDEQ'0'
| 03-C           DLTCOD    IFEQ 'N'
|   C                     EXSR R2D80
| 03-C                     END
|    C                     ADD  1         WI
|    C           WI        CHAINSFLSRD               40
| 02-C                     END
|    C*
11===C           E2D10     ENDSR
     C*==============================================================*
     C*          R2D80 .... CALL PCA0082
     C*==============================================================*
12===C           R2D80     BEGSR
|    C*
|    C                     MOVE DCA04D    AF02
|    C           KEYAF     CHAINAF0                  40
|    C*
| 01-C           DCA16D    IFEQ DCA16H
|   C                     CALL 'PCA0082'
|   C                     PARM DCA01     P190I1  2         公司別
|   C           DCA04D    PARM DCA04D    P190I2  8         會計科目
|   C           DCA12D    PARM DCA12D    P190I3 132        借方金額
|   C           DCA05D    PARM DCA05D    P190I5 20         成本中心
|   C           DCA06D    PARM DCA06D    P190I6 20         對象別
|   C           DCA07D    PARM DCA07D    P190I7 20         相關號碼1
|   C           DCA08D    PARM DCA08D    P190I8 20         相關號碼2
|   C           DCA09D    PARM DCA09D    P190I9  70        日期
|   C           DCA14D    PARM DCA14D    P190IA 132        數量
|   C           DCA15D    PARM DCA15D    P190IB 20         其他
|   C           DCA16D    PARM DCA16D    P190IC 50         摘要
|   C                     PARM 'H'       P190ID  1         檔案
|   C                     PARM           #RTNC   2          FUN KEY
| 01-C                     END
|    C*
| 02-C           #RTNC     IFEQ '03'
|   C                     SETON                     0399
| 02-C                     END
| 03-C           #RTNC     IFEQ '12'
|   C                     SETON                     99
|   C                     MOVE 'SC01'    SCID
| 03-C                     END
|    C                     MOVE *BLANK    ERRID
|    C                     MOVE *BLANK    ERRF
|    C   99                GOTO E2D80
|    C*
|    C                     MOVE AF11      DCA05A
|    C                     MOVE AF12      DCA06A
|    C                     MOVE AF13      DCA07A
|    C                     MOVE AF14      DCA08A
|    C                     MOVE AF15      DCA09A
|    C                     MOVE AF16      DCA14A
|    C                     MOVE AF17      DCA15A
|    C                     MOVE 'N'       DLTCOD
|    C*
|    C                     UPDATSFLSRD
|    C*
12===C           E2D80     ENDSR
     C*==============================================================*
     C*          R2D30 ....SAVE (SFL->DB)
     C*==============================================================*
13===C           R2D30     BEGSR
|    C*零用金號碼採番
|    C                     MOVELWCA10L    P001I3
|    C                     CALL 'PCS001'
|    C                     PARM DCA01     P001I1  2
|    C                     PARM DCA17     P001I2  1
|    C                     PARM           P001I3  40
|    C           WCA02     PARM           P001O1  9
|    C*
|    C                     MOVE '00'      @RTNC
|    C*
|    C                     MOVE DCA01     CA01
|    C                     MOVE WCA02     CA02
|    C                     Z-ADDWCA10     CA10
|    C                     MOVELDCA17     CA17
|    C                     MOVELDCA28     CA28
|    C                     MOVE *BLANK    CA18
|    C*
|    C                     MOVE *BLANK    CA20
|    C                     Z-ADD$A8YMD    CA25
|    C                     MOVE $USER     CA26
|    C*
|    C                     Z-ADD0         CA03
|    C*
|    C                     Z-ADD1         WI      30
|    C           WI        CHAINSFLSRD               40
| 03-C           *IN40     DOWEQ'0'
| 04-C           DLTCOD    IFEQ 'N'
|   C                     ADD  1         CA03
|   C                     EXSR R2D40
|   C                     WRITECA0
| 04-C                     END
|   C                     ADD  1         WI
|   C           WI        CHAINSFLSRD               40
| 03-C                     END
|    C*
|    C*
13===C           E2D30     ENDSR
     C*==============================================================*
     C*          R2D40 ....MOVE DATA -> DETAIL FILE(GLAOPF)
     C*==============================================================*
14===C           R2D40     BEGSR
|    C*
|    C                     MOVE DCA04D    CA04
|    C                     MOVE DCA05D    CA05
|    C                     MOVE DCA05A    CA05A
|    C                     MOVE DCA06D    CA06
|    C                     MOVE DCA06A    CA06A
|    C                     MOVE DCA07D    CA07
|    C                     MOVE DCA07A    CA07A
|    C                     MOVE DCA08D    CA08
|    C                     MOVE DCA08A    CA08A
|    C                     Z-ADDDCA09D    CA09
|    C                     MOVE DCA09A    CA09A
|    C                     Z-ADDDCA12D    CA12
|    C                     Z-ADDDCA14D    CA14
|    C                     MOVE DCA14A    CA14A
|    C                     MOVE DCA15D    CA15
|    C                     MOVE DCA15A    CA15A
|    C                     MOVE DCA16D    CA16
|    C                     TIME           CA97
|    C                     Z-ADD$A8YMD    CA98
|    C                     MOVE $USER     CA99
|    C*
14===C                     ENDSR
     C*==============================================================*
     C*          R3000 .... SELECT  DOPT='1' | DOPT='2'
     C*==============================================================*
15===C           R3000     BEGSR
|    C*
| 01-C           DOPT      IFEQ '1'
|   C           WNO       ADD  1         DCA03             *CA03
|   C                     MOVE *BLANK    DCA161
| 01-C                     END
|    C*
| 02-C           *IN34     DOWEQ'0'
|   C*
|   C                     WRITEDSPC3
|   C                     EXFMTDSPD3
|   C                     MOVEA*ALL'0'   *IN,67
|   C                     Z-ADD*ZERO     #LIN
|   C                     Z-ADD*ZERO     #COL
|   C*
| 03-C           *IN03     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C*
| 04-C           *IN04     CASEQ'1'       R3E00
|   C                     CAS            R3B00
| 04-C                     END
|   C*
| 05-C           *IN04     IFEQ '0'
|   C           *IN05     ANDEQ'0'
|   C           *IN99     ANDEQ'0'
| 06-C           DCA04D    IFNE DCA04
|   C                     EXSR R3C00                       *INIT SCR03
| 06-C                     END
|   C                     EXSR R4000
|   C  N12                SETON                     34
| 05-C                     END
|   C*
| 03*C                     ELSE
|   C                     SETON                     34
| 03-C                     END
|   C*
| 02-C                     END
|    C*
|    C                     SETOF                     34
15===C                     ENDSR
     C*==============================================================*
     C*          R3B00 ....'SC03' CHECK
     C*==============================================================*
16===C           R3B00     BEGSR
|    C*
|    C* CHK會計科目
|    C*
|    C                     MOVE *BLANK    DAF03
| 01-C           DCA04     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     7099
| 01*C                     ELSE
|   C                     MOVE DCA04     AF02
|   C           KEYAF     CHAINAF0                  40
| 02-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     7099
| 02*C                     ELSE
|   C                     MOVELAF03      DAF03
| 03-C           AF08      IFNE '1'
|   C                     MOVEL'UGL0009' ERRID
|   C                     MOVEL'GLMF'    ERRF
|   C                     SETON                     7099
| 03-C                     END
| 02-C                     END
| 01-C                     END
|    C   99                GOTO E3B00
|    C*
|    C* CHK借貸須擇一方輸入
|    C*
| 04-C           DCA12     IFEQ 0
|   C                     MOVEL'UGL0010' ERRID
|   C                     MOVEL'GLMF'    ERRF
|   C                     SETON                     7199
| 04-C                     END
|    C   99                GOTO E3B00
|    C*
16===C           E3B00     ENDSR
     C*==============================================================*
     C*          R3C00 ....INIT 'SC04'
     C*==============================================================*
17===C           R3C00     BEGSR
|    C*
|    C                     MOVE *BLANKS   DCA05
|    C                     MOVE *BLANKS   DCA06
|    C                     MOVE *BLANKS   DCA07
|    C                     MOVE *BLANKS   DCA08
|    C                     Z-ADD0         DCA09
|    C                     Z-ADD0         DCA14
|    C                     MOVE *BLANKS   DCA15
|    C                     MOVE CD02      DCA161
|    C*
17===C                     ENDSR
     C*==============================================================*
     C*          R3E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
18===C           R3E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*科目
| 01-C           #LIN      IFEQ 8
|   C           #COL      ANDGE16
|   C           #COL      ANDLE23
|   C                     CALL 'GLI410'
|   C                     PARM DCA01     P410I1  2
|   C           DCA04     PARM DCA04     P410I2  8
|   C                     PARM           #RTNC   2
| 01-C                     END
|    C*
18===C                     ENDSR
     C*==============================================================*
     C*          R4000 .... 'SC04' SCREEN MAIN ROUTINE
     C*==============================================================*
19===C           R4000     BEGSR
|    C*
|    C                     EXSR R4A00
|    C*
| 01-C           *IN33     DOWEQ'0'                        *CTL LOOP
|   C*
| 02-C           DOPT      IFGE '4'
|   C                     WRITEDSPC4
| 02*C                     ELSE
|   C                     WRITEDSPC3
| 02-C                     END
|   C                     WRITEDSPD3
|   C                     EXFMTDSPD4
|   C                     MOVEA*ALL'0'   *IN,67
|   C                     Z-ADD*ZERO     #LIN
|   C                     Z-ADD*ZERO     #COL
|   C*
| 03-C           *IN03     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C*
| 04-C           DOPT      IFLT '4'
| 05-C           *IN04     CASEQ'1'       R4E00
|   C                     CAS            R4B00            *CHK SC04
| 05-C                     END
| 04-C                     END
|   C*
| 06-C           *IN04     IFEQ '0'
|   C           *IN05     ANDEQ'0'
|   C           *IN99     ANDEQ'0'
|   C                     EXSR R4D00                      *SAVE->SFL
|   C                     SETON                     33
| 06-C                     END
|   C*  *IN12 ON
| 03*C                     ELSE
|   C                     SETON                     33
| 03-C                     END                             *IN12 ENDIF
|   C*
| 01-C                     END                              ENDDO
|    C*
|    C                     SETOF                     33
|    C*
19===C           E4000     ENDSR
     C*==============================================================*
     C*          R4A00 ....INIT 'SC04'之輸入欄位
     C*==============================================================*
20===C           R4A00     BEGSR
|    C*
|    C* INIT SCR03
|    C                     MOVE AF11      W02,1          60
|    C                     MOVE AF12      W02,2          61
|    C                     MOVE AF13      W02,3          62
|    C                     MOVE AF14      W02,4          63
|    C                     MOVE AF15      W02,5          64
|    C                     MOVE AF16      W02,6          65
|    C                     MOVE AF17      W02,7          66
|    C                     MOVEA*BLANKS   W03
|    C                     MOVEA*BLANKS   W04
|    C                     MOVE '0'       WCHK
|    C*
| 01-C                     DO   5         WI      30
|   C           54        ADD  WI        IN
|   C                     MOVE '1'       *IN,IN
| 01-C                     END
|    C*
| 02-C                     DO   7         WI      30
|   C* KEYAA=DAN01+W02,WI
| 03-C           W02,WI    IFNE *BLANKS
|   C           KEYAA     CHAINAA0                  40
|   C                     MOVE AA03      W03,WI
|   C                     MOVE AA04      W04,WI
|   C                     MOVE AA05      W08,WI
|   C*
|   C* 無檢核程式時，該說明不要HIGH LIGHT
|   C*
| 04-C           WI        IFLE 4
| 05-C           AA04      IFNE *BLANK
|   C           54        ADD  WI        IN
|   C                     MOVE '0'       *IN,IN
| 05-C                     END
| 04-C                     END
| 06-C           WI        IFEQ 7
| 07-C           AA04      IFNE *BLANK
|   C                     SETOF                     59
| 07-C                     END
| 06-C                     END
| 03-C                     END
| 02-C                     END
|    C*
|    C                     MOVEA*BLANKS   W06
|    C                     EXSR R4A10
|    C*
20===C                     ENDSR
     C*==============================================================*
     C*          R4A10 ....GET 'SC04'輸入欄位說明
     C*==============================================================*
21===C           R4A10     BEGSR
|    C*
|    C                     MOVE DCA01     WAK01
|    C*
| 01-C                     DO   4         WI
| 02-C           W05,WI    IFNE *BLANKS
|   C           W04,WI    ANDNE*BLANKS
|   C                     MOVE W05,WI    WPARM1
|   C                     MOVE W08,WI    WPARM2
|   C                     CALL W04,WI
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C           W06,WI    PARM           DESC   20
| 02*C                     ELSE
|   C                     MOVELW05,WI    W06,WI
| 02-C                     END
| 01-C                     END
|    C*
| 03-C           DCA09     IFNE 0
|   C                     CALL 'P56'                      *P56(GET'/')
|   C                     PARM DCA09     P5601I  80
|   C                     PARM           P5601O 10
|   C                     MOVELP5601O    W06,5
| 03-C                     END
|    C*
| 04-C           DCA14     IFNE 0
|   C                     CALL 'P55'                      *P55(GET,.)
|   C                     PARM DCA14     P5501I 185
|   C                     PARM 2         P5502I  10
|   C                     PARM           P5501O 24
|   C                     MOVELP5501O    W06,6
| 04-C                     END
|    C*
| 05-C           W04,7     IFNE *BLANK
|   C           DCA15     ANDNE*BLANK
|   C                     MOVE DCA15     WPARM1
|   C                     MOVE W08,7     WPARM2
|   C                     CALL W04,7
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C                     PARM           W06,7
| 05*C                     ELSE
|   C                     MOVELDCA15     W06,7
| 05-C                     END
|    C*
21===C                     ENDSR
     C*==============================================================*
     C*          R4B00 ....'SC04' CHECK
     C*==============================================================*
22===C           R4B00     BEGSR
|    C*
|    C                     MOVE DCA01     WAK01
|    C*
|    C* CHK輸入欄位１－４（輸入控制碼）
|    C*
| 01-C                     DO   4         WI
| 02-C           W07,WI    IFEQ 'V'
|   C           W05,WI    ANDEQ*BLANKS
|   C           WI        ADD  79        IN      20
|   C                     MOVE '1'       *IN,IN
|   C                     SETON                     99
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 02-C                     END
| 01-C  N99                END
|    C   99                GOTO E4B00
|    C*
|    C* CHK輸入欄位１－４& GET說明
|    C*
| 03-C                     DO   4         WI
| 04-C           W05,WI    IFEQ *BLANKS
|   C                     MOVE *BLANK    W06,WI
| 04*C                     ELSE
| 05-C           W04,WI    IFNE *BLANKS
|   C                     MOVE W05,WI    WPARM1
|   C                     MOVE W08,WI    WPARM2
|   C                     CALL W04,WI
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C           W06,WI    PARM           DESC   20
| 06-C           TORN      IFEQ 'N'
|   C           WI        ADD  79        IN      20
|   C                     MOVE '1'       *IN,IN
|   C                     SETON                     99
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 06*C                     ELSE
|   C                     MOVE WPARM1    W05,WI
| 06-C                     END
| 05*C                     ELSE
|   C                     MOVELW05,WI    W06,WI
| 05-C                     END
| 04-C                     END
| 03-C  N99                END
|    C   99                GOTO E4B00
|    C*
| 07-C           W07,5     IFEQ 'V'
|   C           DCA09     ANDEQ0
|   C                     SETON                     8499
|   C                     MOVEL'UPT0012' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 07-C                     END
|    C   99                GOTO E4B00
|    C*
|    C* 輸入欄位５（日期）& GET說明
|    C*
|    C                     MOVE *BLANK    W06,5
| 08-C           DCA09     IFNE 0
|   C                     CALL 'P56'                      *P56(GET'/')
|   C                     PARM DCA09     P5601I  80
|   C                     PARM           P5601O 10
|   C                     MOVELP5601O    W06,5
| 08-C                     END
|    C   99                GOTO E4B00
|    C*
| 09-C           W07,6     IFEQ 'V'
|   C           DCA14     ANDEQ0
|   C                     SETON                     8599
|   C                     MOVEL'UPT0012' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 09-C                     END
|    C   99                GOTO E4B00
|    C*
|    C* 輸入欄位６（數量）& GET說明
|    C*
|    C                     MOVE *BLANK    W06,6
| 10-C           DCA14     IFNE 0
|   C                     CALL 'P55'                      *P55(GET,.)
|   C                     PARM DCA14     P5501I 185
|   C                     PARM 2         P5502I  10
|   C                     PARM           P5501O 24
|   C                     MOVELP5501O    W06,6
| 10-C                     END
|    C   99                GOTO E4B00
|    C*
| 11-C           W07,7     IFEQ 'V'
|   C           DCA15     ANDEQ*BLANK
|   C                     SETON                     8699
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 11-C                     END
|    C   99                GOTO E4B00
|    C*
|    C* CHK輸入欄位７& GET說明
|    C*
| 12-C           DCA15     IFEQ *BLANKS
|   C                     MOVE *BLANK    DCA151
| 12*C                     ELSE
| 13-C           W04,7     IFNE *BLANK
|   C                     MOVE DCA15     WPARM1
|   C                     MOVE W08,7     WPARM2
|   C                     CALL W04,7
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C                     PARM           W06,7
| 14-C           TORN      IFEQ 'N'
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     8699
| 14*C                     ELSE
|   C                     MOVE WPARM1    DCA15
| 14-C                     END
| 13*C                     ELSE
|   C                     MOVELDCA15     W06,7
| 13-C                     END
| 12-C                     END
|    C   99                GOTO E4B00
|    C*
|    C*
|    C* GET摘要
|    C*
| 15-C           *IN05     IFEQ '1'
|   C           DCA161    OREQ *BLANKS
|   C                     MOVE *BLANKS   DCA161
|   C*
| 16-C                     DO   7         WI
| 17-C           W06,WI    IFNE *BLANKS
|   C                     CALL 'GLS001'
|   C                     PARM DCA161    S001I1 80
|   C                     PARM W06,WI    S001I2 80
|   C                     PARM 50        S001I3  30
|   C           DCA161    PARM           S001O1 80
| 17-C                     END
| 16-C                     END
|   C*
| 15-C                     END
22===C           E4B00     ENDSR
     C*==============================================================*
     C*          R4D00 ....SAVE TO SFL
     C*==============================================================*
23===C           R4D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ'1'       R4D10
|   C           DOPT      CASEQ'2'       R4D20
|   C           DOPT      CASEQ'4'       R4D30
| 01-C                     END
|    C*
23===C                     ENDSR
     C*==============================================================*
     C*          R4D10 ....ADD
     C*==============================================================*
24===C           R4D10     BEGSR
|    C*
|    C                     EXSR R4DA0
|    C*
|    C                     MOVE DCA161    DCA16H
|    C                     ADD  DCA12     TOT
|    C*
|    C                     MOVE 'N'       DLTCOD
| 01-C*          DCA12     IFNE 0
|   C           WRRND     ADD  1         RRND
|   C                     Z-ADDRRND      WRRND
|   C                     WRITESFLSRD
| 01-C*                    END
|    C                     ADD  1         WNO
|    C*
24===C                     ENDSR
     C*==============================================================*
     C*          R4D20 ....UPD
     C*==============================================================*
25===C           R4D20     BEGSR
|    C*
|    C                     ADD  DCA12     TOT
|    C                     SUB  DCA12D    TOT
|    C*
|   C                     EXSR R4DA0
|   C                     MOVE 'N'       DLTCOD
| 02-C           DCA12     IFNE 0
|   C                     UPDATSFLSRD
| 04-C                     END
25===C                     ENDSR
     C*==============================================================*
     C*          R4D30 ....DLT
     C*==============================================================*
26===C           R4D30     BEGSR
|    C*
|    C                     SUB  DCA12D    TOT
|    C*
|    C                     MOVE 'Y'       DLTCOD
|    C                     UPDATSFLSRD
|    C                     SUB  1         WNO
|    C*
26===C                     ENDSR
     C*==============================================================*
     C*          R4DA0 ....DSP--> SAVE SFL
     C*==============================================================*
27===C           R4DA0     BEGSR
|    C*
|    C                     Z-ADDDCA12     DCA12D
|    C                     MOVE DCA04     DCA04D
|    C                     MOVE DCA05     DCA05D
|    C                     MOVE W02,1     DCA05A
|    C                     MOVE DCA06     DCA06D
|    C                     MOVE W02,2     DCA06A
|    C                     MOVE DCA07     DCA07D
|    C                     MOVE W02,3     DCA07A
|    C                     MOVE DCA08     DCA08D
|    C                     MOVE W02,4     DCA08A
|    C                     Z-ADDDCA09     DCA09D
|    C                     MOVE W02,5     DCA09A
|    C                     Z-ADDDCA14     DCA14D
|    C                     MOVE W02,6     DCA14A
|    C                     MOVE DCA15     DCA15D
|    C                     MOVE W02,7     DCA15A
|    C                     MOVE DCA161    DCA16D
|    C*
27===C                     ENDSR
     C*==============================================================*
     C*          R4E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
28===C           R4E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*
|    C                     MOVE DCA01     WAK01
|    C                     MOVE '1'       WCHK
|    C*輸入欄位１
| 01-C           #LIN      IFEQ 12
|   C           #COL      ANDGE31
|   C           #COL      ANDLE50
| 02-C           W04,1     IFNE *BLANK
|   C                     MOVE W05,1     WPARM1
|   C                     MOVE W08,1     WPARM2
|   C                     CALL W04,1
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C                     PARM           W06,1
| 03-C           TORN      IFEQ 'Y'
|   C                     MOVE WPARM1    W05,1
| 03-C                     END
| 02-C                     END
| 01-C                     END
|    C   99                GOTO E4E00
|    C*輸入欄位２
| 04-C           #LIN      IFEQ 13
|   C           #COL      ANDGE31
|   C           #COL      ANDLE50
| 05-C           W04,2     IFNE *BLANK
|   C                     MOVE W05,2     WPARM1
|   C                     MOVE W08,2     WPARM2
|   C                     CALL W04,2
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C                     PARM           W06,2
| 06-C           TORN      IFEQ 'Y'
|   C                     MOVE WPARM1    W05,2
| 06-C                     END
| 05-C                     END
| 04-C                     END
|    C   99                GOTO E4E00
|    C*輸入欄位３
| 07-C           #LIN      IFEQ 14
|   C           #COL      ANDGE31
|   C           #COL      ANDLE50
| 08-C           W04,3     IFNE *BLANK
|   C                     MOVE W05,3     WPARM1
|   C                     MOVE W08,3     WPARM2
|   C                     CALL W04,3
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C                     PARM           W06,3
| 09-C           TORN      IFEQ 'Y'
|   C                     MOVE WPARM1    W05,3
| 09-C                     END
| 08-C                     END
| 07-C                     END
|    C   99                GOTO E4E00
|    C*輸入欄位４
| 10-C           #LIN      IFEQ 15
|   C           #COL      ANDGE31
|   C           #COL      ANDLE50
| 11-C           W04,4     IFNE *BLANK
|   C                     MOVE W05,4     WPARM1
|   C                     MOVE W08,4     WPARM2
|   C                     CALL W04,4
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C                     PARM           W06,4
| 12-C           TORN      IFEQ 'Y'
|   C                     MOVE WPARM1    W05,4
| 12-C                     END
| 11-C                     END
| 10-C                     END
|    C   99                GOTO E4E00
|    C*輸入欄位７
| 13-C           #LIN      IFEQ 18
|   C           #COL      ANDGE31
|   C           #COL      ANDLE50
| 14-C           W04,7     IFNE *BLANK
|   C                     MOVE DCA15     WPARM1
|   C                     MOVE W08,7     WPARM2
|   C                     CALL W04,7
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C                     PARM           W06,7
| 15-C           TORN      IFEQ 'Y'
|   C                     MOVE WPARM1    DCA15
| 15-C                     END
| 14-C                     END
| 13-C                     END
|    C   99                GOTO E4E00
|    C*
|    C           E4E00     TAG
|    C                     MOVE '0'       WCHK
|    C*
28===C                     ENDSR
     C*==============================================================*
     C*          ERRSR ....FILE STATUE HANDLING
     C*==============================================================*
29===C           ERRSR     BEGSR
|    C*
|    C                     CALL 'P50'
|    C           DMSG      PARM           P5001O 78
|    C                     SETON                     98
|    C*
29===C                     ENDSR
