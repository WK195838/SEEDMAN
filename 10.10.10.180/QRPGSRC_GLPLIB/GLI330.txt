     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : GLI330                                     *
     H*    PROGRAM NAME : 費用分攤設定全頁查詢                     *
     H*    AUTHOR       : A1087  JOYCE                               *
     H*    CREATE DATE  : 1992.11.03                                 *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       : 泛太總帳                                 *
     F****************************************************************
     FGLAPPF  IF  E           K        DISK
     FGLAFPF  IF  E           K        DISK
     FGLAKPF  IF  E           K        DISK
     FPT#APF  IF  E           K        DISK
     FGLI330D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      111 1160$CHYMD
     I                                      152 161 $USERN
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I                                      211 2160DYMD
     I*DEFIND STRAT KEY
     I            DS
     I                                        1  10 @KAPO
     I            DS
     I                                        1  10 @KAPN
     I                                        1   2 DAP01
     I                                        3  10 DAP02
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN AP03      WAP03
     C           *LIKE     DEFN AP04      WAP04
     C           *LIKE     DEFN AP05      WAP05
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DAP01     PARM           P330I1  2         公司別
     C           DAP02     PARM           P330I2  8         會計科目
     C           WAP03     PARM           P330I3  2         分攤組別
     C           WAP04     PARM           P330I4  6         成本中心
     C           WAP05     PARM           P330I5  1         分攤別
     C                     PARM           @RTNC   2          FUN KEY
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYAP     KLIST                           *MAIN FILE
     C                     KFLD           DAP01
     C                     KFLD           DAP02
     C                     KFLD           WAP03
     C                     KFLD           WAP04
     C                     KFLD           WAP05
     C           KEYAF     KLIST
     C                     KFLD           AP01
     C                     KFLD           AP02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
  01-C           @RTNC     DOWEQ*BLANK
    C                     EXSR R1000                      *SCR PROCESS
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
     C                     IN   PTDA01
     C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM $PFMT     P3104I  1
     C                     PARM $PTYPE    P3105I  1
     C           DDATE     PARM           P3101O  80
     C*
|    C                     MOVEL*BLANK    @RTNC
|    C                     Z-ADD13        @PRCD            .PAGE SIZE
|    C*--------------------------------------------------------------*
|    C                     EXSR R1A00                      .SFL INIT
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....SCREEN  PROCESS
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C**                   Z-ADD*ZERO     #LIN
|    C**                   Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     MOVEL'03'      @RTNC
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C                     MOVEL'12'      @RTNC
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN25     IFEQ '1'                        *NEXT PAGE
|   C                     EXSR R1N00
|   C                     GOTO E1000
| 03-C                     END
|    C*
| 04-C           @KAPO     IFNE @KAPN                      *START KEY
|   C                     EXSR R1A00                      *SFL INIT
|   C                     GOTO E1000
| 04-C                     END
|    C*
|    C                     EXSR R1C00                      *OPTION
|    C*
02===C           E1000     ENDSR
     C*=============================================================*
     C*          R1A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
03===C           R1A00     BEGSR
|    C*
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C                     MOVEL'N'       @BOTOM  1        *BOTTOM
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
|    C**                   EXSR R1A20                      *SFL HEAD
|    C* <<SFL INITIAITAL DATA ADD>>
|    C                     MOVEL@KAPN     @KAPO            *KEY SAVE
|    C                     MOVE *BLANK    DOPT
|    C*
|    C           KEYAP     SETLLAP0
|    C                     MOVE *BLANK    WAP03
|    C                     MOVE *BLANK    WAP04
|    C                     MOVE *BLANK    WAP05
|    C                     EXSR R1A10                      *ADD SFL DTA
|    C*
|    C*
| 01-C           RRN       IFGT 0
|   C                     SETON                     50    *SFLDSP
| 01*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C*
03===C                     ENDSR
     C*=============================================================*
     C*          R1A10 .... ADD SUBFILE DATA
     C*=============================================================*
04===C           R1A10     BEGSR
|    C*
|    C                     ADD  @PRCD     @DRCD
| 01-C           RRN       DOWLT@DRCD
|   C*
|   C                     READ AP0                      46
|   C*
| 02-C           *IN46     IFEQ '0'
|   C                     EXSR R1A11                      *WRITE SFL
| 02*C                     ELSE                            *EOF
|   C                     Z-ADDRRN       @DRCD
|   C                     MOVE 'Y'       @BOTOM
|   C                     SETON                     9653  *SFLEND
| 02-C                     END
|   C*
| 01-C                     END
|    C*
|    C                     Z-ADDRRN       DRRN
|    C*
04===C                     ENDSR
     C*=============================================================*
     C*          R1A11 .... WRITE SUBFILE DATA
     C*=============================================================*
05===C           R1A11     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << SUBFILE DATA VALUE SET >>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* GET會計科目
|    C*
|    C                     MOVE *BLANK    DAF03
|    C           KEYAF     CHAINAF0                  40
|    C  N40                MOVELAF03      DAF03
|    C*
|    C* GET成本中心
|    C*
|    C                     MOVE *BLANK    D#A02
|    C           AP04      CHAIN#A0                  40
|    C  N40                MOVEL#A02      D#A02
|    C*
|    C* GET分攤別
|    C*
|    C           AP05      IFEQ '1'
|    C                     MOVEL'預設'  DAP05
|    C                     ELSE
|    C                     MOVEL'追溯'  DAP05
|    C                     END
|    C* GET日期(FILE->SCREEN)
|    C                     Z-ADD0         DAP06
|    C           AP01      CHAINAK0                  40
| 06-C           *IN40     IFEQ '0'
|    C                     CALL 'P31'
|    C                     PARM AP06      P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM AK11      P3104I  1
|    C                     PARM AK09      P3105I  1
|    C           DAP06     PARM           P3101O  80
| 06-C                     END
|    C*
|    C                     ADD  1         RRN
|    C                     WRITESFLSR
|    C*
05===C                     ENDSR
     C*=============================================================*
     C*          R1A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
06===C**         R1A20     BEGSR
|    C*
|    C*
06===C**                   ENDSR
     C*=============================================================*
     C*          R1C00 ....挑選資料
     C*=============================================================*
07===C           R1C00     BEGSR
|    C*
|    C                     READCSFLSR                  4242
| 01-C           *IN42     DOWEQ'0'
|   C*
| 02-C           DOPT      IFNE *BLANK
|   C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
|   C                     MOVELAP01      P330I1
|   C                     MOVELAP02      P330I2
|   C                     MOVELAP03      P330I3
|   C                     MOVELAP04      P330I4
|   C                     MOVELAP05      P330I5
|   C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|   C                     MOVEL'00'      @RTNC
|   C                     SETON                     42
| 02-C                     END
|   C*
|   C  N42                READCSFLSR                    42
| 01-C                     END
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
08===C**         R1E00     BEGSR
|    C*
|    C*          #CSR      DIV  256       #LIN             *LINE
|    C*                    MVR            #COL             *COLUMN
|    C*
|    C* SFL POSITION KEEP-------------------------------------------*
|    C*                    Z-ADD#DRRN     DRRN
|    C*-------------------------------------------------------------*
08===C**                   ENDSR
     C*=============================================================*
     C*          R1N00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
09===C           R1N00     BEGSR
|    C*
| 01-C           @BOTOM    IFEQ 'Y'                        *EOF
|   C                     SETON                         96*LST PAG
|   C                     Z-ADDRRN       DRRN
| 01*C                     ELSE
|   C                     EXSR R1A10                      *ADD SFL DTA
| 01-C                     END
|    C*
09===C                     ENDSR
     C*==============================================================*
