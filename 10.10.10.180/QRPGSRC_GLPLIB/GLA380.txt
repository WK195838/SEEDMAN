     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : GLA380                                     *
     H*    PROGRAM NAME : 成本中心結構設定                         *
     H*    AUTHOR       : A1152   ANGY                               *
     H*    CREATE DATE  : 81/11/11                                   *
     H*    UPDATE DATE  : A1150 82/06/10                             *
     H*    SYSTEM       :                                            *
     H****************************************************************
     FGLAYPF  UF  E           K        DISK                      A
     FGLAZPF  UF  E           K        DISK                      A
     FPT#APF  IF  E           K        DISK
     FPT#BPF  IF  E           K        DISK
     FGLA380D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E*---------------------------------------------------------------*
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      152 161 $USERN
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN RRN       @LRRN            LAST REC NUM
     C           *LIKE     DEFN RRN       @LPAGE           LAST PAGE
     C           *LIKE     DEFN RRN       @PRRN            PROMPT USE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYAY     KLIST                           *HEAD FILE
     C                     KFLD           DAY01
     C                     KFLD           DAY02
     C           KEYAZ     KLIST                           *DETIL FILE
     C                     KFLD           DAY01
     C                     KFLD           DAY02
     C                     KFLD           HAZ03
     C           KEYAZ1    KLIST                           *DETIL FILE
     C                     KFLD           DAY01
     C                     KFLD           DAY02
     C                     KFLD           DAZ03
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     MOVE 'SC01'    SCID    4
|    C*
|    C                     IN   PTDA01
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           DDATE     PARM           P3101O  80
|    C*
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C*                    Z-ADD$C8YMD    DATE
|    C                     Z-ADD1         DOPT
|    C                     MOVEL*BLANK    DAY01
|    C                     MOVEL*BLANK    DAY02
|    C*--------------------------------------------------------------*
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C*                    Z-ADD*ZERO     #LIN
|    C*                    Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C*          *IN04     IFEQ '1'                        *PROMPT
|   C*                    EXSR R1E00
|   C*                    GOTO E1000
| 02-C*                    END
|    C*
| 03-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 03-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 04-C           *IN99     IFEQ '0'                        *SCR CHK OK
|   C                     EXSR R1C00                      *PRE-SC02
|   C                     MOVEL'SC02'    SCID
| 04-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1A00     BEGSR
|    C*
|    C* AUT CHECK
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM 'GLI380'  S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C   99                GOTO E1A00
|    C*
|    C* CALL SUB-PROGRAM
|    C                     CALL 'GLI380'
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C           DAY01     PARM DAY01     P380I1  2
|    C           DAY02     PARM DAY02     P380I2  6
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     PARM           @RTNC   2
|    C* RETURN CODE CHECK
| 02-C           @RTNC     IFEQ '03'                       *PF3
|   C                     SETON                     03
| 02-C                     END
|    C*
03===C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
04===C           R1B00     BEGSR
|    C*
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*
|    C* DATA CHECK *--------------------------------------------------*
|    C*
| 02-C           DAY01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|    C           DAY02     CHAIN#A0                  40
| 03-C           DOPT      IFEQ 1                          *ADD
| 04-C           *IN40     IFNE '0'
|   C           #A10      ORNE '2'
|   C                     MOVEL'UGL0006' ERRID
|   C                     MOVEL'GLMF'    ERRF
|   C                     SETON                     6199
| 04-C                     END
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
|    C           KEYAY     CHAINAY0                  40
|    C*
| 05-C           DOPT      IFEQ 1                          *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 05-C                     END
|    C   99                GOTO E1B00
|    C*
| 06-C           DOPT      IFNE 1                          *UPD,DEL,INQ
|   C           *IN40     ANDEQ'1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 06-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*
04===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
05===C           R1C00     BEGSR
|    C*
|    C*                    MOVEL@F01,DOPT @FUN             *作業說明
|    C                     SETOF                     3132  *IN CONTROL
|    C                     EXSR R1CC0                      *INIT SFL
|    C* ADD
| 01-C           DOPT      IFEQ 1
|   C                     SETON                     31
|   C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|   C                     EXSR R1CA0                      *INIT HEAD
|   C                     EXSR R1CD0                      *INIT DETAIL
|   C*
| 01-C                     END
|    C* UPD
| 02-C           DOPT      IFEQ 2
|   C                     SETON                     3132
|   C                     EXSR R1CB0                      *HEADING
|   C                     EXSR R1CE0                      *READ DETAIL
|   C                     EXSR R1CD0                      *INIT DETAIL
| 02-C                     END
|    C* DEL & INQ
| 03-C           DOPT      IFEQ 4
|   C           DOPT      OREQ 5
|   C                     EXSR R1CB0                      *HEADING
|   C                     EXSR R1CE0                      *READ DETAIL
|   C                     SETON                     53    *SFLEND
| 03-C                     END
|    C*SFLDSP
| 04-C           @LRRN     IFGT 0
|   C                     Z-ADD1         DRRN
|   C                     SETON                     50    *SFLDSP
| 04*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 04-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)++++++++++++++++++++++++++++++++++*
| 05-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 5                          *INQ
|   C           *LOVAL    SETLLAY0                        *HEAD FILE
| 05-C                     END
|    C*
|    C           *LOVAL    SETLLAZ0                        *DETIAL FILE
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1CA0 ....SFL HEAD INIT : ADD (NO COPY)
     C*==============================================================*
06===C           R1CA0     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << ADD : SUBFILE HEAD DATA INITAILIZE>>
|    C                     MOVEL*BLANK    D#B02
|    C           DAY01     CHAIN#B0                  40
|    C  N40                MOVEL#B02      D#B02
|    C*
|    C                     MOVEL*BLANK    D#A02
|    C           DAY02     CHAIN#A0                  40
|    C  N40                MOVEL#A02      D#A02
|    C                     MOVEL*BLANK    DAY03
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
06===C                     ENDSR
     C*==============================================================*
     C*          R1CB0 ....SFL HEAD INIT : U/D/I/A-COPY
     C*==============================================================*
07===C           R1CB0     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << U/D/I AND ADD-COPY : SUBFILE HEAD DATA INITAILIZE>>
|    C                     MOVEL*BLANK    D#B02
|    C           DAY01     CHAIN#B0                  40
|    C  N40                MOVEL#B02      D#B02
|    C*
|    C                     MOVEL*BLANK    D#A02
|    C           DAY02     CHAIN#A0                  40
|    C  N40                MOVEL#A02      D#A02
|    C                     MOVELAY03      DAY03
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
07===C                     ENDSR
     C*=============================================================*
     C*          R1CC0 ....SUBFILE INIT
     C*=============================================================*
08===C           R1CC0     BEGSR
|    C* SFL SETTING ++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD24        @PRCD            *PGE RCD
|    C                     Z-ADD13        @SFTOP  20       *SFL TOP LIN
|    C                     Z-ADD20        @SFBOT  20       *SFL BOT LIN
|    C                     Z-ADD1         @LNUM   20       *明細行數
|    C* ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @LRRN            *LAST RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
08===C                     ENDSR
     C*==============================================================*
     C*          R1CD0 ....SFL DATA INIT
     C*==============================================================*
09===C           R1CD0     BEGSR
|    C*
|    C                     SETOF                     54    *54 FOR BLANK LOOP
|    C                     Z-ADD@LRRN     RRN
|    C*
|    C           @LRRN     DIV  @PRCD     @LPAGE
|    C                     ADD  1         @LPAGE
|    C           @LPAGE    MULT @PRCD     @DRCD
|    C*
|    C                     EXSR R1CD1                      *INIT SFLDTA
| 01-C           RRN       DOWLT@DRCD
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
| 01-C                     END
|    C*
|    C                     Z-ADDRRN       @LRRN
|    C                     Z-ADDRRN       DRRN
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*          R1CD1 ....SFL INIT DATA ADD
     C*==============================================================*
10===C           R1CD1     BEGSR
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     MOVEL*BLANK    DDEL             .DEL OPT
|    C                     MOVEL'Y'       DNEW             .NEW RECORD
|    C*
|    C*<<OTHER SFLSR FIELD CLEAR >>
|    C                     MOVEL*BLANK    DAZ03
|    C                     MOVEL*BLANK    HAZ03
|    C                     MOVEL*BLANK    #A02D
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          R1CE0 ....DETAIL DATA TO SFL
     C*==============================================================*
11===C           R1CE0     BEGSR
|    C*
|    C                     SETOF                     54    *54
|    C*
|    C           KEYAY     SETLLAZ0
|    C           KEYAY     READEAZ0                      46
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
| 01-C           *IN46     DOWEQ'0'
|   C                     EXSR R1CE1
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
|   C           KEYAY     READEAZ0                      46
|   C*
| 01-C                     END
|    C*
|    C                     Z-ADDRRN       @LRRN
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          R1CE1 ....DETAIL DATA TO SFL
     C*==============================================================*
12===C           R1CE1     BEGSR
|    C*
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     MOVEL*BLANK    DDEL
|    C                     MOVEL*BLANK    DNEW
|    C*
|    C* << OTHER FILE FIELD TO SFLSR >>
|    C                     MOVELAZ03      DAZ03
|    C                     MOVELAZ03      HAZ03
|    C           AZ03      CHAIN#A0                  40
|    C  N40                MOVEL#A02      #A02D
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
13===C           R2000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C                     Z-ADD#DRRN     DRRN
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'                        *PROMPT
| 03-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPDATE
|   C                     EXSR R2E00
| 03-C                     END
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 04-C           *IN25     IFEQ '1'                        *NEXT PAGE
| 05-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPDATE
|   C                     EXSR R1CD0
| 05*C                     ELSE
|   C                     SETON                     96    *EOP
| 05-C                     END
|   C                     GOTO E2000
| 04-C                     END
|    C*
| 06-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C* RELEAS FILE --------------------------------------------------*
|   C           *LOVAL    SETLLAY0
|   C*---------------------------------------------------------------*
|   C******               SETOF                     10    控制A/U AW0
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 06-C                     END
|    C*
| 07-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     EXSR R2B00                      *SC02 CHK
| 07-C                     END
|    C*
| 08-C           *IN05     IFEQ '1'                        *CHECK ONLY
|   C                     GOTO E2000
| 08-C                     END
|    C*
| 09-C           *IN99     IFEQ '0'                        *CHK OK
|   C                     MOVE 'SC01'    SCID
|   C                     EXSR R2D00                      *FILE UPD
| 09-C                     END
|    C*
13===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....'SC02' CHECK
     C*==============================================================*
14===C           R2B00     BEGSR
|    C*
|    C* CHECK HEADING (SFLCR) ++++++++++++++++++++++++++++++++++++++++
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
|    C* CHECK DETAIL (SFLSR)
|    C                     SETON                     54
|    C                     READCSFLSR                  5757
|    C*
| 01-C           *IN57     DOWEQ'0'
|   C           *IN99     ANDEQ'0'
|   C                     EXSR R2B10
|   C                     UPDATSFLSR
|   C*
| 02-C           *IN99     IFEQ '1'
|   C                     Z-ADDRRN       DRRN
| 02*C                     ELSE
|   C                     READCSFLSR                    57
| 02-C                     END
|   C*
| 01-C                     END
|    C   99                GOTO E2B00
|    C*
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* <<CHK TOTAL ETC. (SFLCR)>>
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
14===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2B10 ....'SC02' CHECK - SFL DETAIL
     C*==============================================================*
15===C           R2B10     BEGSR
|    C*
| 01-C           DDEL      IFEQ '4'
|   C                     GOTO E2B10
| 01-C                     END
|    C*
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << CHECK DETAIL FIELD >>
|    C* PS : DNEW=' ' --> 不可清除整筆SFLSR,請使用DDEL作DELETE
|    C*                   (( PROGRAM中請作CHECK ))
|    C*MRC UPDAT
|    C                     MOVEL*BLANK    #A02D
|    C*
| 02-C           DAZ03     IFNE *BLANKS
|   C           HAZ03     ORNE *BLANKS
|   C           DAZ03     ANDEQ*BLANKS
|   C           DAZ03     CHAIN#A0                  40
| 03-C           *IN40     IFNE '0'
|   C           #A10      ORNE '1'
|   C                     MOVEL'UGL0007' ERRID
|   C                     MOVEL'GLMF'    ERRF
|   C                     SETON                     6099
| 03*C                     ELSE
|   C                     MOVE #A02      #A02D
| 03-C                     END
| 02-C                     END
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
15===C           E2B10     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
16===C           R2D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D20
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|    C*
16===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*==============================================================*
17===C           R2D10     BEGSR
|    C* DETAIL<明細檔>
|    C                     READCSFLSR                  5757
|    C*
| 01-C           *IN57     DOWEQ'0'
|   C*
| 02-C           DAZ03     IFNE *BLANKS
|   C*
|   C           KEYAZ1    CHAINAZ0                  40
| 03-C           *IN40     IFEQ '0'                        *DUPLICATE
|   C                     SETON                     6099
|   C                     UPDATSFLSR
| 03*C                     ELSE
|   C                     EXSR R2D11
|   C                     WRITEAZ0
| 03-C                     END
| 02-C                     END                             *MRC CHANGE
|   C                     READCSFLSR                    57*MRC CHANGE
| 01-C                     END
|    C* HEAD<主檔>
|    C           KEYAY     CHAINAY0                  40
|    C                     EXSR R2D12
| 04-C           *IN40     IFEQ '1'
|   C                     WRITEAY0
| 04*C                     ELSE
|   C                     UPDATAY0
| 04-C                     END
|    C*
|    C* N10                EXSR R2D12
|    C* N10                WRITEAY0
|    C*                    SETON                     10
|    C*
| 05-C           *IN99     IFEQ '1'                        *DUPLICATE
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     Z-ADDRRN       DRRN
|   C                     MOVE 'SC02'    SCID
|   C                     GOTO E2D10
| 05-C                     END
|    C*
17===C           E2D10     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
18===C           R2D11     BEGSR
|    C*
|    C                     MOVELDAY01     AZ01
|    C                     MOVELDAY02     AZ02
|    C                     MOVELDAZ03     AZ03
|    C                     TIME           AZ97
|    C                     Z-ADD$A8YMD    AZ98
|    C                     MOVEL$USER     AZ99
|    C*
18===C                     ENDSR
     C*==============================================================*
     C*          R2D12 ....FILE PROCESS : ADD  (ADD NEW)
     C*==============================================================*
19===C           R2D12     BEGSR
|    C*
|    C                     MOVELDAY01     AY01
|    C                     MOVELDAY02     AY02
|    C                     MOVELDAY03     AY03
|    C                     TIME           AY97
|    C                     Z-ADD$A8YMD    AY98
|    C                     MOVEL$USER     AY99
|    C*
19===C                     ENDSR
     C*==============================================================*
     C*          R2D20 ....FILE PROCESS : UPD
     C*==============================================================*
20===C           R2D20     BEGSR
|    C* DETAIL
|    C                     READCSFLSR                  5757
|    C*
| 01-C           *IN57     DOWEQ'0'
|   C*
|   C                     SETON                     40
| 02-C           DNEW      IFEQ *BLANK
|   C           KEYAZ     CHAINAZ0                  40
| 02-C                     END
|   C*
| 03-C           DDEL      IFEQ '4'
|   C  N40                DELETAZ0
|   C*
| 03*C                     ELSE
|   C*
| 04-C           DAZ03     IFNE *BLANKS
|   C*
|   C           KEYAZ1    CHAINAZ0                  40
| 05-C           *IN40     IFEQ '0'                        DUPICATE
|   C           DAZ03     ANDNEHAZ03                      DUPICATE
|   C                     SETON                     6099
|   C                     UPDATSFLSR
| 05*C                     ELSE
|   C                     SETON                     40
| 06-C           DNEW      IFEQ *BLANK
|   C           KEYAZ     CHAINAZ0                  40
| 06-C                     END
|   C                     EXSR R2D11
|   C   40                WRITEAZ0
|   C  N40                UPDATAZ0
| 05-C                     END
|   C*
| 04-C                     END
|   C*
| 03-C                     END
|   C*
|   C                     READCSFLSR                    57
| 01-C                     END
|    C* HEAD
|    C           KEYAY     CHAINAY0                  40
|    C                     EXSR R2D12
| 07-C           *IN40     IFEQ '1'
|   C                     WRITEAY0
| 07*C                     ELSE
|   C                     UPDATAY0
| 07-C                     END
|    C* N10                EXSR R2D12
|    C* N10                UPDATAY0
|    C*
| 08-C           *IN99     IFEQ '1'                        *DUPLICATE
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     MOVE 'SC02'    SCID
|   C                     Z-ADDRRN       DRRN
|   C                     GOTO E2D20
| 08-C                     END
|    C*
20===C           E2D20     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS
     C*==============================================================*
21===C           R2D30     BEGSR
|    C* DETAIL
|    C           KEYAY     SETLLAZ0
|    C           KEYAY     READEAZ0                      46
|    C*
| 01-C           *IN46     DOWEQ'0'
|   C                     DELETAZ0
|   C           KEYAY     READEAZ0                      46
| 01-C                     END
|    C*
|    C                     DELETAY0
|    C*
21===C                     ENDSR
     C*==============================================================*
     C*          R2E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
22===C           R2E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* <<SFL HEAD FIELD>>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* SFL DETAIL
| 01-C           #LIN      IFLT @SFTOP
|   C           #LIN      ORGT @SFBOT
|   C                     GOTO E2E00
| 01-C                     END
| 02-C           #COL      IFLT 7
|   C           #COL      ORGT 12
|   C           #COL      ANDLT34
|   C           #COL      ORGT 39
|   C           #COL      ANDLT61
|   C           #COL      ORGT 66
|   C                     GOTO E2E00
| 02-C                     END
|    C* GET SFL REC
|    C           #LIN      SUB  @SFTOP    @PRRN
|    C           @PRRN     DIV  @LNUM     @PRRN
|    C                     MVR            @LINE   20
|    C*
|    C                     ADD  1         @LINE            .明細行
|    C                     ADD  #DRRN     @PRRN            .PROMPT RRN
|    C                     Z-ADD0         @PRRN1  30
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << SFLSR FIELD >>
| 03-C           @LINE     IFEQ 1
|   C*
| 04-C           #COL      IFGE 7
|   C           #COL      ANDLE12
|   C           @PRRN     CHAINSFLSR                40
|   C   40                GOTO E2E00
| 04-C                     END
| 05-C           #COL      IFGE 33
|   C           #COL      ANDLE38
|   C           @PRRN     ADD  8         @PRRN1
|   C           @PRRN1    CHAINSFLSR                40
|   C   40                GOTO E2E00
| 05-C                     END
| 06-C           #COL      IFGE 59
|   C           #COL      ANDLE64
|   C           @PRRN     ADD  16        @PRRN1
|   C           @PRRN1    CHAINSFLSR                40
|   C   40                GOTO E2E00
| 06-C                     END
|   C                     CALL 'PTI140'
|   C           DAZ03     PARM DAZ03     P140I1  6
|   C                     PARM           @RTNC   2
| 03-C                     END
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* UPD SFL
| 07-C           DAZ03     IFNE HAZ03                      *有修改
|   C                     SETON                     54
|   C           DAZ03     CHAIN#A0                  40
|   C  N40                MOVEL#A02      #A02D
|   C                     UPDATSFLSR
| 07-C                     END
|    C*
22===C           E2E00     ENDSR
     C*==============================================================*
