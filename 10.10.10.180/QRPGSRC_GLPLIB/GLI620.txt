     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : GLI620                                     *
     H*    PROGRAM NAME : 資金預算資料全頁查詢                     *
     H*    AUTHOR       : A1087  JOYCE                               *
     H*    CREATE DATE  : 1992.12.08                                 *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       : 泛太總帳                                 *
     F****************************************************************
     FGLA1LF01IF  E           K        DISK
     FGLADPF  IF  E           K        DISK
     FGLAKPF  IF  E           K        DISK
     FGLI620D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      111 1160$CHYMD
     I                                      152 161 $USERN
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I*DEFIND STRAT KEY
     I            DS
     I                                        1  16 @KA1O
     I            DS
     I                                        1  16 @KA1N
     I                                        1   40WA102
     I                                        5   60DA103
     I                                        7   8 DA101
     I                                        9  16 DA104
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DA101     PARM           P620I1  2         公司別
     C                     PARM           P620I2  30        年度
     C           DA103     PARM           P620I3  20        版次
     C           DA104     PARM           P620I4  8         資金代碼
     C                     PARM           @RTNC   2          FUN KEY
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYA1     KLIST                           *MAIN FILE
     C                     KFLD           WA102
     C                     KFLD           DA103
     C                     KFLD           DA101
     C                     KFLD           DA104
     C           KEYAD     KLIST
     C                     KFLD           A101
     C                     KFLD           A104
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
  01-C           @RTNC     DOWEQ*BLANK
    C                     EXSR R1000                      *SCR PROCESS
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           DDATE     PARM           P3101O  80
|    C*
|    C                     MOVEL*BLANK    @RTNC
|    C                     Z-ADD12        @PRCD            .PAGE SIZE
|    C*--------------------------------------------------------------*
|    C*
|    C           DA101     CHAINAK0                  40
| 01-C           *IN40     IFEQ '0'
|   C*年制為１－西元年，日期格式＝１　ＹＹＭＭＤＤ
|   C*年制為２－中國年，日期格式＝２　ＹＹＹＹＭＭＤＤ
|    C                     Z-ADDP620I2    DA102   30
|   C           DA102     MULT 10000     P3101I
|   C                     CALL 'P31'
|   C                     PARM           P3101I  80
|   C                     PARM AK09      P3102I  1
|   C                     PARM AK09      P3103I  1
|   C                     PARM '2'       P3104I  1
|   C                     PARM '1'       P3105I  1
|   C                     PARM           P3101O  80
|   C           P3101O    DIV  10000     WA102
| 01*C                     ELSE
|   C                     Z-ADD0         WA102
| 01-C                     END
|    C                     EXSR R1A00                      .SFL INIT
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....SCREEN  PROCESS
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     MOVEL'03'      @RTNC
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C                     MOVEL'12'      @RTNC
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN25     IFEQ '1'                        *NEXT PAGE
|   C                     EXSR R1N00
|   C                     GOTO E1000
| 03-C                     END
|    C*
| 04-C           @KA1O     IFNE @KA1N                      *START KEY
|   C                     EXSR R1A00                      *SFL INIT
|   C                     GOTO E1000
| 04-C                     END
|    C*
|    C                     EXSR R1C00                      *OPTION
|    C*
02===C           E1000     ENDSR
     C*=============================================================*
     C*          R1A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
03===C           R1A00     BEGSR
|    C*
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C                     MOVEL'N'       @BOTOM  1        *BOTTOM
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
|    C**                   EXSR R1A20                      *SFL HEAD
|    C* <<SFL INITIAITAL DATA ADD>>
|    C                     MOVEL@KA1N     @KA1O            *KEY SAVE
|    C                     MOVE *BLANK    DOPT
|    C*
|    C           KEYA1     SETLLA10
|    C                     EXSR R1A10                      *ADD SFL DTA
|    C*
| 02-C           RRN       IFGT 0
|   C                     SETON                     50    *SFLDSP
| 02*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 02-C                     END
|    C*
03===C                     ENDSR
     C*=============================================================*
     C*          R1A10 .... ADD SUBFILE DATA
     C*=============================================================*
04===C           R1A10     BEGSR
|    C*
|    C                     ADD  @PRCD     @DRCD
| 01-C           RRN       DOWLT@DRCD
|   C*
|   C                     READ A10                      46
|   C*
| 02-C           *IN46     IFEQ '0'
|   C                     EXSR R1A11                      *WRITE SFL
| 02*C                     ELSE                            *EOF
|   C                     Z-ADDRRN       @DRCD
|   C                     MOVE 'Y'       @BOTOM
|   C                     SETON                     9653  *SFLEND
| 02-C                     END
|   C*
| 01-C                     END
|    C*
|    C                     Z-ADDRRN       DRRN
|    C*
04===C                     ENDSR
     C*=============================================================*
     C*          R1A11 .... WRITE SUBFILE DATA
     C*=============================================================*
05===C           R1A11     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << SUBFILE DATA VALUE SET >>
|    C*
|    C                     MOVELA105      DA105
|    C* GET會計名稱
|    C                     MOVE *BLANK    DAD03
|    C           KEYAD     CHAINAD0                  40
|    C  N40                MOVELAD03      DAD03
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     ADD  1         RRN
|    C                     WRITESFLSR
|    C*
05===C                     ENDSR
     C*=============================================================*
     C*          R1A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
06===C**         R1A20     BEGSR
|    C*
|    C*
06===C**                   ENDSR
     C*=============================================================*
     C*          R1C00 ....挑選資料
     C*=============================================================*
07===C           R1C00     BEGSR
|    C*
|    C                     READCSFLSR                  4242
| 01-C           *IN42     DOWEQ'0'
|   C*
| 02-C           DOPT      IFNE *BLANK
|   C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
|   C                     MOVELA101      P620I1
|   C*
|    C           A101      CHAINAK0                  40
| 01-C           *IN40     IFEQ '0'
|   C*年制為１－西元年，日期格式＝１　ＹＹＭＭＤＤ
|   C*年制為２－中國年，日期格式＝２　ＹＹＹＹＭＭＤＤ
|   C           A102      MULT 10000     P3101I
|   C                     CALL 'P31'
|   C                     PARM           P3101I  80
|   C                     PARM '2'       P3102I  1
|   C                     PARM '1'       P3103I  1
|   C                     PARM AK09      P3104I  1
|   C                     PARM AK09      P3105I  1
|   C                     PARM           P3101O  80
|   C           P3101O    DIV  10000     DA102
| 01*C                     ELSE
|   C                     Z-ADD0         DA102
| 01-C                     END
|   C*
|   C                     Z-ADDDA102     P620I2
|   C                     Z-ADDA103      P620I3
|   C                     MOVELA104      P620I4
|   C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|   C                     MOVEL'00'      @RTNC
|   C                     SETON                     42
| 02-C                     END
|   C*
|   C  N42                READCSFLSR                    42
| 01-C                     END
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
08===C**         R1E00     BEGSR
|    C*
|    C*          #CSR      DIV  256       #LIN             *LINE
|    C*                    MVR            #COL             *COLUMN
|    C*
|    C* SFL POSITION KEEP-------------------------------------------*
|    C*                    Z-ADD#DRRN     DRRN
|    C*-------------------------------------------------------------*
08===C**                   ENDSR
     C*=============================================================*
     C*          R1N00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
09===C           R1N00     BEGSR
|    C*
| 01-C           @BOTOM    IFEQ 'Y'                        *EOF
|   C                     SETON                         96*LST PAG
|   C                     Z-ADDRRN       DRRN
| 01*C                     ELSE
|   C                     EXSR R1A10                      *ADD SFL DTA
| 01-C                     END
|    C*
09===C                     ENDSR
