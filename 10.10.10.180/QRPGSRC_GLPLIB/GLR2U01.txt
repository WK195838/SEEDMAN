     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLR2U01                                    *
     A*    PROGRAM NAME : 四年財務比較表                           *
     A*    AUTHOR       :  A1087  JOYCE                              *
     A*    CREATE DATE  :  1993.01.13                                *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     FGLWF03  UF  E           K        DISK                      A
     FGLASLF02IF  E           K        DISK
     F            AS0                               KRENAMEASLF02
     FGLASLF03IF  E           K        DISK
     F            AS0                               KRENAMEASLF03
     FGLAILF02IF  E           K        DISK
     FGLAFLF03IF  E           K        DISK
     F            AF0                               KRENAMEAFL3
     FGLARPF  IF  E           K        DISK
     FGLAXPF  IF  E           K        DISK
     FPT#BPF  IF  E           K        DISK
     FGLR2U0P1O   E             39     PRINTER
     E*---------------------------------------------------------------
     E*科目餘額
     E                    WAI1       14 13 2
     E                    @W1    10  10  2
     E*---------------------------------------------------------------*
     I*
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     IGLDA01      DS
     I                                        1  16 DA01
     I*
     ILDA        UDS
     I                                      101 110 $USER
     I                                      125 1270$CPY
     I                                      138 138 $INQ
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I                                      217 218 $PRTCD
     I                                      219 219 $PENV
     I* SCREEN INPUT FIELD  ++++++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DAI01
     I                                      503 5050DYEAR
     I                                      506 508 DAR03
     I* PROGRAM CALCULATION FIELD  +++++++++++++++++++++++++++++++++++
     I                                      509 509 #B12
     I* OPEN QUERY FILE SELECT $XXXXX FIELD  +++++++++++++++++++++++++
     I                                      510 511 DAI01S
     I                                      512 513 DAI01E
     I                                      514 5170DAI02S
     I                                      518 5210DAI02E
     I                                      522 522 DAR05
     I                                      523 5240DMM
     I                                      531 538 WAK05
     I                                      539 539 WCLO
     I            DS
     I                                        1 182 WAI1
     I                                        1  132AI10
     I                                       14  262AI11
     I                                       27  392AI12
     I                                       40  522AI13
     I                                       53  652AI14
     I                                       66  782AI15
     I                                       79  912AI16
     I                                       92 1042AI17
     I                                      105 1172AI18
     I                                      118 1302AI19
     I                                      131 1432AI20
     I                                      144 1562AI21
     I                                      157 1692AI22
     I                                      170 1822AI23
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *NAMVAR   DEFN           GLDA01
     C           *NAMVAR   DEFN *LDA      LDA
     C           *LIKE     DEFN AI02      PY01
     C           *LIKE     DEFN AI02      PY02
     C           *LIKE     DEFN AI02      PY03
     C           *LIKE     DEFN AI02      PY04
     C           *LIKE     DEFN AI01      WAI01
     C           *LIKE     DEFN AS04      WAS04            *BREAK
     C           *LIKE     DEFN AS06      WAS06
     C           *LIKE     DEFN AS08      WAS08
     C           *LIKE     DEFN AS10      WAS10
     C           *LIKE     DEFN AS02      WAR02            *KEY
     C           *LIKE     DEFN AI10      WCASH1           *金額
     C           *LIKE     DEFN AI10      WCASH2           *金額
     C           *LIKE     DEFN AI10      WCASH3           *金額
     C           *LIKE     DEFN AI10      WCASH4           *金額
     C           *LIKE     DEFN AI10      WCH1             *小計金額
     C           *LIKE     DEFN AI10      WCH2             *小計金額
     C           *LIKE     DEFN AI10      WCH3             *小計金額
     C           *LIKE     DEFN AI10      WCH4             *小計金額
     C           *LIKE     DEFN AI10      WCASH5           *金額
     C           *LIKE     DEFN AI10      WCASH6           *金額
     C           *LIKE     DEFN AI10      WCASH7           *金額
     C           *LIKE     DEFN AI10      WCASH8           *金額
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEY#B     KLIST
     C                     KFLD           DAI01            *公司別
     C           KEYAF     KLIST
     C                     KFLD           WAI01            *公司別
     C                     KFLD           AF02
     C           KEYAS     KLIST
     C                     KFLD           DAI01            *公司別
     C                     KFLD           WAR02            *報表類別
     C                     KFLD           DAR03            *報表代碼
     C           KEYAX     KLIST
     C                     KFLD           DAI01            *公司別S
     C                     KFLD           AI01             *公司別F
     C           KEYAI     KLIST
     C                     KFLD           AS06
     C                     KFLD           DAI02S           *會計年度
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      .INIT-HEAD
     C*
     C                     EXSR R1000                      .WROK-FILE
     C*
     C                     EXSR R8000                      .PRINT
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ... HEADING INITIAL
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C                     IN   GLDA01
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           PDATE     PARM           P3101O  80
|    C**
| 01-C           #B12      IFEQ '1'
|   C                     MOVELDAI01     WAI01
| 01*C                     ELSE
|   C****************************
|   C*公司別屬虛擬            *
|   C*須CHECK公司結構明細檔 *
|   C****************************
|   C           DAI01     CHAINAX0                  40
|   C  N40                MOVELAX02      WAI01
| 01-C                     END
|    C**************
|    C*<<公司別 >>
|    C                     MOVEL*BLANKS   P#B16
|    C           KEY#B     CHAIN#B0                  40
| 02-C           *IN40     IFEQ '0'
| 03-C           DAR05     IFEQ '1'
|   C                     MOVEL#B16      P#B16
| 03*C                     ELSE
|   C                     MOVEL#B17      P#B16
| 03-C                     END
| 02-C                     END
|    C**************
|    C*<< *DTAARA >>
|    C                     CALL 'GLS006'
|    C                     PARM WAI01     P006I1  2
|    C                     PARM DAI02E    P006I2  40
|    C                     PARM DMM       P006I3  20
|    C                     PARM 'F'       P006I4  1
|    C                     PARM           P006O1  80
|    C                     PARM           P006O2  80
|    C                     PARM           P006O3  1
|    C                     PARM           P006O4  1
|    C*
| 04-C           P006O4    IFNE 'Y'
|   C                     IN   GLDA01
| 05-C           DAR05     IFEQ '1'
|   C                     MOVELDA01      PDA01
| 05*C                     ELSE
|   C                     MOVE DA01      PDA01
| 05-C                     END
| 04-C                     END
|    C*************************
|    C*<<報表格式－報表代碼>>
|    C                     MOVEL*BLANKS   PAR04
|    C                     MOVEL'50'      WAR02
|    C           KEYAS     CHAINAR0                  40
|    C  N40                MOVELAR04      PAR04
|    C***************
|    C*<<會計年度>>
|    C                     Z-ADDDYEAR     PY04P
|    C           DYEAR     SUB  1         PY03P
|    C           DYEAR     SUB  2         PY02P
|    C           DYEAR     SUB  3         PY01P
|    C*
|    C                     Z-ADDDAI02S    PY01
|    C           DAI02S    ADD  1         PY02
|    C           DAI02S    ADD  2         PY03
|    C                     Z-ADDDAI02E    PY04
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ... GET DATA TO WORK FILE
     C*==============================================================*
02===C           R1000     BEGSR
|    C**************************
|    C* 逐筆讀取GLASLF02,    *
|    C* 只取AS06為會計科目,*
|    C* 存入GLWF11.          *
|    C**************************
|    C                     Z-ADD*ZERO     WCASH1
|    C                     Z-ADD*ZERO     WCASH2
|    C                     Z-ADD*ZERO     WCASH3
|    C                     Z-ADD*ZERO     WCASH4
|    C*
|    C           KEYAS     SETLLASLF02
|    C           KEYAS     READEASLF02                   42
|    C   42                MOVEL*BLANK    WAS04
|    C  N42                MOVELAS04      WAS04
|    C                     MOVELAS06      WAS06
|    C                     MOVELAS08      WAS08
|    C                     MOVELAS10      WAS10
| 01-C           *IN42     DOWEQ'0'                        .EOF
|   C*
| 02-C           AS04      IFNE WAS04
|   C                     EXSR R2100                      *註一
|   C                     WRITEGLWF03R
|   C*
|   C                     MOVELAS04      WAS04
|   C                     MOVELAS06      WAS06
|   C                     MOVELAS08      WAS08
|   C                     MOVELAS10      WAS10
| 02-C                     END
|   C*
|   C                     EXSR R2A00                      .DETAIL
|   C*
|   C           KEYAS     READEASLF02                   42
| 01-C                     END
|    C* WRITE最後一筆
| 03-C           WAS04     IFNE *BLANKS
|   C                     EXSR R2100
|   C                     WRITEGLWF03R
| 03-C                     END
|    C**************************
|    C* 逐筆讀取GLASLF03,    *
|    C* 只取AS06為@------  *
|    C* 存入GLWF11.          *
|    C**************************
|    C                     Z-ADD*ZERO     WCASH1
|    C                     Z-ADD*ZERO     WCASH2
|    C                     Z-ADD*ZERO     WCASH3
|    C                     Z-ADD*ZERO     WCASH4
|    C*
|    C           KEYAS     SETLLASLF03
|    C           KEYAS     READEASLF03                   42
|    C   42                MOVEL*BLANK    WAS04
|    C  N42                MOVELAS04      WAS04
|    C                     MOVELAS06      WAS06
|    C                     MOVELAS08      WAS08
|    C                     MOVELAS10      WAS10
|    C*
| 04-C           *IN42     DOWEQ'0'                        .EOF
|   C**********************
|   C*計算基數@T=@BASE1 *
|   C**********************
|   C           2         SUBSTAS06:1    @AS06   2
| 05-C           @AS06     IFEQ '@R'
|   C           4         SUBSTAS06:3    KEY03   4
|   C*
|   C                     Z-ADD*ZERO     @BASE1 132
|   C                     Z-ADD*ZERO     @BASE2 132
|   C                     Z-ADD*ZERO     @BASE3 132
|   C                     Z-ADD*ZERO     @BASE4 132
|   C           KEY03     CHAINGLWF03R              40
|   C  N40                ADD  WF0304    @BASE1
|   C  N40                ADD  WF0305    @BASE2
|   C  N40                ADD  WF0306    @BASE3
|   C  N40                ADD  WF0307    @BASE4
|   C****************************
|   C* @R此筆資料不存入GLWF11 *
|   C****************************
|   C                     GOTO TAG#3
| 05-C                     END
|   C****************************
|   C*
| 06-C           AS04      IFNE WAS04
|   C           WAS04     CHAINGLWF03R              40
|   C*
|   C*
|   C           2         SUBSTWAS06:1   #AS06   2
| 07-C           #AS06     IFNE '@R'
|   C                     EXSR R2100
|   C*
|   C   40                WRITEGLWF03R
|   C  N40                UPDATGLWF03R
|   C*
| 07-C                     END
|   C                     MOVELAS04      WAS04
|   C                     MOVELAS06      WAS06
|   C                     MOVELAS08      WAS08
|   C                     MOVELAS10      WAS10
|   C                     Z-ADD*ZERO     WCASH1
|   C                     Z-ADD*ZERO     WCASH2
|   C                     Z-ADD*ZERO     WCASH3
|   C                     Z-ADD*ZERO     WCASH4
| 06-C                     END
|   C*
|   C                     EXSR R2B00                      .DETAIL
|   C           TAG#3     TAG
|   C           KEYAS     READEASLF03                   42
| 04-C                     END
|    C* WRITE最後一筆
| 08-C           WAS04     IFNE *BLANKS
|   C                     EXSR R2100
|   C                     WRITEGLWF03R
| 08-C                     END
|    C**************************************
|    C* WHEN @T此筆之AS04=*ZERO時,     *
|    C*為GLASLF03第一筆讀入之DATA,     *
|    C* SOMETIMES須重新計算基數@T=@BASE1 *
|    C**************************************
| 09-C           @BASE1    IFEQ *ZERO
|   C           @BASE2    ANDEQ*ZERO
|   C           @BASE3    ANDEQ*ZERO
|   C           @BASE4    ANDEQ*ZERO
|   C*
|   C           KEYAS     SETLLASLF03
|   C           KEYAS     READEASLF03                   42
|   C*
| 10-C           *IN42     DOWEQ'0'                        .EOF
|   C           2         SUBSTAS06:1    @AS06   2
|   C*
| 11-C           @AS06     IFEQ '@R'
|   C           4         SUBSTAS06:3    KEY03   4
|   C           KEY03     CHAINGLWF03R              40
|   C  N40                ADD  WF0304    @BASE1
|   C  N40                ADD  WF0305    @BASE2
|   C  N40                ADD  WF0306    @BASE3
|   C  N40                ADD  WF0307    @BASE4
|   C*
| 11-C                     END                             .@R
|   C           KEYAS     READEASLF03                   42
| 10-C                     END                             .*IN42
| 09-C                     END                             .@BASE1
|    C*
02===C                     ENDSR
     C*==============================================================*
     C*          R2A00 ... GLASLF02 DETAIL
     C*==============================================================*
03===C           R2A00     BEGSR
|    C*****************
|    C* 會計科目檔  *
|    C*****************
|    C           AS06      SETLLAFL3
|    C                     READ AFL3                     46
| 01-C           *IN46     DOWEQ'0'
|   C           AF02      ANDLEAS07
|   C*
|   C*** 會計科目為彙總且不為本期損益
| 02-C***        AF08      IFEQ '2'
|   C***        AF02      ANDNEWAK05
|   C***                  GOTO TAG#1
| 02-C***                  END
|   C*
|   C*****************
|   C* 年度餘額檔  *
|   C*****************
|   C                     Z-ADD*ZEROS    WCH1
|   C                     Z-ADD*ZEROS    WCH2
|   C                     Z-ADD*ZEROS    WCH3
|   C                     Z-ADD*ZEROS    WCH4
|   C           AF02      SETLLAI0
|   C           AF02      READEAI0                      44
|   C   44                GOTO TAG#2
| 03-C           *IN44     DOWEQ'0'
|   C***********************
|   C* AI01公司別屬虛擬 *
|   C***********************
| 04-C           #B12      IFEQ '2'
|   C           KEYAX     SETLLAX0                      42
|   C  N42                GOTO TAG#1
| 04-C                     END
|   C*
|   C*******************
|   C*計算＝小計金額 *
|   C*******************
|   C                     EXSR R2200                      *註二
|   C*
|   C                     ADD  WCASH5    WCH1             *小計金額
|   C                     ADD  WCASH6    WCH2             *小計金額
|   C                     ADD  WCASH7    WCH3             *小計金額
|   C                     ADD  WCASH8    WCH4             *小計金額
|   C*
|   C           AF02      READEAI0                      44
| 03-C                     END
|   C*
|   C           TAG#2     TAG
|   C**************************
|   C** GET上期未年結之金額
| 05-C           WCLO      IFEQ 'N'
|   C** 科目屬實帳目
| 06-C           AF05      IFEQ '1'
|   C* 第一年
|   C                     Z-ADD0         P009O1
|   C                     CALL 'GLS009'
|   C                     PARM WAI01     P009I1  2
|   C                     PARM AF02      P009I2  8
|   C                     PARM PY01      P009I3  40
|   C                     PARM 'F'       P009I4  1
|   C                     PARM           P009O1 132       *上期金額
|   C                     ADD  P009O1    WCH1
|   C* 第二年
|   C                     Z-ADD0         P009O1
|   C                     CALL 'GLS009'
|   C                     PARM WAI01     P009I1  2
|   C                     PARM AF02      P009I2  8
|   C                     PARM PY02      P009I3  40
|   C                     PARM 'F'       P009I4  1
|   C                     PARM           P009O1 132       *上期金額
|   C                     ADD  P009O1    WCH2
|   C* 第三年
|   C                     Z-ADD0         P009O1
|   C                     CALL 'GLS009'
|   C                     PARM WAI01     P009I1  2
|   C                     PARM AF02      P009I2  8
|   C                     PARM PY03      P009I3  40
|   C                     PARM 'F'       P009I4  1
|   C                     PARM           P009O1 132       *上期金額
|   C                     ADD  P009O1    WCH3
|   C* 第四年
|   C                     Z-ADD0         P009O1
|   C                     CALL 'GLS009'
|   C                     PARM WAI01     P009I1  2
|   C                     PARM AF02      P009I2  8
|   C                     PARM PY04      P009I3  40
|   C                     PARM 'F'       P009I4  1
|   C                     PARM           P009O1 132       *上期金額
|   C                     ADD  P009O1    WCH4
| 06-C                     END
| 05-C                     END
|   C*
|   C                     MULT AS05      WCH1
|   C                     ADD  WCH1      WCASH1           *小計金額
|   C                     MULT AS05      WCH2
|   C                     ADD  WCH2      WCASH2           *小計金額
|   C                     MULT AS05      WCH3
|   C                     ADD  WCH3      WCASH3           *小計金額
|   C                     MULT AS05      WCH4
|   C                     ADD  WCH4      WCASH4           *小計金額
|   C*
|   C           TAG#1     TAG
|   C*******************
|   C* 讀下一會計科目
|   C           AF02      SETGTAFL3
|   C*
|   C                     READ AFL3                     46
| 01-C                     END
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          R2B00 ... GLASLF03 DETAIL
     C*==============================================================*
04===C           R2B00     BEGSR
|    C**
|    C           2         SUBSTAS06      @AS06   2
|    C           @AS06     LOKUP@W1                      49
| 01-C           *IN49     IFEQ '1'                        * @0-@9
|   C           4         SUBSTAS06:2    WF301S  4
|   C           4         SUBSTAS07:2    WF301E  4
|   C***********
|   C*  GLWF03 *
|   C***********
|   C           WF301S    SETLLGLWF03R
|   C                     READ GLWF03R                  46
|   C*
| 02-C           *IN46     DOWEQ'0'
|   C           WF0301    ANDLEWF301E
|   C**************************
|   C*計算＝小計金額 WCASH1 *
|   C**************************
|   C                     MULT AS05      WF0304
|   C                     ADD  WF0304    WCASH1
|   C                     MULT AS05      WF0305
|   C                     ADD  WF0305    WCASH2
|   C                     MULT AS05      WF0306
|   C                     ADD  WF0306    WCASH3
|   C                     MULT AS05      WF0307
|   C                     ADD  WF0307    WCASH4
|   C*
|   C                     READ GLWF03R                  46
| 02-C                     END
| 01-C                     END
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R2100 ... 新增／更新一筆至工作檔GLWF11 (註一)
     C*                     SCREEN => FILE. GLWF11
     C*==============================================================*
05===C           R2100     BEGSR
|    C**************************
|    C* AS06前二碼為@-.@S.@C *
|    C*  新增一筆至GLWF11    *
|    C**************************
|    C           2         SUBSTWAS06     @AS06   2
| 01-C           @AS06     IFEQ '@S'
|   C           @AS06     OREQ '@-'
|   C           @AS06     OREQ '@='
|   C           @AS06     OREQ '@C'
|   C                     MOVEL*BLANKS   WF0301
|   C                     MOVELWAS04     WF0301
|   C                     MOVEL*BLANKS   WF0302
|   C                     MOVEL*BLANKS   WF0303
| 02-C           @AS06     IFEQ '@C'
|   C                     MOVEL'C'       WF0302
|   C                     MOVELWAS10     WF0303
| 02*C                     ELSE
|   C                     MOVELWAS06     WF0303
| 02-C                     END
|   C                     Z-ADD*ZERO     WF0304
|   C                     Z-ADD*ZERO     WF0305
|   C                     Z-ADD*ZERO     WF0306
|   C                     Z-ADD*ZERO     WF0307
|   C*******************************
|   C* AS06前二碼不為@-.@S.@C,@= *
|   C*  新增／更新一筆至GLWF03   *
|   C*******************************
| 01*C                     ELSE
|   C           WAS04     CHAINGLWF03R              40
| 03-C           *IN40     IFEQ '0'
|   C                     ADD  WCASH1    WF0304
|   C                     ADD  WCASH2    WF0305
|   C                     ADD  WCASH3    WF0306
|   C                     ADD  WCASH4    WF0307
| 03*C                     ELSE
|   C*
|   C                     MOVEL*BLANKS   WF0301
|   C                     MOVELWAS04     WF0301
|   C                     MOVEL*BLANKS   WF0302
|   C                     MOVELWAS08     WF0302
|   C                     MOVEL*BLANKS   WF0303
|   C                     MOVELWAS10     WF0303
|   C                     Z-ADDWCASH1    WF0304
|   C                     Z-ADDWCASH2    WF0305
|   C                     Z-ADDWCASH3    WF0306
|   C                     Z-ADDWCASH4    WF0307
| 03-C                     END
| 01-C                     END
|    C*
|    C                     Z-ADD*ZERO     WCASH1           *小計金額
|    C                     Z-ADD*ZERO     WCASH2           *小計金額
|    C                     Z-ADD*ZERO     WCASH3           *小計金額
|    C                     Z-ADD*ZERO     WCASH4           *小計金額
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R2200 ... 計算各期金額（註二）
     C*==============================================================*
06===C           R2200     BEGSR
|    C*
|    C                     Z-ADD*ZEROS    WCASH5
|    C                     Z-ADD*ZEROS    WCASH6
|    C                     Z-ADD*ZEROS    WCASH7
|    C                     Z-ADD*ZEROS    WCASH8
|    C*
| 01-C           AI02      IFEQ PY01
|   C                     XFOOTWAI1      WCASH5
| 01-C                     END
| 02-C           AI02      IFEQ PY02
|   C                     XFOOTWAI1      WCASH6
| 02-C                     END
| 03-C           AI02      IFEQ PY03
|   C                     XFOOTWAI1      WCASH7
| 03-C                     END
| 04-C           AI02      IFEQ PY04
|   C                     XFOOTWAI1      WCASH8
| 04-C                     END
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R8000 ... PRINTING MAIN PROCESS
     C*==============================================================*
07===C           R8000     BEGSR
|    C*
|    C                     SETON                     39
|    C                     SETOF                     10
|    C**********************
|    C* 四年財務比較報表 *
|    C**********************
|    C           *LOVAL    SETLLGLWF03R
|    C                     READ GLWF03R                  42
|    C*
| 01-C           *IN42     DOWEQ'0'                        .EOF
|   C*
| 02-C           *IN39     IFEQ '1'
|   C                     WRITEPH1
|   C                     SETOF                     39
| 02-C                     END
|   C*
| 03-C           WF0303    IFEQ '@-      '
|   C* FIRST RECORD
|   C  N10                SETON                     10
|   C                     WRITEPD2
| 03*C                     ELSE
|   C*
| 04-C           WF0303    IFEQ '@S      '
|   C  N10                SETON                     10
|   C                     WRITEPD3
| 04*C                     ELSE
|   C*
| 05-C           WF0303    IFEQ '@=      '
|   C  N10                SETON                     10
|   C                     WRITEPD4
| 05*C                     ELSE
|   C*
|   C                     MOVE *BLANK    P0303
|   C                     MOVELWF0303    P0303
| 06-C           WF0302    IFEQ 'C'
|   C  N10                SETON                     10
|   C                     WRITEPD1@
| 06*C                     ELSE
|   C*
|   C************************
|   C*值全為零者．不列印  *
|   C************************
| 07-C           WF0304    IFNE *ZERO
|   C           WF0305    ORNE *ZERO
|   C           WF0306    ORNE *ZERO
|   C           WF0307    ORNE *ZERO
|   C  N10                SETON                     10
|   C*
|   C                     MOVEA*ALL'0'   *IN,61
|   C*
|   C                     Z-ADDWF0304    P0304A
|   C                     Z-ADDWF0305    P0305A
|   C                     Z-ADDWF0306    P0306A
|   C                     Z-ADDWF0307    P0307A
|   C*
| 08-C           WF0302    IFEQ 'V'
|   C                     Z-ADDWF0304    P0304B
|   C                     Z-ADDWF0305    P0305B
|   C                     Z-ADDWF0306    P0306B
|   C                     Z-ADDWF0307    P0307B
|   C                     SETON                     61
| 08-C                     END
|   C**********
|   C* 第一年
| 09-C           WF0304    IFLT *ZERO
|   C                     SETON                     81
| 09-C                     END
| 10-C           @BASE1    IFEQ *ZERO
|   C                     SETON                     62
| 10*C                     ELSE
|   C           WF0304    DIV  @BASE1    WP100   74
|   C           WP100     MULT 100       PR0304    H
| 11-C           PR0304    IFLT *ZERO
|   C                     SETON                     82
| 11-C                     END
| 10-C                     END
|   C**********
|   C* 第二年
| 12-C           WF0305    IFLT *ZERO
|   C                     SETON                     83
| 12-C                     END
| 13-C           @BASE2    IFEQ *ZERO
|   C                     SETON                     63
| 13*C                     ELSE
|   C           WF0305    DIV  @BASE2    WP100   74
|   C           WP100     MULT 100       PR0305    H
| 14-C           PR0305    IFLT *ZERO
|   C                     SETON                     84
| 14-C                     END
| 13-C                     END
|   C**********
|   C* 第三年
| 15-C           WF0306    IFLT *ZERO
|   C                     SETON                     85
| 15-C                     END
| 16-C           @BASE3    IFEQ *ZERO
|   C                     SETON                     64
| 16*C                     ELSE
|   C           WF0306    DIV  @BASE3    WP100   74
|   C           WP100     MULT 100       PR0306    H
| 17-C           PR0306    IFLT *ZERO
|   C                     SETON                     86
| 17-C                     END
| 16-C                     END
|   C**********
|   C* 第四年
| 18-C           WF0307    IFLT *ZERO
|   C                     SETON                     87
| 18-C                     END
| 19-C           @BASE4    IFEQ *ZERO
|   C                     SETON                     65
| 19*C                     ELSE
|   C           WF0307    DIV  @BASE4    WP100   74
|   C           WP100     MULT 100       PR0307    H
| 20-C           PR0307    IFLE *ZERO
|   C                     SETON                     88
| 20-C                     END
| 19-C                     END
|   C*
|   C                     WRITEPD1
| 07-C                     END
| 06-C                     END
| 05-C                     END
| 04-C                     END
| 03-C                     END
|   C*
|   C                     READ GLWF03R                  42
| 01-C                     END
|    C*************
|    C* GLR2U0P....
| 21-C           *IN10     IFEQ '1'                        .HAS DATA
|   C                     EXSR R8900                       .LR TOTAL
| 21*C                     ELSE                            .NO DATA
|   C                     EXSR R8999                       .LR NO DATA
| 21-C                     END
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R8900 ... LEVEL BREAK LR -- HAS DATA
     C*==============================================================*
08===C           R8900     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << TOTAL LINE DATA PREPARE >>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'GLS008'
|    C                     PARM 'GLR2U0P1'P008I1 10
|    C                     PARM DAR05     P008I2  1
|    C           PSIGN1    PARM           P008O1198
|    C           PSIGN2    PARM           P008O2198
|    C*
|    C                     WRITEPT1
|    C*
08===C                     ENDSR
     C*==============================================================*
     C*          R8999 ... LEVEL BREAK LR -- NO DATA
     C*==============================================================*
09===C           R8999     BEGSR
|    C*
|    C                     WRITEPH1
|    C*
|    C                     WRITEPE9
|    C*
09===C                     ENDSR
     C*==============================================================*
** @W1
@0@1@2@3@4@5@6@7@8@9
