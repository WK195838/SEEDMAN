     A*================================================================
     A* MODI CODE | AUTHOR |  DATE  | DESCRIPTION
     A*----------------------------------------------------------------
     A*  M001     |MICHELLE|01.07.03| A&P增加控制選項
     A*           |        |        | NPND* KEY LIST CHANGED
     A*----------------------------------------------------------------
     A*  M005     |MICHELLE|01.09.26|修正費用種類及費用容量
     A*----------------------------------------------------------------
     A*  M006     |MICHELLE|01.10.30|預算金額允許輸入到百元位
     A*----------------------------------------------------------------
     A*  M007     |MICHELLE|02.03.08|代碼整合，適用於20020701之後
     A*----------------------------------------------------------------
     A*================================================================
     FNPNDPF  UF  E           K        DISK                      A
     FNPNDLF02IF  E           K        DISK
     F            ND0                               KRENAMEND0L
     FGLAKPF  IF  E           K        DISK
     FPA#APF  IF  E           K        DISK
M001AFPA#7PF  IF  E           K        DISK
M001AFPA#ALF10IF  E           K        DISK
     F            #A0                               KRENAME#A0L
      * #A01=SIZE
      *
     FPT#BPF  IF  E           K        DISK
     FNPNAPF  IF  E           K        DISK
     FMTMALF02IF  E           K        DISK
     FNPA001D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    ARR     1   1 10               TABLE
     E*---------------------------------------------------------------*
     E*檔案實績
     E                    @W01       13 13 2
     E*檔案預算
     E                    @W02       13 13 2
     E*螢幕預算
M006ME*                   @W03       13 10 0
M006ME                    @W03       13 10 2
     E*---------------------------------------------------------------*
M001AI*FIELDS同名要作變更
M001AI#A0L
     I              #A01                            L#A01
     I              #A02                            L#A02
     I              #A03                            L#A03
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*LOCAL DATA AREA
     ILDA        UDS
     I                                      101 110 $USER
     I                                      111 1160$CHYMD
     I                                      125 1270$CPY
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      152 161 $USERN
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I                                      217 218 $PRTCD
     I                                      219 219 $PENV
     I           IDS
     I                                        1 169 @W01
     I                                        1  132ND06A
     I                                       14  262ND07A
     I                                       27  392ND08A
     I                                       40  522ND09A
     I                                       53  652ND10A
     I                                       66  782ND11A
     I                                       79  912ND12A
     I                                       92 1042ND13A
     I                                      105 1172ND14A
     I                                      118 1302ND15A
     I                                      131 1432ND16A
     I                                      144 1562ND17A
     I                                      157 1692ND18A
     I           IDS
     I                                        1 169 @W02
     I                                        1  132ND06B
     I                                       14  262ND07B
     I                                       27  392ND08B
     I                                       40  522ND09B
     I                                       53  652ND10B
     I                                       66  782ND11B
     I                                       79  912ND12B
     I                                       92 1042ND13B
     I                                      105 1172ND14B
     I                                      118 1302ND15B
     I                                      131 1432ND16B
     I                                      144 1562ND17B
     I                                      157 1692ND18B
     I           IDS
     I                                        1 130 @W03
M006MI*                                       1  100DND06B
M006MI*                                      11  200DND07B
M006MI*                                      21  300DND08B
M006MI*                                      31  400DND09B
M006MI*                                      41  500DND10B
M006MI*                                      51  600DND11B
M006MI*                                      61  700DND12B
M006MI*                                      71  800DND13B
M006MI*                                      81  900DND14B
M006MI*                                      91 1000DND15B
M006MI*                                     101 1100DND16B
M006MI*                                     111 1200DND17B
M006MI*                                     121 1300DND18B
M006MI                                        1  102DND06B
M006MI                                       11  202DND07B
M006MI                                       21  302DND08B
M006MI                                       31  402DND09B
M006MI                                       41  502DND10B
M006MI                                       51  602DND11B
M006MI                                       61  702DND12B
M006MI                                       71  802DND13B
M006MI                                       81  902DND14B
M006MI                                       91 1002DND15B
M006MI                                      101 1102DND16B
M006MI                                      111 1202DND17B
M006MI                                      121 1302DND18B
     I*
     I            DS
     I                                        1   2 DND03
     I                                        1   1 DND03L
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *NAMVAR   DEFN *LDA      LDA
     C*
     C           *LIKE     DEFN ND02      WND02
     C           *LIKE     DEFN ND02      WND02C
     C*
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYNDL    KLIST                           *HEAD FILE
     C                     KFLD           WND02
     C                     KFLD           DND04
     C                     KFLD           DND05
M001AC                     KFLD           DND19
M001AC                     KFLD           DND20
     C           KEYND     KLIST                           *HEAD FILE
     C                     KFLD           DND01
     C                     KFLD           WND02
     C                     KFLD           DND03
     C                     KFLD           DND04
     C                     KFLD           DND05
M001AC                     KFLD           DND19
M001AC                     KFLD           DND20
     C           KEYNDC    KLIST                           *COPY REC.
     C                     KFLD           DND01C
     C                     KFLD           WND02C
     C                     KFLD           DND03C
     C                     KFLD           DND04C
     C                     KFLD           DND05C
M005AC                     KFLD           DND19C
M005AC                     KFLD           DND20C
     C           KEYNA     KLIST                           *HEAD FILE
     C                     KFLD           WND02
     C                     KFLD           DND05
     C           KEY#A     KLIST
     C                     KFLD           #A01
     C                     KFLD           #A02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           DDATE     PARM           P3101O  80
|    C*
|    C                     MOVE 'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     Z-ADD1         DOPT
|    C                     MOVEL*BLANK    DND01
|    C                     Z-ADD*ZERO     DND02
|    C                     MOVEL*BLANK    DND03
|    C                     MOVEL*BLANK    DND04
|    C                     MOVEL*BLANK    DND05
|    C                     MOVEL*BLANK    DND01C
|    C                     Z-ADD*ZERO     DND02C
|    C                     MOVEL*BLANK    DND03C
|    C                     MOVEL*BLANK    DND04C
|    C                     MOVEL*BLANK    DND05C
M001AC                     MOVEL*BLANK    DND19
M001AC                     MOVEL*BLANK    DND20
M005AC                     MOVEL*BLANK    DND19C
M005AC                     MOVEL*BLANK    DND20C
|    C*--------------------------------------------------------------*
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'                        *PROMPT
|   C                     EXSR R1E00
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 03-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 04-C           *IN99     IFEQ '0'                        *SCR CHK OK
|   C                     EXSR R1C00                      *PRE-SC02
|   C                     MOVEL'SC02'    SCID
| 04-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1A00     BEGSR
|    C*
|    C* AUT CHECK
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM 'NPI001'  S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C   99                GOTO E1A00
|    C*
|    C* CALL SUB-PROGRAM
|    C                     CALL 'NPI001'
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C           DND01     PARM DND01     P001I1  2        公司別
|    C           DND02     PARM DND02     P001I2  30       年度
|    C           DND03     PARM DND03     P00013  2        產品組別
|    C           DND04     PARM DND04     P001I4  2        品牌代碼
|    C           DND05     PARM DND05     P001I5  3         A&P CODE
M001AC           DND19     PARM DND19     P001I6  2        產品種類
M001AC           DND20     PARM DND20     P001I7  1        容量代碼
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     PARM           @RTNC   2
|    C* RETURN CODE CHECK
| 02-C           @RTNC     IFEQ '03'                       *PF3
|   C                     SETON                     03
| 02-C                     END
|    C*
03===C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
04===C           R1B00     BEGSR
|    C*
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*
|    C* NDDA CHECK *--------------------------------------------------*
|    C*
|    C* CHK公司別
|    C                     MOVE *BLANK    #B02
| 02-C           DND01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C**
|    C* CHK 公司別須存在公司檔(PT#BPF)且類別＝１
|    C**
|    C           DND01     CHAIN#B0                  40
| 03-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 03*C                     ELSE
| 04-C           #B12      IFNE '1'
|   C                     MOVEL'UGL0004' ERRID
|   C                     MOVEL'GLMF'    ERRF
|   C                     SETON                     6099
| 04-C                     END
| 03-C                     END
|    C   99                GOTO E1B00
|    C**
|    C* CHK年度
|    C**
| 05-C*          DND02     IFEQ 0
|   C*                    MOVEL'UPT0012' ERRID
|   C*                    MOVEL'PTMF'    ERRF
|   C*                    SETON                     6199
| 05-C*                    END
|    C*  99                GOTO E1B00
|    C* CHG年度
|    C           DND01     CHAINAK0                  40
|    C*
|    C*年制為１－西元年，日期格式＝１　ＹＹＭＭＤＤ
|    C*年制為２－中國年，日期格式＝２　ＹＹＹＹＭＭＤＤ
|    C           DND02     MULT 10000     P3101I
|    C                     CALL 'P31'
|    C                     PARM           P3101I  80
|    C                     PARM AK09      P3102I  1
|    C                     PARM AK09      P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C                     PARM           P3101O  80
| 06-C           P3101O    IFNE 0
|   C           P3101O    DIV  10000     WND02
| 06*C                     ELSE
|   C                     Z-ADD0         WND02
| 06-C                     END
|    C*
|    C           DND02C    MULT 10000     P3101I
|    C                     CALL 'P31'
|    C                     PARM           P3101I  80
|    C                     PARM AK09      P3102I  1
|    C                     PARM AK09      P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C                     PARM           P3101O  80
| 07-C           P3101O    IFNE 0
|   C           P3101O    DIV  10000     WND02C
| 07*C                     ELSE
|   C                     Z-ADD0         WND02C
| 07-C                     END
|    C*
|    C**
|    C* CHK產品組別
|    C**
|    C                     MOVEL*BLANKS   PND03
| 08-C           DND03     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 08-C                     END
|    C   99                GOTO E1B00
      *
| 09-C           DND03     IFEQ '10'
|   C                     MOVEL'白蘭地'PND03
| 09*C                     ELSE
| 10-C           DND03     IFEQ '20'
|   C                     MOVEL'威士忌'PND03
| 10*C                     ELSE
| 11-C           DND03     IFEQ '30'
|   C                     MOVEL'葡萄酒'PND03
| 11*C                     ELSE
|   C                     MOVEL'其他　'PND03
| 11-C                     END
| 10-C                     END
| 09-C                     END
|    C**
|    C* CHK品牌
|    C**
|    C                     MOVE *BLANK    #A03
| 12-C           DND04     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 12*C                     ELSE
| 13-C           DND04     IFEQ '99'
|   C                     MOVELARR,1     #A03
| 13*C                     ELSE
|   C                     MOVEL'BRAND'   #A01
|   C                     MOVELDND04     #A02
|   C           KEY#A     CHAIN#A0                  40
| 14-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 14-C                     END
| 13-C                     END
| 12-C                     END
|    C   99                GOTO E1B00
|    C**
M001AC* CHK種類
|    C**
|   C                     MOVEL*BLANKS   W10    10
| 12-C           DND19     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6699
| 12*C                     ELSE
|   C                     MOVELDND19     W10
|   C           W10       CHAIN#70                  40
| 14-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6699
| 14-C                     END
| 12-C                     END
M001AC   99                GOTO E1B00
|    C**
M001AC* CHK容量
|    C**
|   C                     MOVEL*BLANKS   W10    10
| 12-C           DND20     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6799
| 12*C                     ELSE
|   C                     MOVELDND20     W10
|   C           W10       CHAIN#A0L                 40
| 14-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6799
| 14-C                     END
| 12-C                     END
M001AC   99                GOTO E1B00
|    C**
|    C* CHK A&P CODE
|    C**
|    C                     MOVE *BLANK    NA03
| 15-C           DND05     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6499
| 15*C                     ELSE
|   C           KEYNA     CHAINNA0                  40
| 16-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     616499
| 16*C                     ELSE
| 17-C*M007M     NA04      IFNE 'D'
M007MC           NA04      IFEQ 'T'
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6499
| 17-C                     END
| 16-C                     END
| 15-C                     END
|    C   99                GOTO E1B00
|    C*
|    C           KEYND     CHAINND0                  40
|    C*
| 18-C           DOPT      IFEQ 1                          *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     636499
M001AC                     SETON                     6667
| 18-C                     END
|    C   99                GOTO E1B00
|    C*
| 19-C           DOPT      IFNE 1                          *UPD,DEL,INQ
|   C           *IN40     ANDEQ'1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606162
|   C                     SETON                     636499
M001AC                     SETON                     6667
| 19-C                     END
|    C   99                GOTO E1B00
|    C*
| 20-C           DOPT      IFEQ 1                          *ADD
| 21-C           DND01C    IFNE *BLANK                     *COPY
|   C           DND02C    ORNE *ZEROS
|   C           DND03C    ORNE *BLANK
|   C           DND04C    ORNE *BLANK
|   C           DND05C    ORNE *BLANK
M005AC           DND19C    ORNE *BLANK
M005AC           DND20C    ORNE *BLANK
|   C*
|   C           KEYNDC    CHAINND0                  40
| 22-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT0060' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6599
| 22-C                     END
|   C*
| 21-C                     END
| 20-C                     END
|    C   99                GOTO E1B00
|    C*
|    C* GET會計年度月數
| 23-C           AK10      IFEQ '1'
|   C                     Z-ADD12        MONTH   30
|   C                     SETON                     59
| 23*C                     ELSE
|   C                     Z-ADD13        MONTH
|   C                     SETOF                     59
| 23-C                     END
|    C   99                GOTO E1B00
|    C*
      *新增時年度＋品牌不可重覆
      *新增時年度＋品牌＋A&P CODE＋產品種類＋容量代碼不可重覆
| 24-C           DOPT      IFEQ 1
|   C           DND04     ANDNE'99'
|   C           KEYNDL    SETLLND0L                     40
| 25-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     616364
M001AC                     SETON                     6667
|   C                     SETON                     99
| 25-C                     END
| 24-C                     END
|    C   99                GOTO E1B00
|    C*
      *品牌＋組別不可矛盾
| 26-C           DND04     IFNE '99'
|   C                     SETON                     626399
|   C                     MOVEL'UPC0004' ERRID
|   C                     MOVEL'PCMF'    ERRF
|   C           DND04     SETLLMA0
|   C           DND04     READEMA0                      40
| 27-C           *IN40     DOWEQ'0'
| 28-C           MA11      IFNE *BLANK
|   C                     MOVELMA11      WFLD2   1
| 29-C           WFLD2     IFEQ DND03L
|   C                     SETOF                     626399
|   C                     SETON                     40
| 29-C                     END
| 28-C                     END
|   C  N40      DND04     READEMA0                      40
| 27-C                     END
| 26-C                     END
|   C   99                GOTO E1B00
      *
04===C           E1B00     ENDSR
    C*==============================================================*
    C*          R1C00 ....INIT 'SC02'
    C*==============================================================*
05===C           R1C00     BEGSR
|   C*
| 01-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     SETON                     31    *CAN INPUT
| 01*C                     ELSE                            *DEL, INQ
|   C                     SETOF                     31    *INP PROTECT
| 01-C                     END
|   C*
| 02-C           DOPT      IFEQ 1                          *ADD
|   C           DND01C    ANDEQ*BLANK                     *NO COPY
|   C           DND02C    ANDEQ0                          *NO COPY
|   C           DND03C    ANDEQ*BLANK                     *NO COPY
|   C           DND04C    ANDEQ*BLANK                     *NO COPY
|   C           DND05C    ANDEQ*BLANK                     *NO COPY
M005AC           DND19C    ANDEQ*BLANK                     *NO COPY
M005AC           DND20C    ANDEQ*BLANK                     *NO COPY
|   C                     EXSR R1C10
| 02*C                     ELSE                            *U/D/I
|   C                     EXSR R1C20
| 02-C                     END
|   C*
|   C* RELEASE FILE (ADD & INQUIRY)----------------------------------*
| 03-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 5                          *INQ
|   C           *LOVAL    SETLLND0
| 03-C                     END
|   C*---------------------------------------------------------------*
|   C*
05===C                     ENDSR
    C*==============================================================*
    C*          R1C10 ....'SC02' FIELD INIT : ADD (NO COPY)
    C*==============================================================*
06===C           R1C10     BEGSR
|   C*
|   C                     Z-ADD0         @W03
|   C                     Z-ADD0         DTOT1
|   C*
06===C                     ENDSR
    C*==============================================================*
    C*  R1C20 ....'SC02' FIELD INIT : UPD, DEL, INQ, (ADD & COPY)
    C*==============================================================*
07===C           R1C20     BEGSR
|   C*
|   C                     Z-ADD0         @W03
|   C                     Z-ADD0         DTOT1
|   C                     Z-ADD1         I       30
| 01-C                     DO   MONTH     I
     C           @W02,I    DIV  1000      @W03,I
     C                     ADD  @W03,I    DTOT1
| 01-C                     END
|   C*
|   C*
07===C                     ENDSR
    C*==============================================================*
    C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
    C*==============================================================*
08===C           R1E00     BEGSR
|   C*
|   C           #CSR      DIV  256       #LIN             *LINE
|   C                     MVR            #COL             *COLUMN
|   C*品牌
| 01-C           #LIN      IFEQ 4
|   C           #COL      ANDGE46
|   C           #COL      ANDLE47
|   C                     CALL 'WDS012'
|   C                     PARM 'BRAND   'PARMI1  8
|   C           DND04     PARM DND04     PARMO1 10
| 01-C                     END
M001MC*原第５行以下，改為第６行，以此類推
| 02-C           #LIN      IFEQ 6
|   C           #COL      ANDGE38
|   C           #COL      ANDLE39
|   C                     CALL 'WDS012'
|   C                     PARM 'BRAND   'PARMI1  8
|   C           DND04C    PARM DND04C    PARMO1 10
| 02-C                     END
M001A *費用種類查詢
| 03-C           #LIN      IFEQ 5
|   C           #COL      ANDGE14
|   C           #COL      ANDLE15
|   C                     CALL 'WDS039'
|   C           DND19     PARM DND19     PARMX1  2
| 03-C                     END
M001A *
M005AC           #LIN      IFEQ 6
M005AC           #COL      ANDGE41
M005AC           #COL      ANDLE42
M005AC                     CALL 'WDS039'
M005AC           DND19     PARM DND19     PARMX1  2
M005AC                     END
M001A *費用容量代碼查詢
| 03-C           #LIN      IFEQ 5
|   C           #COL      ANDEQ46
|   C                     CALL 'WDS040'
|   C           DND20     PARM DND20     PARM71  1
| 03-C                     END
M001A *
M005AC           #LIN      IFEQ 6
M005AC           #COL      ANDEQ44
M005AC                     CALL 'WDS040'
M005AC           DND20     PARM DND20     PARM71  1
M005AC                     END
|   C*A&P CODE
M001MC           #LIN      IFEQ 6
     C           #COL      ANDGE14
     C           #COL      ANDLE16
|   C                     CALL 'NPW001'
|   C           DND02     PARM DND02     PARMW1  30
|   C           DND05     PARM DND05     PARMW2  3
| 03-C                     END
      *
M001MC           #LIN      IFEQ 6
M005MC*          #COL      ANDGE41
M005MC*          #COL      ANDLE43
M005MC           #COL      ANDGE46
M005MC           #COL      ANDLE49
|   C                     CALL 'NPW001'
|   C                     PARM DND02C    PARMW1  30
|   C           DND05C    PARM DND05C    PARMW2  3
| 04-C                     END
      *
08===C                     ENDSR
    C*==============================================================*
    C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
    C*==============================================================*
09===C           R2000     BEGSR
|   C*
|   C                     MOVE 'SC02'    SCID
|   C*
| 01-C           *IN03     DOWEQ'0'
|   C           SCID      ANDEQ'SC02'
|   C*
|   C                     EXFMTDSPD2
|   C                     MOVEA*ALL'0'   *IN,60
|   C*
| 02-C           *IN03     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C*
| 03-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     EXSR R2B00                      *SC02 CHK
| 03-C                     END
|   C*
| 04-C           *IN99     IFEQ '0'                        *CHK OK
|   C           *IN05     ANDEQ'0'
|   C                     EXSR R2D00                      *FILE UPD
|   C                     MOVE 'SC01'    SCID
| 04-C                     END
|   C*
| 02*C                     ELSE
|   C                     MOVE 'SC01'    SCID
| 02-C                     END
| 01-C                     END
|   C*
09===C           E2000     ENDSR
    C*==============================================================*
    C*          R2B00 .... 'SC02' CHECK
    C*==============================================================*
10===C           R2B00     BEGSR
|   C*
|   C                     Z-ADD0         DTOT1
|   C                     Z-ADD1         I
| 01-C                     DO   MONTH     I
     C                     ADD  @W03,I    DTOT1
| 01-C                     END
|   C*
10===C           E2B00     ENDSR
    C*==============================================================*
    C*          R2D00 ....FILE PROCESS
    C*==============================================================*
11===C           R2D00     BEGSR
|   C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D20
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|   C*
11===C                     ENDSR
    C*==============================================================*
    C*          R2D10 ....FILE PROCESS : ADD
    C*==============================================================*
12===C           R2D10     BEGSR
|   C*
|   C                     Z-ADD0         @W01
|   C                     EXSR R2D11
|   C                     WRITEND0
|   C*
12===C                     ENDSR
    C*==============================================================*
    C*          R2D11 ....SCREEN FIELD TO FILE
    C*==============================================================*
13===C           R2D11     BEGSR
|   C*
|   C                     MOVELDND01     ND01
|   C                     Z-ADDWND02     ND02
|   C                     MOVELDND03     ND03
|   C                     MOVELDND04     ND04
|   C                     MOVELDND05     ND05
|   C                     MOVELDND05     ND051
M001AC                     MOVELDND19     ND19
M001AC                     MOVELDND20     ND20
|   C                     Z-ADD1         I       30
| 01-C                     DO   MONTH     I
     C           @W03,I    MULT 1000      @W02,I
| 01-C                     END
|   C                     TIME           ND97
|   C                     Z-ADD$A8YMD    ND98
|   C                     MOVEL$USER     ND99
|   C*
13===C                     ENDSR
    C*==============================================================*
    C*          R2D20 ....FILE PROCESS : UPD
    C*==============================================================*
14===C           R2D20     BEGSR
|   C*
|   C                     EXSR R2D11
|   C                     UPDATND0
|   C*
14===C                     ENDSR
    C*==============================================================*
    C*          R2D30 ....FILE PROCESS
    C*==============================================================*
15===C           R2D30     BEGSR
|   C*
|   C                     DELETND0
|   C*
15===C                     ENDSR
**
合併品牌
