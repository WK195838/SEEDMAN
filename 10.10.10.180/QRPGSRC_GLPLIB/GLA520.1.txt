     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : GLA520                                     *
     H*    PROGRAM NAME :年底結轉　　　　                          *
     H*    AUTHOR       : A1150                                      *
     H*    CREATE DATE  : 81/12/07                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :                                            *
     H****************************************************************
     FGLAILF04IF  E           K        DISK
     F            AI0                               KRENAMEAIL4
     FGLAILF01UF  E           K        DISK                      A
     F            AI0                               KRENAMEAIL1
     FGLAKPF  UF  E           K        DISK
     FGLACPF  IF  E           K        DISK
     FGLAFPF  IF  E           K        DISK
     FGLA520D CF  E                    WORKSTN
     E*---------------------------------------------------------------*
     E                    AR1        13  1
     E                    AR2        13 13 2
     E*---------------------------------------------------------------*
     I*
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      152 161 $USERN
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I            DS
     I                                        1  13 AR1
     I                                        1   1 AC03
     I                                        2   2 AC04
     I                                        3   3 AC05
     I                                        4   4 AC06
     I                                        5   5 AC07
     I                                        6   6 AC08
     I                                        7   7 AC09
     I                                        8   8 AC10
     I                                        9   9 AC11
     I                                       10  10 AC12
     I                                       11  11 AC13
     I                                       12  12 AC14
     I                                       13  13 AC15
     I            DS
     I                                        1 169 AR2
     I                                        1  132AI11
     I                                       14  262AI12
     I                                       27  392AI13
     I                                       40  522AI14
     I                                       53  652AI15
     I                                       66  782AI16
     I                                       79  912AI17
     I                                       92 1042AI18
     I                                      105 1172AI19
     I                                      118 1302AI20
     I                                      131 1432AI21
     I                                      144 1562AI22
     I                                      157 1692AI23
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *LIKE     DEFN AI10      WAI10
     C           *LIKE     DEFN AI10      WTEMP
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYAK     KLIST
     C                     KFLD           DAI01            *公司別
     C           KEYAC     KLIST
     C                     KFLD           DAI01
     C                     KFLD           WYR1    40       *結轉年度
     C           KEYAF     KLIST
     C                     KFLD           DAI01
     C                     KFLD           AI03             *會計科目
     C*GLAILF01...
     C           KEYAI     KLIST
     C                     KFLD           DAI01
     C                     KFLD           WYR2
     C                     KFLD           AI03
     C                     KFLD           AI04A
     C                     KFLD           AI05A
     C                     KFLD           AI06A
     C                     KFLD           AI07A
     C                     KFLD           AI08A
     C                     KFLD           AI04
     C                     KFLD           AI05
     C                     KFLD           AI06
     C                     KFLD           AI07
     C                     KFLD           AI08
     C           KEYU1     KLIST
     C                     KFLD           DAI01
     C                     KFLD           WYR2    40       * WYR1+1
     C                     KFLD           AK05             *本期損益
     C           KEYU2     KLIST
     C                     KFLD           DAI01
     C                     KFLD           WYR2             * WYR1+1
     C                     KFLD           AK04             *累積盈虧
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT VALUE
     C*
  01-C           *IN03     DOUEQ'1'
    C                     EXSR R1000                      *SCRN
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....SCREEN INIT VALUE SET
     C*==============================================================*
01===CSR         R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           DDATE     PARM           P3101O  80
|    C*
|    C                     MOVEL*BLANK    DAI01
|    C                     Z-ADD0         DAI02
|    C                     Z-ADD0         DAK13
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
01===C                     ENDSR
     C*===============================================================*
     C*          R1000  ...SCREEN MAIN PROCESS
     C*===============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'
|   C                     GOTO E1000
| 01-C                     END
|    C*
|    C                     EXSR R1B00                      .CHECK      ?
|    C*
|    C*ADD & UPDAT GLAILF01.   /AND/   UPDAT GLAKPF
|    C*
| 02-C           *IN14     IFEQ '1'
|   C           *IN99     ANDEQ*ZERO
|   C                     EXSR R2A00
| 02-C                     END
|    C*
02===C           E1000     ENDSR
     C*===============================================================*
     C*          R1B00  ...CHECK DATA FROM SCREEN KEY-IN
     C*===============================================================*
03===C           R1B00     BEGSR
|    C*
|    C* CHK FUN AUT
|    C*
| 01-C           $ADD      IFNE 'Y'
|   C           $UPD      ORNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*+<<DATA CHECK>>+++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD0         DAK13
|    C*公司別------
| 02-C           DAI01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|    C           KEYAK     CHAINAK0                  40
| 03-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 03-C                     END
|    C   99                GOTO E1B00
|    C************
|    C*上次年結
|    C************
|    C*
| 04-C           AK13      IFNE 0
|   C           AK13      MULT 10000     P3101I
|   C                     CALL 'P31'
|   C                     PARM           P3101I  80
|   C                     PARM '2'       P3102I  1
|   C                     PARM '1'       P3103I  1
|   C                     PARM AK09      P3104I  1
|   C                     PARM AK09      P3105I  1
|   C                     PARM           P3101O  80
|   C           P3101O    DIV  10000     DAK13
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
|    C************
|    C*結轉年度
|    C************
|    C*******************
|    C*轉換日期格式****
|    C*******************
|    C           DAI02     MULT 10000     P3101I
|    C                     CALL 'P31'
|    C                     PARM           P3101I  80
|    C                     PARM AK09      P3102I  1
|    C                     PARM AK09      P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C                     PARM           P3101O  80
|    C           P3101O    DIV  10000     WYR1    40
|    C*
|    C******************************************************
|    C*（會計期間）之結帳碼(AC03-AC15)皆須為Ｙ----
|    C******************************************************
|    C           KEYAC     CHAINAC0                  40
| 05-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     616299
| 05-C                     END
|    C   99                GOTO E1B00
|    C*
| 06-C           1         DO   13        A       20
| 07-C           AR1,A     IFNE 'Y'
|   C                     MOVEL'UGL0036' ERRID
|   C                     MOVEL'GLMF'    ERRF
|   C                     SETON                     6299
|   C                     Z-ADD13        A
| 07-C                     END
| 06-C                     END  1
|    C   99                GOTO E1B00
|    C*
|    C************************
|    C* AK13 不等於0時
|    C*結轉年度須等於AK13+1
|    C************************
|    C                     Z-ADD0         WTMP1
|    C*
| 08-C           AK13      IFNE 0
|   C           AK13      ADD  1         WTMP1   40
|   C*
| 09-C           WYR1      IFNE WTMP1
|   C                     MOVEL'UGL0033' ERRID
|   C                     MOVEL'GLMF'    ERRF
|   C                     SETON                     6299
| 09-C                     END
| 08-C                     END
|    C*
03===CSR         E1B00     ENDSR
     C*==============================================================*
     C*          R2A00 ....PF14  ON
     C*==============================================================*
04===C           R2A00     BEGSR
|    C**
|    C                     MOVEL'UPT2130' ERRID
|    C                     MOVEL'PTMF'    ERRF
|    C                     SETON                     9899
|    C                     WRITEDSPD1
|    C**
|    C           WYR1      ADD  1         WYR2
|    C*
|    C           KEYAC     SETLLAIL4
|    C           KEYAC     READEAIL4                     44
| 01-C           *IN44     DOWEQ'0'
|   C******************************
|   C*GET科目類別(AF05)         *
|   C*若AF05='1'實帳戶        *
|   C*新增／更新一筆至年度餘額檔*
|   C******************************
|   C           KEYAF     CHAINAF0                  40
| 02-C           *IN40     IFEQ '0'
|   C           AF05      ANDEQ'1'                        *實帳戶
|   C*
|   C                     EXSR R2A10
| 02-C                     END
|   C           KEYAC     READEAIL4                     44
| 01-C                     END
|    C***************************
|    C*更新年度餘額檔GLAILF01 --KEYU1
|    C***************************
|    C           KEYU1     CHAINAIL1                 40
| 03-C           *IN40     IFEQ '0'
|   C                     EXSR R2A20
| 03-C                     END
|    C***************************
|    C*更新年度餘額檔GLAILF01 --KEYU2
|    C***************************
|    C           KEYU2     CHAINAIL1                 40
| 04-C           *IN40     IFEQ '0'
|   C                     EXSR R2A30
| 04-C                     END
|    C***************************
|    C*更新總帳控制檔GLAKPF
|    C***************************
|    C                     Z-ADDWYR1      AK13
|    C                     UPDATAK0
|    C*
|    C                     MOVEL'UPT2142' ERRID
|    C                     MOVEL'PTMF'    ERRF
|    C                     SETOF                     98
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R2A10 ....新增年度餘額檔
     C*==============================================================*
05===C           R2A10     BEGSR
|    C*
|    C*計算上期餘額AI10
|    C*
|    C                     Z-ADD0         WAI10
| 01-C           1         DO   13        B       20
|   C                     ADD  AR2,B     WAI10
| 01-C                     END  1
     C*ADD 1 LINE 8209
|   C                     ADD  AI10      WAI10
|    C****************************************************************
|    C*若AF06='1'借方科目時，上期借方(AI10A)=新上期餘額(AI10)
|    C*而上期貸方(AI10B)=0
|    C*否則上期借方(AI10A)=0
|    C*而上期貸方(AI10B)=新上期餘額(AI10)
|    C****************************************************************
|    C*
|    C           KEYAI     CHAINAIL1                 40
|    C*
|    C                     Z-ADDWAI10     AI10
| 02-C           AF06      IFEQ '1'                        *借　方
|   C                     Z-ADDAI10      AI10A
|   C                     Z-ADD0         AI10B
| 02*C                     ELSE
|   C                     Z-ADD0         AI10A
|   C                     Z-ADDAI10      AI10B
| 02-C                     END
|    C*
|    C                     TIME           AI97
|    C                     Z-ADD$A8YMD    AI98
|    C                     MOVEL$USER     AI99
|    C*
| 03-C           *IN40     IFEQ '0'
|   C                     UPDATAIL1
| 03*C                     ELSE
|   C                     Z-ADDWYR2      AI02
|   C*
|   C                     Z-ADD0         AI11
|   C                     Z-ADD0         AI11A
|   C                     Z-ADD0         AI11B
|   C*
|   C                     Z-ADD0         AI12
|   C                     Z-ADD0         AI12A
|   C                     Z-ADD0         AI12B
|   C*
|   C                     Z-ADD0         AI13
|   C                     Z-ADD0         AI13A
|   C                     Z-ADD0         AI13B
|   C*
|   C                     Z-ADD0         AI14
|   C                     Z-ADD0         AI14A
|   C                     Z-ADD0         AI14B
|   C*
|   C                     Z-ADD0         AI15
|   C                     Z-ADD0         AI15A
|   C                     Z-ADD0         AI15B
|   C*
|   C                     Z-ADD0         AI16
|   C                     Z-ADD0         AI16A
|   C                     Z-ADD0         AI16B
|   C*
|   C                     Z-ADD0         AI17
|   C                     Z-ADD0         AI17A
|   C                     Z-ADD0         AI17B
|   C*
|   C                     Z-ADD0         AI18
|   C                     Z-ADD0         AI18A
|   C                     Z-ADD0         AI18B
|   C*
|   C                     Z-ADD0         AI19
|   C                     Z-ADD0         AI19A
|   C                     Z-ADD0         AI19B
|   C*
|   C                     Z-ADD0         AI20
|   C                     Z-ADD0         AI20A
|   C                     Z-ADD0         AI20B
|   C*
|   C                     Z-ADD0         AI21
|   C                     Z-ADD0         AI21A
|   C                     Z-ADD0         AI21B
|   C*
|   C                     Z-ADD0         AI22
|   C                     Z-ADD0         AI22A
|   C                     Z-ADD0         AI22B
|   C*
|   C                     Z-ADD0         AI23
|   C                     Z-ADD0         AI23A
|   C                     Z-ADD0         AI23B
|   C*
|   C                     WRITEAIL1
| 03-C                     END
|    C*
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R2A20 ....更新年度餘額檔
     C*==============================================================*
06===C           R2A20     BEGSR
|    C*
|    C*                    SUB  AI10      AI11             *１期餘額
|    C*                    ADD  AI10      AI11A
|    C                     Z-ADDAI10      WTEMP
|    C                     Z-ADD0         AI10
|    C                     Z-ADD0         AI10A
|    C                     Z-ADD0         AI10B
|    C                     TIME           AI97
|    C                     Z-ADD$A8YMD    AI98
|    C                     MOVEL$USER     AI99
|    C*
|    C                     UPDATAIL1
|    C*                    Z-ADDAI10      WTEMP
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R2A30 ....更新年度餘額檔
     C*==============================================================*
07===C           R2A30     BEGSR
|    C*
|    C*                    ADD  WTEMP     AI11             *１期餘額
|    C*                    ADD  WTEMP     AI11B            *１期貸方
|    C                     ADD  WTEMP     AI10             *１期餘額
|    C                     ADD  WTEMP     AI10B            *１期貸方
|    C                     TIME           AI97
|    C                     Z-ADD$A8YMD    AI98
|    C                     MOVEL$USER     AI99
|    C*
|    C                     UPDATAIL1
|    C*
07===C                     ENDSR
     C*==============================================================*
