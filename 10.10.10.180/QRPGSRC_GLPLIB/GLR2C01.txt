     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLR2C01                                    *
     A*    PROGRAM NAME : 現金流量表                               *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1993.01.09                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     FGLWF03  UF  E           K        DISK                      A
     FGLASLF02IF  E           K        DISK
     F            AS0                               KRENAMEASLF02
     FGLASLF03IF  E           K        DISK
     F            AS0                               KRENAMEASLF03
     FGLAHLF07IF  E           K        DISK
     FGLAXPF  IF  E           K        DISK
     FGLAZPF  IF  E           K        DISK
     FPT#APF  IF  E           K        DISK
     FPT#BPF  IF  E           K        DISK
     FGLR2C0P1O   E             39     PRINTER
     E*---------------------------------------------------------------
     E                    @W1    10  10  2
     E*---------------------------------------------------------------*
     I*
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     IGLDA01      DS
     I                                        1  16 DA01
     I*
     ILDA        UDS
     I                                      101 110 $USER
     I                                      125 1270$CPY
     I                                      138 138 $INQ
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I                                      217 218 $PRTCD
     I                                      219 219 $PENV
     I* SCREEN INPUT FIELD  ++++++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DAH01
     I                                      503 508 DAH05
     I                                      509 5150DAH10S
     I                                      516 5220DAH10E
     I                                      523 525 DAR03
     I**
     I                                      533 5400WAH10S
     I                                      541 5480WAH10E
     I                                      549 549 DAR05
     I                                      550 5530DAI02
     I                                      554 554 #B12
     I                                      555 555 W#A10
     I                                      556 557 DAH01S
     I                                      558 559 DAH01E
     I                                      561 580 DAH05S
     I                                      581 600 DAH05E
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *NAMVAR   DEFN           GLDA01
     C           *NAMVAR   DEFN *LDA      LDA
     C           *LIKE     DEFN AH01      WAI01
     C           *LIKE     DEFN #A01      WAH05
     C           *LIKE     DEFN AS04      WAS04            *BREAK
     C           *LIKE     DEFN AS06      WAS06
     C           *LIKE     DEFN AS08      WAS08
     C           *LIKE     DEFN AS10      WAS10
     C           *LIKE     DEFN AS02      WAR02            *KEY
     C           *LIKE     DEFN AH12      WCASH1           *小計金額
     C           *LIKE     DEFN AH12      WCASH2           *合計金額
     C           *LIKE     DEFN AH12      WCH1             *小計金額
     C           *LIKE     DEFN AH12      WCH2             *合計金額
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEY#B     KLIST
     C                     KFLD           DAH01            *公司別
     C           KEYAS     KLIST
     C                     KFLD           DAH01            *公司別
     C                     KFLD           WAR02            *報表類別
     C                     KFLD           DAR03            *報表代碼
     C           KEYAX     KLIST
     C                     KFLD           DAH01            *公司別S
     C                     KFLD           AH01             *公司別F
     C           KEYAZ     KLIST
     C                     KFLD           DAH01
     C                     KFLD           DAH05
     C                     KFLD           WAH05
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      .INIT-HEAD
     C*
     C                     MOVEL'40'      WAR02
     C                     EXSR R1000                      .WROK-FILE
     C*
     C                     EXSR R8000                      .PRINT
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ... HEADING INITIAL
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C                     IN   GLDA01
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           PDATE     PARM           P3101O  80
|    C**
| 01-C           #B12      IFEQ '1'
|   C                     MOVELDAH01     WAI01
| 01*C                     ELSE
|   C****************************
|   C*公司別屬虛擬            *
|   C*須CHECK公司結構明細檔 *
|   C****************************
|   C           DAH01     CHAINAX0                  40
|   C  N40                MOVELAX02      WAI01
| 01-C                     END
|    C**************
|    C*<<公司別 >>
|    C                     MOVEL*BLANKS   P#B16
|    C           KEY#B     CHAIN#B0                  40
| 02-C           *IN40     IFEQ '0'
| 03-C           DAR05     IFEQ '1'
|   C                     MOVEL#B16      P#B16
| 03*C                     ELSE
|   C                     MOVEL#B17      P#B16
| 03-C                     END
| 02-C                     END
|    C***************
|    C*<<成本中心>>
| 04-C           DAH05     IFNE *BLANK
|   C                     SETON                     30
|   C                     MOVEL*BLANKS   P#A02
|   C           DAH05     CHAIN#A0                  40
| 05-C           *IN40     IFEQ '0'
| 06-C           DAR05     IFEQ '1'
|   C                     MOVEL#A02      P#A02
| 06*C                     ELSE
|   C                     MOVEL#A03      P#A02
| 06-C                     END
| 05-C                     END
| 04-C                     END
|    C**************
|    C*<< *DTAARA >>
|    C                     CALL 'GLS005'
|    C                     PARM WAI01     P005I1  2
|    C                     PARM WAH10E    P005I2  80
|    C                     PARM 'F'       P005I3  1
|    C                     PARM           P005O1  40
|    C                     PARM           P005O2  20
|    C                     PARM           P005O3  1
|    C                     PARM           P005O4  1
|    C*
| 07-C           P005O4    IFNE 'Y'
| 08-C           DAR05     IFEQ '1'
|   C                     MOVELDA01      PDA01
| 08*C                     ELSE
|   C                     MOVE DA01      PDA01
| 08-C                     END
| 07-C                     END
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ... GET DATA TO WORK FILE
     C*==============================================================*
02===C           R1000     BEGSR
|    C**************************
|    C* 逐筆讀取GLASLF02,    *
|    C* 只取AS06為會計科目,*
|    C* 存入GLWF11.          *
|    C**************************
|    C                     Z-ADD*ZERO     WCASH1
|    C                     Z-ADD*ZERO     WCASH2
|    C*
|    C           KEYAS     SETLLASLF02
|    C           KEYAS     READEASLF02                   42
|    C   42                MOVEL*BLANK    WAS04
|    C  N42                MOVELAS04      WAS04
|    C                     MOVELAS06      WAS06
|    C                     MOVELAS08      WAS08
|    C                     MOVELAS10      WAS10
| 01-C           *IN42     DOWEQ'0'                        .EOF
|   C*
| 02-C           AS04      IFNE WAS04
|   C                     EXSR R2100                      *註一
|   C                     WRITEGLWF03R
|   C*
|   C                     MOVELAS04      WAS04
|   C                     MOVELAS06      WAS06
|   C                     MOVELAS08      WAS08
|   C                     MOVELAS10      WAS10
| 02-C                     END
|   C*
|   C                     EXSR R2A00                      .DETAIL
|   C*
|   C           KEYAS     READEASLF02                   42
| 01-C                     END
|    C* WRITE最後一筆
| 03-C           WAS04     IFNE *BLANKS
|   C                     EXSR R2100
|   C                     WRITEGLWF03R
| 03-C                     END
|    C**************************
|    C* 逐筆讀取GLASLF03,    *
|    C* 只取AS06為@------  *
|    C* 存入GLWF11.          *
|    C**************************
|    C                     Z-ADD*ZERO     WCASH1           *小計金額
|    C                     Z-ADD*ZERO     WCASH2           *合計金額
|    C*
|    C           KEYAS     SETLLASLF03
|    C           KEYAS     READEASLF03                   42
|    C   42                MOVEL*BLANK    WAS04
|    C  N42                MOVELAS04      WAS04
|    C                     MOVELAS06      WAS06
|    C                     MOVELAS08      WAS08
|    C                     MOVELAS10      WAS10
|    C*
| 04-C           *IN42     DOWEQ'0'                        .EOF
|   C*
| 05-C           AS04      IFNE WAS04
|   C           WAS04     CHAINGLWF03R              40
|   C*
|   C                     EXSR R2100
|   C*
|   C   40                WRITEGLWF03R
|   C  N40                UPDATGLWF03R
|   C*
|   C                     MOVELAS04      WAS04
|   C                     MOVELAS06      WAS06
|   C                     MOVELAS08      WAS08
|   C                     MOVELAS10      WAS10
|   C                     Z-ADD*ZERO     WCASH1           *小計金額
|   C                     Z-ADD*ZERO     WCASH2           *合計金額
| 05-C                     END
|   C*
|   C                     EXSR R2B00                      .DETAIL
|   C*
|   C           KEYAS     READEASLF03                   42
| 04-C                     END
|    C* WRITE最後一筆
| 06-C           WAS04     IFNE *BLANKS
|   C                     EXSR R2100
|   C                     WRITEGLWF03R
| 06-C                     END
|    C*
02===C                     ENDSR
     C*==============================================================*
     C*          R2A00 ... GLASLF02 DETAIL
     C*==============================================================*
03===C           R2A00     BEGSR
|    C*****************
|    C* 會計傳票檔  *
|    C*****************
|    C                     Z-ADD*ZEROS    WCH1
|    C                     Z-ADD*ZEROS    WCH2
|    C*
|    C           *LOVAL    SETLLAH0
|    C                     READ AH0                      46
| 01-C           *IN46     DOWEQ'0'
|   C*
| 02-C           AH22      IFGE AS06
|   C           AH22      ANDLEAS07
|   C**********************
|   C* AH01公司別屬虛擬 *
|   C**********************
| 03-C           #B12      IFEQ '2'
|   C           KEYAX     SETLLAX0                      42
|   C  N42                GOTO TAG#1
| 03-C                     END
|   C************************
|   C* AH01成本中心屬虛擬 *
|   C************************
| 04-C           #A10      IFEQ '2'
|   C                     MOVELAH05      WAH05
|   C           KEYAZ     SETLLAZ0                      42
|   C  N42                GOTO TAG#1
| 04-C                     END
|   C*
| 05-C           AS08      IFEQ *BLANK
|   C                     ADD  AH23      WCH1             *小計金額
| 05*C                     ELSE
|   C                     ADD  AH23      WCH2             *合計金額
| 05-C                     END
|   C*
| 02-C                     END
|   C*
|   C           TAG#1     TAG
|   C*
|   C                     READ AH0                      46
| 01-C                     END
|    C*
|    C                     MULT AS05      WCH1
|    C                     MULT AS05      WCH2
|    C                     ADD  WCH1      WCASH1           *小計金額
|    C                     ADD  WCH2      WCASH2           *合計金額
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          R2B00 ... GLASLF03 DETAIL
     C*==============================================================*
04===C           R2B00     BEGSR
|    C**
|    C           2         SUBSTAS06      @AS06   2
|    C           @AS06     LOKUP@W1                      49
| 01-C           *IN49     IFEQ '1'                        * @0-@9
|   C           4         SUBSTAS06:2    WF301S  4
|   C           4         SUBSTAS07:2    WF301E  4
|   C***********
|   C*  GLWF03 *
|   C***********
|   C           WF301S    SETLLGLWF03R
|   C                     READ GLWF03R                  46
|   C*
| 02-C           *IN46     DOWEQ'0'
|   C           WF0301    ANDLEWF301E
|   C*
|   C                     MULT AS05      WF0304
|   C                     MULT AS05      WF0305
|   C**************************
|   C*計算＝小計金額 WCASH1 *
|   C*計算＝合計金額 WCASH2 *
|   C**************************
| 03-C           AS08      IFEQ *BLANK
|   C                     ADD  WF0304    WCASH1           *小計金額
|   C                     ADD  WF0305    WCASH1           *小計金額
| 03*C                     ELSE
|   C                     ADD  WF0304    WCASH2           *合計金額
|   C                     ADD  WF0305    WCASH2           *合計金額
| 03-C                     END
|   C*
|   C                     READ GLWF03R                  46
| 02-C                     END
| 01-C                     END
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R2100 ... 新增／更新一筆至工作檔GLWF03 (註一)
     C*                     SCREEN => FILE. GLWF03
     C*==============================================================*
05===C           R2100     BEGSR
|    C*****************************
|    C* AS06前二碼為@-.@=.@S.@C *
|    C*  新增一筆至GLWF03       *
|    C*****************************
|    C           2         SUBSTWAS06     @AS06   2
| 01-C           @AS06     IFEQ '@S'
|   C           @AS06     OREQ '@-'
|   C           @AS06     OREQ '@='
|   C           @AS06     OREQ '@C'
|   C                     MOVEL*BLANKS   WF0301
|   C                     MOVELWAS04     WF0301
|   C                     MOVEL*BLANKS   WF0302
|   C                     MOVEL*BLANKS   WF0303
| 02-C           @AS06     IFEQ '@C'
|   C                     MOVEL'C'       WF0302
|   C                     MOVELWAS10     WF0303
| 02*C                     ELSE
|   C                     MOVELWAS06     WF0303
| 02-C                     END
|   C                     Z-ADD*ZERO     WF0304
|   C                     Z-ADD*ZERO     WF0305
|   C*******************************
|   C* AS06前二碼不為@-.@=.@S.@C *
|   C*  新增／更新一筆至GLWF03   *
|   C*******************************
| 01*C                     ELSE
|   C           WAS04     CHAINGLWF03R              40
| 03-C           *IN40     IFEQ '0'
|   C                     ADD  WCASH1    WF0304
|   C                     ADD  WCASH2    WF0305
| 03*C                     ELSE
|   C*
|   C                     MOVEL*BLANKS   WF0301
|   C                     MOVELWAS04     WF0301
|   C                     MOVEL*BLANKS   WF0302
|   C                     MOVELWAS08     WF0302
|   C                     MOVEL*BLANKS   WF0303
|   C                     MOVELWAS10     WF0303
|   C                     Z-ADDWCASH1    WF0304
|   C                     Z-ADDWCASH2    WF0305
| 03-C                     END
| 01-C                     END
|    C*
|    C                     Z-ADD*ZERO     WCASH1           *小計金額
|    C                     Z-ADD*ZERO     WCASH2           *合計金額
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R8000 ... PRINTING MAIN PROCESS
     C*==============================================================*
06===C           R8000     BEGSR
|    C*
|    C                     SETON                     39
|    C                     SETOF                     10
|    C****************
|    C* 現金流量表 *
|    C****************
|    C           *LOVAL    SETLLGLWF03R
|    C                     READ GLWF03R                  46
|    C*
| 01-C           *IN46     DOWEQ'0'                        .EOF
|   C*
| 02-C           *IN39     IFEQ '1'
|   C                     WRITEPH1
|   C                     SETOF                     39
| 02-C                     END
|   C*
|   C                     MOVEA*ALL'0'   *IN,60
|   C                     MOVEL*BLANK    P0303
|   C                     MOVELWF0303    P0303
|   C*
| 03-C           WF0303    IFEQ '@S      '
|   C           WF0302    OREQ 'C'
| 04-C           WF0302    IFEQ 'C'
|   C                     SETON                     81
| 04-C                     END
|   C                     WRITEPD1B
| 03*C                     ELSE
| 05-C           WF0303    IFEQ '@-      '
|   C           WF0303    OREQ '@=      '
| 06-C           WF0303    IFEQ '@=      '
|   C                     SETON                     80
| 06-C                     END
|   C                     WRITEPD1A
| 05*C                     ELSE
|   C*
|   C                     Z-ADDWF0304    P0304
|   C                     Z-ADDWF0305    P0305
| 07-C           WF0304    IFLT 0
|   C                     SETON                     61
| 07-C                     END
| 08-C           WF0305    IFNE 0
|   C                     SETON                     63
| 08-C                     END
| 09-C           WF0305    IFLT 0
|   C                     SETON                     62
| 09-C                     END
|   C*
| 10-C           WF0304    IFNE 0
|   C           WF0305    ORNE 0
|   C                     WRITEPD1
| 10-C                     END
|   C*
| 05-C                     END
| 03-C                     END
|   C*
|   C                     READ GLWF03R                  46
|   C                     SETON                     10
| 01-C                     END
|    C*************
|    C* GLR2C0P....
| 11-C           *IN10     IFEQ '1'                        .HAS DATA
|   C                     EXSR R8900                       .LR TOTAL
| 11*C                     ELSE                            .NO DATA
|   C                     EXSR R8999                       .LR NO DATA
| 11-C                     END
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R8900 ... LEVEL BREAK LR -- HAS DATA
     C*==============================================================*
07===C           R8900     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << TOTAL LINE DATA PREPARE >>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'GLS008'
|    C                     PARM 'GLR2C0P1'P008I1 10
|    C                     PARM DAR05     P008I2  1
|    C           PSIGN1    PARM           P008O1198
|    C           PSIGN2    PARM           P008O2198
|    C*
|    C                     WRITEPT1
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R8999 ... LEVEL BREAK LR -- NO DATA
     C*==============================================================*
08===C           R8999     BEGSR
|    C*
|    C                     WRITEPH1
|    C*
|    C                     WRITEPE1
|    C*
08===C                     ENDSR
     C*==============================================================*
** @W1
@0@1@2@3@4@5@6@7@8@9
