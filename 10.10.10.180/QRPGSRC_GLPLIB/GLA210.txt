     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : GLA510                                     *
     H*    PROGRAM NAME :會計科目沖帳排序維護                      *
     H*    AUTHOR       : A1150                                      *
     H*    CREATE DATE  : 81/12/29                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :                                            *
     H****************************************************************
     FGLAFPF  UF  E           K        DISK
     FGLAAPF  IF  E           K        DISK
     FGLAEPF  IF  E           K        DISK
     FPT#BPF  IF  E           K        DISK
     FGLA210D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     I*
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      138 138 $INQ
     I                                      136 136 $UPD
     I                                      152 161 $USERN
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I            DS
     I                                        1   50ARF
     I                                        1   10@1F
     I                                        2   20@2F
     I                                        3   30@3F
     I                                        4   40@4F
     I                                        5   50@5F
     I            DS
     I                                        1   50ART
     I                                        1   10@1T
     I                                        2   20@2T
     I                                        3   30@3T
     I                                        4   40@4T
     I                                        5   50@5T
     I            DS
     I                                        1   50WAE04
     I                                        1   10AE041
     I                                        2   20AE042
     I                                        3   30AE043
     I                                        4   40AE044
     I                                        5   50AE045
     I            DS
     I                                        1   5 WAE05
     I                                        1   1 AE051
     I                                        2   2 AE052
     I                                        3   3 AE053
     I                                        4   4 AE054
     I                                        5   5 AE055
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C*
     C           *LIKE     DEFN RRN       @DRCD            *DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            *PAGE REC
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYAF     KLIST                           *HEAD FILE
     C                     KFLD           DAF01
     C                     KFLD           DAF02
     C           KEYAE     KLIST                           *COPY REC.
     C                     KFLD           DAF01
     C                     KFLD           AF10
     C           KEY#B     KLIST
     C                     KFLD           DAF01
     C           KEYAA     KLIST
     C                     KFLD           DAF01
     C                     KFLD           AA02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           DDATE     PARM           P3101O  80
|    C*
|    C                     MOVE 'SC01'    SCID    4
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     MOVEL*BLANK    DAF01
|    C                     MOVEL*BLANK    DAF02
|    C*--------------------------------------------------------------*
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 02-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 03-C           *IN99     IFEQ '0'                        *SCR CHK OK
|   C                     EXSR R1C00                      *PRE-SC02
|   C                     MOVEL'SC02'    SCID
| 03-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1A00     BEGSR
|    C*
|    C* AUT CHECK
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM 'GLI410'  S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C   99                GOTO E1A00
|    C*
|    C* CALL SUB-PROGRAM
|    C                     CALL 'GLI410'
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C           DAF01     PARM DAF01     P410I1  2        *公司別
|    C           DAF02     PARM DAF02     P410I2  8        *會計科目
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     PARM           @RTNC   2
|    C* RETURN CODE CHECK
| 02-C           @RTNC     IFEQ '03'                       *PF3
|   C                     SETON                     03
| 02-C                     END
|    C*
03===C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
04===C           R1B00     BEGSR
|    C*
|    C* CHK FUN AUT
| 01-C           $UPD      IFNE 'Y'
|   C           $INQ      ORNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*
|    C* DATA CHECK *--------------------------------------------------*
|    C*
| 02-C           DAF01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                       6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
| 03-C           DAF02     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                       6199
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
|    C           KEYAF     CHAINAF0                  40
| 04-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
04===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
05===C           R1C00     BEGSR
|    C*
|    C                     Z-ADD6         @PRCD            .PAGE SIZE
|    C*
|    C                     EXSR R2A00                      *SFL INIT
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R2000 ....SCREEN  PROCESS
     C*==============================================================*
06===C           R2000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 03-C           *IN25     IFEQ '1'                        *NEXT PAGE
|   C                     EXSR R2N00
|   C                     GOTO E2000
| 03-C                     END
|    C*
| 04-C*          @K##O     IFNE @K##N                      *ST RT KEY
|   C*                    EXSR R2A00                      *SFL INIT
|   C*                    GOTO E2000
| 04-C*                    END
|    C*
|    C                     EXSR R2C00                      *OPTION
|    C*
06===C           E2000     ENDSR
     C*=============================================================*
     C*          R2A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
07===C           R2A00     BEGSR
|    C*
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C                     MOVEL'N'       @BOTOM  1        *BOTTOM
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
|    C                     EXSR R2A20                      *SFL HEAD
|    C* <<SFL INITIAITAL DATA ADD>>
|    C*                    MOVEL@K##N     @K##O            *KEY SAVE
|    C*
|    C           KEY#B     SETLLAE0
|    C                     EXSR R2A10                      *ADD SFL DTA
|    C*
|    C*
| 01-C           RRN       IFGT 0
|   C                     SETON                     50    *SFLDSP
| 01*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C*
07===C                     ENDSR
     C*=============================================================*
     C*          R2A10 .... ADD SUBFILE DATA
     C*=============================================================*
08===C           R2A10     BEGSR
|    C*
|    C                     ADD  @PRCD     @DRCD
| 01-C           RRN       DOWLT@DRCD
|   C*
|   C           KEY#B     READEAE0                      46
|   C*
| 02-C           *IN46     IFEQ '0'
|   C                     EXSR R2A11                      *WRITE SFL
| 02*C                     ELSE                            *EOF
|   C                     Z-ADDRRN       @DRCD
|   C                     MOVE 'Y'       @BOTOM
|   C                     SETON                     9653  *SFLEND    M
| 02-C                     END
|   C*
| 01-C                     END
|    C                     Z-ADDRRN       DRRN
|    C*
08===C                     ENDSR
     C*=============================================================*
     C*          R2A11 .... WRITE SUBFILE DATA
     C*=============================================================*
09===C           R2A11     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << SUBFILE DATA VALUE SET >>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     SETOF                     99
|    C*
|    C                     Z-ADDAE04      ART
| 01-C           @1F       IFEQ 0
|   C           @1T       ANDNE0
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E2A11
|    C*
| 02-C           @2F       IFEQ 0
|   C           @2T       ANDNE0
|   C                     SETON                     99
| 02-C                     END
|    C   99                GOTO E2A11
|    C*
| 03-C           @3F       IFEQ 0
|   C           @3T       ANDNE0
|   C                     SETON                     99
| 03-C                     END
|    C   99                GOTO E2A11
|    C*
| 04-C           @4F       IFEQ 0
|   C           @4T       ANDNE0
|   C                     SETON                     99
| 04-C                     END
|    C   99                GOTO E2A11
|    C*
| 05-C           @5F       IFEQ 0
|   C           @5T       ANDNE0
|   C                     SETON                     99
| 05-C                     END
|    C   99                GOTO E2A11
|    C*
| 06-C           @1T       IFEQ 0
|   C           @1F       ANDNE0
|   C                     SETON                     99
| 06-C                     END
|    C   99                GOTO E2A11
|    C*
| 07-C           @2T       IFEQ 0
|   C           @2F       ANDNE0
|   C                     SETON                     99
| 07-C                     END
|    C   99                GOTO E2A11
|    C*
| 08-C           @3T       IFEQ 0
|   C           @3F       ANDNE0
|   C                     SETON                     99
| 08-C                     END
|    C   99                GOTO E2A11
|    C*
| 09-C           @4T       IFEQ 0
|   C           @4F       ANDNE0
|   C                     SETON                     99
| 09-C                     END
|    C   99                GOTO E2A11
|    C*
| 10-C           @5T       IFEQ 0
|   C           @5F       ANDNE0
|   C                     SETON                     99
| 10-C                     END
|    C   99                GOTO E2A11
|    C*
| 11-C           *IN99     IFEQ '0'
|   C                     Z-ADDAE04      WAE04
|   C                     MOVELAE05      WAE05
|   C                     ADD  1         RRN
|   C                     WRITESFLSR
| 11-C                     END
|    C*
|    C           E2A11     TAG
|    C                     SETOF                     99
|    C*
09===C                     ENDSR
     C*=============================================================*
     C*          R2A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
10===C           R2A20     BEGSR
|    C*
|    C           KEY#B     CHAIN#B0                  40
|    C   40                MOVEL*BLANK    #B02
|    C*
|    C                     MOVELAF11      AA02
|    C           KEYAA     CHAINAA0                  40
|    C   40                MOVEL*BLANK    AA031
|    C  N40                MOVELAA03      AA031
|    C*
|    C                     MOVELAF12      AA02
|    C           KEYAA     CHAINAA0                  40
|    C   40                MOVEL*BLANK    AA032
|    C  N40                MOVELAA03      AA032
|    C*
|    C                     MOVELAF13      AA02
|    C           KEYAA     CHAINAA0                  40
|    C   40                MOVEL*BLANK    AA033
|    C  N40                MOVELAA03      AA033
|    C*
|    C                     MOVELAF14      AA02
|    C           KEYAA     CHAINAA0                  40
|    C   40                MOVEL*BLANK    AA034
|    C  N40                MOVELAA03      AA034
|    C*
|    C                     MOVELAF15      AA02
|    C           KEYAA     CHAINAA0                  40
|    C   40                MOVEL*BLANK    AA035
|    C  N40                MOVELAA03      AA035
|    C*
|    C           KEYAE     CHAINAE0                  40
| 01-C           *IN40     IFEQ '1'
|   C                     MOVEL*BLANK    DAE03
|   C                     Z-ADD0         ARF
| 01*C                     ELSE
|   C                     Z-ADDAE04      ARF
|   C                     MOVELAE03      DAE03
| 01-C                     END
|    C*
10===C                     ENDSR
     C*=============================================================*
     C*          R2C00 ....挑選資料
     C*=============================================================*
11===C           R2C00     BEGSR
|    C*
|    C                     READCSFLSR                  4242
| 01-C           *IN42     DOWEQ'0'
|   C*
| 02-C           DOPT      IFNE *BLANK
|   C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
|   C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|   C           KEYAF     CHAINAF0                  40
|   C*
|   C                     MOVELAE02      AF10
|   C                     TIME           AF97
|   C                     Z-ADD$A8YMD    AF98
|   C                     MOVEL$USER     AF99
|   C                     UPDATAF0
|   C*
|   C                     MOVEL*BLANK    DOPT
|   C                     UPDATSFLSR
|   C*
|   C                     MOVEL'SC01'    SCID
|   C*
|   C                     SETON                     42
| 02-C                     END
|   C*
|   C  N42                READCSFLSR                    42
| 01-C                     END
|   C*
|   C                     Z-ADDRRN       DRRN
|    C*
11===C                     ENDSR
     C*=============================================================*
     C*          R2N00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
12===C           R2N00     BEGSR
|    C*
| 01-C           @BOTOM    IFEQ 'Y'                        *EOF
|   C                     SETON                         96*LST PAG
|   C                     Z-ADDRRN       DRRN
| 01*C                     ELSE
|   C                     EXSR R2A10                      *ADD SFL DTA
| 01-C                     END
|    C*
12===C                     ENDSR
     C*==============================================================*
