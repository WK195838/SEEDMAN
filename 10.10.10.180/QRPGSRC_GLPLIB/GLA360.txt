     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : GLA360                                     *
     H*    PROGRAM NAME : 會計期間設定                             *
     H*    AUTHOR       : A1034 STEPHANIE                            *
     H*    CREATE DATE  : NOV 18,92'                                 *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       : 泛太總帳                                 *
     F****************************************************************
     FGLACPF  UF  E           K        DISK                      A
     FPT#BPF  IF  E           K        DISK
     FGLAKPF  IF  E           K        DISK
     FGLA360D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    @A2S       13  7 0             期間起SCR
     E                    @A2E       13  7 0             期間迄SCR
     E                    @A3E       13  8 0             期間迄FILE
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      111 1160$CHYMD
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      152 161 $USERN
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I*DEFINE COPY KEY
     I            DS
     I                                        1   5 KACC
     I                                        1   2 DAC01C
     I                                        3   50DAC02C
     I*DEFINE 期間起SCR
     I            DS
     I                                        1  910@A2S
     I                                        1   70D01S
     I                                        8  140D02S
     I                                       15  210D03S
     I                                       22  280D04S
     I                                       29  350D05S
     I                                       36  420D06S
     I                                       43  490D07S
     I                                       50  560D08S
     I                                       57  630D09S
     I                                       64  700D10S
     I                                       71  770D11S
     I                                       78  840D12S
     I                                       85  910D13S
     I*DEFINE 期間迄SCR
     I            DS
     I                                        1  910@A2E
     I                                        1   70D01E
     I                                        8  140D02E
     I                                       15  210D03E
     I                                       22  280D04E
     I                                       29  350D05E
     I                                       36  420D06E
     I                                       43  490D07E
     I                                       50  560D08E
     I                                       57  630D09E
     I                                       64  700D10E
     I                                       71  770D11E
     I                                       78  840D12E
     I                                       85  910D13E
     I*DEFINE 期間迄FILE
     I            DS
     I                                        1 1040@A3E
     I                                        1   80AC03B
     I                                        9  160AC04B
     I                                       17  240AC05B
     I                                       25  320AC06B
     I                                       33  400AC07B
     I                                       41  480AC08B
     I                                       49  560AC09B
     I                                       57  640AC10B
     I                                       65  720AC11B
     I                                       73  800AC12B
     I                                       81  880AC13B
     I                                       89  960AC14B
     I                                       97 1040AC15B
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN AC02      WAC02
     C           *LIKE     DEFN AC02      WAC02C
     C           *LIKE     DEFN AC03A     W02CA
     C           *LIKE     DEFN AC03A     W02A
     C           *LIKE     DEFN AC03A     W02
     C           *LIKE     DEFN AC03A     WA2S
     C           *LIKE     DEFN AC03A     WA2E
     C           *NAMVAR   DEFN           PTDA01
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYAC     KLIST                           *MAIN FILE
     C                     KFLD           DAC01
     C                     KFLD           WAC02
     C           KEYACC    KLIST                           *COPY REC.
     C                     KFLD           DAC01C
     C                     KFLD           WAC02C
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
     C           *IN03     DOWEQ'0'
     C           SCID      CASEQ'SC01'    R1000
     C           SCID      CASEQ'SC02'    R2000
     C                     END
     C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C                     MOVE 'SC01'    SCID    4
     C* FUN AUT
     C                     MOVE $ADD      @A01,1
     C                     MOVE $UPD      @A01,2
     C                     MOVE $DLT      @A01,4
     C                     MOVE $INQ      @A01,5
     C*
     C* SC01 FIELD INITIALIZE *--------------------------------------*
     C                     IN   PTDA01
     C*
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM $PFMT     P3104I  1
     C                     PARM $PTYPE    P3105I  1
     C           DDATE     PARM           P3101O  80
     C*
     C                     Z-ADD1         DOPT
     C                     MOVEL*BLANK    DAC01
     C                     Z-ADD0         DAC02
     C                     MOVEL*BLANK    DAC01C
     C                     Z-ADD0         DAC02C
     C*--------------------------------------------------------------*
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C                     EXFMTDSPD1
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '1'                        *END
     C                     GOTO E1000
     C                     END
     C*
     C                     EXSR R1B00                      *SCR CHK
     C*
     C           *IN99     IFEQ '0'                        *SCR CHK OK
     C                     EXSR R1C00                      *PRE-SC02
     C                     MOVEL'SC02'    SCID
     C                     END
     C*
     C           E1000     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
     C           R1B00     BEGSR
     C*
     C* CHK FUN AUT
     C           @A01,DOPT IFNE 'Y'
     C                     MOVEL'UPT2150' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     99
     C                     END
     C   99                GOTO E1B00
     C*
     C* DATA CHECK *--------------------------------------------------*
| 02-C           DAC01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|   C           DAC01     CHAINAK0                  40
|    C*
| 03-C           DOPT      IFEQ 1                          *ADD
| 04-C           *IN40     ANDEQ'1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 03-C                     END
|    C   99                GOTO E1B00
|    C*年制為１－西元年，日期格式＝１　ＹＹＭＭＤＤ
|    C*年制為２－中國年，日期格式＝２　ＹＹＹＹＭＭＤＤ
     C           DAC02     MULT 10000     P3101I
     C                     CALL 'P31'
     C                     PARM           P3101I  80
     C                     PARM AK09      P3102I  1
     C                     PARM AK09      P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C                     PARM           P3101O  80
     C           P3101O    DIV  10000     WAC02
     C*
     C           KEYAC     CHAINAC0                  40
     C*
     C           DOPT      IFEQ 1                          *ADD
     C           *IN40     ANDEQ'0'
     C                     MOVEL'UPT2020' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     606199
     C                     END
     C   99                GOTO E1B00
     C*
     C           DOPT      IFNE 1                          *UPD,DEL,INQ
     C           *IN40     ANDEQ'1'
     C                     MOVEL'UPT2010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     606199
     C                     END
     C   99                GOTO E1B00
     C*
     C           DOPT      IFEQ 1                          *ADD
     C           KACC      ANDNE'  000'                    *COPY
     C*
     C           DAC02C    MULT 10000     P3101I
     C                     CALL 'P31'
     C                     PARM           P3101I  80
     C                     PARM AK09      P3102I  1
     C                     PARM AK09      P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C                     PARM           P3101O  80
     C           P3101O    DIV  10000     WAC02C
     C*
     C           KEYACC    CHAINAC0                  40
     C           *IN40     IFEQ '1'
     C                     MOVEL'UPT0060' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6299
     C                     END
     C                     END
     C   99                GOTO E1B00
     C*
     C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
     C           R1C00     BEGSR
     C*
     C           DOPT      IFEQ 1                          *ADD
     C           DOPT      OREQ 2                          *UPD
     C                     SETON                     31    *CAN INPUT
     C                     ELSE                            *DEL, INQ
     C                     SETOF                     31    *INP PROTECT
     C                     END
     C*
     C*十二月制：十三期間ND PR
     C           AK10      IFEQ '1'                        *ADD
     C                     SETOF                     32    *INP PROTECT
     C                     ELSE                            *DEL, INQ
     C                     SETON                     32    *CAN INP
     C                     END
     C*
     C           DOPT      IFEQ 1                          *ADD
     C           KACC      IFEQ '  000'                    *NO COPY
     C                     EXSR R1C10
     C                     ELSE                            *COPY
     C                     EXSR R1C30
     C                     EXSR R1C20
     C                     END
     C                     ELSE                            *U/D/I
     C                     EXSR R1C20
     C                     END
     C*
     C           AC01      CHAIN#B0                  40
     C   40                MOVE *BLANKS   #B02
     C*
     C* RELEASE FILE (ADD & INQUIRY)----------------------------------*
     C           DOPT      IFEQ 1                          *ADD
     C           DOPT      OREQ 5                          *INQ
     C           *LOVAL    SETLLAC0
     C                     END
     C*---------------------------------------------------------------*
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1C10 ....'SC02' FIELD INIT : ADD (NO COPY)
     C*==============================================================*
     C           R1C10     BEGSR
     C*
     C                     Z-ADD0         @A2S
     C                     Z-ADD0         @A2E
     C                     MOVE *BLANKS   DAC03
     C                     MOVE *BLANKS   DAC04
     C                     MOVE *BLANKS   DAC05
     C                     MOVE *BLANKS   DAC06
     C                     MOVE *BLANKS   DAC07
     C                     MOVE *BLANKS   DAC08
     C                     MOVE *BLANKS   DAC09
     C                     MOVE *BLANKS   DAC10
     C                     MOVE *BLANKS   DAC11
     C                     MOVE *BLANKS   DAC12
     C                     MOVE *BLANKS   DAC13
     C                     MOVE *BLANKS   DAC14
     C                     MOVE *BLANKS   DAC15
     C*
     C                     TIME           AC97
     C                     Z-ADD$A8YMD    AC98
     C                     MOVEL$USER     AC99
     C*
     C                     ENDSR
     C*==============================================================*
     C*  R1C20 ....'SC02' FIELD INIT : UPD, DEL, INQ,
     C*==============================================================*
     C           R1C20     BEGSR
     C*
     C                     Z-ADD0         @A2S
     C                     Z-ADD0         @A2E
     C*期間１起
     C                     CALL 'P31'
     C                     PARM AC03A     P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM AK11      P3104I  1
     C                     PARM AK09      P3105I  1
     C           @A2S,1    PARM           P3101O  80
     C*
     C                     DO   13        WI      30
     C*期間１－１３迄
     C                     CALL 'P31'
     C                     PARM @A3E,WI   P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM AK11      P3104I  1
     C                     PARM AK09      P3105I  1
     C           @A2E,WI   PARM           P3101O  80
     C*
     C*期間２－１３起＝期間１－１２迄後一天
     C*
     C           WI        ADD  1         WJ      30
     C           WJ        IFLE 13
     C                     CALL 'P37'
     C                     PARM @A2E,WI   P3701I  80
     C                     PARM AK11      P3702I  1
     C                     PARM AK09      P3703I  1
     C                     PARM 1         P3704I  40
     C           @A2S,WJ   PARM           P3701O  80
     C                     END
     C*
     C                     END
     C*
     C           DOPT      IFEQ 1
     C                     MOVE *BLANKS   DAC03
     C                     MOVE *BLANKS   DAC04
     C                     MOVE *BLANKS   DAC05
     C                     MOVE *BLANKS   DAC06
     C                     MOVE *BLANKS   DAC07
     C                     MOVE *BLANKS   DAC08
     C                     MOVE *BLANKS   DAC09
     C                     MOVE *BLANKS   DAC10
     C                     MOVE *BLANKS   DAC11
     C                     MOVE *BLANKS   DAC12
     C                     MOVE *BLANKS   DAC13
     C                     MOVE *BLANKS   DAC14
     C                     MOVE *BLANKS   DAC15
     C                     ELSE
     C                     MOVE AC03      DAC03
     C                     MOVE AC04      DAC04
     C                     MOVE AC05      DAC05
     C                     MOVE AC06      DAC06
     C                     MOVE AC07      DAC07
     C                     MOVE AC08      DAC08
     C                     MOVE AC09      DAC09
     C                     MOVE AC10      DAC10
     C                     MOVE AC11      DAC11
     C                     MOVE AC12      DAC12
     C                     MOVE AC13      DAC13
     C                     MOVE AC14      DAC14
     C                     END
     C*
     C           AC15B     IFEQ 0
     C                     MOVE *BLANKS   DAC15
     C                     ELSE
     C                     MOVE AC15      DAC15
     C                     END
     C*
     C           DOPT      IFEQ 1                          *ADD & COPY
     C                     TIME           AC97
     C                     Z-ADD$A8YMD    AC98
     C                     MOVEL$USER     AC99
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*  R1C30 ....'SC02' FIELD INIT : (ADD & COPY)
     C*==============================================================*
     C           R1C30     BEGSR
     C* EX. ADD 1993 , COPY FROM 1992 -> 1993-1992=1 SCR2'YEAR +1
     C*
     C           WAC02C    MULT 10000     W02CA
     C           WAC02     MULT 10000     W02A
     C           W02A      SUB  W02CA     W02
     C*
     C*期間１起
     C                     ADD  W02       AC03A
     C*
     C                     DO   13        W       30
     C                     ADD  W02       @A3E,W
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           R2000     BEGSR
     C*
     C                     EXFMTDSPD2
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '1'                        *END
     C                     GOTO E2000
     C                     END
     C*
     C           *IN12     IFEQ '1'                        *PREVIOUS
     C* RELEAS FILE --------------------------------------------------*
     C           *LOVAL    SETLLAC0
     C*---------------------------------------------------------------*
     C                     MOVEL'SC01'    SCID
     C                     GOTO E2000
     C                     END
     C*
     C           DOPT      IFEQ 1                          *ADD
     C           DOPT      OREQ 2                          *UPD
     C                     EXSR R2B00                      *SC02 CHK
     C                     END
     C*
     C           *IN05     IFEQ '1'                        *CHECK ONLY
     C                     GOTO E2000
     C                     END
     C*
     C           *IN99     IFEQ '0'                        *CHK OK
     C                     EXSR R2D00                      *FILE UPD
     C                     MOVE 'SC01'    SCID
     C                     END
     C*
     C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....'SC02' CHECK
     C*==============================================================*
     C           R2B00     BEGSR
     C*
     C*期間１起  DATE CHECK
     C                     CALL 'P30'
     C                     PARM @A2S,1    P3001I  80
     C                     PARM AK11      P3002I  1
     C                     PARM AK09      P3003I  1
     C                     PARM           P3001O  1
     C           P3001O    IFEQ 'N'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     MOVEA'1'       *IN,60
     C                     SETON                     99
     C                     END
     C   99                GOTO E2B00
     C*
     C           1         DO   13        WI      30
     C*期間１－１３迄  DATE CHECK  十二月制時，第十三期無須檢核
     C           AK10      IFEQ '2'
     C           AK10      OREQ '1'
     C           WI        ANDNE13
     C                     CALL 'P30'
     C                     PARM @A2E,WI   P3001I  80
     C                     PARM AK11      P3002I  1
     C                     PARM AK09      P3003I  1
     C                     PARM           P3001O  1
     C           P3001O    IFEQ 'N'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C           WI        ADD  60        WK      30
     C                     MOVEA'1'       *IN,WK
     C                     SETON                     99
     C                     END
     C                     END
     C   99                GOTO E2B00
     C*
     C*期間２－１３起＝期間１－１２迄後一天
     C*
     C           WI        ADD  1         WJ      30
     C           WJ        IFLE 13
     C                     CALL 'P37'
     C                     PARM @A2E,WI   P3701I  80
     C                     PARM AK11      P3702I  1
     C                     PARM AK09      P3703I  1
     C                     PARM 1         P3704I  40
     C           @A2S,WJ   PARM           P3701O  80
     C                     END
     C*
     C                     CALL 'P31'
     C                     PARM @A2S,WI   P3101I  80
     C                     PARM AK11      P3102I  1
     C                     PARM AK09      P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           WA2S      PARM           P3101O  80
     C*
     C                     CALL 'P31'
     C                     PARM @A2E,WI   P3101I  80
     C                     PARM AK11      P3102I  1
     C                     PARM AK09      P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           WA2E      PARM           P3101O  80
     C*
     C           AK10      IFEQ '2'
     C           AK10      OREQ '1'
     C           WI        ANDNE13
     C           WA2S      IFGT WA2E
     C                     MOVEL'UPT0041' ERRID
     C                     MOVEL'PTMF'    ERRF
     C           WI        ADD  60        WK      30
     C                     MOVEA'1'       *IN,WK
     C                     SETON                     99
     C                     END
     C                     END
     C   99                GOTO E2B00
     C*
     C                     END
     C*
     C           E2B00     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
     C           R2D00     BEGSR
     C*
     C           DOPT      CASEQ1         R2D10
     C           DOPT      CASEQ2         R2D20
     C           DOPT      CASEQ4         R2D30
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*==============================================================*
     C           R2D10     BEGSR
     C*
     C                     EXSR R2D11
     C                     WRITEAC0
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
     C           R2D11     BEGSR
     C*
     C                     MOVELDAC01     AC01
     C                     Z-ADDWAC02     AC02
     C                     MOVE DAC03     AC03
     C                     MOVE DAC04     AC04
     C                     MOVE DAC05     AC05
     C                     MOVE DAC06     AC06
     C                     MOVE DAC07     AC07
     C                     MOVE DAC08     AC08
     C                     MOVE DAC09     AC09
     C                     MOVE DAC10     AC10
     C                     MOVE DAC11     AC11
     C                     MOVE DAC12     AC12
     C                     MOVE DAC13     AC13
     C                     MOVE DAC14     AC14
     C*
     C*期間１起
     C                     CALL 'P31'
     C                     PARM @A2S,1    P3101I  80
     C                     PARM AK11      P3102I  1
     C                     PARM AK09      P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           AC03A     PARM           P3101O  80
     C*
     C                     DO   13        WI      30
     C*期間１－１３迄
     C                     CALL 'P31'
     C                     PARM @A2E,WI   P3101I  80
     C                     PARM AK11      P3102I  1
     C                     PARM AK09      P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           @A3E,WI   PARM           P3101O  80
     C*
     C                     END
     C*
     C           AK10      IFEQ '1'
     C                     MOVE 'Y'       AC15
     C                     Z-ADD0         AC15B
     C                     ELSE
     C                     MOVE DAC15     AC15
     C                     END
     C*
     C                     TIME           AC97
     C                     Z-ADD$A8YMD    AC98
     C                     MOVEL$USER     AC99
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2D20 ....FILE PROCESS : UPD
     C*==============================================================*
     C           R2D20     BEGSR
     C*
     C                     EXSR R2D11
     C                     UPDATAC0
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS
     C*==============================================================*
     C           R2D30     BEGSR
     C*
     C                     DELETAC0
     C*
     C                     ENDSR
     C*==============================================================*
