     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : GLR1H01                                    *
     H*    PROGRAM NAME :日記帳列印　　　                          *
     H*    AUTHOR       : A1150                                      *
     H*    CREATE DATE  : 81/11/11                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :                                            *
     H****************************************************************
     FGLAHLF01IP  E           K        DISK
     FGLAKPF  IF  E           K        DISK
     FGLAFPF  IF  E           K        DISK
     FPT#YPF  IF  E           K        DISK
     FPT#BPF  IF  E           K        DISK
     FGLR1H0P O   E             39     PRINTER
     E*---------------------------------------------------------------
     IAH0
     I                                              AH02  L1
     I                                              AH10  L2
     I*
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I           UDS
     I                                      101 110 $USER
     I                                      125 1270$CPY
     I                                      138 138 $INQ
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I                                      217 218 $PRTCD
     I                                      219 219 $PENV
     I* PROGRAM USE DATA DEFINE ++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DAH01
     I                                      503 5100DAH10F
     I                                      511 5180DAH10T
     I                                      519 519 DFLD
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEY#B     KLIST
     C                     KFLD           DAH01            *公司別
     C           KEY#Y     KLIST
     C                     KFLD           WTMP1  10        *'AH17'
     C                     KFLD           WTMP2  10        *AH17
     C           KEYAF     KLIST
     C                     KFLD           DAH01
     C                     KFLD           AH04             *會計科目
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR R1000
     C*+ LEVEL BREAK INIT PROCESS +++++++++++++++++++++++++++++++++++
     C   L1                EXSR R7100
     C   L2                EXSR R7200
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     EXSR R2000                      .DETAIL
     C*
     C*+ LEVEL BREAK PROCESS ++++++++++++++++++++++++++++++++++++++++
     CL1 10                EXSR R8100
     CLR 10                EXSR R8900
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     CLRN10                EXSR R8999
     C*==============================================================*
     C*          R1000 ... INITIAL PROCESS
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C                     SETON                     1039
     C                     SETOF                     11    *L1 1ST *BRK
     C*
     C                     IN   PTDA01
     C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM $PFMT     P3104I  1
     C                     PARM $PTYPE    P3105I  1
     C           PDATE     PARM           P3101O  80
     C*
     C           KEY#B     CHAIN#B0                  40
     C   40                MOVEL*BLANK    #B16
     C*製票日期 FOR PH1 ------
     C           KEY#B     CHAINAK0                  40
     C*日期格式轉換FOR PH1 ----------
     C                     CALL 'P31'
     C                     PARM DAH10F    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM AK11      P3104I  1
     C                     PARM AK09      P3105I  1
     C                     PARM           P3101O  80
     C*
     C                     Z-ADDP3101O    P10F
     C*
     C                     CALL 'P31'
     C                     PARM DAH10T    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM AK11      P3104I  1
     C                     PARM AK09      P3105I  1
     C                     PARM           P3101O  80
     C*
     C                     Z-ADDP3101O    P10T
     C*
     C           DFLD      IFEQ '1'
     C                     MOVEL'已過帳'PFLD
     C                     ELSE
     C           DFLD      IFEQ '2'
     C                     MOVEL'已核准'PFLD
     C                     ELSE
     C                     MOVEL'全部　'PFLD
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2000 ... DETAIL
     C*==============================================================*
     C           R2000     BEGSR
     C*
     C*+<<DATA SET>>+++++++++++++++++++++++++++++++++++++++++++++++++++
     C           *IN39     IFEQ '1'
     C                     WRITEPH1
     C                     SETOF                     39
     C* BREAK CONTROL FOR FIELD PRINTED +++++++++++++++++++++++++++++++
     C                     SETON                     3132
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     END
     C*
     C*傳票日期格式轉換--------
     C                     CALL 'P31'
     C                     PARM AH10      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM AK11      P3104I  1
     C                     PARM AK09      P3105I  1
     C                     PARM           P3101O  80
     C*
     C                     Z-ADDP3101O    PAH10
     C*AF03科目-----
     C                     MOVEL*BLANK    PAF03
     C           KEYAF     CHAINAF0                  40
     C           *IN40     IFEQ '0'
     C           AH12      IFNE 0
     C                     MOVELAF03      PAF03
     C                     ELSE
     C                     MOVE AF03      PAF03
     C                     END
     C                     END
     C*#Y03-------
     C                     MOVEL*BLANK    P#Y03
     C                     MOVEL'AH17'    WTMP1
     C                     MOVELAH17      WTMP2
     C           KEY#Y     CHAIN#Y0                  40
     C  N40                MOVEL#Y03      P#Y03
     C*
     C                     WRITEPD1
     C*
     C                     SETOF                     3132
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R7100 ... LEVEL BREAK 1 :INIT PROCESS
     C*==============================================================*
     C           R7100     BEGSR
     C*
     C   11                WRITEPT1
     C*
     C* BREAK CONTROL FOR FIELD PRINTED +++++++++++++++++++++++++++++++
     C                     SETON                     3111
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R7200 ... LEVEL BREAK 2 :INIT PROCESS
     C*==============================================================*
     C           R7200     BEGSR
     C*
     C* BREAK CONTROL FOR FIELD PRINTED +++++++++++++++++++++++++++++++
     C                     SETON                     32
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R8100 ... LEVEL BREAK 1
     C*==============================================================*
     C           R8100     BEGSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* << TOTAL LINE DATA PREPARE >>
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R8900 ... LEVEL BREAK LR -- HAS DATA
     C*==============================================================*
     C           R8900     BEGSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* << TOTAL LINE DATA PREPARE >>
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     WRITEPT1
     C*
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R8999 ... LEVEL BREAK LR -- NO DATA
     C*==============================================================*
     C           R8999     BEGSR
     C*
     C                     EXSR R1000
     C*
     C                     WRITEPH1
     C*
     C                     WRITEPE9
     C*
     C                     ENDSR
