     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLR6C01                                    *
     A*    PROGRAM NAME : 損益表－預算比較                         *
     A*    AUTHOR       :  A1149    HUANG-SHU.MEI                    *
     A*    CREATE DATE  :  1993.01.06                                *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     FPT#APF  IF  E           K        DISK
     FPT#BPF  IF  E           K        DISK
     FGLAKPF  IF  E           K        DISK
     FGLAULF03IF  E           K        DISK
     FGLAXPF  IF  E           K        DISK
     FGLAZPF  IF  E           K        DISK
     FGLAILF02IF  E           K        DISK
     FGLASLF02IF  E           K        DISK
     F            AS0                               KRENAMEASLF02
     FGLASLF03IF  E           K        DISK
     F            AS0                               KRENAMEASLF03
     FGLWF03  UF  E           K        DISK                      A
     FGLR6C0P1O   E             39     PRINTER
     E*---------------------------------------------------------------
     E* AI檔實際金額
     E                    WAI1       13 13 2
     E* AU檔預算金額
     E                    WAU1       13 13 2
     E* @0-@9
     E                    @W1    10  10  2
     E*---------------------------------------------------------------*
     I*
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     IGLDA01      DS
     I                                        1  16 DA01
     I*
     I*LOCAL DATA AREA
     ILDA        UDS
     I                                      101 110 $USER
     I                                      125 1270$CPY
     I                                      138 138 $INQ
     I                                      152 161 $USERN
     I                                      201 2080$A8YMD
     I                                      209 2160$C8YMD
     I                                      217 218 $PRTCD
     I                                      219 219 $PENV
     I* SCREEN INPUT FIELD  ++++++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DAI01
     I                                      503 508 D#A01
     I                                      509 5110DYEAR
     I                                      512 5130DMMS
     I                                      514 5150DMME
     I                                      516 5170DAU03
     I                                      518 520 DAR03
     I* PROGRAM CALCULATION FIELD  +++++++++++++++++++++++++++++++++++
     I                                      521 521 #B12
     I                                      522 522 #A10
     I* OPEN QUERY FILE SELECT $XXXXX FIELD  +++++++++++++++++++++++++
     I                                      523 524 $AI01S
     I                                      525 526 $AI01E
     I                                      527 532 $AI04S
     I                                      533 538 $AI04E
     I                                      539 5420$YEAR
     I                                      543 543 $AR05
     I*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     I*<< 實際金額 >>
     I            DS
     I                                        1 169 WAI1
     I                                        1  132AI11
     I                                       14  262AI12
     I                                       27  392AI13
     I                                       40  522AI14
     I                                       53  652AI15
     I                                       66  782AI16
     I                                       79  912AI17
     I                                       92 1042AI18
     I                                      105 1172AI19
     I                                      118 1302AI20
     I                                      131 1432AI21
     I                                      144 1562AI22
     I                                      157 1692AI23
     I*<< 預算金額 >>
     I            DS
     I                                        1 169 WAU1
     I                                        1  132AU07
     I                                       14  262AU08
     I                                       27  392AU09
     I                                       40  522AU10
     I                                       53  652AU11
     I                                       66  782AU12
     I                                       79  912AU13
     I                                       92 1042AU14
     I                                      105 1172AU15
     I                                      118 1302AU16
     I                                      131 1432AU17
     I                                      144 1562AU18
     I                                      157 1692AU19
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *NAMVAR   DEFN           GLDA01
     C           *NAMVAR   DEFN *LDA      LDA
     C           *LIKE     DEFN AS02      WAR02            *KEY(10)
     C           *LIKE     DEFN AX02      WAX02
     C           *LIKE     DEFN AZ03      WAZ03
     C           *LIKE     DEFN AI01      WAI01
     C           *LIKE     DEFN AI10      WCASH1           *實際金額
     C           *LIKE     DEFN AI10      WCASH2           *預算金額
     C           *LIKE     DEFN AI10      @BASE1
     C           *LIKE     DEFN AI10      @BASE2
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYAF     KLIST
     C                     KFLD           WAI01            *公司別
     C                     KFLD           AI03             *會計科目
     C           KEY#B     KLIST
     C                     KFLD           DAI01            *公司別
     C           KEYAS     KLIST
     C                     KFLD           DAI01            *公司別
     C                     KFLD           WAR02            *報表類別
     C                     KFLD           DAR03            *報表代碼
     C           KEYAU     KLIST
     C                     KFLD           $YEAR            *年度
     C                     KFLD           DAU03            *版次
     C                     KFLD           AS06             *會計科目
     C           KEYAU1    KLIST
     C                     KFLD           $YEAR            *年度
     C                     KFLD           DAU03            *版次
     C           KEYAX     KLIST
     C                     KFLD           DAI01            *公司別S
     C                     KFLD           WAX02            *公司別F
     C           KEYAZ     KLIST
     C                     KFLD           DAI01            *公司別S
     C                     KFLD           D#A01            *成本中心S
     C                     KFLD           WAZ03            *成本中心F
     C           KEYAI     KLIST
     C                     KFLD           AS06             *會計科目
     C                     KFLD           $YEAR            *會計年度
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     SETON                     3938
     C                     EXSR R0100                      .INIT-HEAD
     C*
     C                     EXSR R1000                      .WROK-FILE
     C                     EXSR R2000                      .DETAIL
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ... HEADING INITIAL
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           PDATE     PARM           P3101O  80
|    C*
| 01-C           #B12      IFEQ '1'
|   C                     MOVELDAI01     WAI01
| 01*C                     ELSE
|   C****************************
|   C*公司別屬虛擬            *
|   C*須CHECK公司結構明細檔 *
|   C****************************
|   C           DAI01     CHAINAX0                  40
| 02-C           *IN40     IFEQ '0'
|   C                     MOVELAX02      WAI01
| 02-C                     END
| 01-C                     END
|    C**************
|    C*<<公司別 >>
|    C                     MOVEL*BLANKS   P#B16
|    C           KEY#B     CHAIN#B0                  40
| 03-C           *IN40     IFEQ '0'
| 04-C           $AR05     IFEQ '1'
|   C                     MOVEL#B16      P#B16
| 04*C                     ELSE
|   C                     MOVEL#B17      P#B16
| 04-C                     END
| 03-C                     END
|    C**************
|    C*<< *DTAARA >>
|    C                     CALL 'GLS006'
|    C                     PARM WAI01     P006I1  2
|    C                     PARM $YEAR     P006I2  40
|    C                     PARM DMME      P006I3  20
|    C                     PARM 'F'       P006I4  1
|    C                     PARM           P006O1  80
|    C                     PARM           P006O2  80
|    C                     PARM           P006O3  1
|    C                     PARM           P006O4  1
|    C*
| 05-C           P006O4    IFNE 'Y'
|   C                     IN   GLDA01
| 06-C           $AR05     IFEQ '1'
|   C                     MOVELDA01      PDA01
| 06*C                     ELSE
|   C                     MOVE DA01      PDA01
| 06-C                     END
| 05-C                     END
|    C**********************
|    C*<<會計期間起迄日 >>
|    C           WAI01     CHAINAK0                  40
| 07-C           *IN40     IFEQ '0'
|   C                     CALL 'P31'
|   C                     PARM P006O2    P3101I  80
|   C                     PARM '2'       P3102I  1
|   C                     PARM '1'       P3103I  1
|   C                     PARM AK11      P3104I  1
|   C                     PARM AK09      P3105I  1
|   C           PMME      PARM           P3101O  80
|   C*
|   C                     CALL 'GLS006'
|   C                     PARM WAI01     P006I1  2
|   C                     PARM $YEAR     P006I2  40
|   C                     PARM DMMS      P006I3  20
|   C                     PARM 'F'       P006I4  1
|   C                     PARM           P006O1  80
|   C                     PARM           P006O2  80
|   C                     PARM           P006O3  1
|   C                     PARM           P006O4  1
|   C*
|   C                     CALL 'P31'
|   C                     PARM P006O1    P3101I  80
|   C                     PARM '2'       P3102I  1
|   C                     PARM '1'       P3103I  1
|   C                     PARM AK11      P3104I  1
|   C                     PARM AK09      P3105I  1
|   C           PMMS      PARM           P3101O  80
| 07-C                     END
|    C*
|    C****************
|    C*<<成本中心 >>
|    C                     MOVEL*BLANKS   P#A02
| 08-C           D#A01     IFEQ *BLANKS
|   C                     SETOF                     30
| 08*C                     ELSE
|   C*
|   C                     SETON                     30
|   C           D#A01     CHAIN#A0                  40
| 09-C           *IN40     IFEQ '0'
| 10-C           $AR05     IFEQ '1'
|   C                     MOVEL#A02      P#A02
| 10*C                     ELSE
|   C                     MOVEL#A03      P#A02
| 10-C                     END
|   C*
| 09-C                     END
| 08-C                     END
|    C*************************
|    C*<<報表格式－報表代碼>>
|    C                     MOVEL'10'      WAR02
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ... GET DATA TO WORK FILE
     C*==============================================================*
02===C           R1000     BEGSR
|    C**************************
|    C* 逐筆讀取GLASLF02,    *
|    C* 只取AS06為會計科目,*
|    C* 存入GLWF03.          *
|    C**************************
|    C           KEYAS     SETLLASLF02
|    C           KEYAS     READEASLF02                   42
| 01-C           *IN42     DOWEQ'0'                        .EOF
|   C*
|   C                     EXSR R1A00                      .GLAILF02
|   C                     EXSR R1B00                      .GLAULF03
|   C                     EXSR R1D00                      *註一
|   C   40                WRITEGLWF03R
|   C  N40                UPDATGLWF03R
|   C*
|   C           KEYAS     READEASLF02                   42
| 01-C                     END
|    C**************************
|    C* 逐筆讀取GLASLF03,    *
|    C* 只取AS06為@------  *
|    C* 存入GLWF03.          *
|    C**************************
|    C           KEYAS     SETLLASLF03
|    C           KEYAS     READEASLF03                   42
|    C*
| 02-C           *IN42     DOWEQ'0'                        .EOF
|   C**********************
|   C*紀錄基數@RXXXX    *
|   C**********************
|   C           2         SUBSTAS06:1    @AS06   2
| 03-C           @AS06     IFEQ '@R'
|   C           4         SUBSTAS06:3    KEY03   4
| 03-C                     END
|   C*
|   C                     EXSR R1C00                      .GLWF03
|   C                     EXSR R1D00                      .註一
|   C   40                WRITEGLWF03R
|   C  N40                UPDATGLWF03R
|   C*
|   C           KEYAS     READEASLF03                   42
| 02-C                     END
|    C************************
|    C*計算基數@R=@BASE1-2 *
|    C************************
|    C           KEY03     CHAINGLWF03R              40
| 04-C           *IN40     IFEQ '0'
|   C                     Z-ADDWF0304    @BASE1
|   C                     Z-ADDWF0305    @BASE2
| 04-C                     END                             .*IN40
|    C*
02===C                     ENDSR
     C*==============================================================*
     C*          R2000 ... PRINTING MAIN PROCESS
     C*==============================================================*
03===C           R2000     BEGSR
|    C****************
|    C* 比較損益表 *
|    C****************
|    C           *LOVAL    SETLLGLWF03R
|    C                     READ GLWF03R                  42
|    C*
| 01-C           *IN42     DOWEQ'0'                        .EOF
|   C* FIRST RECORD
| 02-C           *IN10     IFEQ '0'
|   C                     SETON                     10
| 02-C                     END
|   C*
| 03-C           *IN39     IFEQ '1'
|   C                     WRITEPH01
|   C                     SETOF                     39
| 03-C                     END
|   C*
| 04-C           WF0303    IFEQ '@-      '
|   C                     WRITEPD02
| 04*C                     ELSE
|   C*
| 05-C           WF0303    IFEQ '@S      '
|   C                     WRITEPD03
| 05*C                     ELSE
|   C*
|   C                     MOVELWF0303    P0303
|   C******************************
|   C* 判斷合計碼是否為*BLANKS  *
|   C* 再決定20是否SETON      *
|   C******************************
|   C                     Z-ADDWF0304    P0304
|   C                     Z-ADDWF0305    P0305
|   C           WF0304    SUB  WF0305    P03X
|   C                     SETON                     20
|   C*
|   C*************************
|   C* 判斷合計碼是否為Ｖ  *
|   C* 再決定20是否SETOF *
|   C*************************
| 06-C           WF0302    IFEQ 'V'
|   C                     Z-ADDP0304     P0304@
|   C                     Z-ADDP0305     P0305@
|   C                     Z-ADDP03X      P03X@
|   C                     SETOF                     20
| 06-C                     END
|   C******
|   C                     Z-ADD*ZERO     WP100   74
|   C                     Z-ADD*ZERO     PR0304
|   C                     Z-ADD*ZERO     PR0305
|   C                     Z-ADD*ZERO     PR03X
|   C                     SETOF                     313233
|   C******************************
|   C* @R不為零時方SETON 31.32. *
|   C******************************
| 07-C           @BASE1    IFNE *ZERO
|   C                     SETON                     31
|   C           WF0304    DIV  @BASE1    WP100     H
|   C           WP100     MULT 100       PR0304
| 07-C                     END
|   C*
| 08-C           @BASE2    IFNE *ZERO
|   C                     SETON                     32
|   C           WF0305    DIV  @BASE2    WP100     H
|   C           WP100     MULT 100       PR0305
| 08-C                     END
|   C*
| 09-C           WF0305    IFNE *ZERO
|   C           P03X      DIV  WF0305    WP100     H
|   C           WP100     MULT 100       PR03X
|   C                     SETON                     33
| 09-C                     END
|   C**********************
|   C* 判斷值是否為負數 *
|   C* 再決定( ,) SETON *
|   C**********************
|   C                     SETOF                     121314
|   C                     SETOF                     151617
| 10-C           P0304     IFLT *ZERO
|   C                     SETON                     12
| 10-C                     END
|   C*
| 11-C           PR0304    IFLT *ZERO
|   C                     SETON                     13
| 11-C                     END
|   C*
| 12-C           P0305     IFLT *ZERO
|   C                     SETON                     14
| 12-C                     END
|   C*
| 13-C           PR0305    IFLT *ZERO
|   C                     SETON                     15
| 13-C                     END
|   C*
| 14-C           P03X      IFLT *ZERO
|   C                     SETON                     16
| 14-C                     END
|   C*
| 15-C           PR03X     IFLT *ZERO
|   C                     SETON                     17
| 15-C                     END
|   C************************
|   C*值全為零者．不列印  *
|   C************************
| 16-C           WF0302    IFEQ *BLANKS
| 17-C           P0304     IFNE *ZERO
|   C           P0305     ORNE *ZERO
|   C           P03X      ORNE *ZERO
|   C                     WRITEPD01
| 17-C                     END
| 16*C                     ELSE
| 18-C           WF0302    IFEQ 'C'
|   C                     WRITEPD01@
| 18*C                     ELSE
|   C                     WRITEPD01
| 18-C                     END
| 16-C                     END
|   C*
| 05-C                     END
| 04-C                     END
|   C*
|   C                     READ GLWF03R                  42
| 01-C                     END
|    C*************
|    C* GLR6C0P....
| 19-C           *IN10     IFEQ '1'                        .HAS DATA
|   C                     EXSR R8900                       .LR TOTAL
| 19*C                     ELSE                            .NO DATA
|   C                     EXSR R8999                       .LR NO DATA
| 19-C                     END
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          R1A00 ... GLAILF02 DETAIL
     C*==============================================================*
04===C           R1A00     BEGSR
|    C*****************
|    C* 年度餘額檔  *
|    C*****************
|    C           KEYAI     SETLLAI0
|    C                     READ AI0                      46
| 01-C           *IN46     DOWEQ'0'
|   C           AI03      ANDLEAS07
|   C***********************
|   C* DAI01公司別屬虛擬 *
|   C***********************
| 02-C           #B12      IFEQ '2'
|   C                     MOVELAI01      WAX02
|   C           KEYAX     CHAINAX0                  40
|   C   40                GOTO TAG#1
| 02-C                     END
|   C*************************
|   C* D#A01成本中心屬虛擬 *
|   C*************************
| 03-C           #A10      IFEQ '2'
|   C                     MOVELAI04      WAZ03
|   C           KEYAZ     CHAINAZ0                  40
|   C   40                GOTO TAG#1
| 03-C                     END
|   C******************
|   C* 計算實際金額 *
|   C******************
| 04-C           DMMS      DO   DMME      I       20
|   C                     ADD  WAI1,I    WCASH1
| 04-C                     END
|   C*
|   C           TAG#1     TAG
|   C                     READ AI0                      46
| 01-C                     END
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R1B00 ... GLAULF03  DETAIL
     C*==============================================================*
05===C           R1B00     BEGSR
|    C*******************
|    C* 預算資料明細檔*
|    C*******************
|    C           KEYAU     SETLLAU0
|    C           KEYAU1    READEAU0                      46
| 01-C           *IN46     DOWEQ'0'
|   C           AU04      ANDLEAS07
|   C***********************
|   C* DAI01公司別屬虛擬 *
|   C***********************
| 02-C           #B12      IFEQ '2'
|   C                     MOVELAU01      WAX02
|   C           KEYAX     CHAINAX0                  40
|   C   40                GOTO TAG#2
| 02-C                     END
|   C*************************
|   C* D#A01成本中心屬虛擬 *
|   C*************************
| 03-C           #A10      IFEQ '2'
|   C                     MOVELAU05      WAZ03
|   C           KEYAZ     CHAINAZ0                  40
|   C   40                GOTO TAG#2
| 03-C                     END
|   C******************
|   C* 計算預算金額 *
|   C******************
| 04-C           DMMS      DO   DMME      I       20
|   C                     ADD  WAU1,I    WCASH2
| 04-C                     END
|   C*
|   C           TAG#2     TAG
|   C           KEYAU1    READEAU0                      46
| 01-C                     END
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1C00 ... GLASLF03 DETAIL
     C*==============================================================*
06===C           R1C00     BEGSR
|    C**
|    C           2         SUBSTAS06      @AS06   2
|    C           @AS06     LOKUP@W1                      49
| 01-C           *IN49     IFEQ '1'                        * @0-@9
|   C           4         SUBSTAS06:2    WF301S  4
|   C           4         SUBSTAS07:2    WF301E  4
|   C**************
|   C*   GLWF03   *
|   C**************
|   C           WF301S    SETLLGLWF03R
|   C                     READ GLWF03R                  46
| 02-C           *IN46     DOWEQ'0'
|   C           WF0301    ANDLEWF301E
|   C*************
|   C*計算金額 *
|   C*************
|   C                     ADD  WF0304    WCASH1
|   C                     ADD  WF0305    WCASH2
|   C*
|   C                     READ GLWF03R                  46
| 02-C                     END
|   C*
| 01-C                     END
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R1D00 ... 新增／更新一筆至工作檔GLWF03 (註一)
     C*                     SCREEN => FILE. GLWF03
     C*==============================================================*
07===C           R1D00     BEGSR
|    C**************************
|    C* AS06前二碼為@-.@S.@C *
|    C*  新增一筆至GLWF03    *
|    C**************************
|    C           AS04      CHAINGLWF03R              40
|    C           2         SUBSTAS06      @AS06   2
| 01-C           @AS06     IFEQ '@S'
|   C           @AS06     OREQ '@-'
|   C                     MOVEL*BLANKS   WF0301
|   C                     MOVEL*BLANKS   WF0302
|   C                     MOVEL*BLANKS   WF0303
|   C                     MOVELAS04      WF0301
|   C                     MOVEL*BLANKS   WF0302
|   C                     MOVELAS06      WF0303
|   C                     Z-ADD*ZERO     WF0304
|   C                     Z-ADD*ZERO     WF0305
|   C                     Z-ADD*ZERO     WF0306
| 01*C                     ELSE
| 02-C           @AS06     IFEQ '@C'
|   C                     MOVEL*BLANKS   WF0301
|   C                     MOVEL*BLANKS   WF0302
|   C                     MOVEL*BLANKS   WF0303
|   C                     MOVELAS04      WF0301
|   C                     MOVEL'C'       WF0302
|   C                     MOVELAS10      WF0303
|   C                     Z-ADD*ZERO     WF0304
|   C                     Z-ADD*ZERO     WF0305
|   C                     Z-ADD*ZERO     WF0306
|   C*******************************
|   C* AS06前二碼不為@-.@S.@C    *
|   C*  新增／更新一筆至GLWF03   *
|   C*******************************
| 02*C                     ELSE
|   C           AS04      CHAINGLWF03R              40
| 03-C           *IN40     IFEQ '0'
|   C                     MULT AS05      WCASH1
|   C                     MULT AS05      WCASH2
|   C                     ADD  WCASH1    WF0304
|   C                     ADD  WCASH2    WF0305
| 03*C                     ELSE
|   C*
|   C                     MOVEL*BLANKS   WF0301
|   C                     MOVEL*BLANKS   WF0302
|   C                     MOVEL*BLANKS   WF0303
|   C*
|   C                     MOVELAS04      WF0301
|   C                     MOVELAS08      WF0302
|   C                     MOVELAS10      WF0303
|   C                     MULT AS05      WCASH1
|   C                     MULT AS05      WCASH2
|   C                     Z-ADDWCASH1    WF0304
|   C                     Z-ADDWCASH2    WF0305
| 03-C                     END
| 02-C                     END
| 01-C                     END
|    C*
|    C                     Z-ADD*ZERO     WCASH1           *實際金額
|    C                     Z-ADD*ZERO     WCASH2           *預算金額
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R8900 ... LEVEL BREAK LR -- HAS DATA
     C*==============================================================*
12===C           R8900     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << TOTAL LINE DATA PREPARE >>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'GLS008'
|    C                     PARM 'GLR6C0P1'P008I1 10
|    C                     PARM $AR05     P008I2  1
|    C           PSIGN1    PARM           P008O1198
|    C           PSIGN2    PARM           P008O2198
|    C*
|    C                     WRITEPT01
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R8999 ... LEVEL BREAK LR -- NO DATA
     C*==============================================================*
08===C           R8999     BEGSR
|    C*
|    C                     EXSR R0100
|    C*
|    C                     WRITEPH01
|    C*
|    C                     WRITEPE09
|    C*
08===C                     ENDSR
     C*==============================================================*
** @W1
@0@1@2@3@4@5@6@7@8@9
