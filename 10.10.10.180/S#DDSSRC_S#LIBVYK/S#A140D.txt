     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : S#A140                                     *
     A*    PROGRAM NAME :程式基本資料維護                          *
     A*    AUTHOR       : A1165 SERMON                               *
     A*    CREATE DATE  : 85/08/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :程式安全管制系統                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      PRINT
     A                                      MSGLOC(22)
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF18(18)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<S#A140.1>'
     A                                  1 31'程式安全管制系統'
     A            $CHYMD         6Y 0O  1 71EDTCDE(Y)
     A                                  2 31'程式基本資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'程式編號:'
     A            DSP01     R        B  4 13REFFLD(SP01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  1'作業選擇:'
     A            DOPT           1Y 0B  5 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  5 17'( 1.新增'
     A                                  5 28'2.修改'
     A                                  5 37'4.刪除'
     A                                  5 46'5.查詢)'
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3=回主畫面'
     A                                 23 28'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                  1  2'<S#A140.2>'
     A                                  1 31'程式安全管制系統'
     A            $CHYMD         6Y 0O  1 71EDTCDE(Y)
     A                                  2 31'程式基本資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'程式編號:'
     A            DSP01     R        O  4 13REFFLD(SP01)
     A                                  5  1'作業選擇:'
     A            DOPT           1Y 0O  5 13EDTCDE(4)
     A                                  5 17'( 1.新增'
     A                                  5 28'2.修改'
     A                                  5 37'4.刪除'
     A                                  5 46'5.查詢)'
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A                                  7  1'程式名稱:'
     A            DSP02     R        B  7 13REFFLD(SP02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  1'程式類別:'
     A            DSP03     R        B  8 13REFFLD(SP03)
     A                                      VALUES('M' 'P' 'S')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 17'( M-主畫面P-主程式S-副程式)'
     A                                  9  1'備註欄１:'
     A            DSP04     R        B  9 13REFFLD(SP04)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  1'備註欄２:'
     A            DSP05     R        B 10 13REFFLD(SP05)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  1'備註欄３:'
     A            DSP06     R        B 11 13REFFLD(SP06)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F5 =參考資料'
     A                                 23 45'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
