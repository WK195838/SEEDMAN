     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : S#R420                                     *
     A*    PROGRAM NAME :依程式列印管制資料                        *
     A*    AUTHOR       : A1165 SERMON                               *
     A*    CREATE DATE  : 85/08/09                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :程式安全管制系統                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<S#R420>'
     A                                  1 31'程式安全管制系統'
     A            $CHYMD         6Y 0O  1 71EDTCDE(Y)
     A                                  2 30'依程式列印管制資料'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  6 26'程式編號　起:'
     A                                      DSPATR(HI)
     A            DSD02F    R        B  6 42REFFLD(SD02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 26'　　　　　迄:'
     A                                      DSPATR(HI)
     A            DSD02T    R        B  7 42REFFLD(SD02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  9 26'使用者編號起:'
     A                                      DSPATR(HI)
     A            DSD01F    R        B  9 42REFFLD(SD01)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 26'　　　　　迄:'
     A                                      DSPATR(HI)
     A            DSD01T    R        B 10 42REFFLD(SD01)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 17 26'執行環境:'
     A            $PENV          1A  B 17 38VALUES('0' '1' '2')
     A                                 17 40'(0-查詢 1-即時 2-批次)'
     A                                 18 26'列印份數:'
     A            $CPY           3Y 0B 18 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 19 38
     A  27                             21 26'***請準備８０格報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 26'***報表處理中，請稍候***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 23 45'F14 =報表列印'
     A                                 24 67'【泛太資訊】'
