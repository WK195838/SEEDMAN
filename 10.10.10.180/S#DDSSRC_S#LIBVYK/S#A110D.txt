     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : S#A110                                     *
     A*    PROGRAM NAME :使用者基本資料維護                        *
     A*    AUTHOR       : A1165 SERMON                               *
     A*    CREATE DATE  : 85/08/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :程式安全管制系統                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF18(18)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<S#A110.1>'
     A                                  1 31'程式安全管制系統'
     A            $CHYMD         6Y 0O  1 71EDTCDE(Y)
     A                                  2 30'使用者基本資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'使用者編號:'
     A            DSU01     R        B  4 15REFFLD(SU01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  1'作業選擇　:'
     A            DOPT           1Y 0B  5 15VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  5 17'( 1.新增'
     A                                  5 28'2.修改'
     A                                  5 37'4.刪除'
     A                                  5 46'5.查詢)'
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3=回主畫面'
     A                                 23 28'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            DNEW           1A  H
     A            RRN            4Y 0H
     A            DDEL           1A  B 12  2VALUES('D' ' ')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DSC02     R        B 12  6REFFLD(SC02)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSC03     R        B 12 28REFFLD(SC03)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSC04     R        B 12 17REFFLD(SC04)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<S#A110.2>'
     A                                  1 31'程式安全管制系統'
     A            $CHYMD         6Y 0O  1 71EDTCDE(Y)
     A                                  2 30'使用者基本資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'使用者編號:'
     A            DSU01     R        O  4 15REFFLD(SU01)
     A                                  5  1'作業選擇　:'
     A            DOPT           1Y 0O  5 15EDTCDE(4)
     A                                  5 17'( 1.新增'
     A                                  5 28'2.修改'
     A                                  5 37'4.刪除'
     A                                  5 46'5.查詢)'
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  1'使用者名稱:'
     A            DSU02     R        B  7 15REFFLD(SU02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7 26'授權者編號:'
     A            DSU03     R        O  7 40REFFLD(SU03)
     A                                  7 51'授權者名稱:'
     A            DSU03N    R        O  7 65REFFLD(SU02)
     A                                  8  1'所屬公司　:'
     A            DSU07     R        B  8 15REFFLD(SU07)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 26'所屬地區　:'
     A            DSU05     R        B  8 40REFFLD(SU05)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 51'所屬部門　:'
     A            DSU04     R        B  8 65REFFLD(SU04)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  1'備　　　註:'
     A            DSU06     R        B  9 15REFFLD(SU06)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 11  2'D'
     A                                      DSPATR(UL)
     A                                 11  5'管制公司'
     A                                      DSPATR(UL)
     A                                 11 16'管制地區'
     A                                      DSPATR(UL)
     A                                 11 27'管制部門'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3=回主畫面'
     A                                 23 27'F5=參考資料'
     A                                 23 43'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
