     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : S#S110                                     *
     A*    PROGRAM NAME :使用者基本資料全頁查詢                    *
     A*    AUTHOR       : A1165 SERMON                               *
     A*    CREATE DATE  : 85/08/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :程式安全管制系統                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            4Y 0H
     A            DOPT           1   B  6  2DSPATR(CS PC HI)
     A            DSU01     R        O  6  5REFFLD(SU01)
     A            DSU01N    R        O  6 18REFFLD(SU02)
     A            DSU03     R        O  6 31REFFLD(SU01)
     A            DSU03N    R        O  6 44REFFLD(SU02)
     A            DSU06     R        O  6 57REFFLD(SU06)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0160)
     A                                      SFLPAG(0016)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<S#S110.1>'
     A                                  1 31'程式安全管制系統'
     A            $CHYMD         6Y 0O  1 71EDTCDE(Y)
     A                                  2 28'使用者基本資料全頁查詢'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  2'S'
     A                                      DSPATR(UL)
     A                                  4  4'使用者編號'
     A                                      DSPATR(UL)
     A                                  4 17'使用者名稱'
     A                                      DSPATR(UL)
     A                                  4 30'授權者編號'
     A                                      DSPATR(UL)
     A                                  4 43'授權者名稱'
     A                                      DSPATR(UL)
     A                                  4 56'備註                 '
     A                                      DSPATR(UL)
     A            KSU01     R        B  5  5REFFLD(SU01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3=回主畫面'
     A                                 23 26'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
