     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : S#A130                                     *
     A*    PROGRAM NAME :程式管制資料維護                          *
     A*    AUTHOR       : A1165 SERMON                               *
     A*    CREATE DATE  : 85/08/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :程式安全管制系統                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<S#A130.1>'
     A                                  1 31'程式安全管制系統'
     A            $CHYMD         6Y 0O  1 71EDTCDE(Y)
     A                                  2 31'程式管制資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'程式編號　:'
     A                                      DSPATR(HI)
     A            DSD02     R        B  4 15REFFLD(SD02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  1'使用者範圍:'
     A                                      DSPATR(HI)
     A            DSD01F    R        B  5 15REFFLD(SD01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 26'-'
     A            DSD01T    R        B  5 28REFFLD(SD01)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6  1'作業選擇　:'
     A            DOPT           1Y 0B  6 15VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 17'( 1.新增'
     A                                  6 28'2.修改'
     A                                  6 37'4.刪除'
     A                                  6 46'5.查詢)'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3=回主畫面'
     A                                 23 28'F4=欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4Y 0O  9  2EDTCDE(3)
     A            DUSE           1A  B  9 10VALUES('Y' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            SD01      R        O  9 15
     A            SU02      R        O  9 27
     A            SD03      R        B  9 39VALUES('Y' ' ')
     A  31N60                               DSPATR(UL)
     A N31
     AO 60                                  DSPATR(PR)
     A            SD04      R        B  9 41VALUES('Y' ' ')
     A  31N61                               DSPATR(UL)
     A N31
     AO 61                                  DSPATR(PR)
     A            SD05      R        B  9 43VALUES('Y' ' ')
     A  31N62                               DSPATR(UL)
     A N31
     AO 62                                  DSPATR(PR)
     A            SD06      R        B  9 45VALUES('Y' ' ')
     A  31N63                               DSPATR(UL)
     A N31
     AO 63                                  DSPATR(PR)
     A            SD07      R        B  9 52VALUES('Y' ' ')
     A  31N64                               DSPATR(UL)
     A N31
     AO 64                                  DSPATR(PR)
     A            SD08      R        B  9 63VALUES('Y' ' ')
     A  31N65                               DSPATR(UL)
     A N31
     AO 65                                  DSPATR(PR)
     A            SD09      R        B  9 74VALUES('Y' ' ')
     A  31N66                               DSPATR(UL)
     A N31
     AO 66                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0130)
     A                                      SFLPAG(0013)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF10(10)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<S#A130.2>'
     A                                  1 31'程式安全管制系統'
     A            $CHYMD         6Y 0O  1 71EDTCDE(Y)
     A                                  2 31'程式管制資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'程式編號　:'
     A            DSD02     R        O  4 15REFFLD(SD02)
     A                                  5  1'使用者範圍:'
     A            DSD01F    R        O  5 15REFFLD(SD01)
     A                                  5 26'-'
     A            DSD01T    R        O  5 28REFFLD(SD01)
     A                                  6  1'作業選擇　:'
     A            DOPT           1Y 0O  6 15EDTCDE(4)
     A                                  6 17'( 1.新增'
     A                                  6 28'2.修改'
     A                                  6 37'4.刪除'
     A                                  6 46'5.查詢)'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            SP02      R        O  4 26
     A                                  4 59'程式類別:'
     A            SP03N          8O  O  4 71
     A                                  8  1'序號'
     A                                      DSPATR(UL)
     A                                  8  8'使用'
     A                                      DSPATR(UL)
     A                                  8 15'使用者編號／名稱    '
     A                                      DSPATR(UL)
     A                                  8 39'A'
     A                                      DSPATR(UL)
     A                                  8 41'U'
     A                                      DSPATR(UL)
     A                                  8 43'D'
     A                                      DSPATR(UL)
     A                                  8 45'I'
     A                                      DSPATR(UL)
     A            SP04      R        O  8 48DSPATR(UL)
     A            SP05      R        O  8 59DSPATR(UL)
     A            SP06      R        O  8 70DSPATR(UL)
     A*
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3=回主畫面'
     A                                 23 27'F10=重新授權'
     A                                 23 44'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
