     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : S#A150                                     *
     A*    PROGRAM NAME :系統安裝資料維護                          *
     A*    AUTHOR       : A1165 SERMON                               *
     A*    CREATE DATE  : 85/08/16                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :程式安全管制系統                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4Y 0H
     A            SS01      R        H
     A            SS02      R        O  6 14
     A            DSEL           1   B  6 30VALUES('Y' ' ')
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A  *DS3                                SFLLIN(0013)
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0030)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<S#A150.1>'
     A                                  1 31'程式安全管制系統'
     A            $CHYMD         6Y 0O  1 71EDTCDE(Y)
     A                                  2 31'系統安裝資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  5 13'系統名稱   '
     A                                      DSPATR(UL)
     A                                  5 27'已安裝'
     A                                      DSPATR(UL)
     A                                  5 43'系統名稱   '
     A                                      DSPATR(UL)
     A                                  5 57'已安裝'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3=回主畫面'
     A                                 24 67'【泛太資訊】'
