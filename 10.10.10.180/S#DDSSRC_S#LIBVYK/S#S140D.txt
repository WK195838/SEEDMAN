     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : S#S140                                     *
     A*    PROGRAM NAME :程式基本資料全頁查詢                      *
     A*    AUTHOR       : A1165 SERMON                               *
     A*    CREATE DATE  : 85/08/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :程式安全管制系統                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            4Y 0H
     A            DOPT           1   B  7  2DSPATR(CS PC HI)
     A            SP01      R        O  7  5
     A            SP02      R        O  7 17
     A            SP03N          8O  O  7 51
     A            SP04      R        O  8 17
     A            SP05      R        O  8 28
     A            SP06      R        O  8 39
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0007)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      SFLDROP(CF02)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<S#S140.1>'
     A                                  1 31'程式安全管制系統'
     A            $CHYMD         6Y 0O  1 71EDTCDE(Y)
     A                                  2 29'程式基本資料全頁查詢'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  2'S'
     A                                      DSPATR(UL)
     A                                  4  5'程式編號'
     A                                      DSPATR(UL)
     A                                  4 17'程式名稱                      '
     A                                      DSPATR(UL)
     A                                  4 50'程式類別'
     A                                      DSPATR(UL)
     A                                  5 17'備註欄１'
     A                                      DSPATR(UL)
     A                                  5 28'備註欄２'
     A                                      DSPATR(UL)
     A                                  5 39'備註欄３'
     A                                      DSPATR(UL)
     A            DSP01     R        B  6  5REFFLD(SP01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3=回主畫面'
     A                                 23 26'F2=備註說明'
     A                                 23 42'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
