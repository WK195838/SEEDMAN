      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : PUR0201                                       *
      * AUTHER       : A1491  EASON                                  *
      * CREATE DATE  : 97/10/30                                      *
      * UPDATE DATE  : 97/11/17 VINCENT                              *
      * SYSTEM       :                                               *
      * SUBSYSTEM    :採購系統                                     *
      * REMARK       :驗收明細表                                   *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
      ****************************************************************
     FINTXPF  IP  E           K        DISK
     FMANBPF  IF  E           K        DISK
     FMANCPF  IF  E           K        DISK
     FMANDPF  IF  E           K        DISK
     FMANEPF  IF  E           K        DISK
     FPA#BPF  IF  E           K        DISK
     FSCWK13  O   E           K        DISK
     FPUR020P O   E             39     PRINTER
      *
     ITX0
     I                                              TX07  L3
     I                                              TX06  L2
     I                                              TX11  L1
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$PRTID
     I                                      601 602 DCL01
     I                                      603 604 DCL02
     I                                      605 609 DCM02S
     I                                      610 614 DCM02E
     I                                      620 6250DCL07S
     I                                      626 6310DCL07E
     I                                      640 648 DCM03S
     I                                      650 658 DCM03E
     I                                      661 6680DATES
     I                                      671 6780DATEE
     I                                      679 679 DOPT
      *==============================================================*
      *                    KEY LIST
      *==============================================================*
     C           KEYND     KLIST
     C                     KFLD           DCL01
     C                     KFLD           DCL02
     C           KEYNC     KLIST
     C                     KFLD           DCL01
     C                     KFLD           TX11
     C           KEYNE     KLIST
     C                     KFLD           DCL01
     C                     KFLD           DCL02
     C                     KFLD           TX12
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C  N10                EXSR RTN010
     C*
     C   39                EXSR RTN900
      *
     C                     EXSR RTN100
      *
     C*
     CL1 10                EXSR RTNL1
     CL2 10                EXSR RTNL2
     CL3 10                EXSR RTNL3
     CLR                   EXSR RTNLR
     C*==============================================================*
     C*          RTN010....GET HEADING
     C*==============================================================*
01===C           RTN010    BEGSR
| 01-C           DOPT      IFEQ '1'
|   C                     SETON                     71
|   C                     SETOF                     72
|   C                     SETOF                     73
| 01-C                     ENDIF
|     *
| 02-C           DOPT      IFEQ '2'
|   C                     SETOF                     71
|   C                     SETON                     72
|   C                     SETOF                     73
| 02-C                     ENDIF
|     *
| 03-C           DOPT      IFEQ '3'
|   C                     SETOF                     71
|   C                     SETOF                     72
|   C                     SETON                     73
| 03-C                     ENDIF
|     *
|     * GET公司別
|    C                     MOVELDCL01     W1301
|    C           DCL01     CHAIN#B0                  40
|    C   40                MOVEL*BLANK    D#B03
|    C  N40                MOVEL#B03      D#B03
|     * GET通路
|    C                     MOVELDCL02     W1302
|    C           KEYND     CHAINND0                  40
|    C   40                MOVE *BLANK    DND04
|    C  N40                MOVE ND04      DND04
|     *
|    C                     WRITEPH1
|     *
|    C                     SETOF                     39
|    C                     SETON                     10
|    C                     SETON                     303132
|     *
|    C                     Z-ADD0         Q1
|    C                     Z-ADD0         Q2
|    C                     Z-ADD0         Q3
|    C                     Z-ADD0         Q4
|    C                     Z-ADD0         Q5
|    C                     Z-ADD0         Q6
|    C                     Z-ADD0         Q7
|    C                     Z-ADD0         Q8
|    C                     Z-ADD0         Q0T1
|    C                     Z-ADD0         Q0T2
|    C                     Z-ADD0         Q0T3
|    C                     Z-ADD0         Q0T4
|     *
01===C                     ENDSR
      *==============================================================*
      *          RTN100....DETAIL
      *==============================================================*
02===C           RTN100    BEGSR
|     *
|    C           TX07      CHAINNB0                  40
|    C   40                MOVE *BLANK    NB03
|    C                     MOVELTX07      W1303
|    C                     MOVELNB03      W1304
|    C                     Z-ADDTX06      W1305
|     *
|    C                     CALL 'P31'
|    C                     PARM TX06      P31I1   80
|    C                     PARM '2'       P3112   1
|    C                     PARM '1'       P31I3   1
|    C                     PARM '3'       P31I4   1
|    C                     PARM '1'       P31I5   1
|    C           LCL07     PARM           P31IO   80
|     *
|    C           KEYNC     CHAINNC0                  40
|    C   40                MOVE *BLANK    NC05
|    C                     MOVELTX11      W1306
|    C                     MOVELNC05      W1307
|     *
|    C           KEYNE     CHAINNE0                  40
|    C   40                MOVE *BLANK    NE05
|    C                     MOVELTX12      W1308
|    C                     MOVELNE05      W1309
|     *
|    C                     Z-ADDTX18      W1311
|     *
| 02-C           TX03      IFEQ 'B1'
|   C                     Z-SUBTX15      W1310
|   C                     Z-SUBTX19      W1312
|   C                     Z-SUBTX19      W1313
|   C                     SUB  TX19A     W1313
| 02*C                     ELSE
|   C                     Z-ADDTX15      W1310
|   C                     Z-ADDTX19      W1312
|   C                     Z-ADDTX19      W1313
|   C                     ADD  TX19A     W1313
| 02-C                     ENDIF
|     *
     C                     Z-ADDW1310     PTX15
     C                     Z-ADDW1312     PTX19
     C                     Z-ADDW1313     PTX19X
|     *
|    C                     MOVELTX05      W1314
|    C                     MOVELTX08      W1315
|    C                     WRITEW13
|     *
|    C                     WRITEPD1
|    C                     SETOF                     303132
|     *交貨日
|    C                     ADD  W1310     Q0T1
|    C                     ADD  W1312     Q1
|    C                     ADD  W1313     Q2
|     *供應商
|    C                     ADD  W1310     Q0T2
|    C                     ADD  W1312     Q3
|    C                     ADD  W1313     Q4
|     *總計
|    C                     ADD  W1310     Q0T3
|    C                     ADD  W1312     Q5
|    C                     ADD  W1313     Q6
|     *產品
|    C                     ADD  W1310     Q0T4
|    C                     ADD  W1312     Q7
|    C                     ADD  W1313     Q8
|     *
02===C                     ENDSR
      *==============================================================*
      *          RTN900....OVERFLOW
      *==============================================================*
03===C           RTN900    BEGSR
|    C*
|    C                     WRITEPE1
|    C                     WRITEPH1
|    C                     SETON                     303132
|    C                     SETOF                     39
|    C*
03===C                     ENDSR
      *==============================================================*
      *          RTNL1 ...產品改變
      *==============================================================*
04===C           RTNL1     BEGSR
|     *
|    C                     SETON                     32
|    C                     WRITEPD5
|     *
|    C                     Z-ADD0         Q0T4
|    C                     Z-ADD0         Q7
|    C                     Z-ADD0         Q8
|     *
04===C                     ENDSR
      *==============================================================*
      *          RTNL2 ...到貨運日改變
      *==============================================================*
05===C           RTNL2     BEGSR
|     *
|    C                     SETON                     3132
|    C                     WRITEPD2
|     *
|    C                     Z-ADD0         Q0T1
|    C                     Z-ADD0         Q1
|    C                     Z-ADD0         Q2
|     *
05===C                     ENDSR
      *==============================================================*
      *          RTNL3 ...供應商改變
      *==============================================================*
06===C           RTNL3     BEGSR
|     *
|    C                     SETON                     313032
|    C                     WRITEPD3
|     *
|    C                     Z-ADD0         Q0T2
|    C                     Z-ADD0         Q3
|    C                     Z-ADD0         Q4
|     *
06===C                     ENDSR
      *==============================================================*
      *          RTNLR ....(NO DATA)
      *==============================================================*
07===C           RTNLR     BEGSR
| 01-C           *IN10     IFEQ '0'
|   C                     WRITEPH1
|   C                     WRITEPE9
| 01-C                     END
|    C                     WRITEPD4
|    C                     WRITEPE2
|    C                     Z-ADD0         Q0T3
|    C                     Z-ADD0         Q5
|    C                     Z-ADD0         Q6
07===C                     ENDSR
