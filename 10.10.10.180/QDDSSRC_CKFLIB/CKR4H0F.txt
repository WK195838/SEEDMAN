     A*****************************************************************
     A*       ##  CKR4H0F 支票登記簿
     A*****************************************************************
     A                                      JDFTVAL
     A          R CKR4H0R                   JFILE(CKHAPF CKHFPF)
     A          J                           JOIN(CKHAPF CKHFPF)
     A                                      JFLD(HA02 HF02)
     A                                      JFLD(HA03 HF03)
     A**** CKHAPF :票據明細檔
     A            HA01      R               COLHDG('性質')
     A*                                     '1'  -> 應收票據
     A*                                     '2'  -> 應付票據
     A            HA02      R               COLHDG('公司別')
     A            HA03      R               COLHDG('票據號碼')
    .A            HA04      R               COLHDG('客戶／廠商代號')
     A            HA05      R               COLHDG('票據類別')
     A*                                     '1'  -> 本票
     A*                                     '2'  -> 客票
     A*                                     '3'  -> 支票
     A*                                     '4'  -> 保證票
     A*                                     '5'  -> 商業本票
     A            HA06      R               COLHDG('收／開票日')
     A            HA07      R               COLHDG('到期日')
     A            HA08      R               COLHDG('付款銀行')
     A            HA09      R               COLHDG('付款帳號')
     A            HA10      R               COLHDG('台幣金額')
     A            HA11      R               COLHDG('預計兌現日')
     A            HA12      R               COLHDG('收款單號')
    .A            HA13      R               COLHDG('業務人員')
    .A            HA14      R               COLHDG('業務區')
     A            HA15      R               COLHDG('處理日期')
     A            HA16      R               COLHDG('處理帳號')
     A            HA17      R               COLHDG('票據狀況')
     A*                                     '0'  -> 作廢
     A*                                     '1'  -> 庫存
     A*                                     '2'  -> 託收
     A*                                     '3'  -> 貼現
     A*                                     '4'  -> 兌現
     A*                                     '5'  -> 退票
     A            HA18      R               COLHDG('傳票號碼')
     A            HA19      R               COLHDG('匯率１')
     A            HA20      R               COLHDG('匯率２')
     A            HA23      R               COLHDG('財務用途')
     A            HA24      R               COLHDG('票據區')
     A*                                     '1'  -> 本地
     A*                                     '2'  -> 外地
     A            HA25      R               COLHDG('託收方式')
     A*                                     '1'  -> 一般
     A*                                     '2'  -> 送款
     A            HA26      R               COLHDG('對帳記號')
     A*                                     'Y'  -> 已對帳
     A*                                     'N'  -> 未對帳
     A            HA27      R               COLHDG('入帳類別')
     A*                                     'Y'  -> 須入帳
     A*                                     'N'  -> 不入帳
     A            HA28      R               COLHDG('貨款記號')
     A*                                     'Y'  -> 貨款
     A*                                     'N'  -> 非貨款
     A            HA32      R               COLHDG('備註')
     A*
     A**** CKHFPF :開票登錄檔
     A            HF71      R               COLHDG('開票序號')
     A            HF72      R               COLHDG('列印日期')
     A            HF73      R               COLHDG('列印序號')
     A            HF77      R               COLHDG('抬頭')
     A****
     A          K HA01
     A          K HA02
     A          K HA03
