     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    PROGRAM-ID   : GLA160                                    */
     /*    PROGRAM NAME : 追溯分攤                                */
     /*    AUTHOR       : A1087  JOYCE                              */
     /*    CREATE DATE  : 1992.12.11                                */
     /*    UPDATE DATE  :                                           */
     /*    SYSTEM       : 泛太總帳                                */
     /***************************************************************/
             PGM

             DCL        VAR(&DAP01)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&DAP02S)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DAP02E)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DAP03S)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&DAP03E)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&DAP06F)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DOP)      TYPE(*CHAR) LEN(1)

             DCL        VAR(&$PENV)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTCD)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6401I)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6403I)   TYPE(*DEC)  LEN(3 0)

             CHGVAR     VAR(&DAP01)    VALUE(%SST(*LDA 501 2))
             CHGVAR     VAR(&DAP06F)   VALUE(%SST(*LDA 550 8))
             CHGVAR     VAR(&DAP02S)   VALUE(%SST(*LDA 523 8))
             CHGVAR     VAR(&DAP02E)   VALUE(%SST(*LDA 531 8))
             CHGVAR     VAR(&DAP03S)   VALUE(%SST(*LDA 539 2))
             CHGVAR     VAR(&DAP03E)   VALUE(%SST(*LDA 541 2))
             CHGVAR     VAR(&DOP)      VALUE(%SST(*LDA 543 1))

 /*****   CRTPF -> QTEMP  *****/
             CPYF       FROMFILE(GLWF05) TOFILE(QTEMP/GLWF05) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/GLWF05))
             OVRDBF     FILE(GLWF05) TOFILE(QTEMP/GLWF05)

             OVRDBF     FILE(GLAPPF) SHARE(*YES)
             OPNQRYF    FILE((GLAPPF)) QRYSLT(' AP01 *EQ ("' *CAT +
                          &DAP01 *CAT '") & Ap02 *EQ %RANGE("' +
                          *CAT &DAP02S *CAT '" "' *CAT &DAP02E *CAT +
                          '") & ap03 *eq %range("' *CAT &DAP03S +
                          *CAT '" "' *CAT &DAP03E *CAT '") & (ap05 +
                          *eq "2") & ap06C *le ("' *CAT &DAP06F +
                          *CAT '")') KEYFLD(*FILE) MAPFLD((AP06C +
                          AP06 *CHAR 8))

             OVRPRTF    FILE(GLA160P) PAGESIZE(*N 198) CPI(15)
             CALL       GLA1601 PARM('1')

             IF         COND(&DOP *EQ '2') THEN(DO)
             OVRDBF     FILE(GLAHLF01) TOFILE(GLAHLF10) SHARE(*YES)
             OPNQRYF    FILE((GLAHLF10)) OPTION(*ALL) +
                          FORMAT(GLAHLF01) QRYSLT(' (ah01 *eq "' +
                          *CAT &DAP01 *CAT '") & (ah18 *eq " ")') +
                          KEYFLD((AH01) (AH02) (AH03))

             CALL       GLA1701
             CALL       GLA1601 PARM('2')
             CLOF       OPNID(GLAHLF10)
             ENDDO

             CLOF       OPNID(GLAPPF)
             DLTOVR     FILE(*ALL)

             CHGVAR     VAR(&$PENV)  VALUE('1')
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (217 2)) RTNVAR(&$PRTCD)
             CHGVAR     VAR(&P6401I) VALUE('GLA160P')
             CHGVAR     VAR(&P6403I) VALUE(&$CPY)
             CALL       PGM(P64) PARM(&P6401I &$PENV &P6403I &$PRTCD)

 /*****  RECLAIM RESOURCES  *****/
             RCLRSC
             ENDPGM
