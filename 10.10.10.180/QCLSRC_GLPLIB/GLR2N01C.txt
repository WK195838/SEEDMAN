     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    PROGRAM ID   : GLR2N0C                                   */
     /*    PROGRAM NAME :明細帳列印－區間                         */
     /*    AUTHOR       : A1087  JOYCE                              */
     /*    CREATE DATE  : 1992.11.30                                */
     /*    UPDATE DATE  :                                           */
     /*    SYSTEM       : 泛太總帳                                */
     /***************************************************************/
             PGM

             DCL        VAR(&$PENV)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTCD)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6401I)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6403I)   TYPE(*DEC)  LEN(3 0)

             DCL        VAR(&DAH01)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&DAH04S)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DAH04E)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DAH26F)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&DAH26T)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&DAH10F)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DAH10T)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DAH25F)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DAH25T)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DK)       TYPE(*CHAR) LEN(1)

     /*****  CPYF       --> GLWF10                              *****/
             CPYF       FROMFILE(GLWF10) TOFILE(QTEMP/GLWF10) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/GLWF10))
             OVRDBF     FILE(GLWF10) TOFILE(QTEMP/GLWF10)
     /*-------------------------------------------------------------*/
             CHGVAR     VAR(&DAH01)    VALUE(%SST(*LDA 501  2))
             CHGVAR     VAR(&DAH04S)   VALUE(%SST(*LDA 503  8))
             CHGVAR     VAR(&DAH04E)   VALUE(%SST(*LDA 591  8))
             CHGVAR     VAR(&DAH26F)   VALUE(%SST(*LDA 511 10))
             CHGVAR     VAR(&DAH26T)   VALUE(%SST(*LDA 521 10))
             CHGVAR     VAR(&DAH10F)   VALUE(%SST(*LDA 545  8))
             CHGVAR     VAR(&DAH10T)   VALUE(%SST(*LDA 553  8))
             CHGVAR     VAR(&DAH25F)   VALUE(%SST(*LDA 575  8))
             CHGVAR     VAR(&DAH25T)   VALUE(%SST(*LDA 583  8))
             CHGVAR     VAR(&DK)       VALUE(%SST(*LDA 599  1))

             OVRDBF     FILE(GLAHLF02) SHARE(*YES)
             OPNQRYF    FILE((GLAHLF02)) QRYSLT(' AH01 *EQ "' *CAT +
                          &DAH01 *CAT '"  & AH04 *EQ %RANGE("' *CAT +
                          &DAH04S *CAT '" "' *CAT &DAH04E *CAT '") +
                          & AH25C *EQ %RANGE("' *CAT &DAH25F *CAT +
                          '" "' *CAT &DAH25T *CAT '") & AH10C *EQ +
                          %RANGE("' *CAT &DAH10F *CAT '" "' *CAT +
                          &DAH10T *CAT '") & AH26  *EQ %RANGE("' +
                          *CAT &DAH26F *CAT '" "' *CAT &DAH26T *CAT +
                          '") & (AH18  *EQ "V")') KEYFLD(*FILE) +
                          MAPFLD((AH25C AH25 *CHAR 8) (AH10C AH10 +
                          *CHAR 8))

             OVRPRTF    FILE(GLR2N0P) PAGESIZE(*N 198) CPI(15) +
                          OVRFLW(55)

             OVRPRTF    FILE(GLR2N1P) PAGESIZE(*N 198) CPI(15)

             IF         COND(&DK = '1' *OR &DK = '3') THEN(DO)
                        CALL       GLR2N01
                        CALL       GLR2N03
             ENDDO
             CLRPFM     FILE(QTEMP/GLWF10)
             POSDBF     OPNID(GLAHLF02) POSITION(*START)
             IF         COND(&DK = '2' *OR &DK = '3') THEN(DO)
                        CALL       GLR2N02
                        CALL       GLR2N04
             ENDDO

             CLOF       OPNID(GLAHLF02)
             DLTOVR     FILE(GLAHLF02)

             RTVDTAARA  DTAARA(*LDA (219 1)) RTNVAR(&$PENV)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (217 2)) RTNVAR(&$PRTCD)
             CHGVAR     VAR(&P6403I) VALUE(&$CPY)

             IF         COND(&DK = '1' *OR &DK = '3') THEN(DO)
             CHGVAR     VAR(&P6401I) VALUE('GLR2N0P')
             CALL       PGM(P64) PARM(&P6401I &$PENV &P6403I &$PRTCD)
             ENDDO

             IF         COND(&DK = '2' *OR &DK = '3') THEN(DO)
             CHGVAR     VAR(&P6401I) VALUE('GLR2N1P')
             CALL       PGM(P64) PARM(&P6401I &$PENV &P6403I &$PRTCD)
             ENDDO

             ENDPGM
