     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    PROGRAM-ID   : GLR2E0                                    */
     /*    PROGRAM NAME : 比較損益表                              */
     /*    AUTHOR       :  A1149    MAY                             */
     /*    CREATE DATE  :  1992.12.08                               */
     /*    UPDATE DATE  :                                           */
     /*    SYSTEM       : 泛太總帳                                */
     /***************************************************************/
             PGM
             DCL        VAR(&$AI01S)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&$AI01E)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&$AI04S)   TYPE(*CHAR) LEN(6)
             DCL        VAR(&$AI04E)   TYPE(*CHAR) LEN(6)
             DCL        VAR(&$YEARS)   TYPE(*CHAR) LEN(4)
             DCL        VAR(&$YEARE)   TYPE(*CHAR) LEN(4)
             DCL        VAR(&$AR05)   TYPE(*CHAR)  LEN(1)
             DCL        VAR(&LIB)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE)  TYPE(*CHAR) LEN(10)

             DCL        VAR(&$PENV)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTCD)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6401I)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6403I)   TYPE(*DEC)  LEN(3 0)

             CHGVAR     VAR(&$AI01S)   VALUE(%SST(*LDA 519 2))
             CHGVAR     VAR(&$AI01E)   VALUE(%SST(*LDA 521 2))
             CHGVAR     VAR(&$AI04S)   VALUE(%SST(*LDA 523 6))
             CHGVAR     VAR(&$AI04E)   VALUE(%SST(*LDA 529 6))
             CHGVAR     VAR(&$YEARS)   VALUE(%SST(*LDA 535 4))
             CHGVAR     VAR(&$YEARE)   VALUE(%SST(*LDA 539 4))
             CHGVAR     VAR(&$AR05)    VALUE(%SST(*LDA 543 1))

             CHGVAR     VAR(&LIB)   VALUE('GLFLIB')

             CLRPFM     FILE(GLR2E0W1)  /*損益表EXCEL */
             CLRPFM     FILE(GLR2E1W1)  /*科目明細表EXCEL */

      /*損益表工作檔GLWF03 */
             CPYF       FROMFILE(GLWF03) TOFILE(QTEMP/GLWF03) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/GLWF03))
             OVRDBF     FILE(GLWF03) TOFILE(QTEMP/GLWF03)

      /*科目明細表工作檔GLWF04 */
             CPYF       FROMFILE(GLWF04) TOFILE(QTEMP/GLWF04) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/GLWF04))
             OVRDBF     FILE(GLWF04) TOFILE(QTEMP/GLWF04)
      /******/

             OVRDBF     FILE(GLAILF02) SHARE(*YES)
             OPNQRYF    FILE((GLAILF02)) QRYSLT(' AI01 *EQ +
                          %RANGE("' *CAT &$AI01S *CAT '" "' *CAT +
                          &$AI01E *CAT '") & AI04 *EQ %RANGE("' +
                          *CAT &$AI04S *CAT '" "' *CAT &$AI04E +
                          *CAT '")  & AI02 *EQ %RANGE(' *CAT +
                          &$YEARS *CAT ' ' *CAT &$YEARE *CAT +
                          ')') KEYFLD(*FILE)

    /*中英格式判斷*/
             IF         COND(&$AR05 *EQ '1') THEN(DO)
             OVRPRTF    FILE(GLR2E0P1) PAGESIZE(*N 156) CPI(12)
             OVRPRTF    FILE(GLR2E1P1) PAGESIZE(*N 156) CPI(12)
                        ENDDO

             IF         COND(&$AR05 *EQ '2') THEN(DO)
             OVRPRTF    FILE(GLR2E0P1) TOFILE(GLR2E0P2) PAGESIZE(*N +
                          156) CPI(12)
             OVRPRTF    FILE(GLR2E1P1) TOFILE(GLR2E1P2) PAGESIZE(*N +
                          156) CPI(12)
                        ENDDO

                        CALL GLR2E01

             CLOF       OPNID(GLAILF02)
             DLTOVR     FILE(*ALL)
           /********/
             RTVDTAARA  DTAARA(*LDA (219 1)) RTNVAR(&$PENV)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (217 2)) RTNVAR(&$PRTCD)
             CHGVAR     VAR(&P6403I) VALUE(&$CPY)

             IF         COND(&$AR05 = '1' ) THEN(DO)
             CHGVAR     VAR(&P6401I) VALUE('GLR2E0P1')
             CALL       PGM(P64) PARM(&P6401I &$PENV &P6403I &$PRTCD)
             CHGVAR     VAR(&P6401I) VALUE('GLR2E1P1')
             CALL       PGM(P64) PARM(&P6401I &$PENV &P6403I &$PRTCD)
             ENDDO
           /*******/
             IF         COND(&$AR05 = '2' ) THEN(DO)
             CHGVAR     VAR(&P6401I) VALUE('GLR2E0P2')
             CALL       PGM(P64) PARM(&P6401I &$PENV &P6403I &$PRTCD)
             CHGVAR     VAR(&P6401I) VALUE('GLR2E1P2')
             CALL       PGM(P64) PARM(&P6401I &$PENV &P6403I &$PRTCD)
             ENDDO

             /*將GLR2E0P1轉換成CSV FILE AND FTP TO PC */
             CHGVAR     VAR(&FILE) VALUE('GLR2E0W1')
             CALL       PGM(ECL100C) PARM(&LIB &FILE)

             /*將GLR2E1P1轉換成CSV FILE AND FTP TO PC */
             CHGVAR     VAR(&FILE) VALUE('GLR2E1W1')
             CALL       PGM(ECL100C) PARM(&LIB &FILE)

             ENDPGM
