     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    PROGRAM-ID   : GLR2C0                                    */
     /*    PROGRAM NAME : 現金流量表                              */
     /*    AUTHOR       : A1087  JOYCE                              */
     /*    CREATE DATE  : 1993.01.09                                */
     /*    UPDATE DATE  :                                           */
     /*    SYSTEM       : 泛太總帳                                */
     /***************************************************************/
             PGM
             DCL        VAR(&DAH01S)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&DAH01E)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&DAH05S)   TYPE(*CHAR) LEN(20)
             DCL        VAR(&DAH05E)   TYPE(*CHAR) LEN(20)
             DCL        VAR(&WAH10S)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&WAH10E)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DAR05)    TYPE(*CHAR) LEN(1)

             DCL        VAR(&$PENV)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTCD)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6401I)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6403I)   TYPE(*DEC)  LEN(3 0)

             CHGVAR     VAR(&DAH01S)   VALUE(%SST(*LDA 556 2))
             CHGVAR     VAR(&DAH01E)   VALUE(%SST(*LDA 558 2))
             CHGVAR     VAR(&DAH05S)   VALUE(%SST(*LDA 561 20))
             CHGVAR     VAR(&DAH05E)   VALUE(%SST(*LDA 581 20))
             CHGVAR     VAR(&WAH10S)   VALUE(%SST(*LDA 533 8))
             CHGVAR     VAR(&WAH10E)   VALUE(%SST(*LDA 541 8))
             CHGVAR     VAR(&DAR05)    VALUE(%SST(*LDA 549 1))

      /*損益工作檔GLWF03 */
             CPYF       FROMFILE(GLWF03) TOFILE(QTEMP/GLWF03) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/GLWF03))
             OVRDBF     FILE(GLWF03) TOFILE(QTEMP/GLWF03)
      /******/

             OVRDBF     FILE(GLAHLF07) SHARE(*YES)
             OPNQRYF    FILE((GLAHLF07)) QRYSLT(' Ah01 *EQ %RANGE("' +
                          *CAT &DAH01S *CAT '" "' *CAT &DAH01E *CAT +
                          '") & AH05 *EQ %range("' *CAT &DAH05S +
                          *CAT '" "' *CAT &DAH05E *CAT '") & ah10 +
                          *eq %range(' *CAT &WAH10S *CAT ' ' *CAT +
                          &WAH10E *CAT ')') KEYFLD(*FILE)

    /*中英格式判斷*/
             IF         COND(&DAR05 *EQ '2') THEN(DO)
             OVRPRTF    FILE(GLR2C0P1) TOFILE(GLR2C0P2)
                        ENDDO

                        CALL GLR2C01

             CLOF       OPNID(GLAHLF07)
             DLTOVR     FILE(*ALL)
           /********/
             RTVDTAARA  DTAARA(*LDA (219 1)) RTNVAR(&$PENV)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (217 2)) RTNVAR(&$PRTCD)
             CHGVAR     VAR(&P6403I) VALUE(&$CPY)

             IF         COND(&DAR05 *EQ '1') THEN(DO)
             CHGVAR     VAR(&P6401I) VALUE('GLR2C0P1')
             CALL       PGM(P64) PARM(&P6401I &$PENV &P6403I &$PRTCD)
                        ENDDO

             IF         COND(&DAR05 *EQ '2') THEN(DO)
             CHGVAR     VAR(&P6401I) VALUE('GLR2C0P2')
             CALL       PGM(P64) PARM(&P6401I &$PENV &P6403I &$PRTCD)
                        ENDDO

             ENDPGM
