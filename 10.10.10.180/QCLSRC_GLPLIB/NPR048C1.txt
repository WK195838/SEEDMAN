  /* M001  試飲量要減試飲收回量  */
  /* M004 M006D 已作迴轉的傳票，不計算COMMITMENT */
  /* M006  增加促銷案號傳票明細檔*/
             PGM
             DCL        VAR(&DND01)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&DCOMP)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$YEAR)   TYPE(*CHAR) LEN(4)
             DCL        VAR(&$YY)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&DND03S)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&DND03E)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&DND04S)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&DND04E)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&WAH10S)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&WAH10E)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DCON)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&$USER)     TYPE(*CHAR) LEN(10)

             DCL        VAR(&$PENV)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTCD)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6401I)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6403I)   TYPE(*DEC)  LEN(3 0)

             CHGVAR     VAR(&$USER)   VALUE(%SST(*LDA 101 10))
             CHGVAR     VAR(&DND01)   VALUE(%SST(*LDA 501 2))
             CHGVAR     VAR(&DCOMP)   VALUE(%SST(*LDA 501 1))
             CHGVAR     VAR(&$YEAR)   VALUE(%SST(*LDA 535 4))
             CHGVAR     VAR(&$YY)   VALUE(%SST(*LDA 537 2))
             CHGVAR     VAR(&DND03S)   VALUE(%SST(*LDA 541 2))
             CHGVAR     VAR(&DND03E)   VALUE(%SST(*LDA 543 2))
             CHGVAR     VAR(&DND04S)   VALUE(%SST(*LDA 545 2))
             CHGVAR     VAR(&DND04E)   VALUE(%SST(*LDA 547 2))
             CHGVAR     VAR(&WAH10S)   VALUE(%SST(*LDA 551 8))
             CHGVAR     VAR(&WAH10E)   VALUE(%SST(*LDA 561 8))
             CHGVAR     VAR(&DCON)     VALUE(%SST(*LDA 569 1))

      /******/

             CPYF       FROMFILE(NPWF01 ) TOFILE(QTEMP/NPWF01 ) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/NPWF01 ))
             OVRDBF     FILE(NPWF01 ) TOFILE(QTEMP/NPWF01 ) SHARE(*YES)
             CRTDUPOBJ  OBJ(NPWF01L1) FROMLIB(GLALIB) OBJTYPE(*ALL) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPD2104 CPF2130)
             OVRDBF   FILE(NPWF01L1) TOFILE(QTEMP/NPWF01L1)  +
                      SHARE(*YES)
 /* M003A */
             CPYF       FROMFILE(NPWF07) TOFILE(QTEMP/NPWF07) +
                          CRTFILE(*YES)
             MONMSG    MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/NPWF07))
             OVRDBF   FILE(NPWF07) TOFILE(QTEMP/NPWF07) SHARE(*YES)
 /* M006A */
             CPYF       FROMFILE(NPWF08) TOFILE(QTEMP/NPWF08) +
                          CRTFILE(*YES)
             MONMSG    MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/NPWF08))
             OVRDBF   FILE(NPWF08) TOFILE(QTEMP/NPWF08) SHARE(*YES)
             CRTDUPOBJ  OBJ(NPWF08L1) FROMLIB(GLALIB) OBJTYPE(*ALL) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPD2104 CPF2130)
             OVRDBF   FILE(NPWF08L1) TOFILE(QTEMP/NPWF08L1)  +
                      SHARE(*YES)
 /*M003A*/
             CPYF       FROMFILE(NPWF09) TOFILE(QTEMP/NPWF09) +
                          CRTFILE(*YES)
             MONMSG    MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/NPWF09))
             OVRDBF   FILE(NPWF09) TOFILE(QTEMP/NPWF09) SHARE(*YES)
             CRTDUPOBJ  OBJ(NPWF09L1) FROMLIB(GLALIB) OBJTYPE(*ALL) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPD2104 CPF2130)
             OVRDBF   FILE(NPWF09L1) TOFILE(QTEMP/NPWF09L1)  +
                      SHARE(*YES)
 /*M003A*/
             OVRDBF     FILE(NPNDPF) SHARE(*YES)
             OVRDBF     FILE(APUXLF03) SHARE(*YES)
             OVRDBF     FILE(GLAHLF13) SHARE(*YES)
             OPNQRYF    FILE((NPNDPF)) QRYSLT(' ND01 *EQ "' *CAT +
                          &DND01 *CAT '"  & ND02 *EQ ' *CAT &$YEAR +
                          *CAT '  & ND03 *EQ %RANGE("' *CAT &DND03S +
                          *CAT '" "' *CAT &DND03E *CAT '") & ND04 +
                          *EQ %RANGE("' *CAT &DND04S *CAT '" "' +
                          *CAT &DND04E *CAT '") ') KEYFLD(*FILE)
             OPNQRYF    FILE((APUXLF03)) QRYSLT(' UX01 *EQ "' *CAT +
                          &DND01 *CAT '"  & UX05 *EQ ' *CAT &$YEAR +
                          *CAT '  & UX06 *EQ %RANGE("' *CAT &DND03S +
                          *CAT '" "' *CAT &DND03E *CAT '") & UX07 +
                          *EQ %RANGE("' *CAT &DND04S *CAT '" "' +
                          *CAT &DND04E *CAT '") & UX15 *EQ " " &  +
                          UX22 *NE "V" ') KEYFLD(*FILE)
             OPNQRYF    FILE((GLAHLF13)) QRYSLT(' AH01 *EQ "' *CAT +
                          &DND01 *CAT '"  & MAH10 *EQ %RANGE("' +
                          *CAT &WAH10S *CAT '" "' *CAT &WAH10E *CAT +
                          '")  & (mAH04 *EQ "6108") +
                          & Ah20 *ne "V" ') KEYFLD(*FILE) +
                          MAPFLD((MAH04 '%SST(AH04 1 4)' *CHAR 4) +
                          (MAH10 AH10 *CHAR 8))
             OVRDBF     FILE(SOSCLF06) SHARE(*YES)
             OPNQRYF    FILE((SOSCLF06)) QRYSLT('SC01B *EQ "' +
                          *CAT &DCOMP *CAT '" & MSC39 *EQ +
                          %RANGE("' *CAT &WAH10S *CAT '" "' *CAT +
                          &WAH10E *CAT '") & (SC03 *EQ "S4") +
                          & (SC13 *EQ "V") & (SC35 *EQ "N") & SC01B +
                          *NE "K" ') KEYFLD(*FILE) +
                          MAPFLD((MSC39 SC39 *CHAR 8) (SC01B +
                          '%SST(SC01 1 1 )' *CHAR 1))
  /* M001A */
             OVRDBF     FILE(SOSNPF) SHARE(*YES)
             OPNQRYF    FILE((SOSNPF)) QRYSLT('SN01B *EQ "' +
                          *CAT &DCOMP *CAT '" & MSN06 *EQ +
                          %RANGE("' *CAT &WAH10S *CAT '" "' *CAT +
                          &WAH10E *CAT '") & (SN06 *NE 0) & (SN10 +
                          *NE "T") & (MSN02 *EQ "R3") & SN01B *NE +
                          "K" ') MAPFLD((MSN06 SN06 *CHAR 8) (SN01B +
                          '%SST(SN01 1 1 )' *CHAR 1) (MSN02 +
                          '%SST(SN02 3 2)' *CHAR 2))

                        CALL NPR0481
                        CALL NPR0483
   /****/
             IF         COND(&DCON = '1')  THEN(DO)
             OVRPRTF    FILE(NPR048AP) PAGESIZE(*N 198) CPI(15) +
                          USRDTA('預算比較')
                        CALL NPR0484
             CHGVAR     VAR(&P6401I) VALUE('NPR048AP')
             ENDDO

             IF         COND(&DCON = '2')  THEN(DO)
             OVRPRTF    FILE(NPR048BP) PAGESIZE(*N 198) CPI(15) +
                          USRDTA('預算比較')
 /* M003A */
             OVRPRTF    FILE(NPR048CP) PAGESIZE(*N 198) CPI(15) +
                          USRDTA(' COMMIT ')
             OVRPRTF    FILE(NPR048DP) PAGESIZE(*N 198) CPI(15) +
                          USRDTA('品牌預算')
 /* M003A */
                        CALL NPR0482
                        CALL NPR0487
                        CALL NPR0485
                        CALL NPR0486
                        CALL NPR04821
             CHGVAR     VAR(&P6401I) VALUE('NPR048BP')
             ENDDO
   /****/
             CLOF       OPNID(NPNDPF)
             CLOF       OPNID(APUXLF03)
             CLOF       OPNID(GLAHLF13)
             CLOF       OPNID(SOSCLF06)
             CLOF       OPNID(SOSNPF)
             DLTOVR     FILE(*ALL)
           /********/
             RTVDTAARA  DTAARA(*LDA (219 1)) RTNVAR(&$PENV)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (217 2)) RTNVAR(&$PRTCD)
             CHGVAR     VAR(&P6403I) VALUE(&$CPY)

             CALL       PGM(P64) PARM(&P6401I &$PENV &P6403I &$PRTCD)

             ENDPGM
