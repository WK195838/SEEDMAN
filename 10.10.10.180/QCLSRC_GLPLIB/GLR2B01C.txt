     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    DSPF   -ID   : GLR2B0D                                   */
     /*    PROGRAM-ID   : GLR2B0                                    */
     /*    PROGRAM NAME : 試算表                                  */
     /*    AUTHOR       :  A1149    MAY                             */
     /*    CREATE DATE  :  1992.11.20                               */
     /*    UPDATE DATE  :                                           */
     /*    SYSTEM       : 泛太總帳                                */
     /***************************************************************/
     /* 00A需求單號: 1050387                                       */
     /* 00A 2016/08/01 SHARON產生為EXCEL格式                     */
     /****************************************************************/
             PGM

             DCL        VAR(&DAH01)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&#AH10)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&P006O1)   TYPE(*CHAR) LEN(8)

             DCL        VAR(&$PENV)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTCD)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6401I)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6403I)   TYPE(*DEC)  LEN(3 0)
/*00A*/      DCL        VAR(&LIB)   TYPE(*CHAR) LEN(10)
/*00A*/      DCL        VAR(&FILE)  TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&DAH01)   VALUE(%SST(*LDA 527 2))
             CHGVAR     VAR(&#AH10)   VALUE(%SST(*LDA 529 8))
             CHGVAR     VAR(&P006O1)   VALUE(%SST(*LDA 509 8))
/*00A*/      CHGVAR     VAR(&LIB)   VALUE('QTEMP')
/*00A*/      CHGVAR     VAR(&FILE)  VALUE('GLR2B0PF')

/*00A*/      CPYF       FROMFILE(GLR2B0PF) TOFILE(QTEMP/GLR2B0PF) +
                          CRTFILE(*YES)
/*00A*/      MONMSG     MSGID(CPF2817) EXEC(CLRPFM +
                          FILE(QTEMP/GLR2B0PF))
/*00A*/      CLRPFM     FILE(QTEMP/GLR2B0PF)

/*00A*/      OVRDBF     FILE(GLR2B0PF) TOFILE(QTEMP/GLR2B0PF) +
                          SHARE(*YES)

             OVRDBF     FILE(GLAHLF03) SHARE(*YES)

             OPNQRYF    FILE((GLAHLF03)) QRYSLT(' AH01 *EQ ("' *CAT +
                          &DAH01 *CAT '" ) &  +
                           AH10C *EQ %RANGE("' *CAT +
                          &P006O1 *CAT '" "' *CAT &#AH10 *CAT '") +
                          & AH18 *EQ ("V")') KEYFLD((AH01) (AH04)) +
                          MAPFLD((AH10C AH10 *CHAR 8))

             OVRPRTF    FILE(GLR2B0P) PAGESIZE(*N 145) CPI(12)
             CALL       GLR2B01
             CLOF       OPNID(GLAHLF03)
             DLTOVR     FILE(*ALL)

             RTVDTAARA  DTAARA(*LDA (219 1)) RTNVAR(&$PENV)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (217 2)) RTNVAR(&$PRTCD)
             CHGVAR     VAR(&P6401I) VALUE('GLR2B0P')
             CHGVAR     VAR(&P6403I) VALUE(&$CPY)
             CALL       PGM(P64) PARM(&P6401I &$PENV &P6403I &$PRTCD)

/*00A*/      /*將GLR2B0PF轉換成CSV FILE AND FTP TO PC */
/*00A*/         CALL       PGM(ECL100C) PARM(&LIB &FILE)

             ENDPGM
