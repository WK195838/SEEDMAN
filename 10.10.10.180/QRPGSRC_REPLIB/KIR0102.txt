     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    DSPF NAME    : KIR0102                                    *
     H*    AUTHOR       : DEREK                                      *
     H*    CREATE DATE  : 2016/12/14                                 *
     H*    UPDATE DATE  :                                            *
     H*    PROGRAM NAME : KIR0102                                    *
     H*    SYSTEM       :茂世進銷存                                *
     H*    SUBSYSTEM    :專櫃進銷存系統                            *
     H*    REMARK       :茂世業績獎金銷售總表                      *
     H****************************************************************
     FW3EEPF  IP  E           K        DISK
     FPA#APF  IF  E           K        DISK
     FKIR010P O   E             39     PRINTER
     IEE0
     I                                              EE02  L1
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      211 2160DDATE
     I                                      125 1270$CPY
     I                                      138 138 $INQ
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I                                      217 218 $PRTCD
     I                                      219 219 $PENV
     I                                      500 500 $DATA
     I*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     I                                      501 5040DEE01
     I                                      505 5100DEE01S
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEY#A     KLIST
     C                     KFLD           #A01             代碼欄位
     C                     KFLD           #A02             代碼內容
     C*==============================================================*
     C*                    DEFINE
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *LIKE     DEFN EE09      WEE09            單瓶獎金
     C           *LIKE     DEFN EE10      WEE10            銷售獎金
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR R0100
     C*
     C   L1                EXSR R7100
     C*
     C                     EXSR R2000                      PROCESS
     C*
     CL1 10                EXSR R8100
     CLR 10                EXSR R8900
     CLRN10                EXSR R8999
     C*==============================================================*
     C*          R0100 ... INIT
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     SETON                     1039
|    C                     IN   PTDA01
|    C*
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           DDATE     PARM           P3111O  80
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R7100 ... LEVEL BREAK 1 -- INIT
     C*==============================================================*
02===C           R7100     BEGSR
|    C*
|    C                     Z-ADD0         WEE09            S-單瓶獎金
|    C                     Z-ADD0         WEE10            S-銷售獎金
|    C                     Z-ADD0         PEE09            S-單瓶獎金
|    C                     Z-ADD0         PEE10            S-銷售獎金
|    C                     Z-ADD0         PD01             合計
|    C*
|    C                     MOVEL'STORAGE' #A01      P
|    C                     MOVELEE02      #A02      P
|    C           KEY#A     CHAIN#A0                  40
| 01-C           *IN40     IFEQ '0'
|   C                     MOVEL#A03      D#A03     P      專櫃簡稱
| 01*C                     ELSE
|   C                     MOVEL*BLANK    D#A03            專櫃簡稱
| 01-C                     ENDIF
|    C*
02===C                     ENDSR
     C*==============================================================*
     C*          R2000 ... PROCESS
     C*==============================================================*
03===C           R2000     BEGSR
|    C*
|    C                     ADD  EE09      WEE09            S-單瓶獎金
|    C                     ADD  EE10      WEE10            S-銷售獎金
|    C*
03===C           E2000     ENDSR
     C*==============================================================*
     C*          R8100 ... LEVEL BREAK 1 -- TOTAL
     C*==============================================================*
04===C           R8100     BEGSR
|    C*
|    C                     Z-ADDWEE09     PEE09     H      S-單瓶獎金
|    C                     Z-ADDWEE10     PEE10     H      S-銷售獎金
|    C           PEE09     ADD  PEE10     PD01             合計
|    C*
| 01-C           *IN39     IFEQ '1'
|   C                     WRITEPH1
|   C                     SETOF                     39
| 01-C                     END
|    C*
|    C                     WRITEPD1
|    C*
|    C                     ADD  PEE09     PEE09T           S-單瓶獎金
|    C                     ADD  PEE10     PEE10T           S-銷售獎金
|    C                     ADD  PD01      PT01             合計
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R8900 ... LEVEL BREAK LR -- HAS DATA
     C*==============================================================*
05===C           R8900     BEGSR
|    C*
| 01-C           *IN39     IFEQ '1'
|   C                     WRITEPH1
|   C                     SETOF                     39
| 01-C                     END
|    C*
|    C                     WRITEPT1
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R8999 ... LEVEL BREAK LR -- NO DATA
     C*==============================================================*
06===C           R8999     BEGSR
|    C*
|    C                     EXSR R0100
|    C*
|    C                     WRITEPH1
|    C*
|    C                     WRITEPE9
|    C*
06===C                     ENDSR
     C*==============================================================*
