     A*%%TS  SD  19921119  212916  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA410                                     *
     A*    PROGRAM NAME : 會計科目資料維護                         *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.10.27                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A**                                    CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF18(18)
     A**          #LIN           3  0H
     A**          #COL           3  0H
     A                                  1  2'<GLA410.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'會計科目資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAF01     R        B  4 14REFFLD(AF01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'科目代號:'
     A            DAF02     R        B  5 14REFFLD(AF02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 24'(拷貝:'
     A            DAF01C    R        B  5 33REFFLD(AF01)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DAF02C    R        B  5 36REFFLD(AF02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  5 45')'
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0B  6 14VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 24'( 1.新增'
     A                                  6 35'2.修改'
     A                                  6 44'4.刪除'
     A                                  6 53'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 28'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  19921119  212916  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF12(12)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<GLA410.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'會計科目資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAF01     R        O  4 14REFFLD(AF01)
     A            D#B02         12A  O  4 19
     A                                  5  2'科目代號:'
     A            DAF02     R        O  5 14REFFLD(AF02)
     A                                  5 24'(拷貝:'
     A            DAF01C    R        O  5 33REFFLD(AF01)
     A            DAF02C    R        O  5 36REFFLD(AF02)
     A                                  5 45')'
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0O  6 14EDTCDE(4)
     A                                  6 24'( 1.新增'
     A                                  6 35'2.修改'
     A                                  6 44'4.刪除'
     A                                  6 53'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  2'科目中文名稱:'
     A            DAF03     R        B  8 18REFFLD(AF03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'科目英文名稱:'
     A            DAF04     R        B 10 18REFFLD(AF04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  2'科目類別　　:'
     A            DAF05     R        B 12 18REFFLD(AF05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      VALUES('1' '2')
     A                                 12 21'( 1.實帳戶 2.虛帳戶)'
     A                                 14  2'科目屬性　　:'
     A            DAF06     R        B 14 18REFFLD(AF06)
     A                                      VALUES('1' '2')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 21'( 1.借方   2.貸方  )'
     A                                 16  2'科目等級　　:'
     A            DAF08     R        B 16 18REFFLD(AF08)
     A                                      VALUES('1' '2')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16 21'( 1.明細   2.彙總  )'
     A                                 18  2'特定科目　　:'
     A            DAF09     R        B 18 18REFFLD(AF09)
     A                                      VALUES('1' '2' '3' '4')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 21'( 1.現金   2.銀行存款   3.外+
     A                                      幣存款   4.其他)'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'PF3 =回主畫面'
     A                                 23 28'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD3
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA410.3>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'會計科目資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAF01     R        O  4 14REFFLD(AF01)
     A            D#B02         12   O  4 19
     A                                  5  2'科目代號:'
     A            DAF02     R        O  5 14REFFLD(AF02)
     A                                  5 24'(拷貝:'
     A            DAF01C    R        O  5 33REFFLD(AF01)
     A            DAF02C    R        O  5 36REFFLD(AF02)
     A                                  5 45')'
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0O  6 14
     A                                      EDTCDE(4)
     A                                  6 24'( 1.新增'
     A                                  6 35'2.修改'
     A                                  6 44'4.刪除'
     A                                  6 53'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  2'會計科目:'
     A            DAF021    R        O  8 14REFFLD(AF02)
     A            DAF03     R        O  8 23REFFLD(AF03)
     A                                  8 62'輸入　排序　小計'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 10  5'1. 成本中心......:'
     A                                      DSPATR(HI)
     A            DAF11     R        B 10 26REFFLD(AF11)
     A N99                                  DSPATR(HI)
     A  80                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAA31     R        O 10 29REFFLD(AA03)
     A                                 11  5'2. 對象別........:'
     A                                      DSPATR(HI)
     A            DAF12     R        B 11 26REFFLD(AF12)
     A N99                                  DSPATR(HI)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAA32     R        O 11 29REFFLD(AA03)
     A                                 12  5'3. 相關號碼１....:'
     A                                      DSPATR(HI)
     A            DAF13     R        B 12 26REFFLD(AF13)
     A N99                                  DSPATR(HI)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAA33     R        O 12 29REFFLD(AA03)
     A                                 13  5'4. 相關號碼２....:'
     A                                      DSPATR(HI)
     A            DAF14     R        B 13 26REFFLD(AF14)
     A N99                                  DSPATR(HI)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAA34     R        O 13 29REFFLD(AA03)
     A                                 14  5'5. 日期..........:'
     A                                      DSPATR(HI)
     A            DAF15     R        B 14 26REFFLD(AF15)
     A N99                                  DSPATR(HI)
     A  84                                  DSPATR(PC)
     A  84                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAA35     R        O 14 29REFFLD(AA03)
     A                                 15  5'6. 數量..........:'
     A                                      DSPATR(HI)
     A            DAF16     R        B 15 26REFFLD(AF16)
     A N99                                  DSPATR(HI)
     A  85                                  DSPATR(PC)
     A  85                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAA36     R        O 15 29REFFLD(AA03)
     A                                 16  5'7. 其它..........:'
     A                                      DSPATR(HI)
     A            DAF17     R        B 16 26REFFLD(AF17)
     A N99                                  DSPATR(HI)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAA37     R        O 16 29REFFLD(AA03)
     A                                 10 54'控制碼:'
     A                                 11 54'控制碼:'
     A                                 12 54'控制碼:'
     A                                 13 54'控制碼:'
     A                                 14 54'控制碼:'
     A                                 15 54'控制碼:'
     A                                 16 54'控制碼:'
     A            DAF181         1   B 10 65
     A                                      VALUES(' ' 'V')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF182         1   B 11 65
     A                                      VALUES(' ' 'V')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF183         1   B 12 65
     A                                      VALUES(' ' 'V')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF184         1   B 13 65
     A                                      VALUES(' ' 'V')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF185         1   B 14 65
     A                                      VALUES(' ' 'V')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF186         1   B 15 65
     A                                      VALUES(' ' 'V')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF187         1   B 16 65
     A                                      VALUES(' ' 'V')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF191         1  0B 10 71
     A                                      EDTCDE(4)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF192         1  0B 11 71
     A                                      EDTCDE(4)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF193         1  0B 12 71
     A                                      EDTCDE(4)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF194         1  0B 13 71
     A                                      EDTCDE(4)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF195         1  0B 14 71
     A                                      EDTCDE(4)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF201         1   B 10 77
     A                                      VALUES(' ' 'V')
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF202         1   B 11 77
     A                                      VALUES(' ' 'V')
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF203         1   B 12 77
     A                                      VALUES(' ' 'V')
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF204         1   B 13 77
     A                                      VALUES(' ' 'V')
     A  78                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF205         1   B 14 77
     A                                      VALUES(' ' 'V')
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18  8'沖帳代碼......:'
     A                                      DSPATR(HI)
     A            DAF10     R        B 18 26REFFLD(AF10)
     A N99                                  DSPATR(HI)
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAE03     R        O 18 29REFFLD(AE03)
     A                                 19  8'分類帳碼......:'
     A                                      DSPATR(HI)
     A            DAF22     R        B 19 26REFFLD(AF22)
     A N99                                  DSPATR(HI)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DA303     R        O 19 29REFFLD(A303)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 23 45'PF5 =參考資料'
     A                                 23 61'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
