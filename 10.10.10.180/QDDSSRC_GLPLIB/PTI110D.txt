     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PTI110D                                    *
     A*    PROGRAM NAME : 代碼欄位資料全頁查詢                     *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : OCT 27,92'                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PTRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            DOPT           1   B  8  2DSPATR(CS PC HI)
     A                                      VALUES('/' ' ')
     A            RRN            3Y 0O  8  5EDTCDE(3)
     A            #Z01      R        O  8 10
     A            #Z02      R        O  8 22
     A            #Z03      R        O  8 49EDTCDE(3)
     A            #Z04      R        O  8 60EDTCDE(3)
     A            #Z05      R        O  8 72
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*                                     CSRLOC(#LIN #COL)
     A                                      OVERLAY
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0013)
     A                                      CA03(03)
     A*                                     CF04(04)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A*           #LIN           3  0H
     A*           #COL           3  0H
     A                                  1  2'<PTI110.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2 29'代碼欄位資料全頁查詢'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  2'S (=作業) : /-挑選'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5' No 代碼欄位　 代碼名稱       -
     A                                             代碼長度 說明長度 +
     A                                      維護碼'
     A                                      DSPATR(UL)
     A            D#Z01     R        B  7 10REFFLD(#Z01)
     A                                      DSPATR(CS HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'PF3 =回主畫面'
     A                                 23 29'PF12=回前畫面'
     A                                 23 47'PA1 =下一頁'
     A                                 24 11'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
