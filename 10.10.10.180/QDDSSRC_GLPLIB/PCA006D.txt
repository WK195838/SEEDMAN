     A*%%TS  SD  19940107  172329  D910121     REL-V2R2M0  5738-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PCCEPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19940107  133118  D910121     REL-V2R2M0  5738-PW1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<PCA006.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A            DCD01          2A  H
     A                                  2 31'零用金常用傳票設定'
     A                                      DSPATR(RI)
     A                                  4  2'傳票代碼:'
     A            DCD02     R        B  4 14REFFLD(CD02 *LIBL/PCCDPF)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                  4 23'備註:'
     A            DCD03     R        B  4 31REFFLD(CD03 *LIBL/PCCDPF)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                      OVERLAY
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF18=全頁查詢'
     A                                 24 64MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  19940104  151035  D910121     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF06(06)
     A                                      CF12(12)
     A                                      CF18(18)
     A                                      OVERLAY
     A                                      PROTECT
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  6  2'序號　　:'
     A            DCE03     R        O  6 14REFFLD(CE03)
     A                                      EDTCDE(4)
     A                                  7  2'科目　　:'
     A                                      DSPATR(HI)
     A            DCE04     R        B  7 14REFFLD(CE04)
     A N99                                  DSPATR(HI)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A            DAF03     R        O  7 24REFFLD(AF03 *LIBL/GLRF)
     A                                  8  2'請款金額:'
     A            DCE12     R        B  8 14REFFLD(CE12)
     A                                      EDTCDE(K)
     A                                      DSPATR(CS)
     A                                      CHECK(RB)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                      OVERLAY
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 23 44'PF5 =參考資料'
     A                                 23 61'PF6 =分錄設定完畢'
     A                                 24 10'PF12=回前畫面'
     A                                 24 27'PF18=分錄內容查詢'
     A                                 24 64MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD3
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A  31                                  CF04(04)
     A  31                                  CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PROTECT
     A  37                                  FRCDTA
     A  37                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                 11  3'1.'
     A N55                                  DSPATR(HI)
     A            DAA031    R        O 11  6REFFLD(AA03 *LIBL/GLRF)
     A N55                                  DSPATR(HI)
     A                                 11 27':'
     A N55                                  DSPATR(HI)
     A            DCE05     R        B 11 29REFFLD(CE05)
     A N99N55                               DSPATR(HI)
     A  60                                  DSPATR(ND)
     A  60
     AON31                                  DSPATR(PR)
     A  80                                  DSPATR(PC RI)
     A            DCE051        20A  O 11 51
     A                                 12  3'2.'
     A N56                                  DSPATR(HI)
     A            DAA032    R        O 12  6REFFLD(AA03 *LIBL/GLRF)
     A N56                                  DSPATR(HI)
     A                                 12 27':'
     A N56                                  DSPATR(HI)
     A            DCE06     R        B 12 29REFFLD(CE06)
     A N99N56                               DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61
     AON31                                  DSPATR(PR)
     A  81                                  DSPATR(PC RI)
     A            DCE061        20A  O 12 51
     A                                 13  3'3.'
     A N57                                  DSPATR(HI)
     A            DAA033    R        O 13  6REFFLD(AA03 *LIBL/GLRF)
     A N57                                  DSPATR(HI)
     A                                 13 27':'
     A N57                                  DSPATR(HI)
     A            DCE07     R        B 13 29REFFLD(CE07)
     A N99N57                               DSPATR(HI)
     A  62                                  DSPATR(ND)
     A  62
     AON31                                  DSPATR(PR)
     A  82                                  DSPATR(PC RI)
     A            DCE071        20A  O 13 51
     A                                 14  3'4.'
     A N58                                  DSPATR(HI)
     A            DAA034    R        O 14  6REFFLD(AA03 *LIBL/GLRF)
     A N58                                  DSPATR(HI)
     A                                 14 27':'
     A N58                                  DSPATR(HI)
     A            DCE08     R        B 14 29REFFLD(CE08)
     A N99N58                               DSPATR(HI)
     A  63                                  DSPATR(ND)
     A  63
     AON31                                  DSPATR(PR)
     A  83                                  DSPATR(PC RI)
     A            DCE081        20A  O 14 51
     A                                 15  3'5.'
     A            DAA035    R        O 15  6REFFLD(AA03 *LIBL/GLRF)
     A                                 15 27':'
     A            DCE09     R   -1   B 15 29REFFLD(CE09)
     A                                      EDTWRD('   /  /  ')
     A  64                                  DSPATR(ND)
     A  64
     AON31                                  DSPATR(PR)
     A  84                                  DSPATR(PC RI)
     A            DCE091        20A  O 15 51
     A                                 16  3'6.'
     A            DAA036    R        O 16  6REFFLD(AA03 *LIBL/GLRF)
     A                                 16 27':'
     A            DCE14     R        B 16 29REFFLD(CE14)
     A                                      EDTCDE(2)
     A  65                                  DSPATR(ND)
     A  65
     AON31                                  DSPATR(PR)
     A  85                                  DSPATR(PC RI)
     A            DCE141        20A  O 16 51
     A                                 17  3'7.'
     A N59                                  DSPATR(HI)
     A            DAA037    R        O 17  6REFFLD(AA03 *LIBL/GLRF)
     A N59                                  DSPATR(HI)
     A                                 17 27':'
     A N59                                  DSPATR(HI)
     A            DCE15     R        B 17 29REFFLD(CE15)
     A N99N59                               DSPATR(HI)
     A  66                                  DSPATR(ND)
     A  66
     AON31                                  DSPATR(PR)
     A  86                                  DSPATR(PC RI)
     A            DCE151        20A  O 17 51
     A                                 20  6'摘要'
     A                                 20 27':'
     A            DCE16     R        B 20 29REFFLD(CE16)
     A N31                                  DSPATR(PR)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A*----------------------------------------------------------------
     A          R DSPC3
     A                                      OVERLAY
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 23 44'PF5 =參考資料'
     A                                 23 61'PF12=回前畫面'
     A                                 24 64MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A*================================================================
     A          R SFLSR                     SFL
     A*%%TS  SD  19940107  172329  D910121     REL-V2R2M0  5738-PW1
     A            DOPT           1A  B  9  2VALUES(' ' '2' '4' '5')
     A            RRN            3Y 0H
     A            DCE031    R        O  9  4REFFLD(CE03)
     A                                      EDTCDE(4)
     A            DCE041    R        O  9  8REFFLD(CE04)
     A            DAF031        18A  O  9 20
     A            DCE121    R        O  9 46REFFLD(CE12)
     A                                      EDTCDE(K)
     A            DCE161    R        O 10 20REFFLD(CE16)
     A            DNO       R        H      REFFLD(RRN *SRC)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19940107  133329  D910121     REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0005)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      SFLDROP(CF02)
     A                                      OVERLAY
     A                                      PROTECT
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'<PCA006.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'零用金常用傳票設定'
     A                                      DSPATR(RI)
     A                                  4  2'傳票代碼:'
     A            DCD02     R        O  4 14REFFLD(CD02 *LIBL/PCCDPF)
     A                                  4 23'備註:'
     A            DCD03     R        O  4 31REFFLD(CD03 *LIBL/PCCDPF)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  6  2'S (=作業) :'
     A                                  6 17'2-修改'
     A                                  6 28'4-刪除'
     A                                  6 38'5-查詢'
     A                                  8  2'S  NO科目　　　　科目名稱／摘　+
     A                                      要　　　　　　　　申請金額       -
     A                                             '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC4
     A*  91/05/24  18:28:48    Q790601     REL-R03M00  5728-PW1
     A                                 19  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 20 37'合計:'
     A            DDTOT         13Y 2O 20 46EDTCDE(J)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF2 =明細顯示'
     A                                 23 27'PF3 =回主畫面'
     A                                 23 44'PF12=回前畫面'
     A                                 24 64MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD5
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PROTECT
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  6  2'序號　　:'
     A            DCE03     R        O  6 14REFFLD(CE03)
     A                                      EDTCDE(4)
     A            DFUN          14      6 60DSPATR(RI)
     A                                  7  2'科目　　:'
     A                                      DSPATR(HI)
     A            DCE04     R        B  7 14REFFLD(CE04)
     A N99                                  DSPATR(HI)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A            DAF03     R        O  7 24REFFLD(AF03 *LIBL/GLRF)
     A                                  8  2'借方金額:'
     A            DCE12     R        B  8 14REFFLD(CE12)
     A                                      EDTCDE(K)
     A                                      DSPATR(CS)
     A                                      CHECK(RB)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*----------------------------------------------------------------
     A          R DSPC5
     A                                      OVERLAY
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*================================================================
     A          R SFLSRD                    SFL
     A            RRND           3Y 0   9  2
     A            DCE12D    R        H      REFFLD(CE12)
     A            DCE04D    R        H      REFFLD(CE04)
     A            DCE05D    R        H      REFFLD(CE05)
     A            DCE06D    R        H      REFFLD(CE06)
     A            DCE07D    R        H      REFFLD(CE07)
     A            DCE08D    R        H      REFFLD(CE08)
     A            DCE09D    R        H      REFFLD(CE09)
     A            DCE14D    R        H      REFFLD(CE14)
     A            DCE15D    R        H      REFFLD(CE15)
     A            DCE16D    R        H      REFFLD(CE16)
     A            DLTCOD         1   H
     A*----------------------------------------------------------------
     A          R SFLCRD                    SFLCTL(SFLSRD)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
