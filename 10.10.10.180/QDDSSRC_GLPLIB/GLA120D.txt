     A*%%TS  SD  19921119  170150  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA120                                     *
     A*    PROGRAM NAME : 常用傳票登錄                             *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.11.19                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA120.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'常用傳票登錄'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　　:'
     A            DAN01     R        B  4 16REFFLD(AN01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            #B02      R        O  4 19REFFLD(#B02 PTRF)
     A                                  5  2'傳票代碼　:'
     A                                      DSPATR(HI)
     A            DAN02B    R        B  5 16REFFLD(AN02)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 25'-'
     A                                      DSPATR(HI)
     A            DAN02E    R        B  5 27REFFLD(AN02)
     A                                      DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  2'傳票日期　:'
     A            DAH10     R   -1   B  7 16REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8  2'迴轉日期　:'
     A            DAH21     R   -1   B  8 16REFFLD(AH21)
     A                                      EDTWRD('   /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9  2'作業　　　:'
     A            SNO            1A  B  9 16VALUES(' ' '2' '6' '9')
     A                                  9 19'( 2-修改'
     A                                  9 32'6-直接產生'
     A                                  9 47'9-直接產生並列印)'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  98        DMSG          78A  O 22  2DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A*
     A            DOPT           1A  B 10  2VALUES(' ' '2' '6' '9')
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            RRN            3Y 0O 10  4EDTCDE(4)
     A            AN02      R        O 10  8
     A            DAN03         32A  O 10 17
     A            SAH10     R   -1   B 10 50REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            SAH21     R   -1   B 10 60REFFLD(AH21)
     A                                      EDTWRD('   /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DAH02     R        O 10 71REFFLD(AH02)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0110)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF13(13)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 PTMF)
     A**
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLA120.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'常用傳票登錄'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　　:'
     A            DAN01     R        O  4 16REFFLD(AN01)
     A            #B02      R        O  4 19REFFLD(#B02 PTRF)
     A                                  5  2'傳票代碼　:'
     A            DAN02B    R        O  5 16REFFLD(AN02)
     A                                  5 25'-'
     A            DAN02E    R        O  5 27REFFLD(AN02)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  2'S (=作業) :'
     A                                  7 16'2-修改'
     A                                  7 27'6-直接產生'
     A                                  7 42'9-直接產生並列印'
     A                                  9  2'S  No傳票代碼　備註　　　　　 +
     A                                      　　　　　　　　傳票日期　迴轉+
     A                                      日期　傳票號碼'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC2
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  98        DMSG          78A  O 22  2DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PF13=印表設定'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC0
     A                                      ASSUME
     A                                  2  2' '
     A          R DSPD9
     A*%%TS  SD  19921119  170150  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      CF04(04)
     A                                      OVERLAY
     A                                      PROTECT
     A                                      CLRL(*NO)
     A                                  7 30'                          '
     A                                      DSPATR(RI)
     A                                  8 30' '
     A                                      DSPATR(RI)
     A                                  8 32'<GLA120.3>            '
     A                                  8 55' '
     A                                      DSPATR(RI)
     A                                  9 30' '
     A                                      DSPATR(RI)
     A                                  9 32'                      '
     A                                  9 55' '
     A                                      DSPATR(RI)
     A                                 10 30' '
     A                                      DSPATR(RI)
     A                                 10 32'   列印份數:'
     A            $CPY           3Y 0B 10 47EDTCDE(3)
     A                                      CHECK(RB)
     A                                 10 51'   '
     A                                 10 55' '
     A                                      DSPATR(RI)
     A                                 11 30' '
     A                                      DSPATR(RI)
     A                                 11 32'   列表機　:'
     A            $PRTCD         2A  B 11 47DSPATR(HI)
     A                                 11 50'    '
     A                                 11 55' '
     A                                      DSPATR(RI)
     A                                 12 30' '
     A                                      DSPATR(RI)
     A                                 12 31'                        '
     A                                 12 32'                      '
     A                                 12 55' '
     A                                      DSPATR(RI)
     A                                 13 30' '
     A                                      DSPATR(RI)
     A                                 13 32'執行  PF4=欄位查詢'
     A                                 13 55' '
     A                                      DSPATR(RI)
     A                                 14 30'                          '
     A                                      DSPATR(RI)
     A*================================================================
     A          R SFLSRD                    SFL
     A            RRND           3Y 0   9  2
     A            DAH12D    R        H      REFFLD(AH12)
     A            DAH13D    R        H      REFFLD(AH13)
     A            DAH04D    R        H      REFFLD(AH04)
     A            DAH05D    R        H      REFFLD(AH05)
     A            DAH05A    R        H      REFFLD(AH05A)
     A            DAH06D    R        H      REFFLD(AH06)
     A            DAH06A    R        H      REFFLD(AH06A)
     A            DAH07D    R        H      REFFLD(AH07)
     A            DAH07A    R        H      REFFLD(AH07A)
     A            DAH08D    R        H      REFFLD(AH08)
     A            DAH08A    R        H      REFFLD(AH08A)
     A            DAH09D    R        H      REFFLD(AH09)
     A            DAH09A    R        H      REFFLD(AH09A)
     A            DAH11D         1   H
     A            DAH14D    R        H      REFFLD(AH14)
     A            DAH14A    R        H      REFFLD(AH14A)
     A            DAH15D    R        H      REFFLD(AH15)
     A            DAH15A    R        H      REFFLD(AH15A)
     A            DAH16D    R        H      REFFLD(AH16)
     A            DAH22D    R        H      REFFLD(AH22)
     A            DAH24D    R        H      REFFLD(AH24)
     A            DLTCOD         1   H
     A*----------------------------------------------------------------
     A          R SFLCRD                    SFLCTL(SFLSRD)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A*================================================================
     A          R SFLSRC                    SFL
     A            RRNC           3Y 0   9  2
     A            DAH12D    R        H      REFFLD(AH12)
     A            DAH13D    R        H      REFFLD(AH13)
     A            DAH04D    R        H      REFFLD(AH04)
     A            DAH05D    R        H      REFFLD(AH05)
     A            DAH05A    R        H      REFFLD(AH05A)
     A            DAH06D    R        H      REFFLD(AH06)
     A            DAH06A    R        H      REFFLD(AH06A)
     A            DAH07D    R        H      REFFLD(AH07)
     A            DAH07A    R        H      REFFLD(AH07A)
     A            DAH08D    R        H      REFFLD(AH08)
     A            DAH08A    R        H      REFFLD(AH08A)
     A            DAH09D    R        H      REFFLD(AH09)
     A            DAH09A    R        H      REFFLD(AH09A)
     A            DAH11D         1   H
     A            DAH14D    R        H      REFFLD(AH14)
     A            DAH14A    R        H      REFFLD(AH14A)
     A            DAH15D    R        H      REFFLD(AH15)
     A            DAH15A    R        H      REFFLD(AH15A)
     A            DAH16D    R        H      REFFLD(AH16)
     A            DAH22D    R        H      REFFLD(AH22)
     A            DAH24D    R        H      REFFLD(AH24)
     A            DLTCOD         1   H
     A*----------------------------------------------------------------
     A          R SFLCRC                    SFLCTL(SFLSRC)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
