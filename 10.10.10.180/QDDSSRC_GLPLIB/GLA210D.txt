     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA210D                                    *
     A*    PROGRAM NAME :會計科目沖帳排序維護                      *
     A*    AUTHOR       : A1150                                      *
     A*    CREATE DATE  : 81/12/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A                                      CA03(03)
     A          R DSPD1
     A                                      CF18(18)
     A                                  1  2'<GLA210.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 29'會計科目沖帳排序維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAF01     R        B  4 14REFFLD(AF01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'科目代號:'
     A            DAF02     R        B  5 14REFFLD(AF02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*
     A          R SFLSR                     SFL
     A            DOPT           1A  B 16  3DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      VALUES('/' ' ')
     A            RRN            3Y 0O 16  6EDTCDE(4)
     A            AE02      R        O 16 14
     A            AE041          1S 0O 16 24
     A            AE051          1A  O 16 26
     A            AE042          1S 0O 16 33
     A            AE052          1A  O 16 35
     A            AE043          1S 0O 16 44
     A            AE053          1A  O 16 46
     A            AE044          1S 0O 16 56
     A            AE054          1A  O 16 58
     A            AE045          1S 0O 16 66
     A            AE055          1A  O 16 68
     A*
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0006)
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLA210.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 29'會計科目沖帳排序維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAF01     R        O  4 14REFFLD(AF01)
     A            #B02      R        O  4 23REFFLD(#B02 PTRF)
     A                                  5  2'科目代號:'
     A            DAF02     R        O  5 14REFFLD(AF02)
     A            AF03      R        O  5 23
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  7  3'1. 成本中心......:'
     A            AF11      R        O  7 24
     A            AA031     R        O  7 27REFFLD(AA03)
     A                                  8  3'2. 對象別........:'
     A            AF12      R        O  8 24
     A            AA032     R        O  8 27REFFLD(AA03)
     A                                  9  3'3. 相關號碼1.....:'
     A            AF13      R        O  9 24
     A            AA033     R        O  9 27REFFLD(AA03)
     A                                 10  3'4. 相關號碼2.....:'
     A            AF14      R        O 10 24
     A            AA034     R        O 10 27REFFLD(AA03)
     A                                 11  3'5. 日期..........:'
     A            AF15      R        O 11 24
     A            AA035     R        O 11 27REFFLD(AA03)
     A                                 12  3'6. 沖帳代碼......:'
     A            AF10      R        O 12 24
     A            DAE03     R        O 12 27REFFLD(AE03)
     A                                 13  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                 14  3'S (=作業) : /=挑選'
     A                                 15  2' S序號 沖帳組合成本中心對+
     A                                      象別 相關號碼1 相關號碼2 日+
     A                                      期 '
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                 23  2'執行'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
