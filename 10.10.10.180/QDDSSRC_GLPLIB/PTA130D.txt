     A*%%TS  SD  19921105  215520  A1034       REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PTA130                                     *
     A*    PROGRAM NAME :公司基本資料維護                          *
     A*    AUTHOR       : A1149  MAY                                 *
     A*    CREATE DATE  : 1992.10.28                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :基本資料系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PTRF)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<PTA130.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'公司基本資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            D#B01     R        B  4 14REFFLD(#B01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'作業選擇:'
     A            DOPT           1Y 0B  5 14VALUES(1 2 4 5)
     A                                  5 18'( 1.新增'
     A                                  5 29'2.修改'
     A                                  5 38'4.刪除'
     A                                  5 47'5.查詢)'
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 28'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  19921105  215520  A1034       REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF12(12)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<PTA130.2>'
     A                                  1  2'<PTA130.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'公司基本資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            D#B01     R        O  4 14REFFLD(#B01)
     A                                  5  2'作業選擇:'
     A            DOPT           1Y 0O  5 14
     A                                  5 18'( 1.新增'
     A                                  5 29'2.修改'
     A                                  5 38'4.刪除'
     A                                  5 47'5.查詢)'
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  2'公司簡稱:'
     A            D#B02     R        B  7 14REFFLD(#B02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'中文全名:'
     A            D#B03     R        B  8 14REFFLD(#B03)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  2'英文全名:'
     A            D#B15     R        B  9 14REFFLD(#B15)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'統一編號:'
     A            D#B04     R        B 10 14REFFLD(#B04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  2'稅籍編號:'
     A            D#B05     R        B 11 14REFFLD(#B05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  2'公司電話:'
     A            D#B06     R        B 12 14REFFLD(#B06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#B07     R        B 12 27REFFLD(#B07)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  2'工廠電話:'
     A            D#B08     R        B 13 14REFFLD(#B08)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#B09     R        B 13 27REFFLD(#B09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  2'公司地址:'
     A            D#B10     R        B 14 14REFFLD(#B10)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  2'工廠地址:'
     A            D#B11     R        B 15 14REFFLD(#B11)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16  2'所得扣繳'
     A                                 17  2'義務人　:'
     A            D#B13     R        B 17 14REFFLD(#B13)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18  2'媒體單位:'
     A            D#B14     R        B 18 14REFFLD(#B14)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19  2'類別　　:'
     A            D#B12     R        B 19 14REFFLD(#B12)
     A                                      VALUES('1' '2')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19 17'( 1.正式2.虛擬)'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 28'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
