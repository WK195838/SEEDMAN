     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA140D                                    *
     A*    PROGRAM NAME :核准作業　　　　                          *
     A*    AUTHOR       : A1152   ANGY                               *
     A*    CREATE DATE  : 81/11/04                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A                                      CA03(03)
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CF04(04)
     A                                      CF14(14)
     A                                      CF18(18)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA140.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2 35'核准作業'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A*
     A                                  5 27'公司別　:'
     A            DAH01     R        B  5 39REFFLD(AH01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 27'製票日期:'
     A            DAH25F         7  0B  7 39EDTWRD('   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 49'-'
     A            DAH25T         7  0B  7 51EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A*
     A                                  8 27'傳票日期:'
     A            DAH10F         7  0B  8 39EDTWRD('   /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  8 49'-'
     A            DAH10T         7  0B  8 51EDTWRD('   /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A*
     A                                  9 27'傳票號碼:'
     A            DAH02F    R        B  9 39REFFLD(AH02)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                  9 49'-'
     A            DAH02T    R        B  9 51REFFLD(AH02)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A*
     A                                 10 27'製票者  :'
     A            DAH26     R        B 10 39REFFLD(AH26)
     A                                 10 50'(空白表示*ALL)'
     A                                 11 27'傳票來源:'
     A            DAH17     R        B 11 39REFFLD(AH17)
     A                                 11 50'(空白表示*ALL)'
     A*
     A*                                15 29'執行環境:'
     A*           $PENV          1   B 15 41VALUES('0' '1' '2')
     A*                                15 44'( 0-查詢 1-即時 2-批次)'
     A                                 16 27'列印份數:'
     A            $CPY           3Y 0B 16 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 27'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 17 39
     A                                      DSPATR(HI)
     A  27                             21 30'***請準備　８０行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF14=處理作業'
     A                                 24 11'PF18=傳票查詢'
     A                                 24 67'【泛太資訊】'
