     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLS110D                                    *
     A*    PROGRAM NAME : 傳票查詢                                 *
     A*    AUTHOR       : A1139  GRACE                               *
     A*    CREATE DATE  : JUNE 21, 94'                               *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                      CA12(12)
     A                                  1  2'<GLS110.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 34'傳票分錄查詢'
     A                                      DSPATR(RI)
     A                                  3  2'公司別　　:'
     A            DAH01     R        O  3 16REFFLD(AH01)
     A            #B02      R        O  3 19REFFLD(#B02 PTRF)
     A                                  3 55'傳票號碼:'
     A            DAH02     R        O  3 71REFFLD(AH02)
     A                                  4  2'傳票日期　:'
     A            DAH10     R   -1   O  4 16REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A                                  4 59'迴轉日期:'
     A            DAH21     R   -1   O  4 71REFFLD(AH21)
     A                                      EDTWRD('   /  /  ')
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A                                  6  4'序號　　:'
     A            DAH03     R        O  6 16REFFLD(AH03)
     A                                      EDTCDE(4)
     A                                  7  4'科目　　:'
     A                                      DSPATR(HI)
     A            DAH04     R        O  7 16REFFLD(AH04)
     A            DAF03     R        O  7 26REFFLD(AF03)
     A                                  8  4'借方金額:'
     A            DAH12     R        O  8 16REFFLD(AH12)
     A                                      EDTCDE(2)
     A                                  9  4'貸方金額:'
     A            DAH13     R        O  9 16REFFLD(AH13)
     A                                      EDTCDE(2)
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A                                 11  5'1.'
     A N55                                  DSPATR(HI)
     A            DAA031    R        O 11  8REFFLD(AA03)
     A N55                                  DSPATR(HI)
     A                                 11 29':'
     A N55                                  DSPATR(HI)
     A            DAH05     R        O 11 31REFFLD(AH05)
     A N55                                  DSPATR(HI)
     A            DAH051        20A  O 11 53
     A                                 12  5'2.'
     A N56                                  DSPATR(HI)
     A            DAA032    R        O 12  8REFFLD(AA03)
     A N56                                  DSPATR(HI)
     A                                 12 29':'
     A N56                                  DSPATR(HI)
     A            DAH06     R        O 12 31REFFLD(AH06)
     A N56                                  DSPATR(HI)
     A            DAH061        20A  O 12 53
     A                                 13  5'3.'
     A N57                                  DSPATR(HI)
     A            DAA033    R        O 13  8REFFLD(AA03)
     A N57                                  DSPATR(HI)
     A                                 13 29':'
     A N57                                  DSPATR(HI)
     A            DAH07     R        O 13 31REFFLD(AH07)
     A N57                                  DSPATR(HI)
     A            DAH071        20A  O 13 53
     A                                 14  5'4.'
     A N58                                  DSPATR(HI)
     A            DAA034    R        O 14  8REFFLD(AA03)
     A N58                                  DSPATR(HI)
     A                                 14 29':'
     A N58                                  DSPATR(HI)
     A            DAH08     R        O 14 31REFFLD(AH08)
     A N58                                  DSPATR(HI)
     A            DAH081        20A  O 14 53
     A                                 15  5'5.'
     A            DAA035    R        O 15  8REFFLD(AA03)
     A                                 15 29':'
     A            DAH09     R   -1   O 15 31REFFLD(AH09)
     A                                      EDTWRD('   /  /  ')
     A            DAH091        20A  O 15 53
     A                                 16  5'6.'
     A            DAA036    R        O 16  8REFFLD(AA03)
     A                                 16 29':'
     A            DAH14     R        O 16 31REFFLD(AH14)
     A                                      EDTCDE(2)
     A            DAH141        20A  O 16 53
     A                                 17  5'7.'
     A N59                                  DSPATR(HI)
     A            DAA037    R        O 17  8REFFLD(AA03)
     A N59                                  DSPATR(HI)
     A                                 17 29':'
     A N59                                  DSPATR(HI)
     A            DAH15     R        O 17 31REFFLD(AH15)
     A N59                                  DSPATR(HI)
     A            DAH151        20A  O 17 53
     A                                 18  8'資金代碼'
     A                                      DSPATR(HI)
     A                                 18 29':'
     A                                      DSPATR(HI)
     A            DAH22     R        O 18 31REFFLD(AH22)
     A            DAD03     R        O 18 41REFFLD(AD03)
     A                                 19  8'沖帳傳票號碼'
     A                                      DSPATR(HI)
     A                                 19 29':'
     A                                      DSPATR(HI)
     A            DAH24     R        O 19 31REFFLD(AH24)
     A                                 20  8'摘要'
     A                                 20 29':'
     A            DAH16     R        O 20 31REFFLD(AH16)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +3'PF3 =回主畫面'
     A                                    +3'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
