     A*%%TS  SD  19921119  213929  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLI610                                     *
     A*    PROGRAM NAME : 預算資料全頁查詢                         *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.12.07                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19921119  213929  TSPGMR      REL-V2R1M1  5738-PW1
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      VALUES('/' ' ')
     A            RRN            3Y 0O  8  4EDTCDE(4)
     A            AT02      R        O  8  8EDTCDE(3)
     A            AT03      R        O  8 15EDTCDE(3)
     A            AT01      R        O  8 21
     A            AT04      R        O  8 26
     A            DAF03         28   O  8 35
     A            AT06      R        O  8 64EDTCDE(1)
     A            DAT05         32   O  9 35
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921119  213929  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0006)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      SFLDROP(CF02)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLI610.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'預算資料全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  2'S (=作業) : /-挑選'
     A                                  6  2'S  No年度　版次　公司　會計科+
     A                                      目　科目名稱／版次備註           -
     A                                             金額  '
     A                                      DSPATR(UL)
     A            WAT02     R        B  7  8REFFLD(AT02)
     A                                      EDTCDE(4)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DAT03     R        B  7 15REFFLD(AT03)
     A                                      EDTCDE(4)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DAT01     R        B  7 21REFFLD(AT01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DAT04     R        B  7 26REFFLD(AT04)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3=回主畫面'
     A                                    +2'PF2 =明細顯示'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PA1=下一頁'
     A                                 24 10'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
