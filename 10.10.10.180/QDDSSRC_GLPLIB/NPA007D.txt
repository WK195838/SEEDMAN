     A****************************************************************
     A*   人頭馬寰盛洋酒股份有限公司－版權所有    TEL:8773-9099    *
     A*                                                              *
     A*    PROGRAM-ID   : NPA008                                     *
     A*    PROGRAM NAME : 產生新年度預算資料                       *
     A*    AUTHOR       : MICHELLE                                   *
     A*    CREATE DATE  : 2002.02.19                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 產品促銷預算                             *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLALIB/NPRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A**                                    CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF14(14)
     A                                  1  2'<NPA007.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'產生新年度預算資料'
     A                                      DSPATR(RI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  7'公司別　:'
     A            DND01     R        B  4 19REFFLD(ND01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  6  7'預算年度:'
     A            DND02     R        B  6 19REFFLD(ND02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8  7'品　　牌:'
     A            DND04     R        B  8 19REFFLD(ND04)
     A                                    +5'產品組別:'
     A            DND03     R        O    +2REFFLD(ND03)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 10  7'費用種類:'
     A            DND19     R        B 10 19REFFLD(ND04)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 12  7'費用容量:'
     A            DND20          1   B 12 19
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 14  7' A&P類別:'
     A            DNA04          1   B 14 19VALUES('T' 'D' 'N')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                    +2'(T=合計D=明細N=新建明細)'
     A                                 16  2'選擇內容     :'
     A            DOPT           1Y 0B 16 19EDTCDE(4)
     A                                      VALUES(1 2 3)
     A                                 16 23'1.已存在者'
     A                                 16 36'放棄拷貝'
     A                                 17 23'2.已存在者'
     A                                 17 36'全部覆蓋無須確認'
     A                                 18 23'3.已存在者'
     A                                 18 36'逐筆確認是否覆蓋'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF14=處理作業'
     A                                 24 64'【人頭馬寰盛】'
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A
     A          R DSPD2
     A*                                     ALWROL
     A                                      OVERLAY
     A                                      CLRL(*NO)
     A                                 14  3'                                 -
     A                                                                        '
     A                                      DSPATR(RI)
     A                                 15  3' '
     A                                      DSPATR(RI)
     A                                 15 69' '
     A                                      DSPATR(RI)
     A                                 16  3' '
     A                                      DSPATR(RI)
     A                                 16  5' <NPA007.2> '
     A                                    +2'                                 '
     A                                 16 69' '
     A                                      DSPATR(RI)
     A                                 17  3' '
     A                                      DSPATR(RI)
     A                                    +2'                                -
                                                           '
     A                                 17 69' '
     A                                      DSPATR(RI)
     A                                 18  3' '
     A                                      DSPATR(RI)
     A                                 18  5'產品預算:'
     A            DCAT          22A  O 18 17
     A                                    +2'已存在'
     A                                    +2'                '
     A                                 18 69' '
     A                                      DSPATR(RI)
     A                                 19  3' '
     A                                      DSPATR(RI)
     A                                    +2'                        '
     A                                 19 69' '
     A                                      DSPATR(RI)
     A                                 20  3' '
     A                                      DSPATR(RI)
     A                                 20  5'是否覆蓋:'
     A            COVE           1   B 20 17VALUES('Y' 'N')
     A                                 20 22'(Y/N)'
     A                                    +5'全部覆蓋:'
     A            CALL           1   B    +1VALUES(' ' 'Y')
     A                                    +2'(Y)'
     A                                    +2'            '
     A                                 20 69' '
     A                                      DSPATR(RI)
     A                                 21  3' '
     A                                      DSPATR(RI)
     A                                 21 69' '
     A                                      DSPATR(RI)
     A                                 22  3'                                 -
     A                                                                        '
     A                                      DSPATR(RI)
