     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF   -ID   : GLR2F1P2                                   *
     A*    PROGRAM-ID   : GLR2F0                                     *
     A*    PROGRAM NAME : 比較損益表－科目明細表－年度－英文       *
     A*    AUTHOR       :  A1149    MAY                              *
     A*    CREATE DATE  :  1992.12.15                                *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      REF(GLRF)
     A          R PH1                       SKIPB(2)
     A            P#B16     R             51REFFLD(#B16 PTRF)
     A                                      SPACEA(2)
     A                                    61'Detail Report'
     A                                      SPACEA(1)
     A            PDA01          8        63
     A                                      SPACEA(1)
     A            P0303         34        51
     A                                      SPACEA(1)
     A  30                                46'Profit Center :'
     A  30        D#A01          6        62
     A  30        P#A02         32        69
     A  30                                  SPACEA(1)
     A                                    46'Fiscal Year   :'
     A            DYEAR          3  0     62EDTCDE(2)
     A                                      SPACEA(1)
     A                                    46'Fiscal period :'
     A            DMMS           2  0     62
     A                                      EDTCDE(4)
     A                                    65'-'
     A            DMME           2  0     67
     A                                      EDTCDE(4)
     A                                    70'('
     A            PMMS           7  0     72EDTWRD('   /  /  ')
     A                                    82'-'
     A            PMME           7  0     84EDTWRD('   /  /  ')
     A                                    94')'
     A                                      SPACEA(1)
     A                                   103'DATE :'
     A            PDATE          6  0    111EDTWRD('  /  /  ')
     A                                   120'PAGE :'
     A                                   128PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(1)
     A                                   103'TIME :'
     A                                   111TIME
     A                                      EDTWRD('  :  :  ')
     A                                   121'<GLR2F1P>'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ============'
     A                                      SPACEA(1)
     A                                    49'Current'
     A                                    81'Last Year'
     A                                   112'Difference'
     A                                      SPACEA(1)
     A                                    41'---------------------------'
     A                                    73'---------------------------'
     A                                   105'---------------------------'
     A                                      SPACEA(1)
     A                                     4'Account Title'
     A                                    49'Amounts'
     A                                    63'％'
     A                                    81'Amounts'
     A                                    95'％'
     A                                   113'Amounts'
     A                                   127'％'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ============'
     A*
     A          R PD1                       SPACEB(1)
     A            P0402         34         2
     A  12                                41'('
     A            P0403         13  2     42EDTCDE(1)
     A  12                                59')'
     A  13                                61'('
     A  31        PR0403         5  2     62EDTCDE(1)
     A N31                                62' ****'
     A  13                                68')'
     A*
     A  14                                73'('
     A            P0404         13  2     74EDTCDE(1)
     A  14                                91')'
     A  15                                93'('
     A  32        PR0404         5  2     94EDTCDE(1)
     A N32                                94' ****'
     A  15                               100')'
     A*
     A  16                               105'('
     A            P04X          13  2    106EDTCDE(1)
     A  16                               123')'
     A  17                               125'('
     A  33        PR04X          5  2    126EDTCDE(1)
     A N33                               126' ****'
     A  17                               132')'
     A*簽名行....
     A          R PT1                       SPACEB(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ============'
     A                                      SPACEA(1)
     A                                     2'Totals :'
     A  21                                40'('
     A            T0403         13  2     41EDTWRD('  ,   ,   ,  $0.  ')
     A  21                                59')'
     A                                    62'100.00'
     A*
     A  22                                72'('
     A            T0404         13  2     73EDTWRD('  ,   ,   ,  $0.  ')
     A  22                                91')'
     A                                    94'100.00'
     A*
     A  23                               104'('
     A            T04X          13  2    105EDTWRD('  ,   ,   ,  $0.  ')
     A  23                               123')'
     A  24                               125'('
     A            TR04X          5  2    126EDTCDE(1)
     A  24                               132')'
     A*
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ============'
     A                                      SPACEB(1)
     A            PSIGN1       132         1
     A                                      SPACEB(1)
     A            PSIGN2       132         1
     A                                      SPACEB(1)
     A*
