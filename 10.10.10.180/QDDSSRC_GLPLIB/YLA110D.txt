     A*%%TS  SD  19921118  132237  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : YLA110                                     *
     A*    PROGRAM NAME : 傳票登錄　　                             *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.11.06                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A*%%TS  SD  19921111  135115  TSPGMR      REL-V2R1M1  5738-PW1
     A  54                                  SFLNXTCHG
     A            CODE1          3Y 0H
     A            DOPT           1A  B  9  2VALUES('/' '4' ' ')
     A            RRN1           2  0O  9  4
     A                                      EDTCDE(4)
     A            DAH04     R        B  9  9REFFLD(AH0/AH04 *LIBL/GLAHPF)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            AF03      R        O  9 18REFFLD(AF0/AF03 *LIBL/GLAFPF)
     A            DRMON     R        B  9 51REFFLD(AH0/AH12 *LIBL/GLAHPF)
     A                                      EDTCDE(4)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A            CRMON     R        B  9 66REFFLD(AH0/AH13 *LIBL/GLAHPF)
     A                                      EDTCDE(4)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A            DAH161    R        B 10  9REFFLD(AH0/AH16 *LIBL/GLAHPF)
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19921118  132237  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0006)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A  50                                  CF04(04)
     A                                      CF05(05)
     A  50                                  CF06(06)
     A  50                                  CF09(09)
     A  50                                  CF12(12)
     A N50                                  CF13(13)
     A                                      OVERLAY
     A                                      PROTECT
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            DRRN1          3S 0H      SFLRCDNBR
     A                                  1  2'<GLA110.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 36'傳票登錄'
     A                                      DSPATR(RI)
     A            DFUN          14A  O  2 66DSPATR(RI)
     A                                  3  2'公司別　:'
     A            DAH01     R        B  3 14REFFLD(AH01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            #B02      R        O  3 17REFFLD(#B02 PTRF)
     A                                  3 51'上次傳票號碼:'
     A            DAH02     R        O  3 68REFFLD(AH0/AH02 *LIBL/GLAHPF)
     A                                  4  2'傳票日期:'
     A            DAH10     R        B  4 14REFFLD(AH0/AH10 *LIBL/GLAHPF)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTWRD('    /  /  ')
     A                                  4 36'迴轉日期:'
     A            DAH21     R        B  4 48REFFLD(AH0/AH21 *LIBL/GLAHPF)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTWRD('    /  /  ')
     A                                  5 10'DR:'
     A            DRSUM     R        O  5 14REFFLD(AH12)
     A                                      EDTCDE(1)
     A                                  5 38'CR:'
     A            CRSUM     R        O  5 42REFFLD(AH13)
     A                                      EDTCDE(1)
     A                                  6  2'(/:新增　修改 4:刪除)'
     A                                  8  8'科　　目'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 19'/ 摘要                       '
     A                                      DSPATR(UL)
     A                                  8 51'    借　金       貸　金  '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A*%%TS  SD  19921106  133817  TSPGMR      REL-V2R1M1  5738-PW1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  98        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =視窗查詢'
     A                                 23 44'PF5 =參考資料'
     A                                 23 61'PF6 =分錄製作'
     A                                 24 10'PF9 =製作列印'
     A                                 24 27'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC11
     A*%%TS  SD  19921106  133817  TSPGMR      REL-V2R1M1  5738-PW1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  98        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF5 =參考資料'
     A                                 23 44'PF13=列印設定'
     A*                                23 44'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A*%%TS  SD  19921116  105449  TSPGMR      REL-V2R1M1  5738-PW1
     A  54                                  SFLNXTCHG
     A            CODE2          3S 0H
     A            DOPT1          1A  B  9  2VALUES('/' '4' ' ')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            RRN2           2Y 0O  9  5EDTCDE(4)
     A            DFLD1          1   B  9  9VALUES('Y' ' ' 'N')
     A            DAH05         16O  B  9 14
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  33N71                               DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61
     AON30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A            DAH06         16O  B  9 31
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  34N72                               DSPATR(HI)
     A  62                                  DSPATR(ND)
     A  62
     AON30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A            DAH07         16O  B  9 48
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  35N73                               DSPATR(HI)
     A  63                                  DSPATR(ND)
     A  63
     AON30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A            DCMON     R        B  9 66REFFLD(AH0/AH12 *LIBL/GLAHPF)
     A                                      EDTCDE(4)
     A N30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A            DAH08         16O  B 10  8
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  36N74                               DSPATR(HI)
     A  64                                  DSPATR(ND)
     A  64
     AON30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A            DAH09     R        B 10 27REFFLD(AH0/AH09 *LIBL/GLAHPF)
     A                                      EDTCDE(4)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A  37N75                               DSPATR(HI)
     A  65                                  DSPATR(ND)
     A  65
     AON30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A            DAH14     R        B 10 44REFFLD(AH0/AH14 *LIBL/GLAHPF)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A  38N76                               DSPATR(HI)
     A  66                                  DSPATR(ND)
     A  66
     AON30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A            DAH15         16O  B 10 60
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  39N77                               DSPATR(HI)
     A  67                                  DSPATR(ND)
     A  67
     AON30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A            DAH22     R        B 11  8REFFLD(AH0/AH22 *LIBL/GLAHPF)
     A N78                                  DSPATR(HI)
     A  78                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A N30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A            DAH24     R        B 11 19REFFLD(AH0/AH24 *LIBL/GLAHPF)
     A N69                                  DSPATR(HI)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A N30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A            DAH162    R        B 11 31REFFLD(AH0/AH16 *LIBL/GLAHPF)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19921118  130042  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0004)
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A  30                                  CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PROTECT
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            DRRN2          3S 0H      SFLRCDNBR
     A                                  1  2'<GLA110.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 36'傳票登錄'
     A                                      DSPATR(RI)
     A            DFUN          14A  O  2 66DSPATR(RI)
     A                                  4  3'會計科目:'
     A            DAH04     R        O  4 15REFFLD(AH0/AH04 *LIBL/GLAHPF)
     A                                  4 42'金額:'
     A            SMON      R        O  4 50REFFLD(AH0/AH12 *LIBL/GLAHPF)
     A                                      EDTCDE(1)
     A                                  5  2'(/:新增　修改 4:刪除)'
     A                                  6  5'分攤碼'
     A            DAA031        16O  O  6 14
     A  33                                  DSPATR(HI)
     A            DAA032        16O  O  6 31
     A  34                                  DSPATR(HI)
     A            DAA033        16O  O  6 48
     A  35                                  DSPATR(HI)
     A                                  6 70'金　額'
     A            DAA034        16O  O  7  8
     A  36                                  DSPATR(HI)
     A            DAA035        16O  O  7 26
     A  37                                  DSPATR(HI)
     A            DAA036        16O  O  7 43
     A  38                                  DSPATR(HI)
     A            DAA037        16O  O  7 60
     A  39                                  DSPATR(HI)
     A                                  8  7'資金貸碼'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 18'沖帳號碼'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 30'摘　　要                        -
     A                                                      '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC2
     A*%%TS  SD  19921106  133817  TSPGMR      REL-V2R1M1  5738-PW1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  98        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =視窗查詢'
     A                                 23 44'PF5 =參考資料'
     A                                 23 61'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC3
     A*%%TS  SD  19921106  133817  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      FRCDTA
     A                                      LOCK
     A                                      PROTECT
     A N98                           O 22  2'傳票分錄處理中...'
     A                                      DSPATR(HI)
     A N98        AH03      R        O    +1REFFLD(AH03)
     A                                      DSPATR(HI)
     A  98                           O 22  2'ももも此張傳票新增成功!'
     A                                      DSPATR(HI)
     A  98                           O 22 50'傳票號碼:'
     A                                      DSPATR(HI)
     A  98        AH02      R        O 22 62REFFLD(AH02)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR3                    SFL
     A*%%TS  SD  19921106  133817  TSPGMR      REL-V2R1M1  5738-PW1
     A            RRN3           3Y 0H
     A            CODE3          3Y 0O  9  2
     A            HNO3           3Y 0H
     A            HAH04     R        H      REFFLD(AH0/AH04 *LIBL/GLAHPF)
     A            DRMONH    R        H      REFFLD(AH0/AH12 *LIBL/GLAHPF)
     A            CRMONH    R        H      REFFLD(AH0/AH13 *LIBL/GLAHPF)
     A            HAH163    R        H      REFFLD(AH0/AH16 *LIBL/GLAHPF)
     A*----------------------------------------------------------------
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A*%%TS  SD  19921106  142525  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0006)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A*----------------------------------------------------------------
     A          R SFLSR4                    SFL
     A*%%TS  SD  19921106  133817  TSPGMR      REL-V2R1M1  5738-PW1
     A            RRN4           3S 0H
     A            CODE4          3S 0O  9  2
     A            HFLD1          1   H
     A            HAH05         16O  H
     A            HAH06         16O  H
     A            HAH07         16O  H
     A            DCMONH    R        H      REFFLD(AH0/AH12 *LIBL/GLAHPF)
     A            HAH08         16O  H
     A            HAH09     R        H      REFFLD(AH0/AH09 *LIBL/GLAHPF)
     A            HAH14     R        H      REFFLD(AH0/AH14 *LIBL/GLAHPF)
     A            HAH15         16O  H
     A            HAH22     R        H      REFFLD(AH0/AH22 *LIBL/GLAHPF)
     A            HAH24     R        H      REFFLD(AH0/AH24 *LIBL/GLAHPF)
     A            HAH164    R        H      REFFLD(AH0/AH16 *LIBL/GLAHPF)
     A*----------------------------------------------------------------
     A          R SFLCR4                    SFLCTL(SFLSR4)
     A*%%TS  SD  19921106  135735  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0004)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A*----------------------------------------------------------------
     A          R SFLSR5                    SFL
     A*%%TS  SD  19921106  133817  TSPGMR      REL-V2R1M1  5738-PW1
     A            RRN5           3S 0O  9  2
     A            #AH04     R        H      REFFLD(AH0/AH04 *LIBL/GLAHPF)
     A            #AH05         16O  H
     A            #AH06         16O  H
     A            #AH07         16O  H
     A            #AH12     R        H      REFFLD(AH0/AH12 *LIBL/GLAHPF)
     A            #AH13     R        H      REFFLD(AH0/AH12 *LIBL/GLAHPF)
     A            #AH08         16O  H
     A            #AH09     R        H      REFFLD(AH0/AH09 *LIBL/GLAHPF)
     A            #AH14     R        H      REFFLD(AH0/AH14 *LIBL/GLAHPF)
     A            #AH15         16O  H
     A            #AH22     R        H      REFFLD(AH0/AH22 *LIBL/GLAHPF)
     A            #AH24     R        H      REFFLD(AH0/AH24 *LIBL/GLAHPF)
     A            #AH16     R        H      REFFLD(AH0/AH16 *LIBL/GLAHPF)
     A*----------------------------------------------------------------
     A          R SFLCR5                    SFLCTL(SFLSR5)
     A*%%TS  SD  19921106  135735  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0004)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A*----------------------------------------------------------------
     A          R DSPD9
     A                                      CF04(04)
     A                                      OVERLAY
     A                                      PROTECT
     A                                      CLRL(*NO)
     A                                  7 30'                          '
     A                                      DSPATR(RI)
     A                                  8 30' '
     A                                      DSPATR(RI)
     A                                  8 32'<GLA110.3>'
     A                                  8 55' '
     A                                      DSPATR(RI)
     A                                  9 30' '
     A                                      DSPATR(RI)
     A                                  9 55' '
     A                                      DSPATR(RI)
     A                                 10 35'列印份數:'
     A            $CPY           3Y 0B 10 47EDTCDE(3)
     A                                      CHECK(RB)
     A                                 11 35'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 11 47
     A                                      DSPATR(HI)
     A                                 10 30' '
     A                                      DSPATR(RI)
     A                                 10 55' '
     A                                      DSPATR(RI)
     A                                 11 30' '
     A                                      DSPATR(RI)
     A                                 11 55' '
     A                                      DSPATR(RI)
     A                                 12 30' '
     A                                      DSPATR(RI)
     A                                 12 55' '
     A                                      DSPATR(RI)
     A                                 13 30' '
     A                                      DSPATR(RI)
     A                                 13 55' '
     A                                      DSPATR(RI)
     A                                 13 32'執行'
     A                                    +2'PF4=欄位查詢'
     A                                 14 30'                          '
     A                                      DSPATR(RI)
