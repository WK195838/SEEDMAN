     A*%%TS  SD  19921208  171847  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA650                                     *
     A*    PROGRAM NAME :預算資料拷貝－預算                        *
     A*    AUTHOR       : A1150                                      *
     A*    CREATE DATE  : 81/12/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A**                                    CSRLOC(#LIN #COL)
     A          R DSPD1
     A*%%TS  SD  19921208  171847  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF14(14)
     A                                  1  2'<GLA650.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'預算資料拷貝－預算'
     A                                      DSPATR(RI)
     A                                  4  2'自　公司別　:'
     A            DAU01F    R        B  4 18REFFLD(AU01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  6'年度　　:'
     A            DAU02F         3Y 0B  5 18EDTCDE(1)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  6'版次　　:'
     A            DAU03F    R        B  6 18REFFLD(AU03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      EDTCDE(1)
     A                                  7  6'調整比率:'
     A            DFLD           7Y 2B  7 18EDTCDE(2)
     A                                  7 28'%'
     A                                  8  6'科目代號:'
     A            DAU04F    R        B  8 18REFFLD(AU04)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8 27'-'
     A            DAU04T    R        B  8 29REFFLD(AU04)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9  6'成本中心:'
     A            DAU05F    R        B  9 18REFFLD(AU05)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 27'-'
     A            DAU05T    R        B  9 29REFFLD(AU05)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 11  2'至　公司別　:'
     A            DAU01T    R        B 11 18REFFLD(AU01)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 12  6'年度　　:'
     A            DAU02T         3Y 0B 12 18EDTCDE(1)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 13  6'版次　　:'
     A            DAU03T    R        B 13 18REFFLD(AU03)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                      EDTCDE(1)
     A                                 15  2'選擇內容　　:'
     A            DOPT           1Y 0B 15 18EDTCDE(4)
     A                                      VALUES(1 2 3)
     A                                 15 22'1.已存在者　放棄拷貝'
     A                                 16 22'2.已存在者　全部覆蓋無須確認'
     A                                 17 22'3.已存在者　逐筆確認是否覆蓋'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
     A*                                     ALWROL
     A          R DSPD2
     A*%%TS  SD  19921208  102111  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(*NO)
     A                                 14  3'                                  -
     A                                                                       '
     A                                      DSPATR(RI)
     A                                 15  3' '
     A                                      DSPATR(RI)
     A                                 15  5' <GLA650.2> '
     A                                 15 69' '
     A                                      DSPATR(RI)
     A                                 16  3' '
     A                                      DSPATR(RI)
     A                                 16 69' '
     A                                      DSPATR(RI)
     A                                 17  3' '
     A                                      DSPATR(RI)
     A                                 17  5'科目代號:'
     A            DAU04     R        O 17 17REFFLD(AU04)
     A            AF03      R        O 17 26
     A                                 17 69' '
     A                                      DSPATR(RI)
     A                                 18  3' '
     A                                      DSPATR(RI)
     A                                 18  5'成本中心:'
     A            DAU05     R        O 18 17REFFLD(AU05)
     A            #A02      R        O 18 26REFFLD(#A02 PT#APF)
     A                                 18 43'己存在'
     A                                 18 69' '
     A                                      DSPATR(RI)
     A                                 19  3' '
     A                                      DSPATR(RI)
     A                                 19  5'是否覆蓋:'
     A            COVE           1A  B 19 17VALUES('Y' 'N')
     A                                 19 22'(Y/N)'
     A                                 19 69' '
     A                                      DSPATR(RI)
     A                                 20  3'                                  -
     A                                                                       '
     A                                      DSPATR(RI)
     A                                 15 19'                                  -
     A                                                     '
     A                                 16  6'                                  -
     A                                                                  '
     A                                 17 60'        '
     A                                 18 53'               '
     A                                 19 20' '
     A                                 19 28'                                  -
     A                                            '
