     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA420                                     *
     A*    PROGRAM NAME : 欄位名稱資料維護                         *
     A*    AUTHOR       : A1152  ANGY                                *
     A*    CREATE DATE  : 1992.10.27                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<GLA420.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'欄位名稱資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAA01     R        B  4 14REFFLD(AA01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'欄位代碼:'
     A            DAA02     R        B  5 14REFFLD(AA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 18'(拷貝:'
     A            DAA01C    R        B  5 27REFFLD(AA01)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DAA02C    R        B  5 30REFFLD(AA02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  5 33')'
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0B  6 14VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 18'( 1.新增'
     A                                  6 29'2.修改'
     A                                  6 38'4.刪除'
     A                                  6 47'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CSRLOC(#LIN #COL)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA420.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'欄位名稱資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAA01     R        O  4 14REFFLD(AA01)
     A            D#B02         12   O  4 19
     A                                  5  2'欄位代碼:'
     A            DAA02     R        O  5 14REFFLD(AA02)
     A                                  5 18'(拷貝:'
     A            DAA01C    R        O  5 27REFFLD(AA01)
     A            DAA02C    R        O  5 30REFFLD(AA02)
     A                                  5 33')'
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0O  6 14
     A                                      EDTCDE(4)
     A                                  6 18'( 1.新增'
     A                                  6 29'2.修改'
     A                                  6 38'4.刪除'
     A                                  6 47'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  2'欄位名稱:'
     A            DAA03     R        B  8 14REFFLD(AA03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  2'檢核程式:'
     A  31N99                               DSPATR(HI)
     A            DAA04     R        B  9 14REFFLD(AA04)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N99                                  DSPATR(HI)
     A N31                                  DSPATR(PR)
     A            D#Y03         20   O  9 25
     A                                 10  2'傳入參數:'
     A            DAA05     R        B 10 14REFFLD(AA05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 23 44'PF5 =參考資料'
     A                                 23 61'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
