     A****************************************************************
     A*                                                              *
     A*    DSPF NAME    : PCR042AP                                   *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 82/10/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PCR042                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :零用金系統                                *
     A*    REMARK       :科目彙總表                                *
     A****************************************************************
     A                                      REF(PCCAPF)
     A          R PH1                       SKIPB(2)
     A                                    26MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    35'科目彙總表'
     A                                      SPACEA(1)
     A  20                                37'己撥補'
     A                                      SPACEA(1)
     A  21                                37'未撥補'
     A                                      SPACEA(1)
     A  22                                33'全部（撥補別）'
     A                                      SPACEA(1)
     A  23                                37'己支付'
     A                                      SPACEA(1)
     A  24                                37'未支付'
     A                                      SPACEA(1)
     A  25                                33'全部（支付別）'
     A                                      SPACEA(1)
     A                                     1'　公　司　:'
     A            CA01      R             +1REFFLD(CA01)
     A            #B02      R             +1REFFLD(#B02 PTRF)
     A                                      SPACEA(1)
     A                                     1'　辦事處　:'
     A            CA17      R             +1REFFLD(CA17)
     A            TEXT          10A       +1
     A                                      SPACEA(1)
     A                                     1'　登帳日期:'
     A            DCA10S         6  0     +1
     A                                      EDTWRD('  /  /  ')
     A                                    +1'-'
     A            DCA10E         6  0     +1
     A                                      EDTWRD('  /  /  ')
     A                                    46'日期:'
     A            $EGMDY         6  0O    53EDTCDE(Y)
     A                                    63'頁次:'
     A                                    71PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'　支付日期:'
     A            DCA11S         6  0     +1
     A                                      EDTWRD('  /  /  ')
     A                                    +1'-'
     A            DCA11E         6  0     +1
     A                                      EDTWRD('  /  /  ')
     A                                    46'時間:'
     A                                    53TIME
     A                                      SPACEA(1)
     A                                     1'零用金號碼:'
     A            DCA02S    R             +1REFFLD(CA02)
     A                                    +1'-'
     A            DCA02E    R             +1REFFLD(CA02)
     A                                    46' USER :'
     A            $USER         10   O    53
     A                                    63'<PCR042AP>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ================='
     A                                      SPACEA(1)
     A                                     1'會計科目'
     A                                    15'科目簡稱'
     A                                    60'金　額'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       -----------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            CA04      R              2
     A            AF03      R             15REFFLD(AF03 GLRF)
     A            SUM           13  2     55EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT1                       SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       -----------------'
     A                                      SPACEA(1)
     A                                    15'合　　計'
     A            T1TOT1        13  2     55
     A                                      EDTCDE(J)
     A                                    +0'も'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ================='
     A                                      SPACEA(1)
     A                                     6'財務長:'
     A                                    40'出納:'
     A                                      SPACEA(2)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =================='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ================='
     A                                      SPACEA(1)
     A                                     2'<<PCR042>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
