     A*%%TS  SD  19940121  142349  D910121     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*    DSPF NAME    : PCA0041D                                   *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 82/10/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PCA0041                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :零用金作業系統                            *
     A*    REMARK       :支付撥補／取消作業 (A/U/D/I)              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PCCAPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR1                    SFL
     A*%%TS  SD  19940121  142349  D910121     REL-V2R2M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '5' '6' '7')
     A  31                                  DSPATR(UL)
     A  31                                  DSPATR(CS)
     A N31                                  DSPATR(PR)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DCA02     R        O  9  5REFFLD(CA02)
     A            DCA28     R        O  9 15REFFLD(CA28)
     A            MC02      R        O  9 22REFFLD(MC02 RERF)
     A            DCA10          6Y 0O  9 35EDTWRD('  /  /  ')
     A            DCA11          6Y 0O  9 45EDTWRD('  /  /  ')
     A            DCA29          6Y 0O  9 54EDTWRD('  /  /  ')
     A            TOTAL         13Y 2O  9 63EDTCDE(J)
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19940121  142349  D910121     REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0010)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'<PCA004.2>'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A  30                              2 33'支付撥補作業'
     A                                      DSPATR(RI)
     A N30                              2 35'取消作業'
     A                                      DSPATR(RI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  3'公司別 :'
     A            DCA01     R        O  4 16REFFLD(CA01)
     A            D#B02     R        O  4 19REFFLD(#B02 PT#BPF)
     A                                  4 32'辦事處:'
     A            TEXT          10A  O  4 42
     A  30                              4 54'支付撥補日期:'
     A  30        DDATE          6Y 0O  4 70EDTWRD('  /  /  ')
     A                                  5  3'選擇碼 :'
     A            DSEL2          1A  O  5 16
     A  21                              5 19'支付'
     A  22                              5 19'撥補'
     A  23                              5 19'支付撥補'
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      -------'
     A                                  7  3'S(=作業):'
     A                                  7 16'5-傳票查詢'
     A  30                              7 30'6-確認'
     A N30                              7 30'7-取消'
     A                                  8  2' S零用金號碼請款人          +
     A                                      登帳日期　支付日期　撥補日期     -
     A                                         金　額'
     A          R DSPC1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A                                 23 65'【寰盛洋酒】'
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 21  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
