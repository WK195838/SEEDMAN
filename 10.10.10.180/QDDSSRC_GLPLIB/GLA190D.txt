     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA190                                     *
     A*    PROGRAM NAME : 傳票資料補登－副程式                     *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.11.16                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA190.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'傳票資料補登'
     A                                      DSPATR(RI)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  4'科目　　:'
     A            D190I2         8   O  7 16
     A            DAF03     R        O  7 26REFFLD(AF03)
     A                                  8  4'借方金額:'
     A            D190I3        13  2O  8 16
     A                                      EDTCDE(2)
     A                                      DSPATR(CS UL)
     A                                  9  4'貸方金額:'
     A            D190I4        13  2O  9 16
     A                                      EDTCDE(2)
     A                                      DSPATR(CS UL)
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 11  5'1.'
     A N55                                  DSPATR(HI)
     A            DAA031    R        O 11  8REFFLD(AA03)
     A N55                                  DSPATR(HI)
     A                                 11 29':'
     A N55                                  DSPATR(HI)
     A            D190I5        20   B 11 31
     A N99N55                               DSPATR(HI)
     A  60                                  DSPATR(ND)
     A  60
     AON31                                  DSPATR(PR)
     A  80                                  DSPATR(PC RI)
     A            D19I51        20A  O 11 53
     A                                 12  5'2.'
     A N56                                  DSPATR(HI)
     A            DAA032    R        O 12  8REFFLD(AA03)
     A N56                                  DSPATR(HI)
     A                                 12 29':'
     A N56                                  DSPATR(HI)
     A            D190I6        20   B 12 31
     A N99N56                               DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61
     AON31                                  DSPATR(PR)
     A  81                                  DSPATR(PC RI)
     A            D19I61        20A  O 12 53
     A                                 13  5'3.'
     A N57                                  DSPATR(HI)
     A            DAA033    R        O 13  8REFFLD(AA03)
     A N57                                  DSPATR(HI)
     A                                 13 29':'
     A N57                                  DSPATR(HI)
     A            D190I7        20   B 13 31
     A N99N57                               DSPATR(HI)
     A  62                                  DSPATR(ND)
     A  62
     AON31                                  DSPATR(PR)
     A  82                                  DSPATR(PC RI)
     A            D19I71        20A  O 13 53
     A                                 14  5'4.'
     A N58                                  DSPATR(HI)
     A            DAA034    R        O 14  8REFFLD(AA03)
     A N58                                  DSPATR(HI)
     A                                 14 29':'
     A N58                                  DSPATR(HI)
     A            D190I8        20   B 14 31
     A N99N58                               DSPATR(HI)
     A  63                                  DSPATR(ND)
     A  63
     AON31                                  DSPATR(PR)
     A  83                                  DSPATR(PC RI)
     A            D19I81        20A  O 14 53
     A                                 15  5'5.'
     A            DAA035    R        O 15  8REFFLD(AA03)
     A                                 15 29':'
     A            D190I9         7Y 0B 15 31
     A                                      EDTWRD('   /  /  ')
     A  64                                  DSPATR(ND)
     A  64
     AON31                                  DSPATR(PR)
     A  84                                  DSPATR(PC RI)
     A            D19I91        20A  O 15 53
     A                                 16  5'6.'
     A N32                                  DSPATR(ND)
     A            DAA036    R        O 16  8REFFLD(AA03)
     A N32                                  DSPATR(ND)
     A                                 16 29':'
     A N32                                  DSPATR(ND)
     A            D190IA        13  2B 16 31
     A                                      EDTCDE(2)
     A N32
     AO 65                                  DSPATR(ND)
     A N32
     AO 65
     AON31                                  DSPATR(PR)
     A  85                                  DSPATR(PC RI)
     A            D19IA1        20A  O 16 53
     A                                 17  5'7.'
     A N59                                  DSPATR(HI)
     A N32                                  DSPATR(ND)
     A            DAA037    R        O 17  8REFFLD(AA03)
     A N32                                  DSPATR(ND)
     A N59                                  DSPATR(HI)
     A                                 17 29':'
     A N32                                  DSPATR(ND)
     A N59                                  DSPATR(HI)
     A            D190IB        20   B 17 31
     A N99N59                               DSPATR(HI)
     A N32
     AO 66                                  DSPATR(ND)
     A N32
     AO 66
     AON31                                  DSPATR(PR)
     A  86                                  DSPATR(PC RI)
     A            D19IB1        20A  O 17 53
     A*
     A                                 20  8'摘要'
     A N32                                  DSPATR(ND)
     A                                 20 29':'
     A N32                                  DSPATR(ND)
     A            D190IC        50   B 20 31
     A N32                                  DSPATR(ND)
     A N32
     AON31                                  DSPATR(PR)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF5 =參考資料'
     A                                    +2'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
