     A*%%TS  SD  19921119  164647  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PTI140                                     *
     A*    PROGRAM NAME :成本中心資料全頁查詢                      *
     A*    AUTHOR       : A1150                                      *
     A*    CREATE DATE  : 81/11/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :基本資料系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/PTRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            #A02      R        H
     A            DOPT           1   B  8  3DSPATR(CS PC HI)
     A                                      VALUES('/' ' ')
     A            RRN            3Y 0O  8  5EDTCDE(1)
     A            #A11      R        O  8 10
     A            D#Y03         12   O  8 12
     A            #A01      R        O  8 25
     A            D#A02     R   -2   O  8 32REFFLD(#A02)
     A            #A04      R        O  8 46
     A            #A08      R        O  8 59
     A            #A10      R        O  8 72
     A            DFLD           6   O  8 74
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921119  164647  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PTI140.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 29'成本中心資料全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  3'S (=作業) : /-挑選'
     A                                  6  3'S'
     A                                      DSPATR(UL)
     A            D#A11     R        B  7 10REFFLD(#A11)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            D#A01     R        B  7 25REFFLD(#A01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A                                  6  5' No 性質／簡稱   +
     A                                       成本中心／簡稱     +
     A                                       主管電話   +
     A                                       電話       +
     A                                       類別   '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A*%%TS  SD  19921119  164647  TSPGMR      REL-V2R1M1  5738-PW1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PA1 =下一頁'
     A                                 24 11'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
