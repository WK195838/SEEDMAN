     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA450D                                    *
     A*    PROGRAM NAME : 分類帳碼資料維護                         *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : OCT 29,92'                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A*                                     CF04(04)
     A                                      CF18(18)
     A*           #LIN           3  0H
     A*           #COL           3  0H
     A                                  1  2'<GLA450.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2 31'分類帳碼資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  2'公司別　:'
     A            DA301     R        B  4 14REFFLD(A301)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'分類帳碼:'
     A            DA302     R        B  5 14REFFLD(A302)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 18'(拷貝:'
     A            DA301C    R        B  5 27REFFLD(A301)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DA302C    R        B  5 30REFFLD(A302)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  5 33')'
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0B  6 14VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 18'( 1.新增'
     A                                    +1'2.修改'
     A                                    +1'4.刪除'
     A                                    +1'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A*                                23 28'F4 =欄位查詢'
     A                                 23 27'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                  1  2'<GLA450.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2 31'分類帳碼資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  2'公司別　:'
     A            DA301     R        O  4 14REFFLD(A301)
     A            #B02      R           4 18REFFLD(#B02 *LIBL/PTRF)
     A                                  5  2'分類帳碼:'
     A            DA302     R        O  5 14REFFLD(A302)
     A                                  5 18'(拷貝:'
     A            DA301C    R        O  5 27REFFLD(A301)
     A            DA302C    R        O  5 30REFFLD(A302)
     A                                  5 33')'
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0O  6 14EDTCDE(4)
     A                                  6 18'( 1.新增'
     A                                    +1'2.修改'
     A                                    +1'4.刪除'
     A                                    +1'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  9  4'1.成本中心　:'
     A            A3041          1   B  9 22VALUES(' ' 'V')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  4'2.對象別　　:'
     A            A3042          1   B 10 22VALUES(' ' 'V')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  4'3.相關號碼1 :'
     A            A3043          1   B 11 22VALUES(' ' 'V')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  4'4.相關號碼2 :'
     A            A3044          1   B 12 22VALUES(' ' 'V')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  4'5.日期      :'
     A            A3045          1   B 13 22VALUES(' ' 'V')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  6'說明      :'
     A            DA303     R        B 15 22REFFLD(A303)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*                                18  6'異動日期　:'
     A*           AE98      R        O 18 22REFFLD(AE98)
     A*                                     EDTWRD('    /  /  ')
     A*                                19  6'異動時間　:'
     A*           AE97      R        O 19 22REFFLD(AE97)
     A*                                     EDTWRD('  :  :  ')
     A*                                20  6'異動者　　:'
     A*           AE99      R        O 20 22REFFLD(AE99)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F5 =參考資料'
     A                                 23 44'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
