     A*%%TS  SD  19940702  091934  D910121     REL-V2R2M0  5738-PW1
     A*%%EC
      *================================================================
      * MODI CODE | AUTHOR |  DATE  | DESCRIPTION
      *----------------------------------------------------------------
      *  M001     |MICHELLE|02.03.08|重新整合代碼FROM 2002/7/1
      *================================================================
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/NPRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19940702  091934  D910121     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<NPA002.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'A&P CODE維護'
     A                                      DSPATR(RI)
     A                                  2 72TIME
     A                                  3  1'年度    :'
     A            DNA01          3Y 0B  3 13EDTCDE(3)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  4  2'A&P CODE :'
     A            DNA02     R        B  4 13REFFLD(NA02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  4 19'(拷貝:'
     A            DNA01C         3Y 0B  4 28EDTCDE(3)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A            DNA02C    R        B  4 32REFFLD(NA02)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                  4 36')'
     A                                  5  1'作業選擇:'
     A            DOPT           1Y 0B  5 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  5 19'( 1.新增'
     A                                  5 30'2.修改'
     A                                  5 39'4.刪除'
     A                                  5 48'5.查詢)'
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 28'PF18=全頁查詢'
     A                                 24 65'【人頭馬寰盛】'
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  19940702  091934  D910121     REL-V2R2M0  5738-PW1
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'A&P CODE維護'
     A                                      DSPATR(RI)
     A                                  2 72TIME
     A                                  3  1'年度    :'
     A            DNA01          3Y 0O  3 13EDTCDE(3)
     A                                  4  2'A&P CODE :'
     A            DNA02     R        O  4 13REFFLD(NA02)
     A                                  4 19'(拷貝:'
     A            DNA01C         3Y 0O  4 28EDTCDE(3)
     A            DNA02C    R        O  4 32REFFLD(NA02)
     A                                  4 36')'
     A                                  5  1'作業選擇:'
     A            DOPT           1Y 0O  5 14EDTCDE(4)
     A                                  5 19'( 1.新增'
     A                                  5 30'2.修改'
     A                                  5 39'4.刪除'
     A                                  5 48'5.查詢)'
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  2'A&P說明:'
     A            DNA03     R        B  7 13REFFLD(NA03)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'A&P類型:'
     A            DNA04     R        B  8 13REFFLD(NA04)
M001MA*                                     VALUES('T' 'D')
M001MA                                      VALUES('T' 'D' 'N')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M001MA*                                 8 15'( T=合計 D=明細 )'
M001MA                                  8 15'( T=合計 D=明細 N=新建明細)'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF5 =參考資料'
     A                                 23 44'PF12=回前畫面'
     A                                 24 65'【人頭馬寰盛】'
     A                                      DSPATR(HI)
