     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : PCA003D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 10/08/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PCA003D                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :零用金系統                                *
     A*    REMARK       :作廢／取消作業                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PCCAPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '5' '6' '7')
     A  31                                  DSPATR(UL CS PC)
     A N31                                  DSPATR(PR)
     A  60                                  DSPATR(RI)
     A            DCA02     R        O  9  7REFFLD(CA02)
     A            DCA28     R        O  9 20REFFLD(CA28)
     A            MC02      R        O  9 27REFFLD(MC02 RERF)
     A            DCA10          6Y 0O  9 45EDTWRD('  /  /  ')
     A            TOTAL         13Y 2O  9 60EDTCDE(J)
     A            CA17      R        H      REFFLD(CA17)
     A            CA01      R        H      REFFLD(CA01)
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0012)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'<PCA003.2>'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A  20                              2 35'作廢作業'
     A                                      DSPATR(RI)
     A N20                              2 33'作廢取消作業'
     A                                      DSPATR(RI)
     A                                  4  3'公司別 :'
     A            DCA01     R        O  4 16REFFLD(CA01)
     A            D#B02     R        O  4 19REFFLD(#B02 PT#BPF)
     A                                  4 32'辦事處:'
     A            TEXT          10   O  4 42
     A                                  5  2'--------------------
     A                                      --------------------
     A                                      --------------------
     A                                      -------------------'
     A                                  6  3'S(=作業):'
     A                                  6 16'5-傳票查詢'
     A  20                              6 30'6-作廢'
     A N20                              6 30'7-取消'
     A                                  8  2' S 零用金號碼  請款人-
     A                                                       登帳日期-
     A                                                  金　額'
     A          R DSPC1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 21  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 65'【寰盛洋酒】'
