     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLR4E0D                                    *
     A*    PROGRAM NAME :資金代碼資料列印                          *
     A*    AUTHOR       : A1150                                      *
     A*    CREATE DATE  : 81/10/29                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLR4E0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6  0O  1 72EDTCDE(Y)
     A                                  2 31'資金代碼資料列印'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A*
     A                                  5 29'公司別　:'
     A            DAD01F    R        B  5 41REFFLD(AD01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 50'-'
     A            DAD01T    R        B  5 52REFFLD(AD01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 29'資金代碼:'
     A            DAD02F    R        B  7 41REFFLD(AD02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 50'-'
     A            DAD02T    R        B  7 52REFFLD(AD02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 29'類別　　:'
     A            DAD05     R        B  8 41REFFLD(AD05)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8 44'( 1.來源 2.用途 3.全部)'
     A*
     A                                 15 29'執行環境:'
     A            $PENV          1   B 15 41VALUES('0' '1' '2')
     A                                 15 44'(0-查詢 1-即時 2-批次)'
     A                                 16 29'列印份數:'
     A            $CPY           3Y 0B 16 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 29'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 17 41
     A N99                                  DSPATR(HI)
     A  27                             21 30'***請準備８０行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF14 =處理作業'
     A                                 24 67'【泛太資訊】'
