     A*================================================================
     A* MODI CODE | AUTHOR |  DATE  | DESCRIPTION
     A*----------------------------------------------------------------
     A*  M001     |MICHELLE|01.07.03| A&P增加種類代碼及容量選項
     A*----------------------------------------------------------------
     A*================================================================
     A*%%TS  SD  19940630  162342  D910121     REL-V2R2M0  5738-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/NPRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19940627  131808  D910121     REL-V2R2M0  5738-PW1
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      VALUES('/' ' ')
     A            RRN            3Y 0H
     A            ND01      R        O  8  4
     A            ND02      R        O  8  7EDTCDE(3)
     A            ND03      R        O  8 13
     A            ND04      R        O  8 18
M001DA*           D#A03         22A  O  8 21
M001MA*           ND05      R        O  8 44
M001MA*           NA03      R        O  8 48
M001MA            ND05      R        O  8 25
M001MA            NA03      R        O  8 29
M001AA            ND19           2   O  8 60
M001AA            ND20           1   O  8 65
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19940627  132326  D910121     REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      SFLDROP(CF02)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<NPI001.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 29'產品促銷預算全頁查詢'
     A                                      DSPATR(RI)
     A                                  2 72TIME
     A                                  4  2'S (=作業) : /-挑選'
     A                                  5  3'公年'
M001MA*                                 6  2'S司度 別 品牌／說明       -
M001MA*                                             A&P CODE／說明           -
M001MA*                                            '
M001MA                                  6  2'S司度 別 品牌-
M001MA                                       A&P CODE／說明             -
M001MA                                        種類　容量 '
     A                                      DSPATR(UL)
     A            DND01     R        B  7  4REFFLD(ND01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            WND02     R        B  7  7REFFLD(ND02)
     A                                      EDTCDE(4)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DND04     R        B  7 18REFFLD(ND04)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
M001MA*           DND05     R        B  7 44REFFLD(ND05)
M001MA            DND05     R        B  7 25REFFLD(ND05)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DND03     R        B  7 13REFFLD(ND03)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A                                  5 12'組'
     A*----------------------------------------------------------------
     A          R DSPC1
     A*%%TS  SD  19940630  162342  D910121     REL-V2R2M0  5738-PW1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3=回主畫面'
     A                                 23 26'PF12=回前畫面'
     A                                 23 43'PA1=下一頁'
     A                                 23 58'PA2=上一頁'
     A                                 24 65'【人頭馬寰盛】'
