     A*%%TS  SD  19921125  162824  TSPGMR      REL-V2R1M1  5738-PW1
     A*  90/12/25  18:11:10    SSPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : GLI1B0D                                    *
     A*    AUTHOR       : A1150                                      *
     A*    CREATE DATE  : 81/11/13                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : GLI1B0                                     *
     A*    SYSTEM       :泛太總帳                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A                                      CA03(03)
     A          R DSPD1
     A*%%TS  SD  19921125  152008  TSPGMR      REL-V2R1M1  5738-PW1
     A                                  1  2'<GLI1B0.1>'
     A            CONAME        40A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'傳票查詢－區間'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　　:'
     A            DAH01     R        B  4 16REFFLD(AH01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'傳票號碼　:'
     A            DAH02F    R        B  5 16REFFLD(AH02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 27'-'
     A            DAH02T    R        B  5 29REFFLD(AH02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A                                  7  4'製票日期:'
     A            DAH25F    R   -1   B  7 16REFFLD(AH25)
     A                                      EDTWRD('   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 27'-'
     A            DAH25T    R   -1   B  7 29REFFLD(AH25)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8  4'傳票日期:'
     A            DAH10F    R   -1   B  8 16REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  8 27'-'
     A            DAH10T    R   -1   B  8 29REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                  9  4'迴轉日期:'
     A            DAH21F    R   -1   B  9 16REFFLD(AH21)
     A                                      EDTWRD('   /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                  9 27'-'
     A            DAH21T    R   -1   B  9 29REFFLD(AH21)
     A                                      EDTWRD('   /  /  ')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 10  4'製票者　:'
     A            DAH26     R        B 10 16REFFLD(AH26)
     A                                 10 28'(空白表示*ALL)'
     A                                 11  4'傳票來源:'
     A            DAH17     R        B 11 16REFFLD(AH17)
     A                                 11 28'(空白表示*ALL)'
     A                                 12  4'過帳　　:'
     A            DAH18     R        B 12 16REFFLD(AH18)
     A                                      VALUES('1' '2' '3')
     A                                 12 19'( 1.已過帳　2.未過帳　3.全部-
     A                                      )'
     A                                 13  4'核准　　:'
     A            DAH19     R        B 13 16REFFLD(AH19)
     A                                      VALUES('1' '2' '3')
     A                                 13 19'( 1.已核准　2.未核准　3.全部-
     A                                      )'
     A                                 14  4'作廢　　:'
     A            DAH20     R        B 14 16REFFLD(AH18)
     A                                      VALUES('1' '2' '3')
     A                                 14 19'( 1.已作廢　2.未作廢　3.全部-
     A                                      )'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 24 67'【泛太資訊】'
     A*
     A          R SFLSR                     SFL
     A*%%TS  SD  19921125  162824  TSPGMR      REL-V2R1M1  5738-PW1
     A            DOPT           1A  B 11  3DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      VALUES('5' ' ')
     A            RRN            3Y 0H
     A            AH10      R        H
     A            AH21      R        H
     A            AH25      R        H
     A            AH26      R        H
     A            AH02      R        O 11  5
     A            DAH10     R   -1   O 11 16REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A            DAH21     R   -1   O 11 27REFFLD(AH21)
     A                                      EDTWRD('   /  /  ')
     A            AH18      R        O 11 40
     A            AH19      R        O 11 46
     A            AH20      R        O 11 52
     A            AH17      R        O 11 58
     A            D#Y03          6A  O 11 61
     A            SU02      R        O 11 68REFFLD(SU02 S#SUPF)
     A            AH16      R        O 12 15
     A*  90/12/25  18:11:10    SSPGMR      REL-R02M00  5728-PW1
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921125  152008  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0005)
     A                                      SFLDROP(CF02)
     A                                      ROLLUP(25)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLI1B0.2>'
     A            CONAME        40A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'傳票查詢－區間'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　　:'
     A            DAH01     R        O  4 16REFFLD(AH01)
     A            #B02      R        O  4 19REFFLD(#B02 PT#BPF)
     A                                  5  2'傳票號碼　:'
     A            DAH02F    R        O  5 16REFFLD(AH02)
     A                                  5 27'-'
     A            DAH02T    R        O  5 29REFFLD(AH02)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A                                  7  3'S (=作業): 5-傳票查詢'
     A                                  9  3'S傳票號碼  傳票日期 迴轉日期-
     A                                       過帳核准作廢 傳票來源 製+
     A                                      票者  '
     A                                      DSPATR(UL)
     A            D02F      R        B 10  5REFFLD(AH02)
     A                                      DSPATR(CS)
     A*
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF2 =明細顯示'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
