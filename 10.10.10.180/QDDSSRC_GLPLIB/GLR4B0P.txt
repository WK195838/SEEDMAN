     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLR4B0P                                    *
     A*    PROGRAM NAME :會計科目資料列印                          *
     A*    AUTHOR       : A1152                                      *
     A*    CREATE DATE  : 81/11/02                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      REF(GLRF)
     A          R PH1                       SKIPB(2)
     A                                     1MSGCON(040 US#9002 *LIBL/S#MF)
     A                                      SPACEA(2)
     A                                     1'會計科目資料列印'
     A                                      SPACEA(2)
     A                                     1'公司別　:'
     A            PAF01     R             13REFFLD(AF01)
     A            P#B02     R             16REFFLD(#B02 PTRF)
     A                                    96'日期:'
     A            PDATE          6  0    104EDTCDE(Y)
     A                                   113'頁次:'
     A                                   121PAGNBR  EDTCDE(4)
     A                                      SPACEA(1)
     A                                     1'會計科目:'
     A            PAF02F    R             13REFFLD(AF02)
     A                                    22'-'
     A            PAF02T    R             24REFFLD(AF02)
     A                                    96'時間:'
     A                                   104TIME
     A                                      EDTWRD('  :  :  ')
     A                                   115'<GLR4B0P>'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ====='
     A                                      SPACEA(1)
     A                                    80'沖帳'
     A                                   106'輸　排　小　沖　小'
     A                                      SPACEA(1)
     A                                     1'會計科目　中（英）文科目名稱'
     A                                    43'科目類別　科目屬性　等級'
     A                                    70'特定科目　組合　輸入欄位'
     A                                   106'入　序　計　帳　計'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ====='
     A*
     A          R PD1                       SPACEB(1)
     A            AF02      R              2REFFLD(AF02)
     A            AF03      R             11REFFLD(AF03)
     A            PAF05          8        44
     A            PAF06          6        55
     A            PAF08          6        63
     A            PAF09         10        70
     A            AF10      R             82REFFLD(AF10)
     A*
     A          R PD2
     A N46                                  SPACEB(1)
     A            AF04      R             12REFFLD(AF04)
     A*
     A          R PD3                       SPACEA(1)
     A            PINX           1  0     85EDTCDE(4)
     A            PAA03     R             87REFFLD(AA03)
     A            PAF18          1       108
     A            PAF19          1  0    112EDTCDE(4)
     A            PAF20          1       116
     A            PAE04          1  0    120EDTCDE(4)
     A            PAE05          1       124
     A*
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
