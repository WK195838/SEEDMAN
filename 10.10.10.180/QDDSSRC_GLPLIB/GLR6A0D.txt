     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLR6A0D                                    *
     A*    PROGRAM NAME : 年度預算明細表                           *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : DEC 08, 92'                                *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R DSPD1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLR6A0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'年度預算明細表'
     A                                      DSPATR(RI)
     A                                  5 30'公司別　:'
     A            DAU01     R        B  5 42REFFLD(AU01)
     A  60                                  DSPATR(PC RI)
     A                                  7 30'年度　　:'
     A            DAU02     R   -1Y  B  7 42REFFLD(AU02)
     A                                      EDTCDE(3)
     A  61                                  DSPATR(PC RI)
     A                                  8 30'版次　　:'
     A            DAU03     R     Y  B  8 42REFFLD(AU03)
     A                                      EDTCDE(3)
     A  62                                  DSPATR(PC RI)
     A                                  9 30'會計科目:'
     A            DAU04S    R        B  9 42REFFLD(AU04)
     A  63                                  DSPATR(PC RI)
     A                                  9 51'-'
     A            DAU04E    R        B  9 53REFFLD(AU04)
     A  64                                  DSPATR(PC RI)
     A                                 10 30'選擇內容:'
     A            DOP            1   B 10 42VALUES('1' '2')
     A                                 10 45'1.部門明細'
     A                                 11 45'2.公司彙總'
     A*
     A                                 15 30'執行環境:'
     A            $PENV          1   B 15 42VALUES('0' '1' '2')
     A                                 15 48'(0-查詢 1-即時 2-批次)'
     A                                 16 30'列印份數:'
     A            $CPY           3Y 0B 16 42EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 30'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 17 42
     A                                      DSPATR(HI)
     A  27                             21 31'***請準備１３２行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 31'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
