     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA170                                     *
     A*    PROGRAM NAME : 過帳作業                                 *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.11.06                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A                                      CF18(18)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA170.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 35'過帳作業'
     A                                      DSPATR(RI)
     A*
     A                                  6 28'公司別　:'
     A            DAH01     R        B  6 40REFFLD(AH01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  8 28'傳票日期:'
     A            DAH10     R   -1   B  8 40REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8 50'以前'
     A                                  9 28'傳票來源:'
     A                                      DSPATR(HI)
     A            DAH17     R        B  9 40REFFLD(AH17)
     A N99                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 50'(空白表示*ALL )'
     A                                 11 28'前已過帳至'
     A            DAK12     R   -1   O 11 41REFFLD(AK12)
     A                                      EDTWRD('   /  /  ')
     A                                 11 51'止'
     A*
     A                                 16 27'列印份數:'
     A            $CPY           3Y 0B 16 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 27'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 17 39
     A                                      DSPATR(HI)
     A  27                             21 28'**請準備８０行報表紙**'
     A                                      DSPATR(RI)
     A  28                             21 28'**資料處理中，請稍候**'
     A                                      DSPATR(RI BL)
     A
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF14=處理作業'
     A                                    +2'PF18=傳票查詢'
     A                                 24 67'【泛太資訊】'
     A*================================================================
     A*借方
     A          R SFLSRD                    SFL
     A            RRND           3Y 0   9  2
     A            DAH01D    R        H      REFFLD(AH01)
     A            DAH04D    R        H      REFFLD(AH04)
     A            DAH05D    R        H      REFFLD(AH05)
     A            DAH05A    R        H      REFFLD(AH05A)
     A            DAH06D    R        H      REFFLD(AH06)
     A            DAH06A    R        H      REFFLD(AH06A)
     A            DAH07D    R        H      REFFLD(AH07)
     A            DAH07A    R        H      REFFLD(AH07A)
     A            DAH08D    R        H      REFFLD(AH08)
     A            DAH08A    R        H      REFFLD(AH08A)
     A            DAH09D    R        H      REFFLD(AH09)
     A            DAH09A    R        H      REFFLD(AH09A)
     A            DAH12D    R        H      REFFLD(AH12)
     A            DAH13D    R        H      REFFLD(AH13)
     A            DAH14D    R        H      REFFLD(AH14)
     A            DAH14A    R        H      REFFLD(AH14A)
     A            DAH15D    R        H      REFFLD(AH15)
     A            DAH15A    R        H      REFFLD(AH15A)
     A            DAH16D    R        H      REFFLD(AH16)
     A*----------------------------------------------------------------
     A          R SFLCRD                    SFLCTL(SFLSRD)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A*================================================================
     A*貸方
     A          R SFLSRC                    SFL
     A            RRNC           3Y 0   9  2
     A            DAH01D    R        H      REFFLD(AH01)
     A            DAH04D    R        H      REFFLD(AH04)
     A            DAH05D    R        H      REFFLD(AH05)
     A            DAH05A    R        H      REFFLD(AH05A)
     A            DAH06D    R        H      REFFLD(AH06)
     A            DAH06A    R        H      REFFLD(AH06A)
     A            DAH07D    R        H      REFFLD(AH07)
     A            DAH07A    R        H      REFFLD(AH07A)
     A            DAH08D    R        H      REFFLD(AH08)
     A            DAH08A    R        H      REFFLD(AH08A)
     A            DAH09D    R        H      REFFLD(AH09)
     A            DAH09A    R        H      REFFLD(AH09A)
     A            DAH12D    R        H      REFFLD(AH12)
     A            DAH13D    R        H      REFFLD(AH13)
     A            DAH14D    R        H      REFFLD(AH14)
     A            DAH14A    R        H      REFFLD(AH14A)
     A            DAH15D    R        H      REFFLD(AH15)
     A            DAH15A    R        H      REFFLD(AH15A)
     A            DAH16D    R        H      REFFLD(AH16)
     A*----------------------------------------------------------------
     A          R SFLCRC                    SFLCTL(SFLSRC)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
