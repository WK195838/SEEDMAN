     A*%%TS  SD  19921027  134637  PANTECH     REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLIIE0                                     *
     A*    PROGRAM NAME : 科目資料查詢－副程式                     *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : NOV 9, 92'                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3  0H
     A            DOPT           1   B 11  3DSPATR(CS PC HI)
     A                                      VALUES('/' '5')
     A            TAH10          7  0O 11  6EDTWRD('   /  /  ')
     A            AH02      R        O 11 16
     A            AH12      R        O 11 32EDTCDE(2)
     A            AH13      R        O 11 51EDTCDE(2)
     A            AH18      R        O 11 71
     A            AH19      R        O 11 78
     A            AH16      R        O 12 26
     A*----------------------------------------------------------------
     A**                                    CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921027  134637  PANTECH     REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0005)
     A                                      SFLDROP(CF02)
     A                                      OVERLAY
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLI1E0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'科目資料查詢'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAH01     R        O  4 14REFFLD(AH01)
     A            #B02      R        O  4 26REFFLD(#B02 *LIBL/PTRF)
     A                                  5  2'會計科目:'
     A            DAH04     R        O  5 14REFFLD(AH04)
     A            AF03      R        O  5 26
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                  7  3'S (=作業) : /-挑選  5-傳票查+
     A                                      詢'
     A                                  9  3'S  傳票日期　傳票號碼　／摘要   -
     A                                      借方-
     A                                      金額         貸方金額  過帳-
     A                                       核准'
     A                                      DSPATR(UL)
     A            DAH10          7  0B 10  6EDTWRD('   /  /  ')
     A                                      DSPATR(CS HI)
     A  60                                  DSPATR(PC RI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF2 =明細顯示'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PA1 =下一頁'
     A                                    +2'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
