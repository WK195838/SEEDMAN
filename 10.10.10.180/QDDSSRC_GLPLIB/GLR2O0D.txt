     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLR2O0D                                    *
     A*    PROGRAM NAME : 沖帳明細表                               *
     A*    AUTHOR       : A1087   JOYCE                              *
     A*    CREATE DATE  : 1992.12.01                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R DSPD1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLR2O0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 34'沖帳明細表'
     A                                      DSPATR(RI)
     A                                  5 27'公司別　:'
     A            DAL01     R        B  5 39REFFLD(AL01)
     A  60                                  DSPATR(PC RI)
     A                                  7 27'會計科目:'
     A                                      DSPATR(HI)
     A            DAL02S    R        B  7 39REFFLD(AL02)
     A N99                                  DSPATR(HI)
     A  61                                  DSPATR(PC RI)
     A                                  7 48'-'
     A                                      DSPATR(HI)
     A            DAL02E    R        B  7 50REFFLD(AL02)
     A N99                                  DSPATR(HI)
     A  62                                  DSPATR(PC RI)
     A                                  8 27'截止日期:'
     A            DAK12     R   -1   B  8 39REFFLD(AK12)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC RI)
     A                                  9 27'列印選擇:'
     A            DOP            1   B  9 39VALUES('1' '2' '3' '4')
     A                                  9 42'1.總額法－明細'
     A                                 10 42'2.總額法－彙總'
     A                                 11 42'3.淨額法'
     A                                 12 42'4.全部'
     A*
     A                                 15 27'執行環境:'
     A            $PENV          1   B 15 39VALUES('0' '1' '2')
     A                                 15 42'(0-查詢 1-即時 2-批次)'
     A                                 16 27'列印份數:'
     A            $CPY           3Y 0B 16 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 27'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 17 39
     A                                      DSPATR(HI)
     A  27                             21 29'***請準備１３２行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 29'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
