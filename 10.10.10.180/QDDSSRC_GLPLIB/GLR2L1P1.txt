     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF   -ID   : GLR2L1P                                    *
     A*    PROGRAM-ID   : GLR2L0                                     *
     A*    PROGRAM NAME : 科目明細表－中文格式                     *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.12.18                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      REF(GLRF)
     A          R PH1                       SKIPB(2)
     A            P#B16     R             50REFFLD(#B16 PTRF)
     A                                      SPACEA(2)
     A                                    60'科目明細表'
     A                                      SPACEA(1)
     A            PDA01          8        62
     A                                      SPACEA(1)
     A            P0303         34        50
     A                                      SPACEA(1)
     A                                    57'日期:'
     A            DAK12     R   -1        65REFFLD(AK12)
     A                                      EDTWRD('   /  /  ')
     A                                      SPACEA(2)
     A                                   101'日期:'
     A            PDATE          6  0    109EDTWRD('  /  /  ')
     A                                   118'頁次:'
     A                                   126PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'時間:'
     A                                   109TIME
     A                                      EDTWRD('  :  :  ')
     A                                   119'<GLR2L1P>'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       =========='
     A                                      SPACEA(1)
     A                                    45'本　　　　　期'
     A                                    77'去　年　同　期'
     A                                   110'差異金額'
     A                                      SPACEA(1)
     A                                    39'----------------------------'
     A                                    71'----------------------------'
     A                                   102'----------------------------'
     A                                      SPACEA(1)
     A                                     6'項　目'
     A                                    49'金額'
     A                                    61'％'
     A                                    81'金額'
     A                                    93'％'
     A                                   112'金額'
     A                                   124'％'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       =========='
     A*
     A          R PD1                       SPACEB(1)
     A            P0402         34         4
     A  51                                38'('
     A            P0403         13  2     40EDTCDE(1)
     A  51                                57')'
     A  52                                59'('
     A  53        P03B           5  2     60EDTCDE(3)
     A N53                                60'*****'
     A  52                                66')'
     A  50                                70'('
     A            P0404         13  2     72EDTCDE(1)
     A  50                                89')'
     A  55                                91'('
     A  56        P04B           5  2     92EDTCDE(3)
     A N56                                92'*****'
     A  55                                98')'
     A  57                               101'('
     A            P043          13  2    103EDTCDE(1)
     A  57                               120')'
     A  58                               122'('
     A  59        P43B           5  2    123EDTCDE(3)
     A N59                               123'*****'
     A  58                               129')'
     A*
     A          R PT1                       SPACEB(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       =========='
     A                                      SPACEA(1)
     A                                     4'合　計'
     A  54                                38'('
     A            T0403         13  2     39EDTWRD('  ,   ,   ,  $0.  ')
     A  54                                57')'
     A                                    60'100.00'
     A  49                                70'('
     A            T0404         13  2     71EDTWRD('  ,   ,   ,  $0.  ')
     A  49                                89')'
     A                                    92'100.00'
     A  48                               101'('
     A            T043          13  2    102EDTWRD('  ,   ,   ,  $0.  ')
     A  48                               120')'
     A                                   123'100.00'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       =========='
     A                                      SPACEA(1)
     A            PNAME1       132         1
     A                                      SPACEA(1)
     A            PNAME2       132         1
     A*
