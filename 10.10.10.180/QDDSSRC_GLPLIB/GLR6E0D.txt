     A*%%TS  SD  19921111  143541  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF   -ID   : GLR6E0D                                    *
     A*    PROGRAM-ID   : GLR6E0                                     *
     A*    PROGRAM NAME : 科目預算比較表－成本中心                 *
     A*    AUTHOR       :  A1149   HUANG-SHU.MEI                     *
     A*    CREATE DATE  :  1993.01.08                                *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19921111  143541  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<GLR6E0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 69EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 27'科目預算比較表－成本中心'
     A                                      DSPATR(RI)
     A                                  5 27'公司別　:'
     A            DAI01     R        B  5 39REFFLD(AI01 GLAIPF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 27'會計科目:'
     A            DAU04S    R        B  7 39REFFLD(AU04 GLAUPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 48'-'
     A            DAU04E    R        B  7 50REFFLD(AU04 GLAUPF)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 27'會計年度:'
     A                                      DSPATR(HI)
     A            DYEAR          3Y 0B  8 39EDTCDE(4)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(HI)
     A                                  9 27'會計期間:'
     A                                      DSPATR(HI)
     A            DMMS           2Y 0B  9 39EDTCDE(4)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N64                                  DSPATR(HI)
     A                                  9 42'-'
     A            DMME           2Y 0B  9 44EDTCDE(4)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65                                  DSPATR(HI)
     A                                 10 27'版次　　:'
     A            DAU03     R        B 10 39REFFLD(AU03 GLAUPF)
     A                                      EDTCDE(4)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 15 27'執行環境:'
     A            $PENV          1A  B 15 39VALUES('0' '1' '2')
     A                                 15 47'(0-查詢 1-即時 2-批次)'
     A                                 16 27'列印份數:'
     A            $CPY           3Y 0B 16 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 27'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 17 39
     A                                      DSPATR(HI)
     A  27                             21 28'***請準備１３２行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 28'***報表處理中，請稍候***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 23 44'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
