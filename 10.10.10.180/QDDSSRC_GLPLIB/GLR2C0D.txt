     A*%%TS  SD  19921111  143541  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF   -ID   : GLR2C0D                                    *
     A*    PROGRAM-ID   : GLR2C0                                     *
     A*    PROGRAM NAME : 現金流量表                               *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1993.01.09                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19921111  143541  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<GLR2C0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 69EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 34'現金流量表'
     A                                      DSPATR(RI)
     A                                  5 27'公司別　:'
     A            DAH01     R        B  5 39REFFLD(AH01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 27'成本中心:'
     A            DAH05     R        B  7 39REFFLD(AP04)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 47'(空白表示*ALL )'
     A                                  8 27'日　　期:'
     A            DAH10S    R   -1   B  8 39REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 49'-'
     A            DAH10E    R   -1   B  8 51REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 27'報表代碼:'
     A                                      DSPATR(HI)
     A            DAR03     R        B  9 39REFFLD(AR03 GLARPF)
     A N99                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 15 27'執行環境:'
     A            $PENV          1A  B 15 39VALUES('0' '1' '2')
     A                                 15 42'(0-查詢 1-即時 2-批次)'
     A                                 16 27'列印份數:'
     A            $CPY           3Y 0B 16 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 27'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 17 39
     A                                      DSPATR(HI)
     A  27                             21 28'***請準備　８０行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 28'***報表處理中，請稍候***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
