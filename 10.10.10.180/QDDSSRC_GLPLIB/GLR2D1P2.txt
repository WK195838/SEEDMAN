     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF   -ID   : GLR2D1P2                                   *
     A*    PROGRAM-ID   : GLR2D0                                     *
     A*    PROGRAM NAME : 科目明細表-英文                        *
     A*    AUTHOR       :  A1149    MAY                              *
     A*    CREATE DATE  :  1992.11.26                                *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      REF(GLRF)
     A          R PH1                       SKIPB(2)
     A            P#B16     R             23REFFLD(#B16 PTRF)
     A                                      SPACEA(2)
     A                                    32'Detail  Report'
     A                                      SPACEA(1)
     A            PDA01          8        35
     A                                      SPACEA(1)
     A            P0303         34        23
     A                                      SPACEA(1)
     A  31                                17'Profit Center :'
     A  31        D#A01          6        33
     A  31        P#A02         32        40
     A  31                                  SPACEA(1)
     A                                    17'Fiscal Period :'
     A            DAY#S          7  0     33EDTWRD('   /  /  ')
     A                                    43'-'
     A            DAY#E          7  0     45EDTWRD('   /  /  ')
     A                                      SPACEA(1)
     A                                    50'DATE :'
     A            PDATE          6  0     57EDTWRD('  /  /  ')
     A                                    67'PAGE :'
     A                                    74PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(1)
     A                                    50'TIME :'
     A                                    57TIME
     A                                      EDTWRD('  :  :  ')
     A                                    68'<GLR2D1P>'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     5'Account Titles'
     A                                    51'Amounts'
     A                                    72'％'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ===================='
     A*
     A          R PD1                       SPACEB(1)
     A            P0402         34         3
     A  32                                43'('
     A            P0404         13  2     44EDTCDE(1)
     A  32                                61')'
     A  33                                71'('
     A  30        P100           5  2     72EDTCDE(3)
     A N30                                72' ****'
     A  33                                78')'
     A*
     A          R PT1                       SPACEB(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     5'Totals :'
     A  34                                42'('
     A            T0404         13  2     43EDTWRD('  ,   ,   ,  $0.  ')
     A  34                                61')'
     A                                    72'100.00'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ===================='
     A                                      SPACEA(1)
     A            PNAME1        80         1
     A                                      SPACEA(1)
     A            PNAME2        80         1
     A*
