     A*%%TS  SD  19921102  173210  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA352                                     *
     A*    PROGRAM NAME :報表格式設定－資產                        *
     A*    AUTHOR       : A1149  MAY                                 *
     A*    CREATE DATE  : 1992.11.05                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            DNEW           1   H
     A            F#RRN          4S 0H
     A            DDEL           1   B 12  2VALUES('4' ' ')
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DAS04     R        B 12  4REFFLD(AS04)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAS05          1A  B 12 10VALUES('+' '-' ' ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAS06     R        B 12 13REFFLD(AS06)
     A N99                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAS07     R        B 12 23REFFLD(AS07)
     A N99                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAS08     R        B 12 32REFFLD(AS08)
     A                                      VALUES('V' ' ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAS09     R        B 12 34REFFLD(AS09)
     A                                      VALUES('V' ' ')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            A              8A  B 12 37
     A N99                                  DSPATR(HI)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAS10         34O  B 12 46
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*-------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921102  173210  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0009)
     A                                      OVERLAY
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      CF19(19)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A            RRN            4Y 0H
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<GLA352.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'報表格式設定'
     A                                      DSPATR(RI)
     A            D#Y03         16A  O  3 32
     A                                  4  2'公司別　　:'
     A            DAR01     R        O  4 16REFFLD(AR01)
     A            D#B02     R        O  4 20REFFLD(#B02 PT#BPF)
     A                                  5  2'報表代碼　:'
     A            DAR02     R        O  5 16REFFLD(AR02)
     A            DAR03     R        O  5 19REFFLD(AR03)
     A                                  5 23'(拷貝:'
     A            DAR01C    R        O  5 32REFFLD(AR01)
     A            DAR02C    R        O  5 35REFFLD(AR02)
     A            DAR03C    R        O  5 38REFFLD(AR03)
     A                                  5 42')'
     A                                  6  2'作業選擇　:'
     A            DOPT           1Y 0O  6 16EDTCDE(4)
     A                                  6 23'( 1.新增'
     A                                  6 34'2.修改'
     A                                  6 43'4.刪除'
     A                                  6 52'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  2'備註　　　:'
     A            DAR04     R        B  8 16REFFLD(AR04)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 49'格式:'
     A            DAR05     R        B  8 57REFFLD(AR05)
     A                                      VALUES('1' '2')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 59'(1.中文2.英文)'
     A                                  9  2'S (=作業) : 4-刪除'
     A                                 10  1'資產'
     A                                 10 13'----會計科目---合明------------
     A                                      -----報表科目名稱---------------'
     A                                 11  2'S'
     A                                      DSPATR(UL)
     A                                 11  4'  No +/-   起      迄 計細　+
     A                                      會計科目　科目名稱               -
     A                                               '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                 21  2'會計科目起 : @S -空行   @T -+
     A                                      資產總額   @- -虛線   @C -說-
     A                                      明'
     A  98
     AO 99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 23 44'PF5 =參考資料'
     A                                 23 61'PF12=回前畫面'
     A                                 24 10'PF19=負債權益'
     A                                 24 27'PA1 =下一頁'
     A                                 24 44'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
