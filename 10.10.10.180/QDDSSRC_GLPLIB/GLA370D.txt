     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA370                                     *
     A*    PROGRAM NAME : 公司結構設定　　                         *
     A*    AUTHOR       : A1152  ANGY                                *
     A*    CREATE DATE  : 1992.11.09                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<GLA370.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'公司結構設定'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　   :'
     A            DAW01     R        B  4 17REFFLD(AW01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'作業選擇   :'
     A            DOPT           1Y 0B  5 17VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  5 24'( 1.新增'
     A                                  5 35'2.修改'
     A                                  5 44'4.刪除'
     A                                  5 53'5.查詢)'
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            HAX02     R        H      REFFLD(AX02)
     A            DNEW           1   H
     A            RRN            3Y 0H
     A            DDEL           1   B 12  3DSPATR( HI)
     A                                      VALUES('4' ' ')
     A  32                                  DSPATR(UL CS )
     A N32                                  DSPATR(PR)
     A            DAX02     R        B 12  7REFFLD(AX02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31N99                               DSPATR(UL HI )
     A N31                                  DSPATR(PR)
     A            D#B02D    R        O 12 12REFFLD(#B02 PTRF)
     A*----------------------------------------------------------------
     A**                                    CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921027  134637  PANTECH     REL-V2R1M1  5738-PW1
     A                                      CSRLOC(#LIN #COL)
     A                                      SFLSIZ(0054)
     A                                      SFLPAG(0027)
     A                                      SFLLIN(5)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA370.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'公司結構設定'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　   :'
     A            DAW01     R        O  4 17REFFLD(AW01)
     A            D#B02     R        O  4 20REFFLD(#B02 PTRF)
     A                                  5  2'作業選擇   :'
     A            DOPT           1Y 0O  5 17
     A                                      EDTCDE(4)
     A                                  5 24'( 1.新增'
     A                                  5 35'2.修改'
     A                                  5 44'4.刪除'
     A                                  5 53'5.查詢)'
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  2'備註       :'
     A            DAW02     R        B  7 17REFFLD(AW02)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  3'S (=作業) : 4-刪除'
     A                                 11  3'S'
     A                                       DSPATR(UL)
     A                                 11  5'公司'
     A                                       DSPATR(UL HI)
     A                                 11 12'公司簡稱  '
     A                                       DSPATR(UL)
     A                                 11 29'S'
     A                                       DSPATR(UL)
     A                                 11 31'公司'
     A                                       DSPATR(UL HI)
     A                                 11 38'公司簡稱  '
     A                                       DSPATR(UL)
     A                                 11 55'S'
     A                                       DSPATR(UL)
     A                                 11 57'公司'
     A                                       DSPATR(UL HI)
     A                                 11 64'公司簡稱  '
     A                                       DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                    +2'PF5 =參考資料'
     A                                    +2'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
