     A*%%TS  SD  19940419  112845  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*                                                              *
     A*    PROGRAM-ID   : PCA0081                                    *
     A*    PROGRAM NAME : 零用金常用傳票登錄                       *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 1994.02.28                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 零用金系統                               *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A*
     A            DOPT           1A  B 10  2VALUES(' ' '1' '2' '4' '5')
     A                                      DSPATR(PC)
     A            RRN            3Y 0H
     A            DCA031    R        O 10  4REFFLD(AH03)
     A                                      EDTCDE(4)
     A            DCA041    R        O 10  8REFFLD(AH04)
     A            DAF031        18A  O 10 19
     A            DCA121    R        O 10 51REFFLD(AH12)
     A                                      EDTCDE(K)
     A            DCA161    R        O 11 19REFFLD(AH16)
     A            DNO       R        H      REFFLD(RRN *SRC)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19940419  112845  D910074     REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0005)
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      SFLDROP(CF02)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PCA008.3>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'零用金常用傳票登錄'
     A                                      DSPATR(RI)
     A                                  3  2'公司別　　:'
     A            DCA01     R        O  3 16REFFLD(AH01)
     A            #B02      R        O  3 19REFFLD(#B02 PTRF)
     A                                  3 37'辦事處:'
     A            DCA17          1A  O  3 48
     A                                  4  2'零用金號碼:'
     A            DCD02     R        O  4 16REFFLD(AN02)
     A                                  4 37'員工代號:'
     A            DCA28          6   O  4 49
     A            MC02      R             +1REFFLD(MC02 RERF)
     A                                  5  2'登帳日期　:'
     A            DCA10     R   -1   O  5 16REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ------'
     A                                  7  2'S (=作業) :'
     A                                  7 17'1-新增'
     A                                  7 27'2-修改'
     A                                  7 37'4-刪除'
     A                                  7 47'5-查詢'
     A                                  9  2'S  NO科目　　　　科目名稱／摘要+
     A                                      　　　　　　　　　　　　　　金額+
     A                                      　　　　　'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                 20  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ------'
     A                                 21 37'合計:'
     A            TOT           13Y 2O 21 51EDTCDE(1)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  98        DMSG          78A  O 22  2DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF2 =明細顯示'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF6 =分錄製作完畢'
     A                                    +2'PF12=回前畫面'
     A                                 24 65'【人頭馬匯東】'
     A*================================================================
     A          R DSPD3
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PCA008.4>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'零用金常用傳票登錄'
     A                                      DSPATR(RI)
     A                                  3  2'公司別　　:'
     A            DCA01     R        O  3 16REFFLD(AH01)
     A            #B02      R        O  3 19REFFLD(#B02 PTRF)
     A                                  4  2'申請員工　:'
     A            DCA28          6   O  4 16
     A                                  5  2'登帳日期　:'
     A            DCA10     R   -1   O  5 16REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  4'序號　　:'
     A            DCA03     R        O  7 16REFFLD(AH03)
     A                                      EDTCDE(4)
     A                                  8  4'科目　　:'
     A                                      DSPATR(HI)
     A            DCA04     R        B  8 16REFFLD(AH04)
     A N99                                  DSPATR(HI)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A            DAF03     R        O  8 26REFFLD(AF03)
     A                                  9  4'金　　額:'
     A            DCA12     R        B  9 16REFFLD(AH12)
     A                                      EDTCDE(K)
     A                                      DSPATR(CS)
     A                                      CHECK(RB)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*----------------------------------------------------------------
     A          R DSPC3
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF5 =參考資料'
     A                                 24 10'PF12=回前畫面'
     A                                 24 65'【人頭馬匯東】'
     A*----------------------------------------------------------------
     A          R DSPD4
     A*%%TS  SD  19940311  113044  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A  31                                  CF04(04)
     A  31                                  CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PROTECT
     A  37                                  FRCDTA
     A  37                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                 12  5'1.'
     A N55                                  DSPATR(HI)
     A            DAA031    R        O 12  8REFFLD(AA03)
     A N55                                  DSPATR(HI)
     A                                 12 29':'
     A N55                                  DSPATR(HI)
     A            DCA05     R        B 12 31REFFLD(AH05)
     A N99N55                               DSPATR(HI)
     A  60                                  DSPATR(ND)
     A  60
     AON31                                  DSPATR(PR)
     A  80                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
     A            DCA051        20A  O 12 53
     A                                 13  5'2.'
     A N56                                  DSPATR(HI)
     A            DAA032    R        O 13  8REFFLD(AA03)
     A N56                                  DSPATR(HI)
     A                                 13 29':'
     A N56                                  DSPATR(HI)
     A            DCA06     R        B 13 31REFFLD(AH06)
     A N99N56                               DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61
     AON31                                  DSPATR(PR)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
     A            DCA061        20A  O 13 53
     A                                 14  5'3.'
     A N57                                  DSPATR(HI)
     A            DAA033    R        O 14  8REFFLD(AA03)
     A N57                                  DSPATR(HI)
     A                                 14 29':'
     A N57                                  DSPATR(HI)
     A            DCA07     R        B 14 31REFFLD(AH07)
     A N99N57                               DSPATR(HI)
     A  62                                  DSPATR(ND)
     A  62
     AON31                                  DSPATR(PR)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A            DCA071        20A  O 14 53
     A                                 15  5'4.'
     A N58                                  DSPATR(HI)
     A            DAA034    R        O 15  8REFFLD(AA03)
     A N58                                  DSPATR(HI)
     A                                 15 29':'
     A N58                                  DSPATR(HI)
     A            DCA08     R        B 15 31REFFLD(AH08)
     A N99N58                               DSPATR(HI)
     A  63                                  DSPATR(ND)
     A  63
     AON31                                  DSPATR(PR)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A            DCA081        20A  O 15 53
     A                                 16  5'5.'
     A            DAA035    R        O 16  8REFFLD(AA03)
     A                                 16 29':'
     A            DCA09     R   -1   B 16 31REFFLD(AH09)
     A                                      EDTWRD('   /  /  ')
     A  64                                  DSPATR(ND)
     A  64
     AON31                                  DSPATR(PR)
     A  84                                  DSPATR(PC)
     A  84                                  DSPATR(RI)
     A            DCA091        20A  O 16 53
     A                                 17  5'6.'
     A            DAA036    R        O 17  8REFFLD(AA03)
     A                                 17 29':'
     A            DCA14     R        B 17 31REFFLD(AH14)
     A                                      EDTCDE(2)
     A  65                                  DSPATR(ND)
     A  65
     AON31                                  DSPATR(PR)
     A  85                                  DSPATR(PC)
     A  85                                  DSPATR(RI)
     A            DCA141        20A  O 17 53
     A                                 18  5'7.'
     A N59                                  DSPATR(HI)
     A            DAA037    R        O 18  8REFFLD(AA03)
     A N59                                  DSPATR(HI)
     A                                 18 29':'
     A N59                                  DSPATR(HI)
     A            DCA15     R        B 18 31REFFLD(AH15)
     A N99N59                               DSPATR(HI)
     A  66                                  DSPATR(ND)
     A  66
     AON31                                  DSPATR(PR)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(RI)
     A            DCA151        20A  O 18 53
     A                                 19  8'摘要代出序號'
     A                                      DSPATR(HI)
     A                                 19 29':'
     A                                      DSPATR(HI)
     A            DCA03     R        O 19 31REFFLD(AH03)
     A                                 20  8'摘要　　　　'
     A                                      DSPATR(HI)
     A                                 20 29':'
     A                                      DSPATR(HI)
     A            DCA161    R        B 20 31REFFLD(AH16)
     A N31                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R DSPC4
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF12=回前畫面'
     A                                 24 65'【人頭馬匯東】'
     A*================================================================
     A          R SFLSRD                    SFL
     A            RRND           3Y 0   9  2
     A            DCA12D    R        H      REFFLD(AH12)
     A            DCA04D    R        H      REFFLD(AH04)
     A            DCA05D    R        H      REFFLD(AH05)
     A            DCA05A    R        H      REFFLD(AH05A)
     A            DCA06D    R        H      REFFLD(AH06)
     A            DCA06A    R        H      REFFLD(AH06A)
     A            DCA07D    R        H      REFFLD(AH07)
     A            DCA07A    R        H      REFFLD(AH07A)
     A            DCA08D    R        H      REFFLD(AH08)
     A            DCA08A    R        H      REFFLD(AH08A)
     A            DCA09D    R        H      REFFLD(AH09)
     A            DCA09A    R        H      REFFLD(AH09A)
     A            DCA14D    R        H      REFFLD(AH14)
     A            DCA14A    R        H      REFFLD(AH14A)
     A            DCA15D    R        H      REFFLD(AH15)
     A            DCA15A    R        H      REFFLD(AH15A)
     A            DCA16D    R        H      REFFLD(AH16)
     A            DCA16H    R        H      REFFLD(AH16)
     A            DLTCOD         1   H
     A*----------------------------------------------------------------
     A          R SFLCRD                    SFLCTL(SFLSRD)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
