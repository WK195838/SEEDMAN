     A*%%TS  SD  19921111  143541  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF   -ID   : GLR2I0D                                    *
     A*    PROGRAM-ID   : GLR2I0                                     *
     A*    PROGRAM NAME : 比較報表列印                             *
     A*    AUTHOR       :  A1149    MAY                              *
     A*    CREATE DATE  :  1992.12.13                                *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19921111  143541  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<GLR2I0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 69EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'比較報表列印'
     A                                      DSPATR(RI)
     A                                  5 29'公司別　:'
     A            DAI01     R        B  5 41REFFLD(AI01 GLAIPF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 29'成本中心:'
     A            D#A01     R        B  7 41REFFLD(#A01 PT#APF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 49'(空白表示*ALL)'
     A                                  8 29'會計年度:'
     A                                      DSPATR(HI)
     A            DYEAR          3Y 0B  8 41EDTCDE(4)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N62                                  DSPATR(HI)
     A                                  9 29'會計期間:'
     A                                      DSPATR(HI)
     A            DMM            2Y 0B  9 41EDTCDE(4)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(HI)
     A                                 10 29'報表代碼:'
     A                                      DSPATR(HI)
     A            DAR03     R        B 10 41REFFLD(AR03 GLARPF)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N64                                  DSPATR(HI)
     A                                 15 29'執行環境:'
     A            $PENV          1A  B 15 41VALUES('0' '1' '2')
     A                                 15 49'(0-查詢 1-即時 2-批次)'
     A                                 16 29'列印份數:'
     A            $CPY           3Y 0B 16 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 29'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 17 41
     A                                      DSPATR(HI)
     A  27                             21 30'***請準備１３２行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 30'***報表處理中，請稍候***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 23 44'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
