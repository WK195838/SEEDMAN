     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA160                                     *
     A*    PROGRAM NAME : 追溯分攤                                 *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.12.10                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A                                      CF18(18)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA160.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 35'追溯分攤'
     A                                      DSPATR(RI)
     A*
     A                                  5 27'公司別　:'
     A            DAP01     R        B  5 39REFFLD(AP01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 27'傳票日期:'
     A            DAP06     R   -1   B  7 39REFFLD(AP06)
     A                                      EDTWRD('   /  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8 27'選擇內容:'
     A            DOP            1   B  8 39VALUES('1' '2')
     A                                  8 42'( 1.試算 2.正式)'
     A                                 10 25'資料篩選　:'
     A                                 11 27'會計年度:'
     A                                      DSPATR(HI)
     A            DYEAR          3Y 0B 11 39EDTCDE(4)
     A N99                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 12 27'會計期間:'
     A                                      DSPATR(HI)
     A            DMM            2Y 0B 12 39EDTCDE(4)
     A N99                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 12 42'('
     A            DYMDS          7Y 0O 12 44EDTWRD('   /  /  ')
     A                                 12 54'-'
     A            DYMDE          7Y 0O 12 56EDTWRD('   /  /  ')
     A                                 12 66')'
     A                                 13 27'會計科目:'
     A            DAP02S    R        B 13 39REFFLD(AP02)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 13 49'-'
     A            DAP02E    R        B 13 51REFFLD(AP02)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 14 27'分攤組別:'
     A            DAP03S    R        B 14 39REFFLD(AP03)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 14 49'-'
     A            DAP03E    R        B 14 51REFFLD(AP03)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A*
     A                                 16 27'列印份數:'
     A            $CPY           3Y 0B 16 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 27'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 17 39
     A                                      DSPATR(HI)
     A  27                             21 28'**請準備１３２行報表紙**'
     A                                      DSPATR(RI)
     A  28                             21 28'**資料處理中，請稍候**'
     A                                      DSPATR(RI BL)
     A
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF14=處理作業'
     A                                    +2'PF18=傳票查詢'
     A                                 24 67'【泛太資訊】'
     A*================================================================
     A*借方
     A          R SFLSRD                    SFL
     A            RRND           3Y 0   9  2
     A            DAQ02D    R        H      REFFLD(AQ02)
     A            DAQ02B    R        H      REFFLD(AQ02B)
     A            DAQ02A    R        H      REFFLD(AQ02A)
     A            DAQ12D    R        H      REFFLD(AQ12)
     A*
     A            DAH01D    R        H      REFFLD(AH01)
     A            DAH04D    R        H      REFFLD(AH04)
     A            DAH05D    R        H      REFFLD(AH05)
     A            DAH05A    R        H      REFFLD(AH05A)
     A            DAH06D    R        H      REFFLD(AH06)
     A            DAH06A    R        H      REFFLD(AH06A)
     A            DAH07D    R        H      REFFLD(AH07)
     A            DAH07A    R        H      REFFLD(AH07A)
     A            DAH08D    R        H      REFFLD(AH08)
     A            DAH08A    R        H      REFFLD(AH08A)
     A            DAH09D    R        H      REFFLD(AH09)
     A            DAH09A    R        H      REFFLD(AH09A)
     A            DAH12D    R        H      REFFLD(AH12)
     A            DAH13D    R        H      REFFLD(AH13)
     A            DAH14D    R        H      REFFLD(AH14)
     A            DAH14A    R        H      REFFLD(AH14A)
     A            DAH15D    R        H      REFFLD(AH15)
     A            DAH15A    R        H      REFFLD(AH15A)
     A            DAH16D    R        H      REFFLD(AH16)
     A            DAH22D    R        H      REFFLD(AH22)
     A*----------------------------------------------------------------
     A          R SFLCRD                    SFLCTL(SFLSRD)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A*================================================================
     A*貸方
     A          R SFLSRC                    SFL
     A            RRNC           3Y 0   9  2
     A            DAQ02D    R        H      REFFLD(AQ02)
     A            DAQ02B    R        H      REFFLD(AQ02B)
     A            DAQ02A    R        H      REFFLD(AQ02A)
     A            DAQ12D    R        H      REFFLD(AQ12)
     A*
     A            DAH01D    R        H      REFFLD(AH01)
     A            DAH04D    R        H      REFFLD(AH04)
     A            DAH05D    R        H      REFFLD(AH05)
     A            DAH05A    R        H      REFFLD(AH05A)
     A            DAH06D    R        H      REFFLD(AH06)
     A            DAH06A    R        H      REFFLD(AH06A)
     A            DAH07D    R        H      REFFLD(AH07)
     A            DAH07A    R        H      REFFLD(AH07A)
     A            DAH08D    R        H      REFFLD(AH08)
     A            DAH08A    R        H      REFFLD(AH08A)
     A            DAH09D    R        H      REFFLD(AH09)
     A            DAH09A    R        H      REFFLD(AH09A)
     A            DAH12D    R        H      REFFLD(AH12)
     A            DAH13D    R        H      REFFLD(AH13)
     A            DAH14D    R        H      REFFLD(AH14)
     A            DAH14A    R        H      REFFLD(AH14A)
     A            DAH15D    R        H      REFFLD(AH15)
     A            DAH15A    R        H      REFFLD(AH15A)
     A            DAH16D    R        H      REFFLD(AH16)
     A            DAH22D    R        H      REFFLD(AH22)
     A*----------------------------------------------------------------
     A          R SFLCRC                    SFLCTL(SFLSRC)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
