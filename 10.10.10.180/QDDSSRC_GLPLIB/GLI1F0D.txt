     A*%%TS  SD  19921027  134637  PANTECH     REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLIIF0                                     *
     A*    PROGRAM NAME : 傳票查詢　　　　                         *
     A*    AUTHOR       : A1152  ANGY                                *
     A*    CREATE DATE  : 1992.11.05                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            DOPT           1   B  9  2VALUES('5' ' ')
     A  33                                  DSPATR(PC)
     A            RRN            3Y 0O  9  4EDTCDE(4)
     A            DAH04         16   O  9  8
     A            DAF03         22   O  9 25
     A            DAH12         12  2O  9 48EDTCDE(2)
     A            DAH13         12  2O  9 65EDTCDE(2)
     A            AH16      R        O 10 23
     A*----------------------------------------------------------------
     A**                                    CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921027  134637  PANTECH     REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0005)
     A                                      SFLDROP(CF02)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A*                                     ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLI1F0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 35'傳票查詢'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAH01     R        O  4 14REFFLD(AH01)
     A            D#B02         12   O  4 17
     A                                  4 33'迴轉日期:'
     A            DAH21          7  0O  4 45EDTWRD('   /  /  ')
     A                                  4 57'製票者　:'
     A            DSU02         10   O  4 69
     A                                  5  2'傳票號碼:'
     A            DAH02     R        O  5 14REFFLD(AH02)
     A            DAH20          6   O  5 24
     A                                  5 33'傳票日期:'
     A            DAH10          7  0O  5 45EDTWRD('   /  /  ')
     A                                  5 57'製票日期:'
     A            DAH25          7  0O  5 69EDTWRD('   /  /  ')
     A                                  6  2'S=5:明細查詢'
     A                                      DSPATR(HI)
     A                                  6 33'過帳'
     A            DAH18     R           6 41REFFLD(AH18)
     A                                  6 44'核准'
     A            DAH19     R           6 51REFFLD(AH19)
     A                                  6 57'傳票來源:'
     A            DAH17     R           6 69REFFLD(AH17)
     A            D#Y03          6      6 72
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                  8  2'S No借方／貸方科目 -
     A                                       科目名稱／摘　　要      -
     A                                        借方金額       貸方金額  '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                 20  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                 21 37'合計:'
     A            TAH12         13  2O 21 46EDTCDE(2)
     A            TAH13         13  2O 21 64EDTCDE(2)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'PF2 =明細顯示'
     A                                 23 28'PF3 =回主畫面'
     A                                 23 45'PF12=回前畫面'
     A                                 24 11'PA1 =下一頁'
     A                                 24 28'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
