     A*%%TS  SD  19921116  144245  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLI430D                                    *
     A*    PROGRAM NAME : 沖帳組合全頁查詢                         *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : OCT 27,92'                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            DOPT           1   B  8  2DSPATR(CS PC HI)
     A                                      VALUES('/' ' ')
     A            RRN            3Y 0O  8  5EDTCDE(3)
     A            AE01      R        O  8 12
     A            AE02      R        O  8 21
     A            AE041          1  0O  8 31EDTCDE(4)
     A            AE051          1   O  8 33
     A            AE042          1  0O  8 40EDTCDE(4)
     A            AE052          1   O  8 42
     A            AE043          1  0O  8 51EDTCDE(4)
     A            AE053          1   O  8 53
     A            AE044          1  0O  8 63EDTCDE(4)
     A            AE054          1   O  8 65
     A            AE045          1  0O  8 73EDTCDE(4)
     A            AE055          1   O  8 75
     A*----------------------------------------------------------------
     A*                                     CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921116  144245  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLI430.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'沖帳組合全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  2'S (=作業) : /-挑選'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  5' No 公司別沖帳組合成本中心+
     A                                      對象別  相關號碼1 相關號碼2 -
     A                                      日期'
     A                                      DSPATR(UL)
     A            DAE01     R        B  7 12REFFLD(AE01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DAE02     R        B  7 21REFFLD(AE02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3=回主畫面'
     A                                 23 27'F12=回前畫面'
     A                                 23 44'PA1=下一頁'
     A                                 23 61'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
