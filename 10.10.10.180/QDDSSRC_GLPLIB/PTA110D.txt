     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PTA110D                                    *
     A*    PROGRAM NAME : 代碼欄位資料維護                         *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : OCT 27,92'                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PTRF)
     A                                      PRINT
     A*         R DSPD1                     CSRLOC(#LIN #COL)
     A          R DSPD1
     A                                      CA03(03)
     A*                                     CF04(04)
     A                                      CF18(18)
     A*           #LIN           3  0H
     A*           #COL           3  0H
     A                                  1  2'<PTA110.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2 31'代碼欄位資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  2'代碼欄位:'
     A            D#Z01     R        B  4 14REFFLD(#Z01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'作業選擇:'
     A            DOPT           1Y 0B  5 14VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  5 18'( 1.新增'
     A                                    +1'2.修改'
     A                                    +1'4.刪除'
     A                                    +1'5.查詢)'
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A*                                23 28'F4 =欄位查詢'
     A                                 23 27'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03)
     A*                                     CF05(05)
     A                                      CF12(12)
     A                                  1  2'<PTA110.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2 31'代碼欄位資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  2'代碼欄位:'
     A            D#Z01     R        O  4 14REFFLD(#Z01)
     A                                  5  2'作業選擇:'
     A            DOPT           1Y 0O  5 14EDTCDE(4)
     A                                  5 18'( 1.新增'
     A                                    +1'2.修改'
     A                                    +1'4.刪除'
     A                                    +1'5.查詢)'
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  2'代碼名稱:'
     A            D#Z02     R        B  7 14REFFLD(#Z02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'代碼長度:'
     A            D#Z03     R     Y  B  8 14REFFLD(#Z03)
     A                                      EDTCDE(3)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 18'位'
     A                                  9  2'說明長度:'
     A            D#Z04     R     Y  B  9 14REFFLD(#Z04)
     A                                      EDTCDE(3)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 18'位'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A*                                23 45'F5 =參考資料'
     A                                 23 27'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
