     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLR1N0D                                    *
     A*    PROGRAM NAME :傳票核對表　　　                          *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 83/07/25                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLR1N0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6  0O  1 72EDTCDE(Y)
     A                                  2 31'憑證未到資料列印'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A*
     A                                  5 27'公司別　:'
     A            DA701     R        B  5 39REFFLD(AH01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 27'經手人　:'
     A N50                                  DSPATR(HI)
     A            DA705F    R        B  7 39REFFLD(A705)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 53'-'
     A            DA705T    R        B  7 55REFFLD(A705)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 27'傳票號碼:'
     A            DA702F    R        B  9 39REFFLD(A702)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                  9 53'-'
     A            DA702T    R        B  9 55REFFLD(A702)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 11 27'列印內容:'
     A            DSEL           1   B 11 39
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 11 43'(A-憑證未到)'
     A                                 12 43'(B-憑證已到)'
     A                                 13 43'(C-全部)'
     A*
     A                                 15 27'執行環境:'
     A            $PENV          1   B 15 39VALUES('0' '1' '2')
     A                                 15 45'(0-查詢 1-即時 2-批次)'
     A                                 16 27'列印份數:'
     A            $CPY           3Y 0B 16 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 27'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 17 39
     A N99                                  DSPATR(HI)
     A  27                             21 28'**請準備１３２行報表紙**'
     A                                      DSPATR(RI)
     A  28                             21 28'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF14 =處理作業'
     A                                 24 67'【泛太資訊】'
