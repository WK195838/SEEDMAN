     A*%%TS  SD  19921229  092211  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : GLR1J0D                                    *
     A*    AUTHOR       : A1150                                      *
     A*    CREATE DATE  : 81/11/18                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : GLR1J0                                     *
     A*    SYSTEM       :泛太總帳                                  *
     A*    REMARK       :傳票列印                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 '欄位查詢')
     A                                      CF14(14 '處理作業')
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLR1J0.1>'
     A            CONAME        40A  O  1 25MSGID(US#9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 35'傳票列印'
     A                                      DSPATR(RI)
     A                                  5 27'公司別　:'
     A            DAH01     R        B  5 39REFFLD(AH01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  7 27'製票日期:'
     A            D25F           7  0B  7 39EDTWRD('   /  /  ')
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  7 49'-'
     A            D25T           7  0B  7 51EDTWRD('   /  /  ')
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  8 27'傳票日期:'
     A            D10F           7  0B  8 39EDTWRD('   /  /  ')
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                  8 49'-'
     A            D10T           7  0B  8 51EDTWRD('   /  /  ')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                  9 27'傳票號碼:'
     A            DAH02F    R        B  9 39REFFLD(AH02)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                  9 49'-'
     A            DAH02T    R        B  9 51REFFLD(AH02)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 10 27'製票者　:'
     A            DAH26     R        B 10 39REFFLD(AH26)
     A                                 10 51'(空白表示*ALL )'
     A                                 11 27'傳票來源:'
     A                                      DSPATR(HI)
     A            DAH17     R        B 11 39REFFLD(AH17)
     A N99                                  DSPATR(HI)
     A                                 11 51'(空白表示*ALL )'
     A                                 15 27'執行環境:'
     A            $PENV          1   B 15 39VALUES('0' '1' '2')
     A                                 15 45'(0-查詢1-即時2-批次)'
     A                                 16 27'列印份數:'
     A            $CPY           3Y 0B 16 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 27'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 17 39
     A N99                                  DSPATR(HI)
     A*
     A  27                             21 27'**請準備正式傳票報表紙**'
     A                                      DSPATR(RI)
     A  28                             21 27'報表處理中請稍候'
     A                                      DSPATR(RI BL)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF14 =處理作業'
     A                                 24 67'【泛太資訊】'
