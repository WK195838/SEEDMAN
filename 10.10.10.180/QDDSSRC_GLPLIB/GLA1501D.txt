     A*%%TS  SD  19921027  134637  PANTECH     REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA1501                                    *
     A*    PROGRAM NAME : 取消核准　　　　                         *
     A*    AUTHOR       : A1152  ANGY                                *
     A*    CREATE DATE  : 1992.11.09                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            DOPT           1   B  9  2DSPATR(HI)
     A                                      VALUES('5' '6' ' ')
     A  31                                  DSPATR(UL CS PC)
     A N31                                  DSPATR(PR)
     A            RRN            3Y 0O  9  5EDTCDE(4)
     A            DAH02     R        O  9 10REFFLD(AH02)
     A            AH26      R        O  9 21
     A            DSU02         10   O  9 32
     A            DAH25          7  0O  9 45EDTWRD('   /  /  ')
     A            DAH10          7  0O  9 57EDTWRD('   /  /  ')
     A            DAH21          7  0O  9 69EDTWRD('   /  /  ')
     A*----------------------------------------------------------------
     A**                                    CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921027  134637  PANTECH     REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF13(13)
     A*                                     ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLA140.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 35'取消核准'
     A                                      DSPATR(RI)
     A                                  4  2'公司別    :'
     A            DAH01     R        O  4 16REFFLD(AH01)
     A            D#B02     R        O  4 19REFFLD(#B02 PT#BPF)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A                                  6  2'S (=作業) : 5-傳票查詢'
     A                                  6 30'6-取消核准'
     A                                  8  2'S'
     A                                      DSPATR(UL)
     A                                  8  6'NO 傳票號碼 製票者           -
     A                                            製票日期 -
     A                                       傳票日期  迴轉日期'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF12=回前畫面'
     A                                 23 44'PF13=挑選全部'
     A                                 24 67'【泛太資訊】'
