     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA340                                     *
     A*    PROGRAM NAME : 費用分攤設定更正                         *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.11.05                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<GLA340.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'費用分攤設定更正'
     A                                      DSPATR(RI)
     A                                  3  2'公司別　　:'
     A            DAP01     R        B  3 16REFFLD(AP01)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A            #B02      R        O  3 25REFFLD(#B02 PTRF)
     A                                  4  2'會計科目　:'
     A            DAP02     R        B  4 16REFFLD(AP02)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A            DAF031    R        O  4 25REFFLD(AF03)
     A                                  5  2'成本中心　:'
     A            DAP04     R        B  5 16REFFLD(AP04)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A            #A02      R        O  5 25REFFLD(#A02 PTRF)
     A                                  5 51'分攤組別:'
     A            DAP03     R        B  5 63REFFLD(AP03)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A            D#Y03         12   O  5 66
     A                                  6  2'分攤別　　:'
     A            DAP05     R        B  6 16REFFLD(AP05)
     A                                      VALUES('1' '2')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                  6 21'( 1.預設分攤 2.追溯分攤)'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                      OVERLAY
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  98        DMSG          78A  O 22  2DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A            DOPT           1A  B 11  2VALUES(' ' '1' '2' '4' '5')
     A                                      DSPATR(PC)
     A            RRN            3Y 0H
     A            DAQ031    R        O 11  4REFFLD(AQ03)
     A                                      EDTCDE(4)
     A            DAQ041    R        O 11  9REFFLD(AQ04)
     A            DAQ161    R        O 11 18REFFLD(AQ16)
     A            DAQ121    R        O 11 69REFFLD(AQ12)
     A                                      EDTWRD('   .    ')
     A            DAF033    R        O 12 18REFFLD(AF03)
     A            DNO       R        H      REFFLD(RRN *SRC)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0004)
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      CF12(12)
     A                                      CF13(13)
     A                                      SFLDROP(CF02)
     A                                      OVERLAY
     A                                      PROTECT
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLA340.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'費用分攤設定更正'
     A                                      DSPATR(RI)
     A                                  3  2'公司別　　:'
     A            DAP01     R        O  3 16REFFLD(AP01)
     A            #B02      R        O  3 25REFFLD(#B02 PTRF)
     A                                  4  2'會計科目　:'
     A            DAP02     R        O  4 16REFFLD(AP02)
     A            DAF031    R        O  4 25REFFLD(AF03)
     A                                  5  2'成本中心　:'
     A            DAP04     R        O  5 16REFFLD(AP04)
     A            #A02      R        O  5 25REFFLD(#A02 PTRF)
     A                                  5 51'分攤組別:'
     A            DAP03     R        O  5 63REFFLD(AP03)
     A            D#Y03         12   O  5 66
     A                                  6  2'分攤別　　:'
     A            DAP05     R        O  6 16REFFLD(AP05)
     A                                  6 21'( 1.預設分攤 2.追溯分攤)'
     A                                  6 51'啟用日期:'
     A            DAP06     R   -1   B  6 63REFFLD(AP06)
     A                                      EDTWRD('   /  /  ')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                  7  2'備　　註　:'
     A            DAP07     R        B  7 16REFFLD(AP07)
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  2'S (=作業) :'
     A                                  9 17'1-新增'
     A                                    +2'2-修改'
     A                                    +2'4-刪除'
     A                                    +2'5-查詢'
     A                                 10  2'S  NO 會計科目　摘要／科目名稱 -
     A                                                                       -
     A                                      分攤比率%'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC2
     A*  91/05/24  18:28:48    Q790601     REL-R03M00  5728-PW1
     A                                      OVERLAY
     A                                 19  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 20 57'合　計:'
     A            DTOT           7Y 4O 20 69EDTWRD('   .    ')
     A                                    +1'%'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  98        DMSG          78A  O 22  2DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF2 =明細顯示'
     A                                 23 27'PF3 =回主畫面'
     A                                    +2'PF6 =設定更新完畢'
     A                                 24 10'PF12=回前畫面'
     A                                    +2'PF13=設定刪除'
     A                                 24 67'【泛太資訊】'
     A*================================================================
     A          R DSPD3
     A                                      CSRLOC(#LIN #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA340.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A                                  2 31'費用分攤設定更正'
     A                                      DSPATR(RI)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  3  2'公司別　　:'
     A            DAP01     R        O  3 16REFFLD(AP01)
     A            #B02      R        O  3 25REFFLD(#B02 PTRF)
     A                                  4  2'會計科目　:'
     A            DAP02     R        O  4 16REFFLD(AP02)
     A            DAF031    R        O  4 25REFFLD(AF03)
     A                                  5  2'成本中心　:'
     A            DAP04     R        O  5 16REFFLD(AP04)
     A            #A02      R        O  5 25REFFLD(#A02 PTRF)
     A                                  5 51'分攤組別:'
     A            DAP03     R        O  5 63REFFLD(AP03)
     A            D#Y03         12   O  5 66
     A                                  6  2'分攤別　　:'
     A            DAP05     R        O  6 16REFFLD(AP05)
     A                                  6 21'( 1.預設分攤 2.追溯分攤)'
     A                                  6 51'啟用日期:'
     A            DAP06     R   -1   O  6 63REFFLD(AP06)
     A                                      EDTWRD('   /  /  ')
     A                                  7  2'備　　註　:'
     A            DAP07     R        O  7 16REFFLD(AP07)
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  4'序號　　:'
     A            DAQ03     R        O  9 16REFFLD(AQ03)
     A                                      EDTCDE(4)
     A            DFUN          14      9 60DSPATR(RI)
     A                                 10  4'科目　　:'
     A                                      DSPATR(HI)
     A            DAQ04     R        B 10 16REFFLD(AQ04)
     A N99                                  DSPATR(HI)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A            DAF032    R        O 10 25REFFLD(AF03)
     A                                 11  4'分攤比率:'
     A            DAQ12     R        B 11 16REFFLD(AQ12)
     A                                      EDTWRD('   .    ')
     A                                      DSPATR(CS)
     A                                      CHECK(RB)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A                                    +1'%'
     A                                 12  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*----------------------------------------------------------------
     A          R DSPC3
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 23 44'PF5 =參考資料'
     A                                 23 61'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD4
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A  31                                  CF04(04)
     A  31                                  CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PROTECT
     A  37                                  FRCDTA
     A  37                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                 13  5'1.'
     A N55                                  DSPATR(HI)
     A            DAA031    R        O 13  8REFFLD(AA03)
     A N55                                  DSPATR(HI)
     A                                 13 29':'
     A N55                                  DSPATR(HI)
     A            DAQ05     R        B 13 31REFFLD(AQ05)
     A N99N55                               DSPATR(HI)
     A  60                                  DSPATR(ND)
     A  60
     AON31                                  DSPATR(PR)
     A  80                                  DSPATR(PC RI)
     A            DAQ051        20A  O 13 53
     A                                 14  5'2.'
     A N56                                  DSPATR(HI)
     A            DAA032    R        O 14  8REFFLD(AA03)
     A N56                                  DSPATR(HI)
     A                                 14 29':'
     A N56                                  DSPATR(HI)
     A            DAQ06     R        B 14 31REFFLD(AQ06)
     A N99N56                               DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61
     AON31                                  DSPATR(PR)
     A  81                                  DSPATR(PC RI)
     A            DAQ061        20A  O 14 53
     A                                 15  5'3.'
     A N57                                  DSPATR(HI)
     A            DAA033    R        O 15  8REFFLD(AA03)
     A N57                                  DSPATR(HI)
     A                                 15 29':'
     A N57                                  DSPATR(HI)
     A            DAQ07     R        B 15 31REFFLD(AQ07)
     A N99N57                               DSPATR(HI)
     A  62                                  DSPATR(ND)
     A  62
     AON31                                  DSPATR(PR)
     A  82                                  DSPATR(PC RI)
     A            DAQ071        20A  O 15 53
     A                                 16  5'4.'
     A N58                                  DSPATR(HI)
     A            DAA034    R        O 16  8REFFLD(AA03)
     A N58                                  DSPATR(HI)
     A                                 16 29':'
     A N58                                  DSPATR(HI)
     A            DAQ08     R        B 16 31REFFLD(AQ08)
     A N99N58                               DSPATR(HI)
     A  63                                  DSPATR(ND)
     A  63
     AON31                                  DSPATR(PR)
     A  83                                  DSPATR(PC RI)
     A            DAQ081        20A  O 16 53
     A                                 17  5'5.'
     A            DAA035    R        O 17  8REFFLD(AA03)
     A                                 17 29':'
     A            DAQ09     R   -1   B 17 31REFFLD(AQ09)
     A                                      EDTWRD('   /  /  ')
     A  64                                  DSPATR(ND)
     A  64
     AON31                                  DSPATR(PR)
     A  84                                  DSPATR(PC RI)
     A            DAQ091        20A  O 17 53
     A                                 18  5'6.'
     A            DAA036    R        O 18  8REFFLD(AA03)
     A                                 18 29':'
     A            DAQ14     R        B 18 31REFFLD(AQ14)
     A                                      EDTCDE(2)
     A  65                                  DSPATR(ND)
     A  65
     AON31                                  DSPATR(PR)
     A  85                                  DSPATR(PC RI)
     A            DAQ141        20A  O 18 53
     A                                 19  5'7.'
     A N59                                  DSPATR(HI)
     A            DAA037    R        O 19  8REFFLD(AA03)
     A N59                                  DSPATR(HI)
     A                                 19 29':'
     A N59                                  DSPATR(HI)
     A            DAQ15     R        B 19 31REFFLD(AQ15)
     A N99N59                               DSPATR(HI)
     A  66                                  DSPATR(ND)
     A  66
     AON31                                  DSPATR(PR)
     A  86                                  DSPATR(PC RI)
     A            DAQ151        20A  O 19 53
     A                                 20  8'資金代碼'
     A                                      DSPATR(HI)
     A                                 20 29':'
     A                                      DSPATR(HI)
     A            DAQ22     R        B 20 31REFFLD(AQ22)
     A N99                                  DSPATR(HI)
     A N31                                  DSPATR(PR)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A            DAD03     R        O 20 41REFFLD(AD03)
     A                                 21  8'摘要'
     A                                 21 29':'
     A            DAQ16     R        B 21 31REFFLD(AQ16)
     A N31                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R DSPC4
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*================================================================
     A          R SFLSRD                    SFL
     A            RRND           3Y 0   9  2
     A            DAQ12D    R        H      REFFLD(AQ12)
     A            DAQ04D    R        H      REFFLD(AQ04)
     A            DAQ05D    R        H      REFFLD(AQ05)
     A            DAQ06D    R        H      REFFLD(AQ06)
     A            DAQ07D    R        H      REFFLD(AQ07)
     A            DAQ08D    R        H      REFFLD(AQ08)
     A            DAQ09D    R        H      REFFLD(AQ09)
     A            DAQ14D    R        H      REFFLD(AQ14)
     A            DAQ15D    R        H      REFFLD(AQ15)
     A            DAQ16D    R        H      REFFLD(AQ16)
     A            DAQ22D    R        H      REFFLD(AQ22)
     A            DLTCOD         1   H
     A*----------------------------------------------------------------
     A          R SFLCRD                    SFLCTL(SFLSRD)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A*================================================================
     A          R SFLSRC                    SFL
     A            RRNC           3Y 0   9  2
     A            DAQ12D    R        H      REFFLD(AQ12)
     A            DAQ04D    R        H      REFFLD(AQ04)
     A            DAQ05D    R        H      REFFLD(AQ05)
     A            DAQ06D    R        H      REFFLD(AQ06)
     A            DAQ07D    R        H      REFFLD(AQ07)
     A            DAQ08D    R        H      REFFLD(AQ08)
     A            DAQ09D    R        H      REFFLD(AQ09)
     A            DAQ14D    R        H      REFFLD(AQ14)
     A            DAQ15D    R        H      REFFLD(AQ15)
     A            DAQ16D    R        H      REFFLD(AQ16)
     A            DAQ22D    R        H      REFFLD(AQ22)
     A            DLTCOD         1   H
     A*----------------------------------------------------------------
     A          R SFLCRC                    SFLCTL(SFLSRC)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
