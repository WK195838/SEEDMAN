     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA440                                     *
     A*    PROGRAM NAME : 資金代碼資料維護                         *
     A*    AUTHOR       : A1152  ANGY                                *
     A*    CREATE DATE  : 1992.10.28                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A**                                    CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF18(18)
     A**          #LIN           3  0H
     A**          #COL           3  0H
     A                                  1  2'<GLA440.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'資金代碼資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAD01     R        B  4 14REFFLD(AD01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'資金代碼:'
     A            DAD02     R        B  5 14REFFLD(AD02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 24'(拷貝:'
     A            DAD01C    R        B  5 33REFFLD(AD01)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DAD02C    R        B  5 36REFFLD(AD02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  5 47')'
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0B  6 14VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 24'( 1.新增'
     A                                  6 35'2.修改'
     A                                  6 44'4.刪除'
     A                                  6 53'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03)
     A*                                     CF05(05)
     A                                      CF12(12)
     A                                  1  2'<GLA440.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'資金代碼資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAD01     R        O  4 14REFFLD(AD01)
     A            D#B02         12   O  4 19
     A                                  5  2'資金代碼:'
     A            DAD02     R        O  5 14REFFLD(AD02)
     A                                  5 24'(拷貝:'
     A            DAD01C    R        O  5 33REFFLD(AD01)
     A            DAD02C    R        O  5 36REFFLD(AD02)
     A                                  5 47')'
     A                                  6  2'作業選擇:'
     A            DOPT           1Y 0O  6 14
     A                                      EDTCDE(4)
     A                                  6 24'( 1.新增'
     A                                  6 35'2.修改'
     A                                  6 44'4.刪除'
     A                                  6 53'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  2'中文說明:'
     A            DAD03     R        B  8 14REFFLD(AD03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  2'英文說明:'
     A            DAD04     R        B  9 14REFFLD(AD04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'類　　別:'
     A            DAD05     R        B 10 14REFFLD(AD05)
     A                                      VALUES('1' '2')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 17'( 1.來源'
     A                                 10 29'2.用途)'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
