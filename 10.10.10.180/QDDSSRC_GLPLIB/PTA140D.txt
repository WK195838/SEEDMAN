     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PTA140                                     *
     A*    PROGRAM NAME :成本中心資料維護                          *
     A*    AUTHOR       : A1150                                      *
     A*    CREATE DATE  : 81/11/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :基本資料系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/PTRF)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<PTA140.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'成本中心資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'成本中心:'
     A            D#A01     R        B  4 14REFFLD(#A01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'作業選擇:'
     A            DOPT           1Y 0B  5 14VALUES(1 2 4 5)
     A                                  5 18'( 1.新增'
     A                                  5 29'2.修改'
     A                                  5 38'4.刪除'
     A                                  5 47'5.查詢)'
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PTA140.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'成本中心資料維護'
     A                                      DSPATR(RI)
     A                                  4  2'成本中心:'
     A            D#A01     R        O  4 14REFFLD(#A01)
     A                                  5  2'作業選擇:'
     A            DOPT           1Y 0O  5 14
     A                                  5 18'( 1.新增'
     A                                  5 29'2.修改'
     A                                  5 38'4.刪除'
     A                                  5 47'5.查詢)'
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  2'中文簡稱:'
     A            D#A02     R        B  7 14REFFLD(#A02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'英文簡稱:'
     A            D#A03     R        B  8 14REFFLD(#A03)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  2'主管電話:'
     A            D#A04     R        B  9 14REFFLD(#A04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'編製人力:'
     A            D#A05     R        B 10 14REFFLD(#A05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 11  2'費用別　:'
     A            D#A06     R        B 11 14REFFLD(#A06)
     A                                      VALUES('1' '2' '3' '4')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 17'( 1.管理 2.推銷 3.研發 4.+
     A                                      製造)'
     A                                 12  2'所在位置:'
     A            D#A07     R        B 12 14REFFLD(#A07)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  2'電話號碼:'
     A            D#A08     R        B 13 14REFFLD(#A08)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  2'傳真號碼:'
     A            D#A09     R        B 14 14REFFLD(#A09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  2'類別　　:'
     A            D#A10     R        B 15 14REFFLD(#A10)
     A                                      VALUES('1' '2')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 16'( 1.正式 2.虛擬)'
     A                                 16  2'性質　　:'
     A                                      DSPATR(HI)
     A            D#A11     R        B 16 14REFFLD(#A11)
     A N99                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#Y03         12A  O 16 16
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =視窗查詢'
     A                                    +2'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
