     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA430D                                    *
     A*    PROGRAM NAME : 沖帳組合資料維護                         *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : OCT 28,92'                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A*                                     CF04(04)
     A                                      CF18(18)
     A*           #LIN           3  0H
     A*           #COL           3  0H
     A                                  1  2'<GLA430.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2 31'沖帳組合資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  2'公司別　　　　:'
     A            DAE01     R        B  4 20REFFLD(AE01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'沖帳代碼　　　:'
     A            DAE02     R        B  5 20REFFLD(AE02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 24'(拷貝:'
     A            DAE01C    R        B  5 33REFFLD(AE01)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DAE02C    R        B  5 36REFFLD(AE02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  5 39')'
     A                                  6  2'作業選擇　　　:'
     A            DOPT           1Y 0B  6 20VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 24'( 1.新增'
     A                                    +1'2.修改'
     A                                    +1'4.刪除'
     A                                    +1'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A*                                23 28'F4 =欄位查詢'
     A                                 23 27'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                  1  2'<GLA430.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2 31'沖帳組合資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  2'公司別　　　　:'
     A            DAE01     R        O  4 20REFFLD(AE01)
     A            #B02      R           4 24REFFLD(#B02 *LIBL/PTRF)
     A                                  5  2'沖帳代碼　　　:'
     A            DAE02     R        O  5 20REFFLD(AE02)
     A                                  5 24'(拷貝:'
     A            DAE01C    R        O  5 33REFFLD(AE01)
     A            DAE02C    R        O  5 36REFFLD(AE02)
     A                                  5 39')'
     A                                  6  2'作業選擇　　　:'
     A            DOPT           1Y 0O  6 20EDTCDE(4)
     A                                  6 24'( 1.新增'
     A                                    +1'2.修改'
     A                                    +1'4.刪除'
     A                                    +1'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  8 19'排序　小計'
     A                                  9  4'1.成本中心　:'
     A            AE041          1  0B  9 22
     A                                      EDTCDE(4)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            AE051          1   B  9 28VALUES(' ' 'V')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  4'2.對象別　　:'
     A            AE042          1  0B 10 22
     A                                      EDTCDE(4)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            AE052          1   B 10 28VALUES(' ' 'V')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  4'3.相關號碼1 :'
     A            AE043          1  0B 11 22
     A                                      EDTCDE(4)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            AE053          1   B 11 28VALUES(' ' 'V')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  4'4.相關號碼2 :'
     A            AE044          1  0B 12 22
     A                                      EDTCDE(4)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            AE054          1   B 12 28VALUES(' ' 'V')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  4'5.日期      :'
     A            AE045          1  0B 13 22
     A                                      EDTCDE(4)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            AE055          1   B 13 28VALUES(' ' 'V')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  6'說明      :'
     A            DAE03     R        B 15 22REFFLD(AE03)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F5 =參考資料'
     A                                 23 44'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
