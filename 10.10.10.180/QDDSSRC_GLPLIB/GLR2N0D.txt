     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLR2N0D                                    *
     A*    PROGRAM NAME : 明細帳列印－區間                         *
     A*    AUTHOR       : A1087   JOYCE                              *
     A*    CREATE DATE  : 1992.11.30                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R DSPD1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLR2N0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'明細帳列印－區間'
     A                                      DSPATR(RI)
     A                                  5 27'公司別　:'
     A            DAH01     R        B  5 39REFFLD(AH01)
     A  60                                  DSPATR(PC RI)
     A                                  7 27'會計科目:'
     A                                      DSPATR(HI)
     A            DAH04S    R        B  7 39REFFLD(AH04)
     A N99                                  DSPATR(HI)
     A  61                                  DSPATR(PC RI)
     A                                  7 49'-'
     A                                      DSPATR(HI)
     A            DAH04E    R        B  7 51REFFLD(AH04)
     A N99                                  DSPATR(HI)
     A  62                                  DSPATR(PC RI)
     A                                  8 27'傳票日期:'
     A            DAH10S    R   -1   B  8 39REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC RI)
     A                                  8 49'-'
     A            DAH10E    R   -1   B  8 51REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A  64                                  DSPATR(PC RI)
     A                                  9 27'製票日期:'
     A            DAH25S    R   -1   B  9 39REFFLD(AH25)
     A                                      EDTWRD('   /  /  ')
     A  65                                  DSPATR(PC RI)
     A                                  9 49'-'
     A            DAH25E    R   -1   B  9 51REFFLD(AH25)
     A                                      EDTWRD('   /  /  ')
     A  66                                  DSPATR(PC RI)
     A                                 10 27'製票者:'
     A            DAH26     R        B 10 39REFFLD(AH26)
     A                                 10 50'(空白表示*ALL)'
     A                                 11 27'列印碼　:'
     A            DK             1A  B 11 39VALUES('1' '2' '3')
     A                                 11 42'(1-明細 2-合計 3-全部)'
     A                                 12 27'借貸別　:'
     A            DJ             1A  B 12 39VALUES('1' '2' '3')
     A                                 12 42'(1-立帳 2-沖帳 3-全部)'
     A*
     A                                 15 27'執行環境:'
     A            $PENV          1   B 15 39VALUES('0' '1' '2')
     A                                 15 42'(0-查詢 1-即時 2-批次)'
     A                                 16 27'列印份數:'
     A            $CPY           3Y 0B 16 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 27'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 17 39
     A                                      DSPATR(HI)
     A  27                             21 29'***請準備１３２行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 29'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
