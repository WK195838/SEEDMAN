     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA460                                     *
     A*    PROGRAM NAME : 內設資料值維護                           *
     A*    AUTHOR       : A1152  ANGY                                *
     A*    CREATE DATE  : 1992.10.29                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A**                                    CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A**          #LIN           3  0H
     A**          #COL           3  0H
     A                                  1  2'<GLA460.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'內設資料值維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAK01     R        B  4 14REFFLD(AK01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'作業選擇:'
     A            DOPT           1Y 0B  5 14VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  5 18'( 1.新增'
     A                                  5 29'2.修改'
     A                                  5 38'4.刪除'
     A                                  5 47'5.查詢)'
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                  1  2'<GLA460.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'內設資料值維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　:'
     A            DAK01     R        O  4 14REFFLD(AK01)
     A            D#B02         12   O  4 19
     A                                  5  2'作業選擇:'
     A            DOPT           1Y 0O  5 14
     A                                      EDTCDE(4)
     A                                  5 18'( 1.新增'
     A                                  5 29'2.修改'
     A                                  5 38'4.刪除'
     A                                  5 47'5.查詢)'
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  3'01.主管核對作業    :'
     A            DAK02     R        B  7 26REFFLD(AK02)
     A                                      VALUES('Y' 'N')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7 29'(Y/N)'
     A                                  8  3'02.累積盈虧科目    :'
     A            DAK04     R        B  8 26REFFLD(AK04)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF031    R        O  8 36REFFLD(AF03)
     A                                  9  3'03.本期損益科目    :'
     A            DAK05     R        B  9 26REFFLD(AK05)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAF032    R        O  9 36REFFLD(AF03)
     A                                 10  3'04.成本中心　　    :'
     A            DAK06     R        B 10 26REFFLD(AK06)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAA03     R        O 10 36REFFLD(AA03)
     A                                 11  3'05.資金代碼－來源  :'
     A            DAK07     R        B 11 26REFFLD(AK07)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAD031    R        O 11 36REFFLD(AD03)
     A                                 12  3'06.資金代碼－用途  :'
     A            DAK08     R        B 12 26REFFLD(AK08)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAD032    R        O 12 36REFFLD(AD03)
     A                                 13  3'07.年制設定　　　  :'
     A            DAK09     R        B 13 26REFFLD(AK09)
     A                                      VALUES('1' '2')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 38'(1.西元年 2.中國年)'
     A                                 14  3'08.年度會計月數　  :'
     A            DAK10     R        B 14 26REFFLD(AK10)
     A                                      VALUES('1' '2')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 38'(1.十二月 2.十三月)'
     A                                 15  3'09.日期格式　　　  :'
     A            DAK11     R        B 15 26REFFLD(AK11)
     A                                      VALUES('1' '2' '3')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 38'(1.年月日 2.月日年'
     A                                 15 61'3.日月年)'
     A                                 16  3'10.已過帳日期　    :'
     A            DAK12          7  0O 16 26
     A                                      EDTWRD('   /  /  ')
     A                                 17  3'11.已結轉期間　    :'
     A            DAK14          5  0O 17 26EDTWRD('   -  ')
     A                                 18  3'12.已結轉年度　    :'
     A            DAK13          3  0O 18 26EDTCDE(4)
     A*
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'PF3 =回主畫面'
     A                                 23 28'PF5 =參考資料'
     A                                 23 45'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
