     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PTI130                                     *
     A*    PROGRAM NAME :公司基本資料全頁查詢                      *
     A*    AUTHOR       : A1149  MAY                                 *
     A*    CREATE DATE  : 1992.10.28                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :基本資料系統                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PTRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            DOPT           1   B  8  3DSPATR(CS PC HI)
     A                                      VALUES('/' ' ')
     A            RRN            3Y 0O  8  5EDTCDE(1)
     A            #B01      R        O  8 11REFFLD(#B01)
     A            #B02      R        O  8 15REFFLD(#B02)
     A            #B03      R        O  8 28REFFLD(#B03)
     A            #B06      R        O  8 61REFFLD(#B06)
     A            #B12      R        O  8 76REFFLD(#B12)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      OVERLAY
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0013)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PTI130.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 29'公司基本資料全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  3'S (=作業) : /-挑選'
     A                                  6  3'S'
     A                                      DSPATR(UL)
     A                                  6  5' No 公司　公司簡稱   公司全+
     A                                       名                      公司電+
     A                                       話   類別'
     A                                      DSPATR(UL)
     A            D#B01     R        B  7 11REFFLD(#B01)
     A                                      DSPATR(CS HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 28'PF12=回前畫面'
     A                                 23 46'PA1 =下一頁'
     A                                 24 10'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
