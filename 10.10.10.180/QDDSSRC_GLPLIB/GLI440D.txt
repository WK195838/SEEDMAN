     A*%%TS  SD  19921116  154507  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLI440                                     *
     A*    PROGRAM NAME : 資金代碼全頁查詢                         *
     A*    AUTHOR       : A1152  ANGY                                *
     A*    CREATE DATE  : 1992.10.28                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            DOPT           1   B  8  2DSPATR(CS PC HI)
     A                                      VALUES('/' ' ')
     A            RRN            3Y 0O  8  6EDTCDE(4)
     A            AD01      R        O  8 13
     A            AD02      R        O  8 19
     A            AD03      R        O  8 29
     A            DAD05          6   O  8 62
     A*----------------------------------------------------------------
     A**                                    CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921116  154507  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLI440.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'資金代碼全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  2'S (=作業) : /-挑選'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4'   No 公司別資金代碼  說明　+
     A                                      　　　　　　　　　　　　　類別'
     A                                      DSPATR(UL)
     A            DAD01     R        B  7 13REFFLD(AD01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DAD02     R        B  7 19REFFLD(AD02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF12=回前畫面'
     A                                 23 44'PA1 =下一頁'
     A                                 23 59'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
