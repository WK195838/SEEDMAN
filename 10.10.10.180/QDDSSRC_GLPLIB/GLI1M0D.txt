     A*%%TS  SD  19921125  162824  TSPGMR      REL-V2R1M1  5738-PW1
     A*  90/12/25  18:11:10    SSPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : GLI1M0D                                    *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 83/07/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : GLI1M0                                     *
     A*    SYSTEM       :泛太總帳                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLI1M0.1>'
     A            CONAME        40A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'憑證未到資料查詢'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　　:'
     A            DA701     R        B  4 16REFFLD(AH01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'經手人　　:'
     A N54                                  DSPATR(HI)
     A            DA705F    R        B  5 16REFFLD(A705)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 30'-'
     A            DA705T    R        B  5 32REFFLD(A705)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  2'傳票號碼　:'
     A            DA702F    R        B  6 16REFFLD(AH02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6 27'-'
     A            DA702T    R        B  6 29REFFLD(AH02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 30'PF4 =視窗查詢'
     A                                 24 67'【泛太資訊】'
     A*
     A*
     A          R SFLSR                     SFL
     A*%%TS  SD  19921125  162824  TSPGMR      REL-V2R1M1  5738-PW1
     A            DOPT           1A  B 12  3DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      VALUES('5' ' ')
     A            RRN            3Y 0H
     A            A7051     R        O 12  5REFFLD(A705)
     A            A7051A    R        O 12 18REFFLD(A705)
     A            A7021     R        O 12 35REFFLD(A702)
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLI1M0.2>'
     A            CONAME        40A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'憑證未到資料查詢'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　　:'
     A            DA701     R        O  4 16REFFLD(AH01)
     A            #B02      R        O  4 19REFFLD(#B02 PT#BPF)
     A                                  5  2'經手人　　:'
     A            DA705F    R        O  5 16REFFLD(A705)
     A                                  5 30'-'
     A            DA705T    R        O  5 32REFFLD(A705)
     A                                  6  2'傳票號碼　:'
     A            DA702F    R        O  6 16REFFLD(AH02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6 30'-'
     A            DA702T    R        O  6 32REFFLD(AH02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A                                  8  3'S (=作業): 5-明細查詢'
     A                                 10  3'S經手人　　　　　　　　　　  +
     A                                      傳票號碼　　　　　　　　　　　+
     A                                      　　  '
     A                                      DSPATR(UL)
     A            D02F      R        B 11  5REFFLD(A705)
     A                                      DSPATR(CS)
     A*
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =視窗查詢'
     A                                    +2'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
     A          R SFLSR1                    SFL
     A*%%TS  SD  19921125  162824  TSPGMR      REL-V2R1M1  5738-PW1
     A            RRN1           3Y 0H
     A            AH03      R        O 11  4
     A            DAH04         16   O 11  9
     A            AH12      R        O 11 26EDTCDE(2)
     A            AH13      R        O 11 44EDTCDE(2)
     A            A704      R        O 11 62
     A            A706      R        O 11 64EDTCDE(2)
     A            AF03      R        O 12 11
     A            A707      R        O 12 48
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0005)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PROTECT
     A                                      SFLDROP(CF02)
     A*                                     ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          3S 0H      SFLRCDNBR
     A                                  1  2'<GLI1M0.3>'
     A            CONAME        40A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'憑證未到資料查詢'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　　:'
     A            DA701     R        O  4 16REFFLD(AH01)
     A            #B02      R        O  4 19REFFLD(#B02 PT#BPF)
     A                                  5  2'經手人　　:'
     A            A7051     R        O  5 16REFFLD(A705)
     A            A7051A    R        O  5 30REFFLD(A705)
     A                                  6  2'傳票號碼　:'
     A            A7021     R        O  6 16REFFLD(AH02)
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A                                  9 61'------欠憑證------'
     A                                 10  3'序號會計科目            借方-
     A                                      金額        貸方金額  註記 -
     A                                           金額 '
     A                                      DSPATR(UL)
     A*
     A          R DSPC2
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF2 =明細顯示'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF12 =回前畫面'
     A                                 24 67'【泛太資訊】'
