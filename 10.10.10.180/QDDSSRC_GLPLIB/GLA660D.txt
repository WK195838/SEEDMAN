     A*%%TS  SD  19921102  105742  PANTECH     REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA660D                                    *
     A*    PROGRAM NAME : 預算分攤比率碼維護                       *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : DEC 07,92'                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19921102  105742  PANTECH     REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF18(18)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<GLA660.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'預算分攤比率碼維護'
     A                                      DSPATR(RI)
     A                                  4  2'公司別    :'
     A            DA601     R        B  4 16REFFLD(A601)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'分攤比率碼:'
     A            DA602     R        B  5 16REFFLD(A602)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 20'(拷貝:'
     A            DA601C    R        B  5 29REFFLD(A601)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DA602C    R        B  5 32REFFLD(A602)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  5 34')'
     A                                  6  2'作業選擇  :'
     A            DOPT           1Y 0B  6 16VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 20'( 1.新增'
     A                                    +1'2.修改'
     A                                    +1'4.刪除'
     A                                    +1'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 28'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                  1  2'<GLA660.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2 30'預算分攤比率碼維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  2'公司別    :'
     A            DA601     R        O  4 16REFFLD(A601)
     A            #B02      R           4 20REFFLD(#B02 *LIBL/PTRF)
     A                                  5  2'分攤比率碼:'
     A            DA602     R        O  5 16REFFLD(A602)
     A                                  5 20'(拷貝:'
     A            DA601C    R        O  5 29REFFLD(A601)
     A            DA602C    R        O  5 32REFFLD(A602)
     A                                  5 34')'
     A                                  6  2'作業選擇  :'
     A            DOPT           1Y 0O  6 16EDTCDE(4)
     A                                  6 20'( 1.新增'
     A                                    +1'2.修改'
     A                                    +1'4.刪除'
     A                                    +1'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A                                  8  2'備註      :'
     A            DA603     R        B  8 16REFFLD(A603)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'會計期間'
     A                                 10 15'比率%'
     A                                 10 29'會計期間'
     A                                 10 42'比率%'
     A                                 10 57'會計期間'
     A                                 10 70'比率%'
     A*
     A                                 11  6'01'
     A            DA6041    R        B 11 14REFFLD(A6041)
     A                                      EDTWRD('   .    ')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 33'02'
     A            DA6042    R        B 11 41REFFLD(A6042)
     A                                      EDTWRD('   .    ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 61'03'
     A            DA6043    R        B 11 69REFFLD(A6043)
     A                                      EDTWRD('   .    ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  6'04'
     A            DA6044    R        B 13 14REFFLD(A6044)
     A                                      EDTWRD('   .    ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 33'05'
     A            DA6045    R        B 13 41REFFLD(A6045)
     A                                      EDTWRD('   .    ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 61'06'
     A            DA6046    R        B 13 69REFFLD(A6046)
     A                                      EDTWRD('   .    ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  6'07'
     A            DA6047    R        B 15 14REFFLD(A6047)
     A                                      EDTWRD('   .    ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 33'08'
     A            DA6048    R        B 15 41REFFLD(A6048)
     A                                      EDTWRD('   .    ')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 61'09'
     A            DA6049    R        B 15 69REFFLD(A6049)
     A                                      EDTWRD('   .    ')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17  6'10'
     A            DA604A    R        B 17 14REFFLD(A604A)
     A                                      EDTWRD('   .    ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 33'11'
     A            DA604B    R        B 17 41REFFLD(A604B)
     A                                      EDTWRD('   .    ')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 61'12'
     A            DA604C    R        B 17 69REFFLD(A604C)
     A                                      EDTWRD('   .    ')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 19  6'13'
     A N32                                  DSPATR(ND)
     A            DA604D    R        B 19 14REFFLD(A604D)
     A                                      EDTWRD('   .    ')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31
     AON32                                  DSPATR(PR)
     A N32                                  DSPATR(ND)
     A*
     A                                 19 57'合　　計'
     A            TOTAL          7  4O 19 69EDTWRD('   .    ')
     A                                    +1'%'
     A*----------------------------------------------------------------
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF5 =參考資料'
     A                                 23 44'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
