     A*================================================================
     A* MODI CODE | AUTHOR |  DATE  | DESCRIPTION
     A*----------------------------------------------------------------
     A*  M001     |MICHELLE|01.07.03| A&P增加控制選項
     A*----------------------------------------------------------------
     A*  M005     |MICHELLE|01.09.26|修正費用種類及費用容量
     A*----------------------------------------------------------------
     A*  M006     |MICHELLE|01.10.30|預算金額允許輸入到百元位
     A*----------------------------------------------------------------
     A*%%TS  SD  19940702  091650  D910121     REL-V2R2M0  5738-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/NPRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19940702  091650  D910121     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<NPA001.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'產品促銷預算維護'
     A                                      DSPATR(RI)
     A                                  2 72TIME
     A                                  3  2'公司別  :'
     A            DND01     R        B  3 14REFFLD(ND01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  3 34'年度    :'
     A            DND02          3Y 0B  3 46EDTCDE(3)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  4  2'產品組別:'
     A            DND03     R        B  4 14REFFLD(ND03)
     A                                      VALUES('  ' '10' '20' '30' '99')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  4 34'品牌代碼:'
     A                                      DSPATR(HI)
     A            DND04     R        B  4 46REFFLD(ND04)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  3 53'(注意: 99=合併品牌)'
M001AA                                  5  2'費用種類:'
 |   A                                      DSPATR(HI)
 |   A            DND19          2   B  5 14
 |   A  66                                  DSPATR(PC)
M001AA  66                                  DSPATR(RI)
M001AA                                  5 34'費用容量:'
 |   A                                      DSPATR(HI)
 |   A            DND20          1   B  5 46
 |   A  67                                  DSPATR(PC)
M001AA  67                                  DSPATR(RI)
M001MA*自原第５行－第１５行，往下退一行
     A                                  6  3'A&P CODE :'
     A                                      DSPATR(HI)
     A            DND05     R        B  6 14REFFLD(ND05)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  6 19'(拷貝:'
     A            DND01C    R        B  6 28REFFLD(ND01)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A            DND02C         3Y 0B  6 31EDTCDE(3)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A            DND03C    R        B  6 35REFFLD(ND03)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A            DND04C    R        B  6 38REFFLD(ND04)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65                                  DSPATR(HI)
M005AA            DND19C         2   B  6 41
M005AA  65                                  DSPATR(PC)
M005AA  65                                  DSPATR(RI)
M005AA N65                                  DSPATR(HI)
M005AA            DND20C         1   B  6 44
M005AA  65                                  DSPATR(PC)
M005AA  65                                  DSPATR(RI)
M005AA N65                                  DSPATR(HI)
M005MA*           DND05C    R        B  6 41REFFLD(ND05)
M005MA            DND05C    R        B  6 46REFFLD(ND05)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65                                  DSPATR(HI)
M005MA*                                 6 45')'
M005MA                                  6 50')'
     A                                  7  2'作業選擇:'
     A            DOPT           1Y 0B  7 14VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 19'( 1.新增'
     A                                  7 30'2.修改'
     A                                  7 39'4.刪除'
     A                                  7 48'5.查詢)'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 23 44'PF18=全頁查詢'
     A                                 24 65'【人頭馬寰盛】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  19940702  091650  D910121     REL-V2R2M0  5738-PW1
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  1  2'<NPA001.2>'
     A            $USERN        10A  O  2  2
     A                                  2 30'產品促銷預算維護'
     A                                      DSPATR(RI)
     A                                  2 72TIME
     A                                  3  2'公司別  :'
     A            DND01     R        O  3 14REFFLD(ND01)
     A            #B02      R        O  3 17REFFLD(#B02 *LIBL/PT#BPF)
     A                                  3 34'年度    :'
     A            DND02          3Y 0O  3 46EDTCDE(3)
     A                                  3 53'(注意: 99=合併品牌)'
     A                                  4  2'產品組別:'
     A            DND03     R        O  4 14REFFLD(ND03)
     A            PND03          8O  O  4 17
     A                                  4 34'品牌代碼:'
     A            DND04     R        O  4 46REFFLD(ND04)
     A            #A03      R        O  4 49REFFLD(#A03 *LIBL/RERF)
M001AA                                  5  2'費用種類:'
 |   A            DND19          2   O  5 14
M001AA                                  5 34'費用容量:'
 |   A            DND20          1   O  5 46
M001MA*自原第５行－第１５行，往下退一行
     A                                  6  3'A&P CODE :'
     A            DND05     R        O  6 14REFFLD(ND05)
     A            NA03      R        O  6 18REFFLD(NA03)
     A                                  7  2'作業選擇:'
     A            DOPT           1Y 0O  7 14EDTCDE(4)
     A                                  7 19'( 1.新增'
     A                                  7 30'2.修改'
     A                                  7 39'4.刪除'
     A                                  7 48'5.查詢)'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  2'會計期間　　　　　　　金額 '
     A                                      DSPATR(UL)
     A                                  9 37'會計期間　　　　　　　金額 '
     A                                      DSPATR(UL)
     A                                 10  6'01'
     A                                 11  6'03'
     A                                 12  6'05'
     A                                 13  6'07'
     A                                 14  6'09'
     A                                 15  6'11'
     A                                 16  6'13'
     A  59                                  DSPATR(ND)
     A                                 10 41'02'
     A                                 11 41'04'
     A                                 12 41'06'
     A                                 13 41'08'
     A                                 14 41'10'
     A                                 15 41'12'
M006MA*           DND06B        10Y 0B 10 14EDTCDE(J)
M006MA            DND06B        10Y 2B 10 14EDTCDE(J)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M006MA*           DND07B        10Y 0B 10 49EDTCDE(J)
M006MA            DND07B        10Y 2B 10 49EDTCDE(J)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M006MA*           DND08B        10Y 0B 11 14EDTCDE(J)
M006MA            DND08B        10Y 2B 11 14EDTCDE(J)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M006MA*           DND09B        10Y 0B 11 49EDTCDE(J)
M006MA            DND09B        10Y 2B 11 49EDTCDE(J)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M006MA*           DND10B        10Y 0B 12 14EDTCDE(J)
M006MA            DND10B        10Y 2B 12 14EDTCDE(J)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M006MA*           DND11B        10Y 0B 12 49EDTCDE(J)
M006MA            DND11B        10Y 2B 12 49EDTCDE(J)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M006MA*           DND12B        10Y 0B 13 14EDTCDE(J)
M006MA            DND12B        10Y 2B 13 14EDTCDE(J)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M006MA*           DND13B        10Y 0B 13 49EDTCDE(J)
M006MA            DND13B        10Y 2B 13 49EDTCDE(J)
     A  78                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M006MA*           DND14B        10Y 0B 14 14EDTCDE(J)
M006MA            DND14B        10Y 2B 14 14EDTCDE(J)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M006MA*           DND15B        10Y 0B 14 49EDTCDE(J)
M006MA            DND15B        10Y 2B 14 49EDTCDE(J)
     A  80                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M006MA*           DND16B        10Y 0B 15 14EDTCDE(J)
M006MA            DND16B        10Y 2B 15 14EDTCDE(J)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M006MA*           DND17B        10Y 0B 15 49EDTCDE(J)
M006MA            DND17B        10Y 2B 15 49EDTCDE(J)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
M006MA*           DND18B        10Y 0B 16 14EDTCDE(J)
M006MA            DND18B        10Y 2B 16 14EDTCDE(J)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A  59                                  DSPATR(ND)
     A  59
     AON31                                  DSPATR(PR)
     A                                 17 37'合　　計'
M006MA*           DTOT1         10Y 0O 17 48EDTWRD(' ,   ,   ,  $0.-')
M006MA            DTOT1         10Y 2O 17 48EDTWRD('  ,   ,  $0.  -')
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF5 =參考資料'
     A                                 23 44'PF12=回前畫面'
     A                                 24 65'【人頭馬寰盛】'
     A                                 20 21'注意：此預算畫面以仟元為單位'
     A                                      DSPATR(HI)
