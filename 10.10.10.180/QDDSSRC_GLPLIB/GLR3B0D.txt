     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLR3B0D                                    *
     A*    PROGRAM NAME :費用分攤設定列印                          *
     A*    AUTHOR       : A1150                                      *
     A*    CREATE DATE  : 81/11/03                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLR3B0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6  0O  1 72EDTCDE(Y)
     A                                  2 31'費用分攤設定列印'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A*
     A                                  5 29'公司別　:'
     A            DAP01     R        B  5 41REFFLD(AP01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 29'會計科目:'
     A            DAP02F    R        B  7 41REFFLD(AP02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 50'-'
     A            DAP02T    R        B  7 52REFFLD(AP02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 29'分攤組別:'
     A            DAP03F    R        B  8 41REFFLD(AP03)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8 50'-'
     A            DAP03T    R        B  8 52REFFLD(AP03)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 29'成本中心:'
     A            DAP04F    R        B  9 41REFFLD(AP04)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 50'-'
     A            DAP04T    R        B  9 52REFFLD(AP04)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 10 29'分攤別　:'
     A            DAP05     R        B 10 41REFFLD(AP05)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 10 44'( 1.預設分攤 2.追溯分攤)'
     A                                 11 29'啟用日期:'
     A            DAP6F     R   -1   B 11 41REFFLD(AP06)
     A                                      EDTWRD('   /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 11 50'-'
     A            DAP6T     R   -1   B 11 52REFFLD(AP06)
     A                                      EDTWRD('   /  /  ')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A*
     A                                 15 29'執行環境:'
     A            $PENV          1   B 15 41VALUES('0' '1' '2')
     A                                 15 44'(0-查詢 1-即時 2-批次)'
     A                                 16 29'列印份數:'
     A            $CPY           3Y 0B 16 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 29'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 17 41
     A N99                                  DSPATR(HI)
     A  27                             21 30'***請準備１３２行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF14 =處理作業'
     A                                 24 67'【泛太資訊】'
