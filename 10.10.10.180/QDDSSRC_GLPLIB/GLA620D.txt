     A*%%TS  SD  19921119  212916  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA620                                     *
     A*    PROGRAM NAME : 資金預算資料維護                         *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.12.09                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF18(18)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA620.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'資金預算資料維護'
     A                                      DSPATR(RI)
     A                                  3  2'公司別　　　:'
     A            DA101     R        B  3 18REFFLD(A101)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  4  2'年度　　　　:'
     A            DA102          3  0B  4 18EDTCDE(3)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5  2'版次　　　　:'
     A            DA103     R        B  5 18REFFLD(A103)
     A                                      EDTCDE(4)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6  2'資金代碼　　:'
     A                                      DSPATR(HI)
     A            DA104     R        B  6 18REFFLD(A104)
     A N99                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  6 27'(拷貝:'
     A            DA101C    R        B  6 36REFFLD(A101)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A            DA102C         3  0B  6 39EDTCDE(3)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A            DA103C    R        B  6 43REFFLD(A103)
     A                                      EDTCDE(4)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A            DA104C    R        B  6 46REFFLD(A104)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  6 55')'
     A                                  7  2'作業選擇　　:'
     A            DOPT           1Y 0B  7 18VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  7 27'( 1.新增'
     A                                  7 38'2.修改'
     A                                  7 47'4.刪除'
     A                                  7 56'5.查詢)'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A*
     A            DNEW           1   H
     A            DOP            1A  B 14  3VALUES(' ' '4' '6' '7')
     A                                      DSPATR(CS)
     A N99                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A            RRN            3Y 0H
     A            DA205     R        B 14  7REFFLD(A205)
     A N99                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            D#A02         12A  O 14 16
     A            DPER1          5  2B 14 30EDTWRD('  0.  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DTOT          13  2O 14 39EDTCDE(1)
     A            DA206     R        B 14 60REFFLD(A206)
     A                                      EDTCDE(J)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            SA205     R        H      REFFLD(A205)
     A            SA206     R        H      REFFLD(A206)
     A            SA207     R        H      REFFLD(A207)
     A            SA208     R        H      REFFLD(A208)
     A            SA209     R        H      REFFLD(A209)
     A            SA210     R        H      REFFLD(A210)
     A            SA211     R        H      REFFLD(A211)
     A            SA212     R        H      REFFLD(A212)
     A            SA213     R        H      REFFLD(A213)
     A            SA214     R        H      REFFLD(A214)
     A            SA215     R        H      REFFLD(A215)
     A            SA216     R        H      REFFLD(A216)
     A            SA217     R        H      REFFLD(A217)
     A            SA218     R        H      REFFLD(A218)
     A            SA219     R        H      REFFLD(A219)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      CSRLOC(#LIN #COL)
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA620.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'資金預算資料維護'
     A                                      DSPATR(RI)
     A                                  3  2'公司別　　　:'
     A            DA101     R        O  3 18REFFLD(A101)
     A            #B02      R        O  3 21REFFLD(#B02 PTRF)
     A                                  4  2'年度　　　　:'
     A            DA102          3  0O  4 18EDTCDE(3)
     A                                  5  2'版次　　　　:'
     A            DA103     R        O  5 18REFFLD(A103)
     A                                      EDTCDE(4)
     A                                  6  2'資金代碼　　:'
     A            DA104     R        O  6 18REFFLD(A104)
     A                                  6 27'(拷貝:'
     A            DA101C    R        O  6 36REFFLD(A101)
     A            DA102C         3  0O  6 39EDTCDE(3)
     A            DA103C    R        O  6 43REFFLD(A103)
     A                                      EDTCDE(4)
     A            DA104C    R        O  6 46REFFLD(A104)
     A                                  6 55')'
     A                                  7  2'作業選擇　　:'
     A            DOPT           1Y 0O  7 18EDTCDE(4)
     A                                  7 27'( 1.新增'
     A                                  7 38'2.修改'
     A                                  7 47'4.刪除'
     A                                  7 56'5.查詢)'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  2'版次備註　　:'
     A            DA105     R        B  9 18REFFLD(A105)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 10  2'說　　明　　:'
     A            DAD03         30A  O 10 18
     A                                 10 50'分攤比率:'
     A                                      DSPATR(HI)
     A            DA602     R        B 10 62REFFLD(A602)
     A N99                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA603         16A  O 10 64
     A                                 11 54'基數:'
     A            DBAS          13  2B 11 62EDTWRD('  ,   ,   ,  $0.  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 11  3'S (=作業)  :'
     A N31                             11 18'4-刪除 6-月份明細 7-重新分攤'
     A  31                             11 18'6-月份明細'
     A                                 13  2' S '
     A                                      DSPATR(UL)
     A                                 13  6'成本中心'
     A                                      DSPATR(UL HI)
     A                                 13 17'名稱     '
     A                                      DSPATR(UL)
     A                                 13 29'比率%'
     A                                      DSPATR(UL)
     A                                 13 37'                                -
     A                                      金額  '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                 21 49'總金額:'
     A            DA106     R        O 21 59REFFLD(A106)
     A                                      EDTWRD('  ,   ,   ,  $0.  ')
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF5 =參考資料'
     A                                    +2'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD3
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                  1  2'<GLA620.3>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'資金預算資料維護'
     A                                      DSPATR(RI)
     A                                  3  2'公司別　　　:'
     A            DA101     R        O  3 18REFFLD(A101)
     A            #B02      R        O  3 21REFFLD(#B02 PTRF)
     A                                  4  2'年度　　　　:'
     A            DA102          3  0O  4 18EDTCDE(3)
     A                                  5  2'版次　　　　:'
     A            DA103     R        O  5 18REFFLD(A103)
     A                                      EDTCDE(4)
     A                                  6  2'資金代碼　　:'
     A            DA104     R        O  6 18REFFLD(A104)
     A                                  6 27'(拷貝:'
     A            DA101C    R        O  6 36REFFLD(A101)
     A            DA102C         3  0O  6 39EDTCDE(3)
     A            DA103C    R        O  6 43REFFLD(A103)
     A                                      EDTCDE(4)
     A            DA104C    R        O  6 46REFFLD(A104)
     A                                  6 55')'
     A                                  7  2'作業選擇　　:'
     A            DOPT           1Y 0O  7 18EDTCDE(4)
     A                                  7 27'( 1.新增'
     A                                  7 38'2.修改'
     A                                  7 47'4.刪除'
     A                                  7 56'5.查詢)'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  9  2'說　　明　　:'
     A            DAD03         30A  O  9 18
     A                                  9 50'分攤比率:'
     A            DA602     R        O  9 62REFFLD(A602)
     A            DA603         16A  O  9 64
     A                                 10  2'成本中心　　:'
     A            DA205     R        O 10 18REFFLD(A205)
     A            D#A02         12A  O 10 25
     A                                 10 54'金額:'
     A            DA206     R        O 10 62REFFLD(A206)
     A                                      EDTWRD('  ,   ,   ,  $0.  ')
     A                                 12  2'會計期間　　　　　　　金額 '
     A                                      DSPATR(UL)
     A                                 12 37'會計期間　　　　　　　金額 '
     A                                      DSPATR(UL)
     A                                 13  6'01'
     A                                 14  6'03'
     A                                 15  6'05'
     A                                 16  6'07'
     A                                 17  6'09'
     A                                 18  6'11'
     A                                 19  6'13'
     A  59                                  DSPATR(ND)
     A                                 13 41'02'
     A                                 14 41'04'
     A                                 15 41'06'
     A                                 16 41'08'
     A                                 17 41'10'
     A                                 18 41'12'
     A            DA207     R        B 13 14REFFLD(A207)
     A                                      EDTCDE(J)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA208     R        B 13 49REFFLD(A208)
     A                                      EDTCDE(J)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA209     R        B 14 14REFFLD(A209)
     A                                      EDTCDE(J)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA210     R        B 14 49REFFLD(A210)
     A                                      EDTCDE(J)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA211     R        B 15 14REFFLD(A211)
     A                                      EDTCDE(J)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA212     R        B 15 49REFFLD(A212)
     A                                      EDTCDE(J)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA213     R        B 16 14REFFLD(A213)
     A                                      EDTCDE(J)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA214     R        B 16 49REFFLD(A214)
     A                                      EDTCDE(J)
     A  78                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA215     R        B 17 14REFFLD(A215)
     A                                      EDTCDE(J)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA216     R        B 17 49REFFLD(A216)
     A                                      EDTCDE(J)
     A  80                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA217     R        B 18 14REFFLD(A217)
     A                                      EDTCDE(J)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA218     R        B 18 49REFFLD(A218)
     A                                      EDTCDE(J)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DA219     R        B 19 14REFFLD(A219)
     A                                      EDTCDE(J)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  59                                  DSPATR(ND)
     A  59
     AO 31                                  DSPATR(PR)
     A                                 19 37'合　　計'
     A            DTOT1         13  2O 19 48EDTWRD('  ,   ,   ,  $0.  ')
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF5 =參考資料'
     A                                    +2'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
