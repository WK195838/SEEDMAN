     A*%%TS  SD  19940627  164141  D910121     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF   -ID   : GLR2A0D                                    *
     A*    PROGRAM-ID   : GLR2A0                                     *
     A*    PROGRAM NAME : 分類帳                                   *
     A*    AUTHOR       :  A1149    MAY                              *
     A*    CREATE DATE  :  1992.11.11                                *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19940627  162627  D910121     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<GLR2A0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 69EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 36'分類帳'
     A                                      DSPATR(RI)
     A                                  5 28'公司別　:'
     A            DAH01     R        B  5 40REFFLD(AH01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 28'選擇內容:'
     A            DOPT           1A  B  7 40VALUES('1' '2')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  9 28'會計年度:'
     A                                      DSPATR(HI)
     A            DYEAR          3Y 0B  9 40EDTCDE(4)
     A N99                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 10 28'會計期間:'
     A                                      DSPATR(HI)
     A            DYY            2Y 0B 10 40EDTCDE(4)
     A N99                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 10 49'-'
     A            DYMD           7Y 0B 10 51EDTWRD('   /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 12 28'挑選明細:'
     A            DSEL           1A  B 12 40VALUES('N' 'Y')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 12 42'(Y/N)'
     A                                 13 28'會計科目:'
     A            DAH04S    R        B 13 43REFFLD(AH04)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 13 52'-'
     A            DAH04E    R        B 13 54REFFLD(AH04)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 13 40'N'
     A                                 14 40'Y'
     A            DAH04     R        B 14 43REFFLD(AH04)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                 15 28'執行環境:'
     A            $PENV          1A  B 15 40VALUES('0' '1' '2')
     A                                 15 45'(0-查詢 1-即時 2-批次)'
     A                                 16 28'列印份數:'
     A            $CPY           3Y 0B 16 40EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 28'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 17 40DSPATR(HI)
     A  27                             21 28'***請準備　８０行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 28'***報表處理中，請稍候***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3=回主畫面'
     A                                    +2'PF4=欄位查詢'
     A                                    +2'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
     A                                  7 45'1.總分類帳－日計表'
     A                                  8 45'2.明細分類帳'
     A          R DSPD2
     A*%%TS  SD  19940627  164141  D910121     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<GLR2A0.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 69EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 36'分類帳'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DAH01     R        O  4 13REFFLD(AH01)
     A            #B02      R        O    +1REFFLD(#B02 PTRF)
     A                                  5  1'會計科目:'
     A            DAH04     R        O  5 13REFFLD(AH04)
     A            DAF03         20   O    +1
     A                                  4 51'會計年度:'
     A                                      DSPATR(HI)
     A            DYEAR          3Y 0O  4 63EDTCDE(4)
     A                                  5 51'會計期間:'
     A                                      DSPATR(HI)
     A            DYY            2Y 0O  5 63EDTCDE(4)
     A                                  5 66'-'
     A            DYMD           7Y 0O  5 68EDTWRD('   /  /  ')
     A                                  6  1'--------------------   ------------
     A                                      ---------   --------------------'
     A                                  7  1'輸入欄位           :'
     A                                  7 32'起'
     A                                  7 54'訖'
     A                                  8  1'--------------------   ------------
     A                                      ---------   --------------------'
     A            DAA031    R        O  9  1REFFLD(AA03)
     A N56                                  DSPATR(HI)
     A                                  9 22':'
     A N56                                  DSPATR(HI)
     A  51                                  DSPATR(ND)
     A            DAH05S    R        B  9 24REFFLD(AH05)
     A N56N99                               DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  51                                  DSPATR(PR)
     A  51                                  DSPATR(ND)
     A                                  9 45'-'
     A N56                                  DSPATR(HI)
     A  51                                  DSPATR(ND)
     A            DAH05E    R        B  9 47REFFLD(AH05)
     A N56N99                               DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  51                                  DSPATR(PR)
     A  51                                  DSPATR(ND)
     A            DAA032    R        O 10  1REFFLD(AA03)
     A N57                                  DSPATR(HI)
     A                                 10 22':'
     A N57                                  DSPATR(HI)
     A  52                                  DSPATR(ND)
     A            DAH06S    R        B 10 24REFFLD(AH06)
     A N57N99                               DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  52                                  DSPATR(PR)
     A  52                                  DSPATR(ND)
     A                                 10 45'-'
     A N57                                  DSPATR(HI)
     A  52                                  DSPATR(ND)
     A            DAH06E    R        B 10 47REFFLD(AH06)
     A N57N99                               DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  52                                  DSPATR(PR)
     A  52                                  DSPATR(ND)
     A            DAA033    R        O 11  1REFFLD(AA03)
     A N58                                  DSPATR(HI)
     A                                 11 22':'
     A N58                                  DSPATR(HI)
     A  53                                  DSPATR(ND)
     A            DAH07S    R        B 11 24REFFLD(AH07)
     A N58N99                               DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  53                                  DSPATR(PR)
     A  53                                  DSPATR(ND)
     A                                 11 45'-'
     A N58                                  DSPATR(HI)
     A  53                                  DSPATR(ND)
     A            DAH07E    R        B 11 47REFFLD(AH07)
     A N58N99                               DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  53                                  DSPATR(PR)
     A  53                                  DSPATR(ND)
     A            DAA034    R        O 12  1REFFLD(AA03)
     A N59                                  DSPATR(HI)
     A                                 12 22':'
     A N59                                  DSPATR(HI)
     A  54                                  DSPATR(ND)
     A            DAH08S    R        B 12 24REFFLD(AH08)
     A N59N99                               DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  54                                  DSPATR(PR)
     A  54                                  DSPATR(ND)
     A                                 12 45'-'
     A N59                                  DSPATR(HI)
     A  54                                  DSPATR(ND)
     A            DAH08E    R        B 12 47REFFLD(AH08)
     A N59N99                               DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  54                                  DSPATR(PR)
     A  54                                  DSPATR(ND)
     A            DAA035    R        O 13  1REFFLD(AA03)
     A                                 13 22':'
     A  55                                  DSPATR(ND)
     A            DAH09S    R   -1   B 13 24REFFLD(AH09)
     A                                      EDTWRD('   /  /  ')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  55                                  DSPATR(PR)
     A  55                                  DSPATR(ND)
     A                                 13 45'-'
     A  55                                  DSPATR(ND)
     A            DAH09E    R   -1   B 13 47REFFLD(AH09)
     A                                      EDTWRD('   /  /  ')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  55                                  DSPATR(PR)
     A  55                                  DSPATR(ND)
     A                                 14  1'--------------------   ------------
     A                                      ---------   --------------------'
     A  27                             21 28'***請準備　８０行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 28'***報表處理中，請稍候***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3=回主畫面'
     A                                 23 27'PF4=欄位查詢'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
