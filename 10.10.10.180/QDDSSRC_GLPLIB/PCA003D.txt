     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : PCA003D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 10/08/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PCA003D                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :零用金系統                                *
     A*    REMARK       :作廢／取消作業                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PCCAPF)
     A                                      PRINT
     A                                      CA03(03)
     A                                      MSGLOC(24)
     A          R DSPD1
     A                                      CSRLOC(#ROW #COL)
     A                                      CF04(04)
     A            #ROW           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PCA003.1>'
     A*           CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A                                  1 27MSGCON(030 URE9999 *LIBL/REMF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'作廢／作廢取消作業'
     A                                      DSPATR(RI)
     A                                  5 24'公司別　:'
     A            DCA01     R        B  5 36REFFLD(CA01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6 24'辦事處　:'
     A            DCA17          1A  B  6 36VALUES(' ' '1' '2' '3')
     A                                  6 39'(1=台北2=台中3=高雄 " "=全+
     A                                      部)'
     A                                  8 24'登帳日期:'
     A            DCA10S         6Y 0B  8 36EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 46'-'
     A            DCA10E         6Y 0B  8 48EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8 57'(MM/DD/YY)'
     A                                  9 22'零用金號碼:'
     A            DCA02S         9A  B  9 36
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 46'-'
     A            DCA02E         9A  B  9 48
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 11 24'作業別　:'
     A            DSEL1          1   B 11 36
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 11 39'(A=作廢 B=作廢取消)'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'PF3 =回主畫面'
     A                                 24 65'【寰盛洋酒】'
