     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLR2M0D                                    *
     A*    PROGRAM NAME : 明細帳列印－科目                         *
     A*    AUTHOR       : A1087   JOYCE                              *
     A*    CREATE DATE  : 1992.11.25                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R DSPD1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLR2M0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'明細帳列印－科目'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DAH01     R        B  4 13REFFLD(AH01)
     A  60                                  DSPATR(PC RI)
     A                                  5  1'會計科目:'
     A                                      DSPATR(HI)
     A            DAH04     R        B  5 13REFFLD(AH04)
     A N99                                  DSPATR(HI)
     A  61                                  DSPATR(PC RI)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLR2M0.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'明細帳列印－科目'
     A                                      DSPATR(RI)
     A                                  4  1'公司別  :'
     A            DAH01     R        O  4 13REFFLD(AH01)
     A                                  4 22'製票者:'
     A            DAH26     R        B  4 32REFFLD(AH26)
     A                                  4 45'傳票日期:'
     A            DAH10S    R   -1   B  4 57REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A  62                                  DSPATR(PC RI)
     A                                  4 67'-'
     A            DAH10E    R   -1   B  4 69REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC RI)
     A                                  5  1'會計科目:'
     A            DAH04     R        O  5 13REFFLD(AH04)
     A            DAF03         20   O  5 22
     A                                  5 45'製票日期:'
     A            DAH25S    R   -1   B  5 57REFFLD(AH25)
     A                                      EDTWRD('   /  /  ')
     A  78                                  DSPATR(PC RI)
     A                                  5 67'-'
     A            DAH25E    R   -1   B  5 69REFFLD(AH25)
     A                                      EDTWRD('   /  /  ')
     A  79                                  DSPATR(PC RI)
     A                                  6  1'--------------------   ------------
     A                                      ---------   --------------------  --
     A                                      ---  ----'
     A                                  7  1'輸入欄位           :'
     A                                  7 32'起'
     A                                  7 54'訖'
     A                                  7 68'排序　小計'
     A                                  8  1'--------------------   ------------
     A                                      ---------   --------------------  --
     A                                      ---  ----'
     A            DAA031    R        O  9  1REFFLD(AA03)
     A N56                                  DSPATR(HI)
     A                                  9 22':'
     A N56                                  DSPATR(HI)
     A  51                                  DSPATR(ND)
     A            DAH05S    R        B  9 24REFFLD(AH05)
     A N56N99                               DSPATR(HI)
     A  64                                  DSPATR(PC RI)
     A  51                                  DSPATR(PR ND)
     A                                  9 45'-'
     A N56                                  DSPATR(HI)
     A  51                                  DSPATR(ND)
     A            DAH05E    R        B  9 47REFFLD(AH05)
     A N56N99                               DSPATR(HI)
     A  64                                  DSPATR(PC RI)
     A  51                                  DSPATR(PR ND)
     A            DS1            1Y 0B  9 71EDTCDE(Z)
     A                                      RANGE(1 5)
     A  71                                  DSPATR(PC RI)
     A  51                                  DSPATR(PR ND)
     A            DL1            1A  B  9 77VALUES(' ' 'V')
     A  51                                  DSPATR(PR ND)
     A            DAA032    R        O 10  1REFFLD(AA03)
     A N57                                  DSPATR(HI)
     A                                 10 22':'
     A N57                                  DSPATR(HI)
     A  52                                  DSPATR(ND)
     A            DAH06S    R        B 10 24REFFLD(AH06)
     A N57N99                               DSPATR(HI)
     A  65                                  DSPATR(PC RI)
     A  52                                  DSPATR(PR ND)
     A                                 10 45'-'
     A N57                                  DSPATR(HI)
     A  52                                  DSPATR(ND)
     A            DAH06E    R        B 10 47REFFLD(AH06)
     A N57N99                               DSPATR(HI)
     A  65                                  DSPATR(PC RI)
     A  52                                  DSPATR(PR ND)
     A            DS2            1Y 0B 10 71EDTCDE(Z)
     A                                      RANGE(1 5)
     A  72                                  DSPATR(PC RI)
     A  52                                  DSPATR(PR ND)
     A            DL2            1A  B 10 77VALUES(' ' 'V')
     A  52                                  DSPATR(PR ND)
     A            DAA033    R        O 11  1REFFLD(AA03)
     A N58                                  DSPATR(HI)
     A                                 11 22':'
     A N58                                  DSPATR(HI)
     A  53                                  DSPATR(ND)
     A            DAH07S    R        B 11 24REFFLD(AH07)
     A N58N99                               DSPATR(HI)
     A  66                                  DSPATR(PC RI)
     A  53                                  DSPATR(PR ND)
     A                                 11 45'-'
     A N58                                  DSPATR(HI)
     A  53                                  DSPATR(ND)
     A            DAH07E    R        B 11 47REFFLD(AH07)
     A N58N99                               DSPATR(HI)
     A  66                                  DSPATR(PC RI)
     A  53                                  DSPATR(PR ND)
     A            DS3            1Y 0B 11 71EDTCDE(Z)
     A                                      RANGE(1 5)
     A  73                                  DSPATR(PC RI)
     A  53                                  DSPATR(PR ND)
     A            DL3            1A  B 11 77VALUES(' ' 'V')
     A  53                                  DSPATR(PR ND)
     A            DAA034    R        O 12  1REFFLD(AA03)
     A N59                                  DSPATR(HI)
     A                                 12 22':'
     A N59                                  DSPATR(HI)
     A  54                                  DSPATR(ND)
     A            DAH08S    R        B 12 24REFFLD(AH08)
     A N59N99                               DSPATR(HI)
     A  67                                  DSPATR(PC RI)
     A  54                                  DSPATR(PR ND)
     A                                 12 45'-'
     A N59                                  DSPATR(HI)
     A  54                                  DSPATR(ND)
     A            DAH08E    R        B 12 47REFFLD(AH08)
     A N59N99                               DSPATR(HI)
     A  67                                  DSPATR(PC RI)
     A  54                                  DSPATR(PR ND)
     A            DS4            1Y 0B 12 71EDTCDE(Z)
     A                                      RANGE(1 5)
     A  74                                  DSPATR(PC RI)
     A  54                                  DSPATR(PR ND)
     A            DL4            1A  B 12 77VALUES(' ' 'V')
     A  54                                  DSPATR(PR ND)
     A            DAA035    R        O 13  1REFFLD(AA03)
     A                                 13 22':'
     A  55                                  DSPATR(ND)
     A            DAH09S    R   -1   B 13 24REFFLD(AH09)
     A                                      EDTWRD('   /  /  ')
     A  68                                  DSPATR(PC RI)
     A  55                                  DSPATR(PR ND)
     A                                 13 45'-'
     A  55                                  DSPATR(ND)
     A            DAH09E    R   -1   B 13 47REFFLD(AH09)
     A                                      EDTWRD('   /  /  ')
     A  68                                  DSPATR(PC RI)
     A  55                                  DSPATR(PR ND)
     A            DS5            1Y 0B 13 71EDTCDE(Z)
     A                                      RANGE(1 5)
     A  75                                  DSPATR(PC RI)
     A  55                                  DSPATR(PR ND)
     A            DL5            1A  B 13 77VALUES(' ' 'V')
     A  55                                  DSPATR(PR ND)
     A                                 14  1'--------------------   ------------
     A                                      ---------   --------------------  --
     A                                      ---  ----'
     A                                 15 28'列印碼　:'
     A            DK             1A  B 15 40VALUES('1' '2' '3')
     A                                 15 43'(1-明細 2-合計 3-全部)'
     A                                 16 28'借貸別　:'
     A            DJ             1A  B 16 40VALUES('1' '2' '3')
     A                                 16 43'(1-立帳 2-沖帳 3-全部)'
     A                                 17 28'執行環境:'
     A            $PENV          1   B 17 40VALUES('0' '1' '2')
     A                                 17 43'(0-查詢 1-即時 2-批次)'
     A                                 18 28'列印份數:'
     A            $CPY           3Y 0B 18 40EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 28'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 19 40
     A                                      DSPATR(HI)
     A  27                             21 29'***請準備１３２行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 29'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
