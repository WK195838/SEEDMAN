     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有    TEL:5042266      *
     A*                                                              *
     A*    DSPF NAME    : PTR540D                                    *
     A*    AUTHOR       : MERCURY                                    *
     A*    CREATE DATE  : 82/10/03                                   *
     A*    UPDATE DATE  : 02/02/94 AN MING HSIA                      *
     A*    PROGRAM NAME : PTR540                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :成本中心明細表                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<PTR540.1>'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2 33'成本中心明細表'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  2 62'時間:'
     A                                  2 70TIME
     A*
     A                                  6 24'成本中心代號:'
     A*                                     DSPATR(HI)
     A            D#A01S         6A  B  6 40
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  6 47'-'
     A            D#A01E         6A  B  6 49
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A*
     A                                 15 29'執行環境:'
     A            $PENV          1   B 15 41VALUES('0' '1' '2')
     A                                 15 44'( 0-查詢 1-即時 2-批次)'
     A                                 16 29'列印份數:'
     A            $CPY           3Y 0B 16 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 29'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2   B 17 41
     A                                      DSPATR(HI)
     A  27                             21 30'***請準備８０行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'PF3 =回主畫面'
     A                                 23 28'PF4 =欄位查詢'
     A                                 23 45'PF14 =處理作業'
     A                                 24 67'【泛太資訊】'
