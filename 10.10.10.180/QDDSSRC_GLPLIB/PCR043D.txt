     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : PCR043D                                    *
     A*    AUTHOR       : AN MAIN HSIA                               *
     A*    CREATE DATE  : 01/22/94                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PCR043                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :零用金系統                                *
     A*    REMARK       :成本中心彙總表                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PCCAPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A                                      CA03(03)
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CF04(04)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'<PCR043.1>'
     A                                  2 33'成本中心彙總表'
     A                                      DSPATR(RI)
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 24'公司別　:'
     A            DCA01     R        B  5 36REFFLD(CA01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6 24'辦事處　:'
     A            COMP           1A  B  6 36VALUES(' ' '1' '2' '3')
     A                                  6 39'(1=台北2=台中3=高雄 " "=全+
     A                                      部)'
     A                                  8 24'登帳日期:'
     A            DCA10S         6Y 0B  8 36EDTWRD('  /  /  ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                  8 45'-'
     A            DCA10E         6Y 0B  8 47EDTWRD('  /  /  ')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                  8 56'(MM/DD/YY)'
     A                                  9 24'支付日期:'
     A            DCA11S         6Y 0B  9 36EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 45'-'
     A            DCA11E         6Y 0B  9 47EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 56'(MM/DD/YY)'
     A                                 10 22'零用金號碼:'
     A            DCA02S         9A  B 10 36
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 10 46'-'
     A            DCA02E         9A  B 10 48
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 12 24'支付別　:'
     A            DSEL3          1A  B 12 36VALUES('A' 'B' 'C')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                 12 39'(A=支付B=未支付C=全部)'
     A                                 13 24'撥補別　:'
     A            DSEL1          1A  B 13 36VALUES('A' 'B' 'C')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 13 39'(A=撥補B=未撥補C=全部)'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                 23 11'PF3 =回主畫面'
     A                                 23 29'PF4=視窗查詢'
     A                                 24 65'【寰盛洋酒】'
     A                                 16 23'執行環境 :'
     A            $EVR           1Y 0B 16 36EDTCDE(3)
     A                                 16 41'(1-即時   2-批次)'
     A                                 17 23'列印份數'
     A                                 17 34':'
     A            $CPY           3Y 0B 17 36EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 23'列表機'
     A                                      DSPATR(HI)
     A                                 18 34':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 36CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備　８０行報表紙***'
     A                                      DSPATR(HI)
