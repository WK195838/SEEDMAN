     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLR2Q0P                                    *
     A*    PROGRAM NAME :傳票張數統計列印                          *
     A*    AUTHOR       : A1150                                      *
     A*    CREATE DATE  : 81/11/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      REF(GLRF)
     A          R PH1                       SKIPB(2)
     A            #B16      R             24REFFLD(#B16 PTRF)
     A                                      SPACEA(2)
     A                                    33'傳票張數統計'
     A                                      SPACEA(1)
     A            PFLD           8        36
     A                                      SPACEA(1)
     A                                    32'會計年度:'
     A            WYEAR          3  0     44EDTCDE(1)
     A                                      SPACEA(1)
     A                                    32'會計期間:'
     A            WPERD          2  0     44EDTCDE(1)
     A                                    48'('
     A            PAB02F    R   -1        50REFFLD(AB02)
     A                                      EDTWRD('   /  /  ')
     A                                    60'-'
     A            PAB02T    R   -1        62REFFLD(AB02)
     A                                      EDTWRD('   /  /  ')
     A                                    72')'
     A                                      SPACEA(2)
     A                                    49'日期:'
     A            PDATE          6  0     57EDTCDE(Y)
     A                                    66'頁次:'
     A                                    74PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(1)
     A                                    49'時間:'
     A                                    57TIME
     A                                      EDTWRD('  :  :  ')
     A                                    67'<GLR2Q0P>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =================='
     A                                      SPACEA(1)
     A                                    20'日　　期'
     A                                    37'傳票統計'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =================='
     A*
     A          R PD1                       SPACEB(1)
     A            PAB02     R   -1        20REFFLD(AB02)
     A                                      EDTWRD('   /  /  ')
     A            AB03      R             41EDTCDE(4)
     A*
     A          R PT1                       SPACEB(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =================='
     A                                      SPACEA(1)
     A                                    19'本月傳票合計'
     A            TAB03          4  0     40EDTCDE(4)
     A*
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
