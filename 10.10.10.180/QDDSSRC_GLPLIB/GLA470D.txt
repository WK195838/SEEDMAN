     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA470                                     *
     A*    PROGRAM NAME : 會計科目資料拷貝                         *
     A*    AUTHOR       : A1152  ANGY                                *
     A*    CREATE DATE  : 1992.10.29                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A**                                    CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF14(14)
     A**          #LIN           3  0H
     A**          #COL           3  0H
     A                                  1  2'<GLA470.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'會計科目資料拷貝'
     A                                      DSPATR(RI)
     A                                  4  2'自'
     A                                  4  7'公司別　:'
     A            DAF01F    R        B  4 19REFFLD(AF01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  7'科目代號:'
     A            DAF02F    R        B  5 19REFFLD(AF02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 28'-'
     A            DAF02T    R        B  5 30REFFLD(AF02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7  2'至'
     A                                  7  7'公司別　:'
     A            DAF01T    R        B  7 19REFFLD(AF01)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9  2'選擇內容     :'
     A            DOPT           1Y 0B  9 19EDTCDE(4)
     A                                      VALUES(1 2 3)
     A                                  9 23'1.已存在者'
     A                                  9 36'放棄拷貝'
     A                                 10 23'2.已存在者'
     A                                 10 36'全部覆蓋無須確認'
     A                                 11 23'3.已存在者'
     A                                 11 36'逐筆確認是否覆蓋'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A
     A          R DSPD2
     A*                                     ALWROL
     A                                      OVERLAY
     A                                      CLRL(*NO)
     A                                 13  3'                                 -
     A                                                                        '
     A                                      DSPATR(RI)
     A                                 14  3' '
     A                                      DSPATR(RI)
     A                                 14 69' '
     A                                      DSPATR(RI)
     A                                 15  3' '
     A                                      DSPATR(RI)
     A                                 15  5' <GL470.2> '
     A                                 15 69' '
     A                                      DSPATR(RI)
     A                                 16  3' '
     A                                      DSPATR(RI)
     A                                 16 69' '
     A                                      DSPATR(RI)
     A                                 17  3' '
     A                                      DSPATR(RI)
     A                                 17  5'科目代號:'
     A            DAF02     R        O 17 17REFFLD(AF02)
     A                                 17 27'已存在'
     A                                 17 69' '
     A                                      DSPATR(RI)
     A                                 18  3' '
     A                                      DSPATR(RI)
     A                                 18  5'科目名稱:'
     A            DAF03     R        O 18 17REFFLD(AF03)
     A                                 18 69' '
     A                                      DSPATR(RI)
     A                                 19  3' '
     A                                      DSPATR(RI)
     A                                 19  5'是否覆蓋:'
     A            COVE           1   B 19 17VALUES('Y' 'N')
     A                                 19 22'(Y/N)'
     A                                 19 69' '
     A                                      DSPATR(RI)
     A                                 20  3' '
     A                                      DSPATR(RI)
     A                                 20 69' '
     A                                      DSPATR(RI)
     A                                 21  3'                                 -
     A                                                                        '
     A                                      DSPATR(RI)
