     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA380                                     *
     A*    PROGRAM NAME : 成本中心結構設定　                       *
     A*    AUTHOR       : A1152  ANGY                                *
     A*    CREATE DATE  : 1992.11.11                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(GLFLIB/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<GLA380.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'成本中心結構設定'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　   :'
     A            DAY01     R        B  4 17REFFLD(AY01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'成本中心   :'
     A            DAY02     R        B  5 17REFFLD(AY02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  2'作業選擇   :'
     A            DOPT           1Y 0B  6 17VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 24'( 1.新增'
     A                                  6 35'2.修改'
     A                                  6 44'4.刪除'
     A                                  6 53'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            HAZ03     R        H      REFFLD(AZ03)
     A            DNEW           1   H
     A            RRN            3Y 0H
     A            DDEL           1   B 13  3DSPATR(HI)
     A                                      VALUES('4' ' ')
     A  32                                  DSPATR(UL CS)
     A N32                                  DSPATR(PR)
     A            DAZ03     R        B 13  7REFFLD(AZ03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31N99                               DSPATR(UL HI)
     A N31                                  DSPATR(PR)
     A            #A02D     R        O 13 14REFFLD(#A02 PTRF)
     A*----------------------------------------------------------------
     A**                                    CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921027  134637  PANTECH     REL-V2R1M1  5738-PW1
     A                                      CSRLOC(#LIN #COL)
     A                                      SFLSIZ(0048)
     A                                      SFLPAG(0024)
     A                                      SFLLIN(1)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA380.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 35'成本中心結構設定'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　   :'
     A            DAY01     R        O  4 17REFFLD(AY01)
     A            D#B02     R        O  4 25REFFLD(#B02 PTRF)
     A                                  5  2'成本中心   :'
     A            DAY02     R        O  5 17REFFLD(AY02)
     A            D#A02     R        O  5 25REFFLD(#A02 PTRF)
     A                                  6  2'作業選擇   :'
     A            DOPT           1Y 0O  6 17
     A                                      EDTCDE(4)
     A                                  6 24'( 1.新增'
     A                                  6 35'2.修改'
     A                                  6 44'4.刪除'
     A                                  6 53'5.查詢)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  2'備註       :'
     A            DAY03     R        B  8 17REFFLD(AY03)
     A  31                                  DSPATR(UL PC)
     A N31                                  DSPATR(PR)
     A                                 10  3'S (=作業) : 4-刪除'
     A                                 12  3'S'
     A                                       DSPATR(UL)
     A                                 12  5'成本中心           '
     A                                       DSPATR(UL HI)
     A                                 12 30'S'
     A                                       DSPATR(UL)
     A                                 12 32'成本中心           '
     A                                       DSPATR(UL HI)
     A                                 12 57'S'
     A                                       DSPATR(UL)
     A                                 12 59'成本中心           '
     A                                       DSPATR(UL HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                    +2'PF5 =參考資料'
     A                                    +2'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
