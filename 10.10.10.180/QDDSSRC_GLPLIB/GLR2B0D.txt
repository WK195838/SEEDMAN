     A*%%TS  SD  19921111  143541  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF   -ID   : GLR2B0D                                    *
     A*    PROGRAM-ID   : GLR2B0                                     *
     A*    PROGRAM NAME : 試算帳                                   *
     A*    AUTHOR       :  A1149    MAY                              *
     A*    CREATE DATE  :  1992.11.19                                *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19921111  143541  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<GLR2B0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 69EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 36'試算表'
     A                                      DSPATR(RI)
     A                                  5 30'公司別　:'
     A            DAH01     R        B  5 42REFFLD(AH01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 30'日　期　:'
     A            DAH10          7  0B  7 42EDTWRD('   /  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8 30'科目位數:'
     A            DOPT           1A  B  8 42
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 46'( 1 - 8 )'
     A                                 15 29'執行環境:'
     A            $PENV          1A  B 15 41VALUES('0' '1' '2')
     A                                 15 46'(0-查詢 1-即時 2-批次)'
     A                                 16 29'列印份數:'
     A            $CPY           3Y 0B 16 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 29'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 17 41
     A                                      DSPATR(HI)
     A  27                             21 29'***請準備１３２行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 29'***報表處理中，請稍候***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 23 44'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
