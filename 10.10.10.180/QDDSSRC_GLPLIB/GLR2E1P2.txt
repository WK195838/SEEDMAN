     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF   -ID   : GLR2E1P1                                   *
     A*    PROGRAM-ID   : GLR2E0                                     *
     A*    PROGRAM NAME : 比較損益表－科目明細表－英文             *
     A*    AUTHOR       :  A1149    MAY                              *
     A*    CREATE DATE  :  1992.12.08                                *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      REF(GLRF)
     A          R PH1                       SKIPB(2)
     A            P#B16     R             63REFFLD(#B16 PTRF)
     A                                      SPACEA(2)
     A                                    73'Detail Report'
     A                                      SPACEA(1)
     A            PDA01          8        75
     A                                      SPACEA(1)
     A            P0303         34        63
     A                                      SPACEA(1)
     A  30                                57'Profit Center :'
     A  30        D#A01          6        74
     A  30        P#A02         32        81
     A  30                                  SPACEA(1)
     A                                    57'Fiscal Year   :'
     A            DYEAR          3  0     74EDTCDE(2)
     A                                      SPACEA(1)
     A                                    57'Fiscal period :'
     A            DMM            2  0     74
     A                                    77'('
     A            PMMS           7  0     79EDTWRD('   /  /  ')
     A                                    89'-'
     A            PMME           7  0     91EDTWRD('   /  /  ')
     A                                   101')'
     A                                      SPACEA(1)
     A                                   128'DATE :'
     A            PDATE          6  0    135EDTWRD('  /  /  ')
     A                                   145'PAGE :'
     A                                   152PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(1)
     A                                   128'TIME :'
     A                                   135TIME
     A                                      EDTWRD('  :  :  ')
     A                                   146'<GLR2E1P>'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ======'
     A                                      SPACEA(1)
     A                                    48'Current'
     A                                    79'Prior'
     A                                   107'Difference'
     A                                   136'Year to Date'
     A                                      SPACEA(1)
     A                                    39'---------------------------'
     A                                    69'---------------------------'
     A                                    99'---------------------------'
     A                                   129'---------------------------'
     A                                      SPACEA(1)
     A                                     5'Account Title'
     A                                    47'Amounts'
     A                                    61'％'
     A                                    77'Amounts'
     A                                    91'％'
     A                                   107'Amounts'
     A                                   121'％'
     A                                   137'Amounts'
     A                                   151'％'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ======'
     A*
     A          R PD1                       SPACEB(1)
     A            P0402         34         2
     A  12                                39'('
     A            P0403         13  2     40EDTCDE(1)
     A  12                                57')'
     A  13                                59'('
     A  31        PR0403         5  2     60EDTCDE(1)
     A N31                                60' ****'
     A  13                                66')'
     A*
     A  14                                69'('
     A            P0404         13  2     70EDTCDE(1)
     A  14                                87')'
     A  15                                89'('
     A  32        PR0404         5  2     90EDTCDE(1)
     A N32                                90' ****'
     A  15                                96')'
     A*
     A  16                                99'('
     A            P04X          13  2    100EDTCDE(1)
     A  16                               117')'
     A  17                               119'('
     A  33        PR04X          5  2    120EDTCDE(1)
     A N33                               120' ****'
     A  17                               126')'
     A*
     A  18                               129'('
     A            P0405         13  2    130EDTCDE(1)
     A  18                               147')'
     A  19                               149'('
     A  34        PR0405         5  2    150EDTCDE(1)
     A N34                               150' ****'
     A  19                               156')'
     A*
     A*簽名行....
     A          R PT1                       SPACEB(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ======'
     A                                      SPACEA(1)
     A                                     4'Totals :'
     A  21                                38'('
     A            T0403         13  2     39EDTWRD('  ,   ,   ,  $0.  ')
     A  21                                57')'
     A                                    60'100.00'
     A*
     A  22                                68'('
     A            T0404         13  2     69EDTWRD('  ,   ,   ,  $0.  ')
     A  22                                87')'
     A                                    90'100.00'
     A*
     A  23                                98'('
     A            T04X          13  2     99EDTWRD('  ,   ,   ,  $0.  ')
     A  23                               117')'
     A  24                               119'('
     A            TR04X          5  2    120EDTCDE(1)
     A  24                               126')'
     A*
     A  25                               128'('
     A            T0405         13  2    129EDTWRD('  ,   ,   ,  $0.  ')
     A  25                               147')'
     A                                   150'100.00'
     A*
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ======'
     A                                      SPACEB(1)
     A            PSIGN1       156         1
     A                                      SPACEB(1)
     A            PSIGN2       156         1
     A                                      SPACEB(1)
     A*
