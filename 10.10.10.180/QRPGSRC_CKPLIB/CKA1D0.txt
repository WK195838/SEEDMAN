     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : CKA1D0                                     *
     H*    PROGRAM NAME :銀行帳戶資料維護                          *
     H*    AUTHOR       : A1152   ANGY                               *
     H*    CREATE DATE  : 92/09/17                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :                                            *
     F****************************************************************
     FCKHDLF01UF  E           K        DISK                      A
     FCKHBLF01IF  E           K        DISK
     FPA#APF  IF  E           K        DISK
     FGLAFPF  IF  E           K        DISK
     FCKA1D0D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710HBSR
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      209 2160$C8YMD
     I                                      201 2080$A8YMD
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I*DEFINE COPY KEY
     I            DS
     I                                        1   8 KHDC
     I                                        1   2 DHD01C
     I                                        3   8 DHD04C
     I*
     I            DS
     I                                        1  14 DHD02
     I                                        9  14 DHD021
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *LIKE     DEFN HD01      #B001I
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYHD     KLIST                           *MAIN FILE
     C                     KFLD           DHD01
     C                     KFLD           DHD04
     C           KEYHDC    KLIST                           *COPY REC.
     C                     KFLD           DHD01C
     C                     KFLD           DHD04C
     C           KEY#A5    KLIST                           *GET #A03
     C                     KFLD           D#A05   8
     C                     KFLD           DHD051 10
     C           KEYAF     KLIST
     C                     KFLD           AF01
     C                     KFLD           AF02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000            *SCR1
    C           SCID      CASEQ'SC02'    R2000            *SCR2
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           DDATE     PARM           P3101O  80
|    C*
|    C                     MOVE 'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
     C* SC01 FIELD INITIALIZE *--------------------------------------*
     C                     Z-ADD1         DOPT
     C                     MOVEL$COR      DHD01
     C*公司檔
     C                     CALL 'CKS#B0'
     C                     PARM DHD01     #B001I
     C           DHD01N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C*
|    C                     MOVEL*BLANK    DHD04
|    C                     MOVEL*BLANK    DHD01C
|    C                     MOVEL*BLANK    DHD04C
|    C*--------------------------------------------------------------*
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C* N99                SETON                     88
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E1000
| 01-C                     END
|    C*
|    C*
| 02-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 02-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 03-C           *IN99     IFEQ '0'                        *SCR CHK OK
|   C                     EXSR R1C00                      *PRE-SC02
|   C                     MOVEL'SC02'    SCID
| 03-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1A00     BEGSR
|    C*
|    C* AUT CHECK
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM 'CKI1D0'  S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C   99                GOTO E1A00
|    C*
|    C* CALL SUB-PROGRAM
|    C                     CALL 'CKI1D0'
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C           DHD01     PARM DHD01     P130I1  2
|    C           DHD04     PARM DHD04     P130I2  6
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     PARM           @RTNC   2
|    C* RETURN CODE CHECK
| 02-C           @RTNC     IFEQ '03'                       *PF3
|   C                     SETON                     03
| 02-C                     END
|    C*
03===C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
04===C           R1B00     BEGSR
|    C*
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*
|    C* DATA CHECK *--------------------------------------------------*
     C                     MOVEL*BLANK    DHD01N
| 02-C           DHD01     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
     C*公司檔
     C                     CALL 'CKS#B0'
     C                     PARM DHD01     #B001I
     C           DHD01N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
| 03-C           #B099O    IFNE 'Y'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 03*C                     END
|    C*
| 04-C           DHD04     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
|    C           KEYHD     CHAINHD0                  40
|    C*
| 05-C           DOPT      IFEQ 1                          *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 05-C                     END
|    C   99                GOTO E1B00
|    C*
| 06-C           DOPT      IFNE 1                          *UPD,DEL,INQ
|   C           *IN40     ANDEQ'1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 06-C                     END
|    C   99                GOTO E1B00
|    C*
| 07-C           DOPT      IFEQ 1                          *ADD
|   C           KHDC      ANDNE*BLANK                     *COPY
|   C           KEYHDC    CHAINHD0                  40
| 08-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 08-C                     END
| 07-C                     END
|    C   99                GOTO E1B00
|    C*
04===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
05===C           R1C00     BEGSR
|    C*
| 01-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     SETON                     31    *CAN INPUT
| 01*C                     ELSE                            *DEL, INQ
|   C                     SETOF                     31    *INP PROTECT
| 01-C                     END
|    C*
| 02-C           DOPT      IFEQ 1                          *ADD
|   C           KHDC      ANDEQ*BLANK                     *NO COPY
|   C                     EXSR R1C10
| 02*C                     ELSE                            *U/D/I
|   C                     EXSR R1C20
| 02-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)----------------------------------*
|    C           DOPT      IFEQ 1                          *ADD
|    C           DOPT      OREQ 5                          *INQ
|    C           *LOVAL    SETLLHD0
|    C                     END
|    C*---------------------------------------------------------------*
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1C10 ....'SC02' FIELD INIT : ADD (NO COPY)
     C*==============================================================*
06===C           R1C10     BEGSR
|    C*
|    C                     MOVEL*BLANKS   DHD03
|    C                     MOVEL*BLANK    DHD02
|    C                     MOVEL*BLANKS   DHB03
|    C                     MOVEL*BLANKS   DHD05
|    C                     MOVEL*BLANKS   D#A03
|    C                     MOVEL'NTD'     DHD06
|    C                     Z-ADD*ZEROS    DHD07
|    C                     MOVEL*BLANK    DHD08
|    C                     MOVEL*BLANKS   AF03
|    C                     MOVEL*BLANKS   DHD09
|    C                     MOVEL*BLANKS   DHD10
|    C                     MOVEL*BLANKS   DHD21
|    C                     MOVEL*BLANKS   DHD22
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*  R1C20 ....'SC02' FIELD INIT : UPD, DEL, INQ, (ADD & COPY)
     C*==============================================================*
07===C           R1C20     BEGSR
|    C*
|    C                     MOVELHD02      DHD02
|    C                     MOVELHD03      DHD03
|    C           DHD03     CHAINHB0                  40    *GET HB03
|    C           *IN40     IFEQ '0'
|    C                     MOVELHB03      DHB03
|    C                     ELSE
|    C                     MOVEL*BLANK    DHB03
|    C                     END
|    C                     MOVELHD05      DHD05
|    C                     MOVEL'ACCTYPE' D#A05
|    C                     MOVE *BLANK    DHD051
|    C                     MOVELDHD05     DHD051
|    C                     MOVEL*BLANK    D#A03
|    C           KEY#A5    CHAIN#A0                  40    *GET #A03
     C           *IN40     IFEQ '0'
|   C                     MOVEL#A03      D#A03
| 02-C                     END
|    C*
|    C                     MOVELHD06      DHD06
|    C                     MOVELHD21      DHD21
|    C                     MOVELHD22      DHD22
|    C                     Z-ADDHD07      DHD07
|    C                     MOVELHD08      DHD08
|    C                     MOVELDHD01     AF01
|    C                     MOVELDHD08     AF02
|    C                     MOVEL*BLANK    AF03
|    C           KEYAF     CHAINAF0                  40    *GET MAS..
|    C                     MOVELHD09      DHD09
|    C                     MOVELHD10      DHD10
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
08===C           R2000     BEGSR
|    C*
|    C                     EXFMTDSPD2
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C* RELEAS FILE --------------------------------------------------*
|   C           *LOVAL    SETLLHD0
|   C*---------------------------------------------------------------*
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 02-C           *IN04     IFEQ '1'
|   C                     EXSR R2E00                      .PROMPT
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 03-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     EXSR R2B00                      *SC02 CHK
| 03-C                     END
|    C*
| 04-C           *IN05     IFEQ '1'                        *CHECK ONLY
|   C                     GOTO E2000
| 04-C                     END
|    C*
| 05-C           *IN99     IFEQ '0'                        *CHK OK
|   C                     EXSR R2D00                      *FILE UPD
|   C                     MOVE 'SC01'    SCID
| 05-C                     END
|    C*
08===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....'SC02' CHECK
     C*==============================================================*
09===C           R2B00     BEGSR
|    C*帳號
| 01-C           DHD02     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 01-C                     END
|    C   99                GOTO E2B00
     C*
|    C           DHD021    IFNE DHD04
|    C                     MOVEL'UPT0030' ERRID
|    C                     MOVEL'PTMF'    ERRF
|    C                     SETON                     6299
|    C                     END
     C   99                GOTO E2B00
     C*銀行
     C                     MOVEL*BLANK    DHB03
| 01-C           DHD03     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO E2B00
|    C*
|    C           DHD03     CHAINHB0                  40
| 02-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E2B00
|    C                     MOVELHB03      DHB03
|    C*帳戶種類
     C                     MOVEL*BLANK    D#A03
| 03-C           DHD05     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 03-C                     END
|    C   99                GOTO E2B00
|    C*
|    C                     MOVEL'ACCTYPE' D#A05
     C                     MOVE *BLANK    DHD051
|    C                     MOVELDHD05     DHD051
|    C           KEY#A5    CHAIN#A0                  40
| 04-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 04-C                     END
|    C   99                GOTO E2B00
     C           *IN40     IFEQ '0'
|   C                     CALL 'P65'
|   C                     PARM #A03      P6501I 80
|   C                     PARM 10        P6502I  30
|   C           D#A03     PARM           P6511O 80
|   C                     PARM           P6512O 80
| 02-C                     END
|    C*
|    C*使用幣別
| 07-C           DHD06     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6799
| 07-C                     END
|    C   99                GOTO E2B00
     C*會計科目
     C                     MOVEL*BLANK    AF03
| 08-C           DHD08     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6499
| 08-C                     END
|    C   99                GOTO E2B00
|    C*
|    C                     MOVELDHD01     AF01
|    C                     MOVELDHD08     AF02
|    C           KEYAF     CHAINAF0                  40
| 09-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6499
| 09-C                     END
|    C   99                GOTO E2B00
|    C*
09===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
10===C           R2D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D20
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*==============================================================*
11===C           R2D10     BEGSR
|    C*
|    C                     EXSR R2D11
|    C                     WRITEHD0
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
12===C           R2D11     BEGSR
|    C*
|    C                     MOVELDHD01     HD01
|    C                     MOVELDHD02     HD02
|    C                     MOVELDHD03     HD03
|    C                     MOVELDHD04     HD04
|    C                     MOVELDHD05     HD05
|    C                     MOVELDHD06     HD06
|    C                     Z-ADDDHD07     HD07
|    C                     MOVELDHD08     HD08
|    C                     MOVELDHD09     HD09
|    C                     MOVELDHD10     HD10
|    C                     Z-ADD*ZERO     HD13
|    C                     MOVELDHD21     HD21
|    C                     MOVELDHD22     HD22
|    C                     TIME           HD97
|    C                     Z-ADD$A8YMD    HD98
|    C                     MOVEL$USER     HD99
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R2D20 ....FILE PROCESS : UPD
     C*==============================================================*
13===C           R2D20     BEGSR
|    C*
|    C                     EXSR R2D11
|    C                     UPDATHD0
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS
     C*==============================================================*
14===C           R2D30     BEGSR
|    C*
|    C                     DELETHD0
|    C*
14===C                     ENDSR
     C*==============================================================*
     C*          R2E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
05===C           R2E00     BEGSR
|    C*
|    C           HBSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*銀行代碼
| 01-C           #LIN      IFEQ 9
|   C           #COL      ANDGE13
|   C           #COL      ANDLE19
|    C                     CALL 'CKI1C0'
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C           DHD03     PARM DHD03     P120I1  7
|    C                     PARM           P120I2 20
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     PARM           @RTNC   2
| 01-C                     END
|    C*
|    C*帳戶種類
| 01-C           #LIN      IFEQ 10
|   C           #COL      ANDGE13
|   C           #COL      ANDLE13
|   C                     CALL 'WDS012'
|   C                     PARM 'ACCTYPE' S01I    8
|   C                     PARM *BLANK    S0102I 10
     C                     MOVELS0102I    DHD05
     C                     MOVEL*BLANK    S0101I
     C                     MOVEL*BLANK    S0102I
| 01-C                     END
|    C*
|    C*會計科目
| 01-C           #LIN      IFEQ 11
|   C           #COL      ANDGE13
|   C           #COL      ANDLE20
|    C                     CALL 'GLI410'
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     PARM DHD01     P410I1  2
|    C           DHD08     PARM DHD08     P410I2  8
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     PARM           @RTNC   2
| 01-C                     END
|    C*
     C                     ENDSR
     C*==============================================================*
