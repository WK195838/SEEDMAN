     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : CKR4C02                                    *
     H*    PROGRAM NAME :簽收條　　　　　　　                      *
     H*    AUTHOR       : A1150                                      *
     H*    CREATE DATE  : 81/10/07                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :                                            *
     H****************************************************************
     FCKHAPF  IP  E           K        DISK
     FCKHBLF01IF  E           K        DISK
     FCKHFLF04IF  E           K        DISK
     FPA#APF  IF  E           K        DISK
     FPT#XPF  IF  E           K        DISK
     FCKR4C1P O   E             39     PRINTER
     E*---------------------------------------------------------------
     I* KEY BREAK DEFINE
     IHA0
     I                                              HA06  L1
     I                                              HA02  L2
     I                                              HA04  L2
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I* LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      111 1160$CHYMD
     I                                      201 2080$A8YMD
     I                                      138 138 $INQ
     I* PROGRAM USE DATA DEFINE ++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DHA02F
     I                                      505 506 DHA02T
     I                                      508 509 DHA23
     I                                      510 5170DHA06F
     I                                      518 5250DHA06T
     I*                                     526 530 DHA04F
     I*                                     532 536 DHA04T
     I                                      537 537 WKIND
     I                                      538 547 DHA04F
     I                                      548 557 DHA04T
     I/COPY QRPGSRC,PT#XDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *LIKE     DEFN #A01      WTMPZ1
     C           *LIKE     DEFN #A02      WTMPZ2
     C           *LIKE     DEFN #X01      D#X03
     C*
     C           *LIKE     DEFN HA02      #B001I
     C           *LIKE     DEFN HA02      #C001I
     C           *LIKE     DEFN HA04      #C002I
     C           *LIKE     DEFN HA02      #D001I
     C           *LIKE     DEFN HA04      #D002I
     C           *LIKE     DEFN HA02      #E001I
     C           *LIKE     DEFN HA13      #E002I
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEY#A     KLIST
     C                     KFLD           WTMPZ1
     C                     KFLD           WTMPZ2
     C           KEYHB     KLIST
     C                     KFLD           HA08
     C           KEYHF     KLIST
     C                     KFLD           HF03
     C                     KFLD           HF02
     C           KEY#X3    KLIST                           .CHAIN #X0
     C                     KFLD           D#X03
     C                     KFLD           DHA02F
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR R1000
     C*+ LEVEL BREAK INIT PROCESS +++++++++++++++++++++++++++++++++++
     C   L1                EXSR R7100
     C   L2                EXSR R7200
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     EXSR R2000
     C*+ LEVEL BREAK PROCESS ++++++++++++++++++++++++++++++++++++++++
     CL2 10                EXSR R8200
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     CLRN10                EXSR R8999
     C*==============================================================*
     C*          R1000 ... INITIAL PROCESS
     C*==============================================================*
     C           R1000     BEGSR
     C                     SETON                     1039
     C                     Z-ADD0         PNO
     C                     IN   PTDA01
     C*
     C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM $PFMT     P3104I  1
     C                     PARM $PTYPE    P3105I  1
     C           PDATE     PARM           P3101O  80
     C*
     C*CLEAR TOTAL FIELD
     C                     Z-ADD*ZERO     PSUM
     C*
     C                     MOVEL'CK'      D#X03
     C           KEY#X3    CHAIN#X0                  40
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     ENDSR
     C*==============================================================*
     C*          R2000 ... DETAIL
     C*==============================================================*
     C           R2000     BEGSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C           *IN39     IFEQ '1'
     C                     WRITEPH1
     C                     SETOF                     39
     C* BREAK CONTROL FOR FIELD PRINTED +++++++++++++++++++++++++++++++
     C                     SETON                     31
     C                     END
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*到期日
     C                     CALL 'P31'
     C                     PARM HA07      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           PHA07     PARM           P3101O  80
     C*
     C           KEYHB     CHAINHB0                  40
     C   40                MOVEL*BLANK    HB02             *付款銀行
     C                     ADD  HA10      PSUM
     C*
     C                     WRITEPD1
     C* BREAK CONTROL FOR FIELD PRINTED +++++++++++++++++++++++++++++++
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     SETOF                     31
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R7100 ... LEVEL BREAK 1 :INIT PROCESS
     C*==============================================================*
     C           R7100     BEGSR
     C*
     C*開票日
     C                     CALL 'P31'
     C                     PARM HA06      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           PHA06     PARM           P3101O  80
     C*
     C                     SETON                     31
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R7200 ... LEVEL BREAK 2 :INIT PROCESS
     C*==============================================================*
     C           R7200     BEGSR
     C                     ADD  1         PNO
     C*
     C*GET廠商
     C                     MOVEL*BLANK    W#D04  42
     C                     MOVEL*BLANK    P#D04
     C           KEYHF     CHAINCKHFLF04             40
     C           *IN40     IFEQ '0'
     C                     MOVELHF77      W#D04
     C                     ELSE
     C                     CALL 'CKS#D0'
     C                     PARM HA02      #D001I
     C                     PARM HA04      #D002I
     C                     PARM           #D003O 10
     C           W#D04     PARM           #D004O 42
     C                     PARM           #D005O 62
     C                     PARM           #D098O 50
     C                     PARM           #D099O  1
     C                     END
     C*
     C                     CALL 'P65'
     C                     PARM W#D04     P6501I 80
     C                     PARM 32        P6502I  30
     C           P#D04     PARM           P6511O 80
     C                     PARM           P6512O 80
     C*
     C*GET財務用途++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVEL*BLANK    #A03P
     C                     MOVEL'CKUSED'  WTMPZ1
     C                     MOVELHA23      WTMPZ2
     C           KEY#A     CHAIN#A0                  40
     C           *IN40     IFEQ '0'
     C                     CALL 'P65'
     C                     PARM #A03      P6501I 80
     C                     PARM 10        P6502I  30
     C           #A03P     PARM           P6511O 80
     C                     PARM           P6512O 80
     C                     END
     C*
     C                     SETON                     39
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R8200 ... LEVEL BREAK 2
     C*==============================================================*
     C           R8200     BEGSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     WRITEPT1
     C                     Z-ADD0         PSUM
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R8999 ... LEVEL BREAK LR -- NO DATA
     C*==============================================================*
     C           R8999     BEGSR
     C*
     C                     EXSR R1000
     C*
     C                     WRITEPH1
     C*
     C                     WRITEPE1
     C*
     C                     ENDSR
