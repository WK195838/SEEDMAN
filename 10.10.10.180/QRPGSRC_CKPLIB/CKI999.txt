     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : CKI999                                     *
     H*    PROGRAM NAME :應收票據全頁查詢                          *
     H*    AUTHOR       : TINA                                       *
     H*    CREATE DATE  : 92/09/22                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :                                            *
     F****************************************************************
     FCKHALF99IF  E           K        DISK
     F*KHALF97IF  E           K        DISK
     F*           HA0                               KRENAMEHA0L
     FCKHBLF01IF  E           K        DISK
     FPT#XPF  IF  E           K        DISK
     FCKHDLF01IF  E           K        DISK
     FCKI999D CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      209 2160$C8YMD
     I                                      201 2080$A8YMD
     I                                      152 161 $USERN
     I*DEFIND STRAT KEY
     I            DS
     I                                        1  10 @KHAO
     I            DS
     I                                        1  10 @KHAN
     I                                        1  10 DHA03
     I            DS
     I                                        1  14 HA16
     I                                        9  14 HA161
     I/COPY QRPGSRC,PT#XDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAG REC NUM
     C*
     C           *LIKE     DEFN HA02      #B001I
     C           *LIKE     DEFN HA02      #C001I
     C           *LIKE     DEFN HA04      #C002I
     C           *LIKE     DEFN HA02      #D001I
     C           *LIKE     DEFN HA04      #D002I
     C           *LIKE     DEFN HA02      #E001I
     C           *LIKE     DEFN HA13      #E002I
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DHA03     PARM           P130I2 10         票號
     C*          HA04      PARM           P130O1  9         客戶
     C*          DHA06S    PARM           P130O2  60        收票日
     C*          DHA07S    PARM           P130O3  60        到期日
     C*          DHA55     PARM           P130O5  90        未沖
     C*          HA13      PARM           P130O6 10         業務員
     C                     PARM           @RTNC   2          FUN KEY
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYHA     KLIST                           *MAIN FILE
     C                     KFLD           DHA03
     C           KEYHD3    KLIST                           *GET HD03
     C                     KFLD           HA02
     C                     KFLD           HA161
     C           KEY#X3    KLIST                           .CHAIN #X0
     C                     KFLD           D#X03   2
     C                     KFLD           HA02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
     C           @RTNC     DOWEQ*BLANK
     C                     EXSR R1000                      *SCR PROCESS
     C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C* INIT VALUE SET *---------------------------------------------*
     C                     MOVEL*BLANK    @RTNC
     C*--------------------------------------------------------------*
     C                     EXSR R1A00                      .SFL INIT
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1000 ....SCREEN  PROCESS
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C                     WRITEDSPC1
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '1'                        *END
     C                     MOVEL'03'      @RTNC
     C                     GOTO E1000
     C                     END
     C*
     C           *IN12     IFEQ '1'                        *PREVIOUS
     C                     MOVEL'12'      @RTNC
     C                     GOTO E1000
     C                     END
     C*
     C           *IN25     IFEQ '1'                        *NEXT PAGE
     C                     EXSR R1B00
     C                     GOTO E1000
     C                     END
     C*
     C           @KHAO     IFNE @KHAN                      *START KEY
     C                     EXSR R1A00                      *SFL INIT
     C                     GOTO E1000
     C                     END
     C*
     C           RRN       IFGT 0
     C                     EXSR R1C00                      *OPTION
     C                     END
     C*
     C           E1000     ENDSR
     C*=============================================================*
     C*          R1A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
     C           R1A00     BEGSR
     C*
     C                     Z-ADD0         DRRN
     C                     Z-ADD0         RRN
     C                     Z-ADD0         @DRCD            *DSP RCD
     C                     MOVEL'N'       @BOTOM  1        *BOTTOM
     C*
     C                     SETON                     52    *SFLCLR
     C                     WRITESFLCR
     C                     SETOF                     52
     C*
     C                     SETON                     51    *SFLDSPCTL
     C                     SETOF                     50    *SFLDSP
     C*
     C                     EXSR R1A20                      *SFL HEAD
     C* <<SFL INITIAITAL DATA ADD>>
     C                     MOVEL@KHAN     @KHAO            *KEY SAVE
     C                     Z-ADD13        @PRCD            *PAGE REC
     C*
     C           KEYHA     SETLLHA0
     C                     EXSR R1A10                      *ADD SFL DTA
     C*
     C*
     C           RRN       IFGT 0
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     99    *NO DTA
     C                     MOVEL'UPT2050' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1A10 .... ADD SUBFILE DATA
     C*=============================================================*
     C           R1A10     BEGSR
     C*
     C                     ADD  @PRCD     @DRCD
     C           RRN       DOWLT@DRCD
     C*
     C                     READ HA0                      46
     C*
     C           *IN46     IFEQ '0'
     C                     EXSR R1A11                      *WRITE SFL
     C                     ELSE                            *EOF
     C                     Z-ADDRRN       @DRCD
     C                     MOVE 'Y'       @BOTOM
     C                     SETON                     96
     C                     END
     C*
     C                     END
     C*
     C                     Z-ADDRRN       DRRN
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1A11 .... WRITE SUBFILE DATA
     C*=============================================================*
     C           R1A11     BEGSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
     C*<<SUBFILE DATA VALUE SET>>
     C*<GET收票日>
     C                     MOVEL'CK'      D#X03
     C           KEY#X3    CHAIN#X0                  40
     C                     CALL 'P31'
     C                     PARM HA06      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHA06     PARM           P3101O  80
     C                     Z-ADDDHA06     DHA06S  60
     C*
     C*<GET客戶名稱>
     C                     MOVEL*BLANK    D#C03
     C*
     C                     CALL 'CKS#C0'
     C                     PARM HA02      #C001I
     C                     PARM HA04      #C002I
     C           D#C03     PARM           #C003O 10
     C                     PARM           #C004O 42
     C                     PARM           #C005O 10
     C                     PARM           #C098O 50
     C                     PARM           #C099O  1
     C*
     C*<GET銀行名稱>
     C           HA17      IFEQ '1'
     C*
     C           HA08      CHAINHB0                  40
     C           *IN40     IFEQ '0'
     C                     MOVELHB03      DHB02
     C                     ELSE
     C                     MOVEL*BLANK    DHB02
     C                     END
     C*
     C                     ELSE
     C           KEYHD3    CHAINHD0                  40
     C           *IN40     IFEQ '0'
     C*
     C           HD03      CHAINHB0                  40
     C           *IN40     IFEQ '0'
     C                     MOVELHB02      DHB02
     C                     ELSE
     C                     MOVEL*BLANK    DHB02
     C                     END
     C*
     C                     ELSE
     C                     MOVEL*BLANK    DHB02
     C                     END
     C*
     C                     END
     C*<GET到期日>
     C                     CALL 'P31'
     C                     PARM HA07      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHA07     PARM           P3101O  80
     C                     Z-ADDDHA07     DHA07S  60
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
     C                     ADD  1         RRN
     C                     WRITESFLSR
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
     C           R1A20     BEGSR
     C*
     C                     IN   PTDA01
     C*
     C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM $PFMT     P3104I  1
     C                     PARM $PTYPE    P3105I  1
     C           DDATE     PARM           P3101O  80
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1B00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
     C           R1B00     BEGSR
     C*
     C           @BOTOM    IFEQ 'Y'                        *EOF
     C                     SETON                         96*LST PAG
     C                     Z-ADDRRN       DRRN
     C                     ELSE
     C                     EXSR R1A10                      *ADD SFL DTA
     C                     END
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1C00 ....挑選資料
     C*=============================================================*
     C           R1C00     BEGSR
     C*
     C                     READCSFLSR                    42
     C           *IN42     DOWEQ'0'
     C*
     C           DOPT      IFNE *BLANK
     C                     MOVELHA03      P130I2
     C*                    MOVELDHA06S    P130O2
     C*                    MOVELDHA07S    P130O3
     C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
     C*          HA03      CHAINHA0L                 45
     C*                    MOVELHA04      P130O1
     C*                    MOVELDHA55     P130O5
     C*                    MOVELHA13      P130O6
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVEL'00'      @RTNC
     C                     SETON                     42
     C                     END
     C*
     C  N42                READCSFLSR                    42
     C                     END
     C*
     C                     ENDSR
