     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : CKA180                                     *
     H*    PROGRAM NAME : 銀行對帳單維護                           *
     H*    AUTHOR       : A1149    HUANG-SHU.MEI                     *
     H*    CREATE DATE  : 1993.02.22                                 *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       : 票據系統                                 *
     H****************************************************************
     FCKHALF01UF  E           K        DISK
     F            HA0                               KRENAMEHALF01
     FCKHALF11UF  E           K        DISK
     F            HA1                               KRENAMEHALF11
     FCKHAJF06IF  E           K        DISK
     FCKHBLF01IF  E           K        DISK
     FCKHCLF01UF  E           K        DISK                      A
     FCKHDLF01IF  E           K        DISK
     FPT#XPF  IF  E           K        DISK
     FCKA180D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      209 2160$C8YMD
     I                                      201 2080$A8YMD
     I*
     I*DEFINE START KEY
     I            DS
     I                                        1  16 @KHAO
     I            DS
     I                                        1  16 @KHAN
     I                                        1   2 DHC01
     I                                        3  16 $HC02
     I/COPY QRPGSRC,PT#XDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN HA15      $HA15
     C           *LIKE     DEFN HC03      $HC03
     C*
     C           *LIKE     DEFN HA02      #B001I
     C           *LIKE     DEFN HA02      #C001I
     C           *LIKE     DEFN HA04      #C002I
     C           *LIKE     DEFN HA02      #D001I
     C           *LIKE     DEFN HA04      #D002I
     C           *LIKE     DEFN HA02      #E001I
     C           *LIKE     DEFN HA13      #E002I
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYHA1    KLIST
     C                     KFLD           DHC01
     C                     KFLD           $HC02            *帳號全碼
     C           KEYHA2    KLIST
     C                     KFLD           HA01
     C                     KFLD           HA02             *公司別
     C                     KFLD           HA03
     C           KEYHA3    KLIST
     C                     KFLD           HA02
     C                     KFLD           HA15
     C                     KFLD           HA31
     C                     KFLD           HA01
     C           KEYHA4    KLIST
     C                     KFLD           DHC08            *交易類別
     C                     KFLD           DHC01            *公司別
     C                     KFLD           DHC05            *票據號碼
     C           KEYHB     KLIST
     C                     KFLD           HD03             *銀行代碼
     C           KEYHC1    KLIST
     C                     KFLD           DHC01
     C                     KFLD           $HC02            *帳號全碼
     C           KEYHC2    KLIST
     C                     KFLD           DHC01
     C                     KFLD           $HC02            *帳號全碼
     C                     KFLD           HA15             *往來日期
     C           KEYHC3    KLIST
     C                     KFLD           DHC01
     C                     KFLD           $HC02            *帳號全碼
     C                     KFLD           $HC03            *往來日期
     C           KEYHD     KLIST
     C                     KFLD           DHC01            *公司別
     C                     KFLD           DHC02            *帳號簡碼
     C           KEY#X     KLIST
     C                     KFLD           D#X03   2
     C                     KFLD           DHC01
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
     C           *IN03     DOWEQ'0'
     C           SCID      CASEQ'SC01'    R1000
     C           SCID      CASEQ'SC02'    R2000
     C           SCID      CASEQ'SC03'    R3000
     C                     END
     C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C                     IN   PTDA01
     C*
     C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM $PFMT     P3104I  1
     C                     PARM $PTYPE    P3105I  1
     C           DDATE     PARM           P3101O  80
     C*
     C                     MOVE 'SC01'    SCID    4
     C* SC01 FIELD INITIALIZE *--------------------------------------*
     C                     MOVEL$COR      DHC01
     C                     MOVEL*BLANK    DHC01N
     C*公司檔
     C                     CALL 'CKS#B0'
     C                     PARM DHC01     #B001I
     C           DHC01N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C*
     C                     MOVE *BLANKS   DHC02
     C*--------------------------------------------------------------*
     C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C* N99                SETON                     88
     C*
     C                     EXFMTDSPD1
     C                     MOVEA*ALL'0'   *IN,60
     C                     Z-ADD*ZERO     #LIN
     C                     Z-ADD*ZERO     #COL
     C*
     C           *IN03     IFEQ '1'
     C                     GOTO E1000
     C                     END
     C*
     C           *IN04     IFEQ '1'                        *BROWSE
     C                     EXSR R1E00
     C                     GOTO E1000
     C                     END
     C*
     C                     EXSR R1B00                      *SCR CHK
     C*
     C           *IN99     IFEQ '0'                        *SCR CHK OK
     C                     EXSR R1D00
     C                     EXSR R1C00
     C                     MOVEL'SC02'    SCID
     C                     END
     C*
     C           E1000     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
     C           R1B00     BEGSR
     C* DATA CHECK *--------------------------------------------------*
     C*<<公司別>>
     C                     MOVE *BLANKS   DHC01N
     C           DHC01     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6199
     C                     ELSE
     C*公司檔
     C                     CALL 'CKS#B0'
     C                     PARM DHC01     #B001I
     C           DHC01N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C           #B099O    IFNE 'Y'
     C                     MOVEL'UPT0030' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6199
     C                     END
     C*
     C                     END
     C   99                GOTO E1B00
     C*
     C*<<銀行帳號>>
     C           DHC02     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6299
     C                     ELSE
     C           KEYHD     CHAINHD0                  40
     C           *IN40     IFEQ '0'
     C                     MOVE HD02      $HC02
     C                     ELSE
     C                     MOVEL'UPT2010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6299
     C                     END
     C                     END
     C   99                GOTO E1B00
     C*
     C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
     C           R1C00     BEGSR
     C*
     C* INIT VALUE SET *---------------------------------------------*
     C                     Z-ADD13        @PRCD            .PAGE SIZE
     C*--------------------------------------------------------------*
     C                     EXSR R2A00                      .SFL INIT
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1D00 ....UPDATE CKHCLF01
     C*==============================================================*
     C           R1D00     BEGSR
     C*
     C           KEYHC1    SETLLHC0
     C           KEYHC1    READEHC0                      42
     C           *IN42     DOWEQ'0'
     C           HC07      IFEQ 'I'
     C                     MOVE 'Y'       HC07
     C                     UPDATHC0
     C                     END
     C*
     C           KEYHC1    READEHC0                      42
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
     C           R1E00     BEGSR
     C*
     C           #CSR      DIV  256       #LIN             *LINE
     C                     MVR            #COL             *COLUMN
     C*銀行帳號
     C           #LIN      IFEQ 5
     C           #COL      ANDGE13
     C           #COL      ANDLE18
     C                     CALL 'CKI1D0'
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM DHC01     P130I1  2
     C           DHC02     PARM DHC02     P130I2  6
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM           @RTNC   2
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2000 ....SCREEN  2  PROCESS
     C*==============================================================*
     C           R2000     BEGSR
     C*
     C                     WRITEDSPC1
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '1'                        *END
     C                     GOTO E2000
     C                     END
     C*
     C           *IN06     IFEQ '1'                        *PREVIOUS
     C                     EXSR R2E00
     C                     MOVE 'SC03'    SCID
     C                     GOTO E2000
     C                     END
     C*
     C           *IN12     IFEQ '1'                        *PREVIOUS
     C                     MOVE 'SC01'    SCID
     C                     GOTO E2000
     C                     END
     C*
     C           *IN25     IFEQ '1'                        *NEXT PAGE
     C                     EXSR R2N00
     C                     GOTO E2000
     C                     END
     C*
     C           @KHAO     IFNE @KHAN                      *START KEY
     C                     EXSR R2A00                      *SFL INIT
     C                     GOTO E2000
     C                     END
     C*
     C                     EXSR R2C00                      *OPTION
     C*
     C           E2000     ENDSR
     C*=============================================================*
     C*          R2A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
     C           R2A00     BEGSR
     C*
     C                     Z-ADD0         DRRN
     C                     Z-ADD0         RRN
     C                     Z-ADD0         @DRCD            *DSP RCD
     C                     MOVEL'N'       @BOTOM  1        *BOTTOM
     C*
     C                     SETON                     52    *SFLCLR
     C                     WRITESFLCR
     C                     SETOF                     52
     C*
     C                     SETON                     51    *SFLDSPCTL
     C                     SETOF                     5053  *SFLDSP,END
     C*
     C                     EXSR R2A20                      *SFL HEAD
     C* <<SFL INITIAITAL DATA ADD>>
     C                     MOVEL@KHAN     @KHAO            *KEY SAVE
     C*
     C           KEYHA1    SETLLCKHAJF06
     C                     EXSR R2A10                      *ADD SFL DTA
     C*
     C           RRN       IFGT 0
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     99    *NO DTA
     C                     MOVEL'UPT2050' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R2A10 .... ADD SUBFILE DATA
     C*=============================================================*
     C           R2A10     BEGSR
     C*
     C                     ADD  @PRCD     @DRCD
     C           RRN       DOWLT@DRCD
     C*
     C           KEYHA1    READECKHAJF06                 46
     C*
     C           *IN46     IFEQ '0'
     C                     EXSR R2A11                      *WRITE SFL
     C                     ELSE                            *EOF
     C                     Z-ADDRRN       @DRCD
     C                     MOVE 'Y'       @BOTOM
     C                     SETON                     9653  *SFLEND
     C                     END
     C*
     C                     END
     C*
     C                     Z-ADDRRN       DRRN
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R2A11 .... WRITE SUBFILE DATA
     C*=============================================================*
     C           R2A11     BEGSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* << SUBFILE DATA VALUE SET >>
     C*對帳記號HA26 ....
     C                     MOVEL*BLANKS   DHA26
     C*往來日期HA15 ....
     C                     MOVEL'CK'      D#X03
     C           KEY#X     CHAIN#X0                  40
     C                     CALL 'P31'
     C                     PARM HA15      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHA15     PARM           P3101O  80
     C*交易類別HA01 ....
     C           HC08      IFEQ '1'
     C                     MOVEL'收票'  DHA01C
     C                     END
     C           HC08      IFEQ '2'
     C                     MOVEL'付票'  DHA01C
     C                     END
     C           HC08      IFEQ '3'
     C                     MOVEL'存入'  DHA01C
     C                     END
     C           HC08      IFEQ '4'
     C                     MOVEL'提出'  DHA01C
     C                     END
     C           HC08      IFEQ '5'
     C                     MOVEL'轉存'  DHA01C
     C                     END
     C           HC08      IFEQ '6'
     C                     MOVEL'轉提'  DHA01C
     C                     END
     C*
     C*票據號碼HA03 ....
     C           HA01      IFGT '2'                        *存提轉
     C                     MOVEL*BLANKS   DHA03
     C                     ELSE
     C                     MOVELHA03      DHA03            *收付票
     C                     END
     C*收入金額
     C           HA01      IFEQ '1'
     C           HA01      OREQ '3'
     C           HA01      OREQ '5'
     C                     Z-ADDHA10      DHA10I
     C                     Z-ADD0         DHA10O
     C                     END
     C*支出金額
     C           HA01      IFEQ '2'
     C           HA01      OREQ '4'
     C           HA01      OREQ '6'
     C                     Z-ADDHA10      DHA10O
     C                     Z-ADD0         DHA10I
     C                     END
     C*到期日期HA07 ....
     C           HA01      IFLE '2'
     C                     MOVEL'CK'      D#X03
     C           KEY#X     CHAIN#X0                  40
     C                     CALL 'P31'
     C                     PARM HA07      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHA07     PARM           P3101O  80
     C*
     C                     ELSE
     C                     Z-ADD*ZERO     DHA07
     C                     END
     C*序號
     C           HA01      IFLE '2'
     C                     Z-ADD*ZERO     HA31
     C                     END
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     ADD  1         RRN
     C                     WRITESFLSR
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R2A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
     C           R2A20     BEGSR
     C*
     C                     MOVE $HC02     DHC02F
     C*
     C           KEYHB     CHAINHB0                  40
     C   40                MOVEL*BLANKS   DHC02N
     C  N40                MOVELHB03      DHC02N
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R2N00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
     C           R2N00     BEGSR
     C*
     C           @BOTOM    IFEQ 'Y'                        *EOF
     C                     SETON                         96*LST PAG
     C                     Z-ADDRRN       DRRN
     C                     ELSE
     C                     EXSR R2A10                      *ADD SFL DTA
     C                     END
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R2C00 ....挑選資料
     C*=============================================================*
     C           R2C00     BEGSR
     C*
     C                     SETON                     54    *SFLNXTCHG
     C                     READCSFLSR                  5757
     C           *IN57     DOWEQ'0'
     C*
     C           DHA26     IFEQ 'Y'
     C                     EXSR R2C10
     C                     EXSR R2C20
     C                     END
     C*
     C                     READCSFLSR                  5757
     C                     END
     C                     MOVE 'SC01'    SCID
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2C10 ....UPDTAE CKHALF01
     C*==============================================================*
     C           R2C10     BEGSR
     C*UPDATE CKHALF01
     C           HA01      IFLE '2'
     C           KEYHA2    CHAINHALF01               40
     C  N40                MOVEL'Y'       HA26
     C  N40                UPDATHALF01
     C*
     C                     ELSE
     C           KEYHA3    CHAINHALF11               40
     C  N40                MOVEL'Y'       HA26
     C  N40                UPDATHALF11
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2C20 ....UPDTAE CKHALF01
     C*==============================================================*
     C           R2C20     BEGSR
     C*UPDATE CKHALF01
     C*
     C           KEYHC2    SETGTHC0
     C           KEYHC2    REDPEHC0                      45
     C           *IN45     IFEQ '0'
     C           HC04      ADD  1         HC04
     C                     ELSE
     C                     Z-ADD1         HC04
     C                     END
     C*
     C                     Z-ADDHA15      HC03
     C                     MOVELHA03      HC05
     C                     MOVELHA01      HC08
     C                     MOVEL'Y'       HC07
     C           DHA10O    IFEQ *ZERO
     C                     Z-ADDDHA10I    HC06
     C                     ELSE
     C                     Z-ADDDHA10O    HC06
     C                     END
     C*
     C                     MOVELDHC01     HC01
     C                     MOVEL$HC02     HC02
     C*
     C                     TIME           HC97
     C                     Z-ADD$A8YMD    HC98
     C                     MOVEL$USER     HC99
     C*
     C                     WRITEHC0
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2E00 ....INIT 'SC03'
     C*==============================================================*
     C           R2E00     BEGSR
     C*
     C                     MOVEL*BLANKS   DHC08
     C                     MOVEL*BLANKS   DHC05
     C                     Z-ADD*ZERO     DHC03
     C                     Z-ADD*ZERO     DHC06
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R3000 .... 'SC03' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           R3000     BEGSR
     C*
     C                     EXFMTDSPD2
     C                     MOVEA*ALL'0'   *IN,60
     C                     Z-ADD*ZERO     #LIN
     C                     Z-ADD*ZERO     #COL
     C*
     C           *IN03     IFEQ '1'                        *END
     C                     GOTO E3000
     C                     END
     C*
     C           *IN12     IFEQ '1'                        *PREVIOUS
     C* RELEAS FILE --------------------------------------------------*
     C*          *LOVAL    SETLLHA0
     C*---------------------------------------------------------------*
     C                     MOVEL'SC02'    SCID
     C                     GOTO E3000
     C                     END
     C*
     C                     EXSR R3B00                      *CHECK
     C*
     C           *IN99     IFEQ '0'                        *CHK OK
     C                     EXSR R3D00                      *FILE UPD
     C                     MOVE 'SC02'    SCID
     C                     END
     C*
     C           E3000     ENDSR
     C*==============================================================*
     C*          R3B00 ....'SC03' CHECK
     C*==============================================================*
     C           R3B00     BEGSR
     C*<往來日期>
     C           DHC03     IFEQ *ZERO
     C                     MOVEL'UPT0012' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6399
     C                     END
     C   99                GOTO E3B00
     C*
     C                     MOVEL'CK'      D#X03
     C           KEY#X     CHAIN#X0                  40
     C                     CALL 'P30'
     C                     PARM DHC03     P3001I  80
     C                     PARM #X101     P3002I  1
     C                     PARM #X102     P3003I  1
     C                     PARM           P3011O  1
     C           P3011O    IFNE 'Y'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6399
     C                     END
     C   99                GOTO E3B00
     C*<交易類別>
     C*
     C*票據號碼
     C           DHC08     IFGT '2'
     C           DHC05     IFNE *BLANKS
     C                     MOVEL'UPT0011' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6599
     C                     END
     C*收付票 ....
     C                     ELSE
     C           DHC05     IFEQ *BLANKS
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6599
     C                     ELSE
     C           KEYHA4    CHAINHALF01               40
     C           *IN40     IFEQ '1'
     C                     MOVEL'UPT2010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6599
     C                     END
     C                     END
     C                     END
     C   99                GOTO E3B00
     C*<票額>
     C           DHC06     IFLE *ZERO
     C                     MOVEL'UPT0012' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6699
     C                     END
     C   99                GOTO E3B00
     C*
     C           E3B00     ENDSR
     C*==============================================================*
     C*          R3D00 ....FILE PROCESS
     C*==============================================================*
     C           R3D00     BEGSR
     C*WRITE CKHCLF01
     C                     CALL 'P31'
     C                     PARM DHC03     P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           $HC03     PARM           P3101O  80
     C*
     C           KEYHC3    SETGTHC0
     C           KEYHC3    REDPEHC0                      45
     C           *IN45     IFEQ '0'
     C           HC04      ADD  1         HC04
     C                     ELSE
     C                     Z-ADD1         HC04
     C                     END
     C*
     C                     MOVEL*BLANKS   HC08
     C                     MOVEL*BLANKS   HC05
     C                     MOVEL*BLANKS   HC07
     C                     Z-ADD*ZERO     HC03
     C                     Z-ADD*ZERO     HC06
     C                     MOVEL*BLANKS   HC01
     C                     MOVEL*BLANKS   HC02
     C*
     C                     MOVELDHC08     HC08
     C                     MOVELDHC05     HC05
     C*                    MOVEL'I'       HC07
     C                     Z-ADD$HC03     HC03
     C                     Z-ADDDHC06     HC06
     C                     MOVELDHC01     HC01
     C                     MOVEL$HC02     HC02
     C*
     C                     TIME           HC97
     C                     Z-ADD$A8YMD    HC98
     C                     MOVEL$USER     HC99
     C*
     C                     WRITEHC0
     C*
     C                     ENDSR
