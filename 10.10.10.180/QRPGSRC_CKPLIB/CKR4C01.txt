     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : CKR4C01                                    *
     H*    PROGRAM NAME :簽收條／信封／掛號單                      *
     H*    AUTHOR       : A1150                                      *
     H*    CREATE DATE  : 81/10/06                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :                                            *
     H****************************************************************
     FPT#XPF  IF  E           K        DISK
     FPA#APF  IF  E           K        DISK
     FCKR4C0D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*LOCAL DATA AREA
     ILDA        UDS
     I                                      101 110 $USER
     I                                      209 2160$C8YMD
     I                                      201 2080$A8YMD
     I                                      138 138 $INQ
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      125 1270$CPY
     I                                      219 219 $PENV
     I                                      217 218 $PRTCD
     I* PROGRAM USE DATA DEFINE ++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DHA02F
     I                                      505 506 DHA02T
     I                                      508 509 DHA23
     I                                      510 5170DHA06F
     I                                      518 5250DHA06T
     I*                                     526 530 DHA04F
     I*                                     532 536 DHA04T
     I                                      537 537 WKIND
     I                                      538 547 DHA04F
     I                                      548 557 DHA04T
     I/COPY QRPGSRC,PT#XDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *NAMVAR   DEFN *LDA      LDA
     C           *LIKE     DEFN #A01      WTMPZ1
     C           *LIKE     DEFN #A02      WTMPZ2
     C           *LIKE     DEFN #X01      WTMPX
     C*
     C           *LIKE     DEFN DHA02     #B001I
     C           *LIKE     DEFN DHA02     #C001I
     C           *LIKE     DEFN HA04F     #C002I
     C           *LIKE     DEFN DHA02     #D001I
     C           *LIKE     DEFN HA04F     #D002I
     C*          *LIKE     DEFN DHA02     #E001I
     C*          *LIKE     DEFN HA13      #E002I
     C*==============================================================*
     C*          IDENTIFY A PARAMETER LIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C           *IN10     PARM *IN10     IN10    1
     C*==============================================================*
     C*          KEY LIST
     C*==============================================================*
     C           KEY#A     KLIST
     C                     KFLD           WTMPZ1
     C                     KFLD           WTMPZ2
     C           KEY#X     KLIST
     C                     KFLD           WTMPX
     C                     KFLD           DHA02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C   10                EXSR R0200                      *INIT MSG.
     C  N10                EXSR R0100                      *INIT VALUE
     C*
     C                     MOVEL*BLANK    @PRT    1        *PRINT
     C           *IN03     DOUEQ'1'
     C           @PRT      OREQ 'Y'
     C                     EXSR R1000                      *SCRN
     C                     END
     C*
     C   03                SETON                     LR
     C                     RETRN
     C*==============================================================*
     C*          R0100 ....SCREEN INIT VALUE SET
     C*==============================================================*
     CSR         R0100     BEGSR
     C*
     C                     SETON                     10
     C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     IN   PTDA01
     C*
     C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM $PFMT     P3104I  1
     C                     PARM $PTYPE    P3105I  1
     C           DDATE     PARM           P3101O  80
     C*
     C                     MOVEL$COR      DHA02
     C                     MOVEL*BLANKS   DHA02N
     C*公司檔
     C                     CALL 'CKS#B0'
     C                     PARM DHA02     #B001I
     C           DHA02N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C*
     C                     MOVEL*BLANK    DHA23
     C                     Z-ADD0         HA06F
     C                     Z-ADD0         HA06T
     C                     MOVEL*BLANK    HA04F
     C                     MOVEL*BLANK    HA04T
     C                     MOVEL*BLANK    DKIND
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R0200 ....INITIAL VALUE
     C*==============================================================*
     CSR         R0200     BEGSR
     C*
     C                     SETON                     2799  報表格式MESSAGE
     C*
     C           $PENV     IFEQ '0'                        INQUIRY
     C                     MOVEL'UPT2142' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C           $PENV     IFEQ '1'                        ON-LINE
     C                     MOVEL'UPT2140' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C           $PENV     IFEQ '2'                        BATCH
     C                     MOVEL'UPT2110' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C                     ENDSR
     C*===============================================================*
     C*          R1000  ...SCREEN MAIN PROCESS
     C*===============================================================*
     C           R1000     BEGSR
     C*
     C* N99                SETON                     88
     C*
     C                     EXFMTDSPD1
     C                     MOVEA*ALL'0'   *IN,60
     C                     Z-ADD*ZERO     #LIN
     C                     Z-ADD*ZERO     #COL
     C*
     C           *IN03     IFEQ '1'
     C                     GOTO E1000
     C                     END
     C*
     C           *IN04     IFEQ '1'
     C                     EXSR R1E00                      .PROMPT
     C                     GOTO E1000
     C                     END
     C*
     C                     EXSR R1B00                      .CHECK      ?
     C*
     C           *IN99     IFEQ '0'
     C                     SETOF                     27
     C                     SETON                     28    .執行中
     C                     WRITEDSPD1
     C                     SETOF                     28
     C*
     C                     OUT  LDA
     C                     MOVEL'Y'       @PRT
     C                     END
     C*
     C           E1000     ENDSR
     C*===============================================================*
     C*          R1B00  ...CHECK DATA FROM SCREEN KEY-IN
     C*===============================================================*
     C           R1B00     BEGSR
     C*
     C*+<<DATA CHECK>>+++++++++++++++++++++++++++++++++++++++++++++++++
     C*公司別
     C                     MOVEL*BLANK    DHA02N
     C           DHA02     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6099
     C                     END
     C   99                GOTO E1B00
     C*
     C           DHA02     IFNE *BLANK
     C                     CALL 'CKS#B0'
     C                     PARM DHA02     #B001I
     C           DHA02N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C           #B099O    IFNE 'Y'
     C                     MOVEL'UPT0030' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6099
     C                     END
     C                     END
     C   99                GOTO E1B00
     C*
     C           DHA02     IFNE *BLANKS
     C                     MOVELDHA02     DHA02F
     C                     MOVELDHA02     DHA02T
     C                     ELSE
     C                     MOVEL*LOVAL    DHA02F
     C                     MOVEL*HIVAL    DHA02T
     C                     END
     C*
     C*財務用途
     C                     MOVEL*BLANK    #A03D
     C           DHA23     IFEQ *BLANK
     C* MAY 1993.11.24     MOVEL'UPT0010' ERRID
     C* MAY 1993.11.24     MOVEL'PTMF'    ERRF
     C* MAY 1993.11.24     SETON                       6199
     C                     ELSE
     C                     MOVEL'CKUSED'  WTMPZ1
     C                     MOVELDHA23     WTMPZ2
     C           KEY#A     CHAIN#A0                  40
     C           *IN40     IFEQ '0'
|   C                     CALL 'P65'
|   C                     PARM #A03      P6501I 80
|   C                     PARM 10        P6502I  30
|   C           #A03D     PARM           P6511O 80
|   C                     PARM           P6512O 80
     C                     ELSE
     C                     MOVEL'UPT2010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                       6199
     C                     END
     C                     END
     C   99                GOTO E1B00
     C*
     C*---開票日期
     C* --起
     C           HA06F     IFEQ 0
     C                     Z-ADD0         DHA06F
     C                     Z-ADD0         HA06F
     C                     ELSE
     C                     Z-ADDHA06F     WA01    80
     C                     MOVEL'CK'      WTMPX
     C           KEY#X     CHAIN#X0                  40
     C                     CALL 'P30'
     C                     PARM WA01      P3001I  80
     C                     PARM #X101     P3002I  1
     C                     PARM #X102     P3003I  1
     C                     PARM           P3011O  1
     C           P3011O    IFNE 'Y'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6299
     C                     END
     C   99                GOTO E1B00
     C*<DATE SCREEN-->FILE>
     C                     CALL 'P31'
     C                     PARM HA06F     P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           DHA06F    PARM           P3101O  80
     C                     END
     C*
     C* ---迄
     C           HA06T     IFEQ 0
     C*                    Z-ADD9999999   HA06T
     C                     Z-ADD99999999  DHA06T
     C                     ELSE
     C                     Z-ADDHA06T     WA02    80
     C                     MOVEL'CK'      WTMPX
     C           KEY#X     CHAIN#X0                  40
     C                     CALL 'P30'
     C                     PARM WA02      P3001I  80
     C                     PARM #X101     P3002I  1
     C                     PARM #X102     P3003I  1
     C                     PARM           P3011O  1
     C           P3011O    IFNE 'Y'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6399
     C                     END
     C   99                GOTO E1B00
     C*<DATE SCREEN-->FILE>
     C                     CALL 'P31'
     C                     PARM HA06T     P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           DHA06T    PARM           P3101O  80
     C                     END
     C*
     C           DHA06F    IFGT DHA06T
     C                     MOVEL'UPT0042' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     626399
     C                     END
     C*
     C   99                GOTO E1B00
     C*
     C*廠商代號
     C           HA04F     IFEQ *BLANK
     C                     MOVEL*LOVAL    DHA04F
     C                     ELSE
     C                     MOVELHA04F     DHA04F
     C                     END
     C*
     C           HA04T     IFEQ *BLANK
     C                     MOVEL*HIVAL    DHA04T
     C                     ELSE
     C                     MOVELHA04T     DHA04T
     C                     END
     C*
     C           DHA04F    IFGT DHA04T
     C                     MOVEL'UPT0042' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     646599
     C                     END
     C*
     C   99                GOTO E1B00
     C*報表種類
     C           DKIND     IFNE '1'
     C           DKIND     ANDNE'2'
     C           DKIND     ANDNE'3'
     C                     MOVEL'UPT0030' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6699
     C                     END
     C*
     C   99                GOTO E1B00
     C                     MOVELDKIND     WKIND
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* CHK FUN AUT
     C           $INQ      IFNE 'Y'
     C                     MOVEL'UPT2150' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     99
     C                     END
     C   99                GOTO E1B00
     C*
     CSR         E1B00     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
     C           R1E00     BEGSR
     C*
     C           #CSR      DIV  256       #LIN             *LINE
     C                     MVR            #COL             *COLUMN
     C*財務用途
     C           #LIN      IFEQ 8
     C           #COL      ANDGE35
     C           #COL      ANDLE36
     C*
     C                     MOVEL*BLANKS   S0101I
     C                     MOVEL*BLANKS   S0102I
     C                     CALL 'WDS012'
     C                     PARM 'CKUSED'  S01I    8
     C           DHA23     PARM DHA23     S0102I 10
     C*
     C                     END
     C*廠商代號
     C           #LIN      IFEQ 12
     C           #COL      ANDGE35
     C           #COL      ANDLE40
     C*
     C                     MOVEL*BLANKS   S0101I
     C                     MOVEL*BLANKS   S0102I
     C                     MOVEL'PT#D'    S0101I
     C                     MOVE DHA02     S0101I
     C                     CALL 'CKS010'
     C                     PARM           S0101I 10
     C           HA04F     PARM HA04F     S0102I 10
     C                     PARM           S0101O 18
     C                     END
     C*
     C           #LIN      IFEQ 12
     C           #COL      ANDGE44
     C           #COL      ANDLE49
     C*
     C                     MOVEL*BLANKS   S0101I
     C                     MOVEL*BLANKS   S0102I
     C                     MOVEL'PT#D'    S0101I
     C                     MOVE DHA02     S0101I
     C                     CALL 'CKS010'
     C                     PARM           S0101I 10
     C           HA04T     PARM HA04T     S0102I 10
     C                     PARM           S0101O 18
     C                     END
     C*
     C*印表機
     C           #LIN      IFEQ 20
     C           #COL      ANDGE36
     C           #COL      ANDLE37
     C                     CALL 'P64I'
     C           $PRTCD    PARM $PRTCD    P64II1  2
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
