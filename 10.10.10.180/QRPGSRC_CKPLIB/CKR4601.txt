     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : CKR460                                     *
     H*    PROGRAM NAME :銀行資金預測表                            *
     H*    AUTHOR       : A1149    MAY                               *
     H*    CREATE DATE  : 1992.10.05                                 *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       : 票據系統                                 *
     H****************************************************************
     FPT#XPF  IF  E           K        DISK
     FCKR460D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*LOCAL DATA AREA
     ILDA        UDS
     I                                      101 110 $USER
     I                                      125 1270$CPY
     I                                      138 138 $INQ
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      209 2160$C8YMD
     I                                      201 2080$A8YMD
     I                                      219 219 $PENV
     I                                      217 218 $PRTCD
     I* PROGRAM USE DATA DEFINE ++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DHA02F
     I                                      505 506 DHA02T
     I                                      509 514 DHA16F
     I                                      515 520 DHA16T
     I                                      521 5280WHA11T
     I/COPY QRPGSRC,PT#XDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *NAMVAR   DEFN *LDA      LDA
     C*
     C           *LIKE     DEFN DHA02F    #B001I
     C*          *LIKE     DEFN DHA02     #C001I
     C*          *LIKE     DEFN DHA04F    #C002I
     C*          *LIKE     DEFN DHA02     #D001I
     C*          *LIKE     DEFN DHA04F    #D002I
     C*          *LIKE     DEFN DHA02     #E001I
     C*          *LIKE     DEFN HA13      #E002I
     C*==============================================================*
     C*          IDENTIFY A PARAMETER LIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C           *IN10     PARM *IN10     IN10    1
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEY#X     KLIST
     C                     KFLD           D#X03   2
     C                     KFLD           DHA02F
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C   10                EXSR R0200                      *INIT MSG.
     C  N10                EXSR R0100                      *INIT VALUE
     C*
     C                     MOVEL*BLANK    @PRT    1        *PRINT
     C           *IN03     DOUEQ'1'
     C           @PRT      OREQ 'Y'
     C                     EXSR R1000                      *SCRN
     C                     END
     C*
     C   03                SETON                     LR
     C                     RETRN
     C*==============================================================*
     C*          R0100 ....SCREEN INIT VALUE SET
     C*==============================================================*
     CSR         R0100     BEGSR
     C*
     C                     SETON                     1027
     C                     IN   PTDA01
     C*
     C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM $PFMT     P3104I  1
     C                     PARM $PTYPE    P3105I  1
     C           DDATE     PARM           P3101O  80
     C*
     C                     MOVEL$COR      DHA02F
     C                     MOVEL*BLANKS   DHA02N
     C*公司檔
     C                     CALL 'CKS#B0'
     C                     PARM DHA02F    #B001I
     C           DHA02N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C*
     C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     MOVEL*BLANK    DHA16F
     C                     MOVEL*BLANK    DHA16T
     C                     Z-ADD0         DHA11T
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVEL'CK'      D#X03
     C           KEY#X     CHAIN#X0                  40
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R0200 ....INITIAL VALUE
     C*==============================================================*
     CSR         R0200     BEGSR
     C*
     C                     SETON                     2799  報表格式MESSAGE
     C*
     C           $PENV     IFEQ '0'                        INQUIRY
     C                     MOVEL'UPT2142' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C           $PENV     IFEQ '1'                        ON-LINE
     C                     MOVEL'UPT2140' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C           $PENV     IFEQ '2'                        BATCH
     C                     MOVEL'UPT2110' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C                     ENDSR
     C*===============================================================*
     C*          R1000  ...SCREEN MAIN PROCESS
     C*===============================================================*
     C           R1000     BEGSR
     C*
     C* N99                SETON                     88
     C*
     C                     EXFMTDSPD1
     C                     MOVEA*ALL'0'   *IN,60
     C                     Z-ADD*ZERO     #LIN
     C                     Z-ADD*ZERO     #COL
     C*
     C           *IN03     IFEQ '1'
     C                     GOTO E1000
     C                     END
     C*
     C           *IN04     IFEQ '1'
     C                     EXSR R1E00                      .PROMPT
     C                     GOTO E1000
     C                     END
     C*
     C                     EXSR R1B00                      .CHECK      ?
     C*
     C           *IN99     IFEQ *ZERO
     C                     SETOF                     27
     C                     SETON                     28    .執行中
     C                     WRITEDSPD1
     C                     SETOF                     28
     C*
     C                     OUT  LDA
     C                     MOVEL'Y'       @PRT
     C                     END
     C*
     C           E1000     ENDSR
     C*===============================================================*
     C*          R1B00  ...CHECK DATA FROM SCREEN KEY-IN
     C*===============================================================*
     C           R1B00     BEGSR
     C*+<<DATA CHECK>>+++++++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVEL*BLANKS   DHA02N
     C           DHA02F    IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6199
     C                     END
     C   99                GOTO E1B00
     C*
     C           DHA02F    IFNE *BLANKS
     C                     CALL 'CKS#B0'
     C                     PARM DHA02F    #B001I
     C           DHA02N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C           #B099O    IFNE 'Y'
     C                     MOVEL'UPT0030' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6099
     C                     END
     C*
     C                     END
     C   99                GOTO E1B00
     C*
     C           DHA02F    IFEQ *BLANK
     C                     MOVEL*LOVAL    DHA02F
     C                     MOVEL*HIVAL    DHA02T
     C                     MOVEL*BLANKS   DHA02N
     C                     ELSE
     C                     MOVELDHA02F    DHA02T
     C                     END
     C*
     C           DHA16F    IFEQ *BLANK
     C                     MOVEL*LOVAL    DHA16F
     C                     END
     C*
     C           DHA16T    IFEQ *BLANK
     C                     MOVEL*HIVAL    DHA16T
     C                     END
     C*
     C           DHA16F    IFGT DHA16T
     C                     MOVEL'UPT0042' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6299
     C                     END
     C   99                GOTO E1B00
     C*
     C           DHA11T    IFEQ 0
     C*                    Z-ADD9999999   DHA11T
     C                     Z-ADD99999999  WHA11T
     C                     ELSE
     C*
     C                     CALL 'P30'
     C                     PARM DHA11T    P3001I  80
     C                     PARM #X101     P3002I  1
     C                     PARM #X102     P3003I  1
     C                     PARM           P3011O  1
     C*
     C           P3011O    IFEQ 'N'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6399
     C                     END
     C   99                GOTO E1B00
     C*<DATE SCREEN-->FILE>
     C                     CALL 'P31'
     C                     PARM DHA11T    P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           WHA11T    PARM           P3101O  80
     C                     END
     C*
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* CHK FUN AUT
     C           $PENV     IFNE '2'
     C           $INQ      ANDNE'Y'
     C                     MOVEL'UPT2150' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     99
     C                     END
     C   99                GOTO E1B00
     C*
     CSR         E1B00     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
     C           R1E00     BEGSR
     C*
     C           #CSR      DIV  256       #LIN             *LINE
     C                     MVR            #COL             *COLUMN
     C*銀行帳號
     C           #LIN      IFEQ 8
     C           #COL      ANDGE36
     C           #COL      ANDLE41
     C                     CALL 'CKI1D0'
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM DHA02F    P130I1  2
     C           DHA16F    PARM DHA16F    P130I2  6
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM           @RTNC   2
     C                     END
     C*
     C           #LIN      IFEQ 8
     C           #COL      ANDGE45
     C           #COL      ANDLE50
     C                     CALL 'CKI1D0'
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM DHA02F    P130I1  2
     C           DHA16T    PARM DHA16T    P130I2  6
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM           @RTNC   2
     C                     END
     C*
     C*印表機
     C           #LIN      IFEQ 20
     C           #COL      ANDGE35
     C           #COL      ANDLE36
     C                     CALL 'P64I'
     C           $PRTCD    PARM $PRTCD    P64II1  2
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
