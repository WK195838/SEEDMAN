     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM NAME :應付票據資料維護                          *
     H*    AUTHOR       : A1150                                      *
     H*    CREATE DATE  : 92/09/22                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :                                            *
     F****************************************************************
     FCKHALF01UF  E           K        DISK                      A
     FLY#SPF  UF  E           K        DISK
     FCKHELF01IF  E           K        DISK
     FCKHDLF01IF  E           K        DISK
     FCKHBLF01IF  E           K        DISK
     FCKHFLF04IF  E           K        DISK
     FPA#APF  IF  E           K        DISK
     FPT#XPF  IF  E           K        DISK
     FCKA120D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      209 2160$C8YMD
     I                                      201 2080$A8YMD
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      139 139 $RMK01
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I*DEFINE COPY KEY
     I            DS
     I                                        1  12 KCC
     I                                        1   2 DHA02C
     I                                        3  12 DHA03C
     I/COPY QRPGSRC,PT#XDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *LIKE     DEFN #X01      D#X03
     C           *LIKE     DEFN #A01      W#A01
     C           *LIKE     DEFN #A02      W#A02
     C           *LIKE     DEFN HA10      WHA10O
     C           *LIKE     DEFN HA06      WHA06
     C           *LIKE     DEFN HA07      WHA07
     C           *LIKE     DEFN HA11      WHA11
     C*
     C           *LIKE     DEFN HA02      #B001I
     C           *LIKE     DEFN HA02      #D001I
     C           *LIKE     DEFN HA04      #D002I
     C           *LIKE     DEFN HA02      #G001I
     C           *LIKE     DEFN HA04      #G002I
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYHA     KLIST                           *MAIN FILE
     C                     KFLD           DHA01   1
     C                     KFLD           DHA02
     C                     KFLD           DHA03
     C           KEYHF     KLIST                           *MAIN FILE
     C                     KFLD           DHA03
     C                     KFLD           DHA02
     C           KEYHAC    KLIST                           *COPY REC.
     C                     KFLD           DHA01
     C                     KFLD           DHA02C
     C                     KFLD           DHA03C
     C           KEY#A     KLIST
     C                     KFLD           W#A01
     C                     KFLD           W#A02
     C           KEY#X     KLIST
     C                     KFLD           D#X03
     C                     KFLD           DHA02
     C           KEYHD     KLIST
     C                     KFLD           DHA02
     C                     KFLD           DHA09N
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
  01-C           *IN03     DOWEQ'0'
  02-C           SCID      CASEQ'SC01'    R1000
    C           SCID      CASEQ'SC02'    R2000
  02-C                     END
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           DDATE     PARM           P3101O  80
|    C*
|    C                     MOVEL'SC01'    SCID    4
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*
|    C* SC01 FIELD INITIALIZE *--------------------------------------*
|    C                     Z-ADD1         DOPT
|    C*公司檔
|    C                     MOVEL$COR      DHA02
|    C                     CALL 'CKS#B0'
|    C                     PARM DHA02     #B001I
|    C           DHA02N    PARM           #B002O 16
|    C                     PARM           #B003O 34
|    C                     PARM           #B098O 50
|    C                     PARM           #B099O  1
|    C*
|    C                     MOVEL*BLANK    DHA03
|    C                     MOVEL*BLANK    DHA02C
|    C                     MOVEL*BLANK    DHA03C
|    C                     MOVEL'09'      DHA23            雜項
|    C                     MOVEL'Y'       DHA27            入帳
|    C*--------------------------------------------------------------*
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C* N99                SETON                     88
|    C*
|    C                     EXFMTDSPD1
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN18     IFEQ '1'                        *BROWSE
|   C                     EXSR R1A00
|   C                     GOTO E1000
| 02-C                     END
|    C*
|    C                     EXSR R1B00                      *SCR CHK
|    C*
| 03-C           *IN99     IFEQ '0'                        *SCR CHK OK
|   C                     EXSR R1C00                      *PRE-SC02
|   C                     MOVEL'SC02'    SCID
| 03-C                     END
|    C*
02===C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
03===C           R1A00     BEGSR
|    C*
|    C* AUT CHECK
|    C                     CALL 'S#S01'
|    C                     PARM $USER     S0101I 10
|    C                     PARM 'CKI120'  S0102I 10
|    C                     PARM           S0101O  1
| 01-C           S0101O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C   99                GOTO E1A00
|    C*
|    C* CALL SUB-PROGRAM
|    C                     CALL 'CKI120'
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C           DHA02     PARM DHA02     P000I1  2
|    C           DHA03     PARM DHA03     P000I2 10
|    C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     PARM           @RTNC   2
|    C* RETURN CODE CHECK
| 02-C           @RTNC     IFEQ '03'                       *PF3
|   C                     SETON                     03
| 02-C                     END
|    C*
03===C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
04===C           R1B00     BEGSR
|    C*
|    C* CHK FUN AUT
| 01-C           @A01,DOPT IFNE 'Y'
|   C                     MOVEL'UPT2150' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     99
| 01-C                     END
|    C   99                GOTO E1B00
|    C*
|    C* DATA CHECK *--------------------------------------------------*
|    C*公司檔
| 02-C           DHA02     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     CALL 'CKS#B0'
|    C                     PARM DHA02     #B001I
|    C           DHA02N    PARM           #B002O 16
|    C                     PARM           #B003O 34
|    C                     PARM           #B098O 50
|    C                     PARM           #B099O  1
| 03-C           #B099O    IFNE 'Y'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                       6099
| 03-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*---票據號碼
| 04-C           DHA03     IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                       6199
| 04-C                     END
|    C   99                GOTO E1B00
|    C*
|    C                     MOVE '2'       DHA01
|    C           KEYHA     CHAINHA0                  40
|    C*---新增
| 05-C           DOPT      IFEQ 1                          *ADD
|   C           *IN40     ANDEQ'0'
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 05-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*---修改，刪除，查詢
| 06-C           DOPT      IFNE 1                          *UPD,DEL,INQ
|   C           *IN40     ANDEQ'1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     606199
| 06-C                     END
|    C   99                GOTO E1B00
|    C*
|    C*---拷貝
| 07-C           DOPT      IFEQ 1                          *ADD
|   C           KCC       ANDNE*BLANK                     *COPY
|   C           KEYHAC    CHAINHA0                  40
| 08-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT0060' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 08-C                     END
| 07-C                     END
|    C   99                GOTO E1B00
|    C*修改／刪除
| 09-C           DOPT      IFEQ 2
|   C           DOPT      OREQ 4
|   C*－已作廢
| 10-C           HA17      IFEQ '0'
|   C                     MOVEL'UCK0014' ERRID
|   C                     MOVEL'CKMF'    ERRF
|   C                     SETON                     606199
| 10-C                     END
|   C   99                GOTO E1B00
|   C*－已兌現
| 11-C           HA17      IFEQ '4'
|   C                     MOVEL'UCK0008' ERRID
|   C                     MOVEL'CKMF'    ERRF
|   C                     SETON                     606199
| 11-C                     END
|   C   99                GOTO E1B00
|   C*
|   C*已產生傳票者不可修改或刪除.....
|   C* MAY UPDATE 1993.06.10
|   C*          KEYHA     SETLLHE0                  41
|   C* N41      KEYHA     READEHE0                      46
| 12-C*          *IN46     DOWEQ'0'
|   C*          *IN99     ANDEQ'0'
| 13-C*          HE07      IFNE *BLANK
|   C*                    MOVEL'UCK0001' ERRID
|   C*                    MOVEL'CKMF'    ERRF
|   C*                    SETON                     606199
|   C*                    MOVEL*BLANK    HE07
| 13-C*                    END
|   C*          KEYHA     READEHE0                      46
| 12-C*                    END
|   C*  99                GOTO E1B00
|   C*
| 14-C*          HA18      IFNE *BLANKS
|   C*                    MOVEL'UCK0001' ERRID
|   C*                    MOVEL'CKMF'    ERRF
|   C*                    SETON                     606199
| 14-C*                    END
|   C*  99                GOTO E1B00
|   C**** MAY 1993.06.10
|   C*
| 09-C                     END
|    C*
04===C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
05===C           R1C00     BEGSR
|    C*
| 01-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     SETON                     31    *CAN INPUT
|   C                     SETOF                     59
| 02-C           $RMK01    IFEQ 'Y'
|   C                     SETON                     59
| 02-C                     END
| 01*C                     ELSE                            *DEL, INQ
|   C                     SETOF                     3159  *INP PROTECT
| 01-C                     END
|    C*
| 03-C           DOPT      IFEQ 1                          *ADD
|   C           KCC       ANDEQ*BLANK                     *NO COPY
|   C                     EXSR R1C10
| 03*C                     ELSE                            *U/D/I
|   C                     EXSR R1C20
| 03-C                     END
|    C*
|    C* RELEASE FILE (ADD & INQUIRY)----------------------------------*
| 04-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 5                          *INQ
|   C           *LOVAL    SETLLHA0
| 04-C                     END
|    C*---------------------------------------------------------------*
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1C10 ....'SC02' FIELD INIT : ADD (NO COPY)
     C*==============================================================*
06===C           R1C10     BEGSR
|    C*票據狀況
|    C                     MOVEL*BLANK    W#A01
|    C                     MOVEL*BLANK    W#A02
|    C                     MOVEL*BLANK    DHA17C
|    C                     MOVEL'2'       DHA17
|    C                     MOVEL'HA17'    W#A01
|    C                     MOVELDHA17     W#A02
|    C           KEY#A     CHAIN#A0                  40
     C           *IN40     IFEQ '0'
|   C                     CALL 'P65'
|   C                     PARM #A03      P6501I 80
|   C                     PARM 10        P6502I  30
|   C           DHA17C    PARM           P6511O 80
|   C                     PARM           P6512O 80
| 02-C                     END
|    C*
|    C*
|    C                     MOVEL*BLANK    DHA23
|    C                     MOVEL*BLANK    DHA23C
|    C                     MOVEL*BLANK    DHA04
|    C                     MOVEL*BLANK    D#D03
|    C                     MOVEL*BLANK    DHA05
|    C                     MOVEL*BLANK    DHA05C
|    C                     Z-ADD0         DHA06
|    C                     Z-ADD0         DHA07
|    C                     Z-ADD0         DHA11
|    C                     MOVEL*BLANK    DHA08
|    C                     MOVEL*BLANK    HB03N
|    C                     MOVEL*BLANK    DHA09N
|    C                     MOVEL*BLANK    HD02
|    C                     Z-ADD0         DHA10
|    C                     MOVEL'N'       DHA26
|    C*新增時處理日期為SYSTEM-DATE..
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM #X101     P3104I  1
|    C                     PARM #X102     P3105I  1
|    C           DHA15     PARM           P3101O  80
|    C*                    Z-ADD0         DHA15
|    C                     MOVEL*BLANK    DHA16
|    C                     MOVEL'Y'       DHA27
|    C                     MOVEL'Y'       DHA28
|    C                     MOVEL*BLANK    DHA18
|    C                     MOVEL*BLANK    DHA51
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*  R1C20 ....'SC02' FIELD INIT : UPD, DEL, INQ, (ADD & COPY)
     C*==============================================================*
07===C           R1C20     BEGSR
|    C*廠商
|    C                     MOVELHA04      DHA04
     C           KEYHF     CHAINCKHFLF04             40
     C           *IN40     IFEQ '0'
     C                     MOVELHF77      D#D03
     C                     ELSE
|    C                     CALL 'CKS#D0'
|    C                     PARM DHA02     #D001I
|    C                     PARM DHA04     #D002I
|    C           D#D03     PARM           #D003O 18
|    C                     PARM           #D004O 42
|    C                     PARM           #D005O 62
|    C                     PARM           #D098O 50
|    C                     PARM           #D099O  1
     C                     END
|    C*票類
|    C                     MOVEL*BLANK    W#A01
|    C                     MOVEL*BLANK    W#A02
|    C                     MOVEL*BLANK    DHA05C
|    C                     MOVELHA05      DHA05
|    C                     MOVEL'CKTYPE'  W#A01
|    C                     MOVELDHA05     W#A02
|    C           KEY#A     CHAIN#A0                  40
     C           *IN40     IFEQ '0'
|   C                     CALL 'P65'
|   C                     PARM #A03      P6501I 80
|   C                     PARM 10        P6502I  30
|   C           DHA05C    PARM           P6511O 80
|   C                     PARM           P6512O 80
| 02-C                     END
|    C*
|    C*收開票日
|    C                     MOVEL'CK'      D#X03
|    C           KEY#X     CHAIN#X0                  40
|    C                     CALL 'P31'
|    C                     PARM HA06      P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM #X101     P3104I  1
|    C                     PARM #X102     P3105I  1
|    C           DHA06     PARM           P3101O  80
|    C*到期日
|    C                     CALL 'P31'
|    C                     PARM HA07      P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM #X101     P3104I  1
|    C                     PARM #X102     P3105I  1
|    C           DHA07     PARM           P3101O  80
|    C*付款銀行
|    C                     MOVEL*BLANK    HB03N
|    C                     MOVELHA08      DHA08
|    C           DHA08     CHAINHB0                  40
|    C  N40                MOVELHB03      HB03N
|    C*付款帳號
|    C                     MOVE HA09      DHA09N
|    C                     MOVE HA09      HD02
|    C*金額
|    C                     Z-ADDHA10      DHA10
|    C                     Z-ADDHA10      WHA10O
|    C*預兌日期
|    C                     CALL 'P31'
|    C                     PARM HA11      P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM #X101     P3104I  1
|    C                     PARM #X102     P3105I  1
|    C           DHA11     PARM           P3101O  80
|    C*** UPDATE 1993.08.15 BY MAY ... START
|    C***新增時處理日期應為系統日，處理帳號應為空白
|    C*處理日期
| 01-C           DOPT      IFNE 1
|   C                     CALL 'P31'
|   C                     PARM HA15      P3101I  80
|   C                     PARM '2'       P3102I  1
|   C                     PARM '1'       P3103I  1
|   C                     PARM #X101     P3104I  1
|   C                     PARM #X102     P3105I  1
|   C           DHA15     PARM           P3101O  80
|   C*處理帳號
|   C                     MOVE HA16      DHA16
| 01*C                     ELSE
|   C*
|   C                     Z-ADDDHA06     DHA15
|   C                     MOVEL*BLANKS   DHA16
| 01-C                     END
|    C*** UPDATE 1993.08.15 BY MAY ... END
|    C*票據狀況
| 02-C           DOPT      IFEQ 1
|   C                     MOVEL'2'       DHA17
| 02*C                     ELSE
|   C                     MOVELHA17      DHA17
| 02-C                     END
|    C                     MOVEL*BLANK    W#A01
|    C                     MOVEL*BLANK    W#A02
|    C                     MOVEL'HA17'    W#A01
|    C                     MOVELDHA17     W#A02
|    C                     MOVEL*BLANK    DHA17C
|    C           KEY#A     CHAIN#A0                  40
     C           *IN40     IFEQ '0'
|   C                     CALL 'P65'
|   C                     PARM #A03      P6501I 80
|   C                     PARM 6         P6502I  30
|   C           DHA17C    PARM           P6511O 80
|   C                     PARM           P6512O 80
| 02-C                     END
|    C*
|    C*來源用途
|    C                     MOVELHA23      DHA23
|    C                     MOVEL*BLANK    DHA23C
|    C                     MOVEL*BLANK    W#A01
|    C                     MOVEL*BLANK    W#A02
|    C                     MOVEL'CKUSED'  W#A01
|    C                     MOVELDHA23     W#A02
|    C           KEY#A     CHAIN#A0                  40
     C           *IN40     IFEQ '0'
|   C                     CALL 'P65'
|   C                     PARM #A03      P6501I 80
|   C                     PARM 10        P6502I  30
|   C           DHA23C    PARM           P6511O 80
|   C                     PARM           P6512O 80
| 02-C                     END
|    C*
|    C*對帳記號
|    C                     MOVELHA26      DHA26
|    C*入帳類別
|    C                     MOVELHA27      DHA27
|    C*貨款記號
|    C                     MOVELHA28      DHA28
|    C*備註
|    C                     MOVELHA32      DHA32
|    C                     MOVELHA18      DHA18
|    C                     MOVELHA51      DHA51
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
08===C           R2000     BEGSR
|    C*
|    C                     EXFMTDSPD2
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C* RELEAS FILE --------------------------------------------------*
|   C           *LOVAL    SETLLHA0
|   C           *LOVAL    SETLLHE0
|   C           *LOVAL    SETLLHD0
|   C*---------------------------------------------------------------*
|   C                     MOVEL'SC01'    SCID
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 03-C           *IN04     IFEQ '1'
|   C                     EXSR R2E00                      .PROMPT
|   C                     GOTO E2000
| 03-C                     END
|    C*
| 04-C           DOPT      IFEQ 1                          *ADD
|   C           DOPT      OREQ 2                          *UPD
|   C                     EXSR R2B00                      *SC02 CHK
| 04-C                     END
|    C*
| 05-C           *IN05     IFEQ '1'                        *CHECK ONLY
|   C                     GOTO E2000
| 05-C                     END
|    C*
| 06-C           *IN99     IFEQ '0'                        *CHK OK
|   C                     EXSR R2D00                      *FILE UPD
|   C                     MOVE 'SC01'    SCID
| 06-C                     END
|    C*
08===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....'SC02' CHECK
     C*==============================================================*
09===C           R2B00     BEGSR
|    C*---廠商代號
| 01-C           DHA04     IFEQ *BLANK
|   C                     MOVEL*BLANKS   D#D03
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 01-C                     END
|    C   99                GOTO E2B00
|    C*
| 01-C           D#D03     IFEQ *BLANK
|    C                     CALL 'CKS#D0'
|    C                     PARM DHA02     #D001I
|    C                     PARM DHA04     #D002I
|    C           D#D03     PARM           #D003O 18
|    C                     PARM           #D004O 42
|    C                     PARM           #D005O 62
|    C                     PARM           #D098O 50
|    C                     PARM           #D099O  1
| 02-C           #D099O    IFNE 'Y'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6099
| 02-C                     END
| 02-C                     END
|    C   99                GOTO E2B00
|    C*---票據類別
|    C                     MOVEL*BLANK    DHA05C
| 03-C           DHA05     IFEQ ' '
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 03-C                     END
|    C   99                GOTO E2B00
|    C*
| 04-C           DHA05     IFEQ '2'                        不可為客票
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 04-C                     END
|    C   99                GOTO E2B00
|    C*
|    C                     MOVEL*BLANK    W#A01
|    C                     MOVEL*BLANK    W#A02
|    C                     MOVEL*BLANK    DHA05C
|    C                     MOVEL'CKTYPE'  W#A01
|    C                     MOVELDHA05     W#A02
|    C           KEY#A     CHAIN#A0                  40
| 05-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6199
| 05*C                     ELSE
|   C                     CALL 'P65'
|   C                     PARM #A03      P6501I 80
|   C                     PARM 10        P6502I  30
|   C           DHA05C    PARM           P6511O 80
|   C                     PARM           P6512O 80
| 05-C                     END
|    C   99                GOTO E2B00
|    C*---開票日
| 06-C           DHA06     IFEQ 0
|   C                     MOVEL'UPT0012' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 06*C                     ELSE
|   C*
|   C                     Z-ADDDHA06     WA01    80
|   C                     MOVEL*BLANK    #X01
|   C                     MOVEL*BLANK    #X02
|   C                     MOVEL'CK'      D#X03
|   C           KEY#X     CHAIN#X0                  40
|   C*---CHECK DATE
|   C                     CALL 'P30'
|   C                     PARM WA01      P3001I  80
|   C                     PARM #X101     P3002I  1
|   C                     PARM #X102     P3003I  1
|   C                     PARM           P3011O  1
| 07-C           P3011O    IFNE 'Y'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6299
| 07-C                     END
| 06-C                     END
|    C   99                GOTO E2B00
|    C*
|    C                     Z-ADDDHA06     DHA15
|    C*---到期日
| 08-C           DHA07     IFEQ 0
|   C                     MOVEL'UPT0012' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 08-C                     END
|    C   99                GOTO E2B00
|    C*
|    C                     Z-ADDDHA07     WA02    80
|    C                     MOVEL*BLANK    #X01
|    C                     MOVEL*BLANK    #X02
|    C                     MOVEL'CK'      D#X03
|    C           KEY#X     CHAIN#X0                  40
|    C                     CALL 'P30'
|    C                     PARM WA02      P3001I  80
|    C                     PARM #X101     P3002I  1
|    C                     PARM #X102     P3003I  1
|    C                     PARM           P3011O  1
| 09-C           P3011O    IFNE 'Y'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6399
| 09-C                     END
|    C   99                GOTO E2B00
|    C*** UPDATE 1993.09.15 BY MAY .. START
|    C***開票日不可大於到期日
|    C                     CALL 'P31'
|    C                     PARM DHA06     P3101I  80
|    C                     PARM #X101     P3102I  1
|    C                     PARM #X102     P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           WHA06     PARM           P3101O  80
|    C*
|    C                     CALL 'P31'
|    C                     PARM DHA07     P3101I  80
|    C                     PARM #X101     P3102I  1
|    C                     PARM #X102     P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           WHA07     PARM           P3101O  80
|    C*
| 10-C           WHA06     IFGT WHA07
|   C                     MOVEL'UCK0019' ERRID
|   C                     MOVEL'CKMF'    ERRF
|   C                     SETON                     626399
| 10-C                     END
|    C   99                GOTO E2B00
|    C*---預兌日期
| 11-C           DHA11     IFEQ 0
|   C                     Z-ADDDHA07     DHA11
| 11-C                     END
|    C*
|    C                     Z-ADDDHA11     WA02    80
|    C                     CALL 'P30'
|    C                     PARM WA02      P3001I  80
|    C                     PARM #X101     P3002I  1
|    C                     PARM #X102     P3003I  1
|    C                     PARM           P3011O  1
| 12-C           P3011O    IFNE 'Y'
|   C                     MOVEL'UPT0040' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6599
| 12-C                     END
|    C   99                GOTO E2B00
|    C*** UPDATE 1993.09.23 BY MAY .. START
|    C***預兌日期不可小於到期日
|    C                     CALL 'P31'
|    C                     PARM DHA11     P3101I  80
|    C                     PARM #X101     P3102I  1
|    C                     PARM #X102     P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           WHA11     PARM           P3101O  80
|    C*
| 13-C           WHA11     IFLT WHA07
|   C                     MOVEL'UCK0021' ERRID
|   C                     MOVEL'CKMF'    ERRF
|   C                     SETON                     636599
| 13-C                     END
|    C   99                GOTO E2B00
|    C*---付款帳號
|    C                     MOVEL*BLANK    HD02
| 14-C           DHA09N    IFEQ *BLANK
|   C                     MOVEL'UPT0010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6499
| 14-C                     END
|    C   99                GOTO E2B00
|    C*
|    C           KEYHD     CHAINHD0                  40
|    C           *LOVAL    SETLLHD0
| 15-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6499
|   C                     MOVEL*BLANKS   DHA08
|   C                     MOVEL*BLANKS   HA16
| 15-C                     END
|    C   99                GOTO E2B00
|    C*
|    C                     MOVELHD03      DHA08
|    C                     MOVE HD02      DHA16
|    C*
|    C           DHA08     CHAINHB0                  40
|    C  N40                MOVELHB03      HB03N
|    C*---票面金額
| 16-C           DHA10     IFEQ 0
|   C                     MOVEL'UPT0012' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6699
| 16-C                     END
|    C   99                GOTO E2B00
|    C*---財務用途
|    C                     MOVEL*BLANK    DHA23C
| 17-C*          DHA23     IFEQ *BLANK
|   C*                    MOVEL'UPT0010' ERRID
|   C*                    MOVEL'PTMF'    ERRF
|   C*                    SETON                     6799
| 17-C*                    END
|    C*  99                GOTO E2B00
|    C*
| 18-C           DHA23     IFNE *BLANK
|   C                     MOVEL*BLANK    W#A01
|   C                     MOVEL*BLANK    W#A02
|   C                     MOVEL'CKUSED'  W#A01
|   C                     MOVELDHA23     W#A02
|   C           KEY#A     CHAIN#A0                  40
| 19-C           *IN40     IFEQ '1'
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     6799
| 19*C                     ELSE
|   C                     CALL 'P65'
|   C                     PARM #A03      P6501I 80
|   C                     PARM 10        P6502I  30
|   C           DHA23C    PARM           P6511O 80
|   C                     PARM           P6512O 80
| 19-C                     END
|   C   99                GOTO E2B00
| 18-C                     END
|    C*
09===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
10===C           R2D00     BEGSR
|    C*
| 01-C           DOPT      CASEQ1         R2D10
|   C           DOPT      CASEQ2         R2D20
|   C           DOPT      CASEQ4         R2D30
| 01-C                     END
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS : ADD
     C*==============================================================*
11===C           R2D10     BEGSR
|    C*WRITE CKHALF01
|    C                     EXSR R2D11
|    C                     WRITEHA0
|    C*UPDATE廠商檔
     C           HA23      IFNE '09'
     C           HA04      CHAIN#S0                  40
     C                     ADD  HA10      #S35
     C                     UPDAT#S0
     C                     END
     C*
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          R2D11 ....SCREEN FIELD TO FILE
     C*==============================================================*
12===C           R2D11     BEGSR
|    C*
|    C                     MOVEL'2'       HA01
|    C                     MOVELDHA02     HA02
|    C                     MOVELDHA03     HA03
|    C                     MOVELDHA04     HA04
|    C                     MOVELDHA05     HA05
|    C*
|    C                     CALL 'P31'
|    C                     PARM DHA06     P3101I  80
|    C                     PARM #X101     P3102I  1
|    C                     PARM #X102     P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           HA06      PARM           P3101O  80
|    C*
|    C                     CALL 'P31'
|    C                     PARM DHA07     P3101I  80
|    C                     PARM #X101     P3102I  1
|    C                     PARM #X102     P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           HA07      PARM           P3101O  80
|    C*
|    C                     MOVELDHA08     HA08
|    C                     MOVE HD02      HA09
|    C                     Z-ADDDHA10     HA10
|    C*
|    C                     CALL 'P31'
|    C                     PARM DHA11     P3101I  80
|    C                     PARM #X101     P3102I  1
|    C                     PARM #X102     P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           HA11      PARM           P3101O  80
|    C*
|    C                     MOVE *BLANK    HA12
|    C                     MOVE *BLANK    HA13
|    C                     MOVE *BLANK    HA14
|    C                     Z-ADDHA06      HA15
|    C                     MOVE HD02      HA16
|    C                     MOVELDHA17     HA17
|    C                     MOVELDHA23     HA23
|    C                     MOVEL'1'       HA24
|    C                     MOVE *BLANK    HA25
|    C                     MOVELDHA26     HA26
|    C                     MOVELDHA27     HA27
|    C                     MOVELDHA28     HA28
|    C                     MOVELDHA32     HA32
|    C*
| 01-C           DOPT      IFEQ 1
|   C                     MOVE *BLANK    HA18
|   C                     Z-ADD1         HA19
|   C                     Z-ADD1         HA20
|   C                     MOVEL*BLANKS   HA51
|   C                     MOVEL*BLANKS   HA52
|   C                     Z-ADDHA06      HA53             *開票日
|   C                     Z-ADDHA07      HA54             *到期日
|   C                     Z-ADD*ZERO     HA55
|   C                     Z-ADD*ZERO     HA56
|   C                     MOVEL*BLANKS   HA57
|   C                     MOVEL*BLANKS   HA58
|   C                     MOVEL*BLANKS   HA59
|   C                     MOVEL*BLANKS   HA60
|   C                     MOVEL*BLANKS   HA61
| 01*C                     ELSE
|   C*修改－未列印傳票
| 02-C           HA51      IFEQ *BLANKS
|   C                     Z-ADDHA06      HA53
| 02-C                     END
| 03-C           HA52      IFEQ *BLANKS
|   C                     Z-ADDHA07      HA54
| 03-C                     END
| 01-C                     END
|    C*
|    C                     TIME           HA97
|    C                     Z-ADD$A8YMD    HA98
|    C                     MOVEL$USER     HA99
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          R2D20 ....FILE PROCESS : UPD
     C*==============================================================*
13===C           R2D20     BEGSR
|    C*UPDATE CKHALF01
|    C                     EXSR R2D11
|    C                     UPDATHA0
|    C*UPDATE廠商檔
|    C           WHA10O    SUB  DHA10     DHA10M  90
|    C           DHA10     SUB  WHA10O    DHA10P  90
13===C                     ENDSR
     C*==============================================================*
     C*          R2D30 ....FILE PROCESS..DLT
     C*==============================================================*
14===C           R2D30     BEGSR
|    C*DELETE CKHALF01
|    C                     DELETHA0
|    C*UPDATE廠商檔
|    C                     Z-SUBWHA10O    DHA10M  90
|    C*UPDATE廠商檔
     C           HA04      CHAIN#S0                  40
     C                     SUB  HA10      #S35
     C                     UPDAT#S0
     C*
14===C                     ENDSR
     C*==============================================================*
     C*          R2E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
15===C           R2E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C*廠商代號
| 01-C           #LIN      IFEQ 8
|   C           #COL      ANDGE13
|   C           #COL      ANDLE22
|   C*
|   C                     MOVEL*BLANKS   S0101I
|   C                     MOVEL*BLANKS   S0102I
|   C                     MOVEL'PT#D'    S0101I
|   C                     MOVE DHA02     S0101I
|   C*
|   C                     CALL 'CKS010'
|   C                     PARM           S0101I 10
|   C           DHA04     PARM DHA04     S0102I 10
|   C           D#D03     PARM           S010O  18
| 01-C                     END
|    C*
|    C*票據類別
| 02-C           #LIN      IFEQ 9
|   C           #COL      ANDGE13
|   C           #COL      ANDLE13
|   C*
|   C                     MOVEL*BLANKS   S0101I
|   C                     MOVEL*BLANKS   S0102I
|   C*
|   C                     CALL 'CKS010'
|   C                     PARM 'CKTYPE'  S0101I 10
|   C           DHA05     PARM DHA05     S0102I 10
|   C           DHA05C    PARM           S010O  18
| 02-C                     END
|    C*
|    C*付款帳號
| 03-C           #LIN      IFEQ 11
|   C           #COL      ANDGE13
|   C           #COL      ANDLE19
|   C                     CALL 'CKI1D0'
|   C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|   C                     PARM DHA02     P130I1  2
|   C           DHA09N    PARM DHA09N    P130I2  6
|   C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|   C                     PARM           @RTNC   2
| 03-C                     END
|    C*
|    C*財務用途
| 04-C           #LIN      IFEQ 13
|   C           #COL      ANDGE13
|   C           #COL      ANDLE14
|   C*
|   C                     MOVEL*BLANKS   S0101I
|   C                     MOVEL*BLANKS   S0102I
|   C*
|   C                     CALL 'CKS010'
|   C                     PARM 'CKUSED'  S0101I 10
|   C           DHA23     PARM DHA23     S0102I 10
|   C           DHA23C    PARM           S010O  18
| 04-C                     END
|    C*
15===C                     ENDSR
     C*==============================================================*
