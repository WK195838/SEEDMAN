     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : PTI#X0                                     *
     H*    PROGRAM NAME :系統參數全頁查詢                          *
     H*    AUTHOR       : A1152   ANGY                               *
     H*    CREATE DATE  : 92/09/23                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :                                            *
     F****************************************************************
     FPT#XPF  IF  E           K        DISK
     FCKHDLF01IF  E           K        DISK
     FPTI#X0D CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      209 2160$C8YMD
     I                                      152 161 $USERN
     I                                      201 2080$A8YMD
     I*DEFIND STRAT KEY
     I            DS
     I                                        1   4 @K#XO
     I            DS
     I                                        1   4 @K#XN
     I                                        1   2 D#X01
     I                                        3   4 D#X02
     I/COPY QRPGSRC,PT#XDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAG REC NUM
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           D#X01     PARM           P130I1  2
     C           D#X02     PARM           P130I2  2
     C                     PARM           @RTNC   2          FUN KEY
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEY#X     KLIST                           *MAIN FILE
     C                     KFLD           D#X01
     C                     KFLD           D#X02
     C           KEYHD     KLIST                           *MAIN FILE
     C                     KFLD           D#X02
     C                     KFLD           #X104
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
  01-C           @RTNC     DOWEQ*BLANK
    C                     EXSR R1000                      *SCR PROCESS
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     IN   PTDA01
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           DDATE     PARM           P3101O  80
|    C*
|    C* INIT VALUE SET *---------------------------------------------*
|    C                     MOVEL*BLANK    @RTNC
|    C*--------------------------------------------------------------*
|    C                     EXSR R1A00                      .SFL INIT
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1000 ....SCREEN  PROCESS
     C*==============================================================*
02===C           R1000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     MOVEL'03'      @RTNC
|   C                     GOTO E1000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C                     MOVEL'12'      @RTNC
|   C                     GOTO E1000
| 02-C                     END
|    C*
| 03-C           *IN25     IFEQ '1'                        *NEXT PAGE
|   C                     EXSR R1B00
|   C                     GOTO E1000
| 03-C                     END
|    C*
| 04-C           @K#XO     IFNE @K#XN                      *START KEY
|   C                     EXSR R1A00                      *SFL INIT
|   C                     GOTO E1000
| 04-C                     END
|    C*
|    C                     EXSR R1C00                      *OPTION
|    C*
02===C           E1000     ENDSR
     C*=============================================================*
     C*          R1A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
03===C           R1A00     BEGSR
|    C*
|    C                     Z-ADD0         DRRN
|    C                     Z-ADD0         RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C                     MOVEL'N'       @BOTOM  1        *BOTTOM
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
|    C* <<SFL INITIAITAL DATA ADD>>
|    C                     MOVEL@K#XN     @K#XO            *KEY SAVE
|    C                     Z-ADD15        @PRCD            *PAGE REC
|    C*
|    C           KEY#X     SETLL#X0
|    C                     EXSR R1A10                      *ADD SFL DTA
|    C*
|    C*
| 01-C           RRN       IFGT 0
|   C                     SETON                     50    *SFLDSP
| 01*C                     ELSE
|   C                     SETON                     99    *NO DTA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 01-C                     END
|    C*
03===C                     ENDSR
     C*=============================================================*
     C*          R1A10 .... ADD SUBFILE DATA
     C*=============================================================*
04===C           R1A10     BEGSR
|    C*
|    C                     ADD  @PRCD     @DRCD
|01--C           RRN       DOWLT@DRCD
|   C*
|   C                     READ #X0                      46
|   C*
|01-C           *IN46     IFEQ '0'
|  C                     EXSR R1A11                      *WRITE SFL
|01*C                     ELSE                            *EOF
|  C                     Z-ADDRRN       @DRCD
|  C                     MOVE 'Y'       @BOTOM
|  C                     SETON                     9653  *SFLEND
|01-C                     END
|   C*
|01- C                     END
|    C*
|    C                     Z-ADDRRN       DRRN
|    C*
04===C                     ENDSR
     C*=============================================================*
     C*          R1A11 .... WRITE SUBFILE DATA
     C*=============================================================*
05===C           R1A11     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
     C*<<SUBFILE DATA VALUE SET>>
|    C*<GET日期格式>
|    C           #X101     IFEQ '1'
|    C                     MOVEL'00YYMMDD'D#X101
|    C                     END
|    C           #X101     IFEQ '2'
|    C                     MOVEL'YYYYMMDD'D#X101
|    C                     END
|    C           #X101     IFEQ '3'
|    C                     MOVEL'00MMDDYY'D#X101
|    C                     END
|    C           #X101     IFEQ '4'
|    C                     MOVEL'MMDDYYYY'D#X101
|    C                     END
|    C           #X101     IFEQ '5'
|    C                     MOVEL'00DDMMYY'D#X101
|    C                     END
|    C           #X101     IFEQ '6'
|    C                     MOVEL'DDMMYYYY'D#X101
|    C                     END
|    C*<GET曆別>
|    C           #X102     IFEQ '1'
|    C                     MOVEL'西曆'  D#X102
|    C                     END
|    C           #X102     IFEQ '2'
|    C                     MOVEL'國曆'  D#X102
|    C                     END
|    C*<GET現金帳戶全碼>
|    C                     MOVE *BLANK    D#X104
|    C           KEYHD     CHAINHD0                  40
|    C  N40                MOVE HD02      D#X104
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
|    C                     ADD  1         RRN
|    C                     WRITESFLSR
|    C*
05===C                     ENDSR
     C*=============================================================*
     C*          R1B00 ....ROLLUP MAIN ROUTINE
     C*=============================================================*
07===C           R1B00     BEGSR
|    C*
| 01-C           @BOTOM    IFEQ 'Y'                        *EOF
|   C                     SETON                         96*LST PAG
|   C                     Z-ADDRRN       DRRN
|   C                     ELSE
|   C                     EXSR R1A10                      *ADD SFL DTA
| 01-C                     END
|    C*
07===C                     ENDSR
     C*=============================================================*
     C*          R1C00 ....挑選資料
     C*=============================================================*
08===C           R1C00     BEGSR
|    C*
|    C                     READCSFLSR                    42
|01--C           *IN42     DOWEQ'0'
|   C*
|01-C           DOPT      IFNE *BLANK
|  C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
|  C                     MOVEL#X01      P130I1
|  C                     MOVEL#X02      P130I2
|  C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|  C                     MOVEL'00'      @RTNC
|  C                     SETON                     42
|01-C                     END
|   C*
|   C  N42                READCSFLSR                    42
|01- C                     END
|    C*
08===C                     ENDSR
