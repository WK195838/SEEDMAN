     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : CKA160                                     *
     H*    PROGRAM NAME :票據退票作業                              *
     H*    AUTHOR       : A1149   MAY                                *
     H*    CREATE DATE  : 1992.09.25                                 *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :票據系統                                  *
     H****************************************************************
     FCKHALF01UF  E           K        DISK
     FCKHELF01O   E           K        DISK                      A
     FCKHBLF01IF  E           K        DISK
     FPT#XPF  IF  E           K        DISK
     FPA#APF  IF  E           K        DISK
     FCKA160D CF  E                    WORKSTN
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      209 2160$C8YMD
     I                                      201 2080$A8YMD
     I/COPY QRPGSRC,PT#XDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *LIKE     DEFN #A01      W#A01
     C           *LIKE     DEFN #A02      W#A02
     C           *LIKE     DEFN #X01      D#X03
     C           *LIKE     DEFN HA01      DHA01
     C*
     C           *LIKE     DEFN HA02      #B001I
     C           *LIKE     DEFN HA02      #C001I
     C           *LIKE     DEFN HA04      #C002I
     C           *LIKE     DEFN HA02      #E001I
     C           *LIKE     DEFN HA13      #E002I
     C           *LIKE     DEFN HA02      #F001I
     C           *LIKE     DEFN HA04      #F002I
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYHA     KLIST
     C                     KFLD           DHA01
     C                     KFLD           DHA02
     C                     KFLD           DHA03
     C           KEYHB     KLIST
     C                     KFLD           DHA08
     C           KEY#X     KLIST
     C                     KFLD           D#X03
     C                     KFLD           DHA02
     C           KEY#A     KLIST
     C                     KFLD           W#A01
     C                     KFLD           W#A02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
     C           *IN03     DOWEQ'0'
     C           SCID      CASEQ'SC01'    R1000            *SCR1
     C           SCID      CASEQ'SC02'    R2000            *SCR2
     C                     END
     C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C                     IN   PTDA01
     C*
     C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM $PFMT     P3104I  1
     C                     PARM $PTYPE    P3105I  1
     C           DDATE     PARM           P3101O  80
     C*
     C                     MOVE 'SC01'    SCID    4
     C                     MOVE '1'       DHA01
     C* SC01 FIELD INITIALIZE *--------------------------------------*
     C*公司檔
     C                     MOVEL$COR      DHA02
     C                     CALL 'CKS#B0'
     C                     PARM DHA02     #B001I
     C           DHA02N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C*
     C                     MOVEL*BLANK    DHA03
     C*--------------------------------------------------------------*
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C* N99                SETON                     88
     C*
     C                     EXFMTDSPD1
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '1'
     C                     GOTO E1000
     C                     END
     C*
     C           *IN18     IFEQ '1'                        *BROWSE
     C                     EXSR R1A00
     C                     GOTO E1000
     C                     END
     C*
     C                     EXSR R1B00                      *SCR CHK
     C*
     C           *IN99     IFEQ '0'                        *SCR CHK OK
     C                     EXSR R1C00
     C                     MOVEL'SC02'    SCID
     C                     END
     C*
     C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....全頁查詢 (PF18 : CALL SUB-PROGRAM)
     C*==============================================================*
     C           R1A00     BEGSR
     C*
     C* AUT CHECK
     C                     CALL 'S#S01'
     C                     PARM $USER     S0101I 10
     C                     PARM 'CKI110'  S0102I 10
     C                     PARM           S0101O  1
     C           S0101O    IFEQ 'N'
     C                     SETON                     99
     C                     MOVEL'UPT2150' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C   99                GOTO E1A00
     C*
     C* CALL SUB-PROGRAM
     C                     CALL 'CKI110'
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM DHA02     P110I1  2
     C           DHA03     PARM DHA03     P110I2 10
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM           @RTNC   2
     C* RETURN CODE CHECK
     C           @RTNC     IFEQ '03'                       *PF3
     C                     SETON                     03
     C                     END
     C*
     C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
     C           R1B00     BEGSR
     C* DATA CHECK *--------------------------------------------------*
     C                     MOVE *BLANKS   DHA02N
     C           DHA02     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6199
     C                     END
     C   99                GOTO E1B00
     C*公司檔
     C*                    MOVEL$COR      DHA02
     C                     CALL 'CKS#B0'
     C                     PARM DHA02     #B001I
     C           DHA02N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C           #B099O    IFNE 'Y'
     C                     MOVEL'UPT0030' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6199
     C                     END
     C   99                GOTO E1B00
     C*
     C           DHA03     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6299
     C                     END
     C   99                GOTO E1B00
     C*
     C           KEYHA     CHAINHA0                  40
     C           *IN40     IFEQ '1'
     C                     MOVEL'UPT2010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     616299
     C                     END
     C   99                GOTO E1B00
     C*作廢
     C           HA17      IFEQ '0'
     C                     MOVEL'UCK0014' ERRID
     C                     MOVEL'CKMF'    ERRF
     C                     SETON                     616299
     C                     END
     C   99                GOTO E1B00
     C*兌現
     C           HA17      IFEQ '4'
     C                     MOVEL'UCK0008' ERRID
     C                     MOVEL'CKMF'    ERRF
     C                     SETON                     616299
     C                     END
     C   99                GOTO E1B00
     C*退票
     C           HA17      IFEQ '5'
     C                     MOVEL'UCK0009' ERRID
     C                     MOVEL'CKMF'    ERRF
     C                     SETON                     616299
     C                     END
     C   99                GOTO E1B00
     C*
     C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC02'
     C*==============================================================*
     C           R1C00     BEGSR
     C*
     C                     EXSR R1C10                      *ALL BLANKS
     C                     EXSR R1C20                      *PRE-SC02
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1C10 ....'SC02' FIELD INIT
     C*==============================================================*
     C           R1C10     BEGSR
     C*
     C                     MOVEL*BLANKS   DHA04
     C                     MOVEL*BLANK    D#C03
     C                     MOVEL*BLANKS   DHA05
     C                     MOVEL*BLANKS   DHA05C
     C                     Z-ADD*ZEROS    DHA06
     C                     Z-ADD*ZEROS    DHA07
     C                     MOVEL*BLANKS   DHA08
     C                     MOVEL*BLANKS   DHB03
     C                     MOVEL*BLANKS   DHA09
     C                     Z-ADD*ZEROS    DHA10
     C                     Z-ADD*ZEROS    DHA11
     C                     MOVEL*BLANKS   DHA16
     C                     MOVEL*BLANKS   DHA17
     C                     MOVEL*BLANKS   DHA17C
     C                     MOVEL*BLANKS   DHA13
     C                     MOVEL*BLANKS   DHA14
     C                     MOVEL*BLANKS   DHA23
     C                     MOVEL*BLANKS   DHA23C
     C                     MOVEL*BLANK    DHA24
     C                     MOVEL*BLANK    DHA26
     C                     MOVEL*BLANK    DHA27
     C                     MOVEL*BLANK    DHA28
     C*
     C                     ENDSR
     C*==============================================================*
     C*  R1C20 ....'SC02' FIELD INIT
     C*==============================================================*
     C           R1C20     BEGSR
     C*客戶
     C                     MOVELHA04      DHA04
     C                     CALL 'CKS#C0'
     C                     PARM DHA02     #C001I
     C                     PARM DHA04     #C002I
     C           D#C03     PARM           #C003O 10
     C                     PARM           #C004O 42
     C                     PARM           #C005O 10
     C                     PARM           #C098O 50
     C                     PARM           #C099O  1
     C*票類
     C                     MOVEL*BLANKS   W#A01
     C                     MOVEL*BLANKS   W#A02
     C                     MOVEL*BLANKS   DHA05C
     C                     MOVELHA05      DHA05
     C                     MOVEL'CKTYPE'  W#A01
     C                     MOVELDHA05     W#A02
     C           KEY#A     CHAIN#A0                  40    *GET #A03
     C           *IN40     IFEQ '0'
|   C                     MOVEL#A03      DHA05C
| 02-C                     END
|    C*
     C*收開票日
     C                     MOVEL'CK'      D#X03
     C           KEY#X     CHAIN#X0                  40
     C                     CALL 'P31'
     C                     PARM HA06      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHA06     PARM           P3101O  80
     C*到期日
     C                     CALL 'P31'
     C                     PARM HA07      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHA07     PARM           P3101O  80
     C*付款銀行
     C                     MOVELHA08      DHA08
     C                     MOVEL*BLANK    DHB03
     C           DHA08     CHAINHB0                  40    *GET HB03
     C  N40                MOVELHB03      DHB03
     C*帳號
     C                     MOVE HA09      DHA09
     C*金額
     C                     Z-ADDHA10      DHA10
     C*預兌日期
     C                     CALL 'P31'
     C                     PARM HA11      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHA11     PARM           P3101O  80
     C*業務人員檔
     C                     MOVELHA13      DHA13
     C                     CALL 'CKS#E0'
     C                     PARM DHA02     #E001I
     C                     PARM DHA13     #E002I
     C           D#E03     PARM           #E003O 14
     C                     PARM           #E098O 50
     C                     PARM           #E099O  1
     C*業務區
     C                     MOVELHA14      DHA14
     C*處理日期
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHA15     PARM           P3101O  80
     C*處理帳號
     C                     MOVELHA16      DHA16
     C*票據狀況
     C                     MOVEL*BLANKS   W#A01
     C                     MOVEL*BLANKS   W#A02
     C                     MOVEL*BLANKS   DHA17C
     C                     MOVELHA17      DHA17
     C                     MOVEL'CKSTS'   W#A01
     C                     MOVELDHA17     W#A02
     C           KEY#A     CHAIN#A0                  40    *GET #A03
     C           *IN40     IFEQ '0'
|   C                     MOVEL#A03      DHA17C
| 02-C                     END
|    C*
     C*來源用途
     C                     MOVEL*BLANK    W#A01
     C                     MOVEL*BLANK    W#A02
     C                     MOVEL*BLANK    DHA23C
     C                     MOVELHA23      DHA23
     C                     MOVEL'CKUSED'  W#A01
     C                     MOVELDHA23     W#A02
     C           KEY#A     CHAIN#A0                  40
     C           *IN40     IFEQ '0'
|   C                     MOVEL#A03      DHA23C
| 02-C                     END
|    C*
     C*票據區
     C                     MOVELHA24      DHA24
     C*對帳記號
     C                     MOVELHA26      DHA26
     C*入帳類別
     C                     MOVELHA27      DHA27
     C*貨款記號
     C                     MOVELHA28      DHA28
     C*備註
     C                     MOVELHA32      DHA32
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           R2000     BEGSR
     C*
     C*
     C                     EXFMTDSPD2
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '1'                        *END
     C                     GOTO E2000
     C                     END
     C*
     C           *IN12     IFEQ '1'                        *PREVIOUS
     C* RELEAS FILE --------------------------------------------------*
     C           *LOVAL    SETLLHA0
     C*---------------------------------------------------------------*
     C                     MOVEL'SC01'    SCID
     C                     GOTO E2000
     C                     END
     C*
     C                     EXSR R2B00
     C*
     C           *IN99     IFEQ '0'                        *CHK OK
     C                     EXSR R2D00                      *FILE UPD
     C                     MOVE 'SC01'    SCID
     C                     END
     C*
     C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....'SC02' CHECK
     C*==============================================================*
     C           R2B00     BEGSR
     C*
     C                     MOVEL'CK'      D#X03
     C           KEY#X     CHAIN#X0                  40
     C                     CALL 'P30'
     C           DHA15     PARM DHA15     P3001I  80
     C                     PARM #X101     P3002I  1
     C                     PARM #X102     P3003I  1
     C                     PARM           P3011O  1
     C*
     C           P3011O    IFEQ 'N'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6199
     C                     END
     C   99                GOTO E2B00
     C*
     C*
     C           E2B00     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
     C           R2D00     BEGSR
     C*<UPDATE客戶檔>
     C*                    Z-SUBHA10      DHA10M  90
     C*                    CALL 'CKS#F0'
     C*                    PARM DHA02     #F001I           公司別
     C*                    PARM DHA04     #F002I           客戶代號
     C*                    PARM HA10      #F003I  90       應收帳款
     C*                    PARM DHA10M    #F004I  90       應收票據
     C*                    PARM DHA10M    #F005I  90       已收未沖
     C*                    PARM 0         #F006I  90       信用額度
     C*                    PARM           #F098O  90       備用
     C*                    PARM           #F099O  1        成功否
     C*<UPDATE CKHALF01>
     C           KEYHA     CHAINHA0                  40
     C           *IN40     IFEQ '0'
     C                     CALL 'P31'
     C                     PARM DHA15     P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           HA15      PARM           P3101O  80
     C                     MOVEL'5'       HA17
     C                     TIME           HA97
     C                     Z-ADD$A8YMD    HA98
     C                     MOVEL$USER     HA99
     C                     UPDATHA0
     C                     END
     C*<UPDATE CKHELF01>
     C                     MOVE HA01      HE01
     C                     MOVE HA02      HE02
     C                     MOVE HA03      HE03
     C                     MOVE HA16      HE04
     C                     MOVE HA15      HE05
     C                     MOVE '5'       HE06
     C                     MOVE *BLANKS   HE07
     C                     MOVE DHA17     HE08
     C                     TIME           HE97
     C                     Z-ADD$A8YMD    HE98
     C                     MOVEL$USER     HE99
     C                     WRITEHE0
     C*
     C                     ENDSR
