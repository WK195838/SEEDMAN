     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : CKR4I0                                     *
     H*    PROGRAM NAME :列印票據資料－變動                        *
     H*    AUTHOR       : A1162                                      *
     H*    CREATE DATE  : 84/02/08                                   *
     H*    SYSTEM       :                                            *
     H****************************************************************
     FPT#XPF  IF  E           K        DISK
     FCKHDLF01IF  E           K        DISK
     FCKHIPF  IF  E           K        DISK
     FCKR4I0D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*LOCAL DATA AREA
     ILDA        UDS
     I                                      101 110 $USER
     I                                      125 1270$CPY
     I                                      138 138 $INQ
     I                                      141 141 $RMK03
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      201 2080$A8YMD
     I                                      217 218 $PRTCD
     I                                      219 219 $PENV
     I* PROGRAM USE DATA DEFINE ++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DHF02
     I                                      505 510 DHF16
     I                                      511 5180WHF06F
     I                                      519 5260WHF06T
     I                                      527 5300DHF71F
     I                                      531 5340DHF71T
     I                                      535 535 DHF76
     I                                      536 536 DTYPE
     I                                      537 537 DTEST
     I                                      538 538 DHI15
     I                                      539 539 DHI13
     I                                      540 542 DHI03
     I/COPY QRPGSRC,PT#XDS01
     C*==============================================================*
     C*          IDENTIFY A PARAMETER LIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C           *IN10     PARM *IN10     IN10    1
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *NAMVAR   DEFN *LDA      LDA
     C           *LIKE     DEFN #X01      W#X01
     C*
     C           *LIKE     DEFN DHF02     #B001I
     C*=============================================================
     C*                    KEY LIST
     C*=========================================================
     C           KEY#X     KLIST
     C                     KFLD           W#X01
     C                     KFLD           DHF02
     C           KEYHD     KLIST
     C                     KFLD           DHF02
     C                     KFLD           DHF16
     C           KEYHI     KLIST
     C                     KFLD           DHF02
     C                     KFLD           DHF76
     C                     KFLD           HI03
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C   10                EXSR R0200                      *INIT MSG.
     C  N10                EXSR R0100                      *INIT VALUE
     C*
     C                     MOVEL*BLANK    @PRT    1        *PRINT
     C           *IN03     DOUEQ'1'
     C           @PRT      OREQ 'Y'
     C                     EXSR R1000                      *SCRN
     C                     END
     C*
     C   03                SETON                     LR
     C                     RETRN
     C*==============================================================*
     C*          R0100 ....SCREEN INIT VALUE SET
     C*==============================================================*
     CSR         R0100     BEGSR
     C*
     C                     SETON                     1027
     C                     IN   PTDA01
     C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM $PFMT     P3104I  1
     C                     PARM $PTYPE    P3105I  1
     C           DDATE     PARM           P3101O  80
     C*
     C                     MOVEL$COR      DHF02
     C*公司檔
     C                     CALL 'CKS#B0'
     C                     PARM DHF02     #B001I
     C           DHF02N    PARM           #B002O 22
     C                     PARM           #B003O 32
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C*
     C                     MOVEL*BLANK    DHF16
     C                     Z-ADD*ZERO     DHF06F
     C                     Z-ADD*ZERO     DHF06T
     C                     Z-ADD*ZERO     DHF71F
     C                     Z-ADD*ZERO     DHF71T
     C                     MOVEL'3'       DHF76
     C                     MOVEL'1'       DTYPE
| 03-C           $RMK03    IFNE 'Y'                        *LILY
|   C                     SETOF                     32    *LILY-PR
     C                     ELSE
     C                     SETON                     32    *LILY-UL
| 03-C                     END                             *LILY
     C*
     C                     MOVEL'N'       DTEST
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R0200 ....INITIAL VALUE
     C*==============================================================*
     CSR         R0200     BEGSR
     C*
     C                     SETON                     2799  報表格式MESSAGE
     C*
     C           $PENV     IFEQ '0'                        INQUIRY
     C                     MOVEL'UPT2142' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C           $PENV     IFEQ '1'                        ON-LINE
     C                     MOVEL'UPT2140' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C           $PENV     IFEQ '2'                        BATCH
     C                     MOVEL'UPT2110' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C                     ENDSR
     C*===============================================================*
     C*          R1000  ...SCREEN MAIN PROCESS
     C*===============================================================*
     C           R1000     BEGSR
     C*
     C  N99                SETON                     88
     C*
     C                     EXFMTDSPD1
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '1'
     C                     GOTO E1000
     C                     END
     C*
     C           *IN04     IFEQ '1'
     C                     EXSR R1E00                      .PROMPT
     C                     GOTO E1000
     C                     END
     C*
     C           *IN08     IFEQ '1'
     C                     CALL 'CKR4I03C'                 .PROMPT
     C                     GOTO E1000
     C                     END
     C*
     C                     EXSR R1B00                      .CHECK      ?
     C*
     C           *IN99     IFEQ *ZERO
     C                     SETOF                     27
     C                     SETON                     28    .執行中
     C                     WRITEDSPD1
     C                     SETOF                     28
     C*
     C                     OUT  LDA
     C                     MOVEL'Y'       @PRT
     C                     END
     C*
     C           E1000     ENDSR
     C*===============================================================*
     C*          R1B00  ...CHECK DATA FROM SCREEN KEY-IN
     C*===============================================================*
     C           R1B00     BEGSR
     C*
     C*+<<DATA CHECK>>+++++++++++++++++++++++++++++++++++++++++++++++++
     C*---公司別
     C                     MOVEL*BLANK    DHF02N
     C           DHF02     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6199
     C                     END
     C   99                GOTO E1B00
     C*
     C                     CALL 'CKS#B0'
     C                     PARM DHF02     #B001I
     C           DHF02N    PARM           #B002O 22
     C                     PARM           #B003O 32
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C           #B099O    IFNE 'Y'
     C                     MOVEL'UPT2010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6199
     C                     END
     C   99                GOTO E1B00
     C*---銀行帳號
     C           DHF16     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6299
     C                     END
     C   99                GOTO E1B00
     C*
     C           KEYHD     CHAINHD0                  40
     C           *IN40     IFEQ '1'
     C                     MOVEL'UPT2010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6299
     C                     END
     C   99                GOTO E1B00
     C*
     C*---開票日期－起
     C                     MOVEL'CK'      W#X01
     C           KEY#X     CHAIN#X0                  40
     C*
     C           DHF06F    IFEQ *ZERO
     C                     Z-ADD010101    DHF06F
     C                     Z-ADD010101    WHF06F
     C                     ELSE
     C                     CALL 'P30'
     C                     PARM DHF06F    P3001I  80
     C                     PARM #X101     P3002I  1
     C                     PARM #X102     P3003I  1
     C                     PARM           P3011O  1
     C           P3011O    IFNE 'Y'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6399
     C                     ELSE
     C                     CALL 'P31'
     C                     PARM DHF06F    P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           WHF06F    PARM           P3101O  80
     C                     END
     C                     END
     C   99                GOTO E1B00
     C*---開票日期－迄
     C           DHF06T    IFEQ *ZERO
     C                     Z-ADD99991231  WHF06T
     C                     CALL 'P31'
     C                     PARM WHF06T    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHF06T    PARM           P3101O  80
     C                     ELSE
     C                     MOVEL*BLANK    CHECK   1
     C                     CALL 'P30'
     C                     PARM DHF06T    P3001I  80
     C                     PARM #X101     P3002I  1
     C                     PARM #X102     P3003I  1
     C           CHECK     PARM           P3011O  1
     C           CHECK     IFNE 'Y'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6499
     C                     ELSE
     C                     CALL 'P31'
     C                     PARM DHF06T    P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           WHF06T    PARM           P3101O  80
     C                     END
     C                     END
     C   99                GOTO E1B00
     C*---RANGE CHECK
     C           WHF06F    IFGT WHF06T
     C                     MOVEL'UPT0041' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     636499
     C                     END
     C   99                GOTO E1B00
     C*---開票序號
     C           DHF71F    IFEQ *ZERO
     C                     Z-ADD1         DHF71F
     C                     END
     C           DHF71T    IFEQ *ZERO
     C                     Z-ADD9999      DHF71T
     C                     END
     C*---RANGE CHECK
     C           DHF71F    IFGT DHF71T
     C                     MOVEL'UPT0041' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6599
     C                     END
     C   99                GOTO E1B00
     C*
     C                     MOVELHD21      HI03
     C           DHF76     IFEQ '1'
     C                     MOVEL'HD22'    HI03
     C                     END
     C                     MOVEL*BLANK    HI13
     C                     MOVEL*BLANK    HI15
     C           KEYHI     CHAINHI0                  40
     C                     MOVELHI03      DHI03
     C                     MOVELHI13      DHI13
     C                     MOVELHI15      DHI15
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* CHK FUN AUT
     C           $PENV     IFNE '2'
     C           $INQ      ANDNE'Y'
     C                     MOVEL'UPT2150' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     99
     C                     END
     C   99                GOTO E1B00
     C*
     CSR         E1B00     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
     C           R1E00     BEGSR
     C*
     C           #CSR      DIV  256       #LIN             *LINE
     C                     MVR            #COL             *COLUMN
     C*銀行帳號
     C           #LIN      IFEQ 7
     C           #COL      ANDGE36
     C           #COL      ANDLE41
     C                     CALL 'CKI1D0'
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM DHF02     P130I1  2
     C           DHF16     PARM DHF16     P130I2  6
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM           @RTNC   2
     C                     END
     C*印表機
     C           #LIN      IFEQ 20
     C           #COL      ANDGE36
     C           #COL      ANDLE37
     C                     CALL 'P64I'
     C           $PRTCD    PARM $PRTCD    P64II1  2
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
