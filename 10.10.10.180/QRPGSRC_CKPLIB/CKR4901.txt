     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : CKR490                                     *
     H*    PROGRAM NAME :票齡分析表                                *
     H*    AUTHOR       : A1087  JOYCE                               *
     H*    CREATE DATE  : 1992.10.06                                 *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :票據系統                                  *
     H****************************************************************
     FPA#APF  IF  E           K        DISK
     FCKR490D CF  E                    WORKSTN      KINFDS DSPFDS
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*LOCAL DATA AREA
     ILDA        UDS
     I                                      101 110 $USER
     I                                      111 1160$CHYMD
     I                                      201 2080$A8YMD
     I                                      125 1270$CPY
     I                                      138 138 $INQ
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      209 2160$C8YMD
     I                                      219 219 $PENV
     I                                      217 218 $PRTCD
     I* PROGRAM USE DATA DEFINE ++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DHA02
     I                                      505 505 DHA01
     I                                      506 511 DHA16F
     I                                      512 517 DHA16T
     I                                      518 518 DTYPE1
     I                                      519 519 DTYPE2
     T** SET RANGE FIELD
     I                                      520 521 WHA02F
     I                                      524 525 WHA02T
     I                                      528 528 WHA01F
     I                                      529 529 WHA01T
     I                                      530 530 WTP2F
     I                                      531 531 WTP2T
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *NAMVAR   DEFN *LDA      LDA
     C*
     C           *LIKE     DEFN DHA02     #B001I
     C*          *LIKE     DEFN DHA02     #C001I
     C*          *LIKE     DEFN DHA04F    #C002I
     C*          *LIKE     DEFN DHA02     #D001I
     C*          *LIKE     DEFN DHA04F    #D002I
     C*          *LIKE     DEFN DHA02     #E001I
     C*          *LIKE     DEFN HA13      #E002I
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEY#A     KLIST
     C                     KFLD           #A01
     C                     KFLD           #A02
     C*==============================================================*
     C*          IDENTIFY A PARAMETER LIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C           *IN10     PARM *IN10     IN10    1
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C   10                EXSR R0200                      *INIT MSG.
     C  N10                EXSR R0100                      *INIT VALUE
     C*
     C                     MOVEL*BLANK    @PRT    1        *PRINT
     C           *IN03     DOUEQ'1'
     C           @PRT      OREQ 'Y'
     C                     EXSR R1000                      *SCRN
     C                     END
     C*
     C   03                SETON                     LR
     C                     RETRN
     C*==============================================================*
     C*          R0100 ....SCREEN INIT VALUE SET
     C*==============================================================*
     CSR         R0100     BEGSR
     C*
     C                     SETON                     1027
     C                     IN   PTDA01
     C*
     C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM $PFMT     P3104I  1
     C                     PARM $PTYPE    P3105I  1
     C           DDATE     PARM           P3101O  80
     C*
     C                     MOVEL$COR      DHA02
     C                     MOVEL*BLANKS   DHA02N
     C*公司檔
     C                     CALL 'CKS#B0'
     C                     PARM DHA02     #B001I
     C           DHA02N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C                     MOVEL*BLANK    DHA01
     C                     MOVEL*BLANK    DHA16F
     C                     MOVEL*BLANK    DHA16T
     C                     MOVEL'Y'       DTYPE1
     C                     MOVEL*BLANK    DTYPE2
     C                     MOVEL*BLANK    DTYPEC
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R0200 ....INITIAL VALUE
     C*==============================================================*
     CSR         R0200     BEGSR
     C*
     C                     SETON                     2799  報表格式MESSAGE
     C*
     C           $PENV     IFEQ '0'                        INQUIRY
     C                     MOVEL'UPT2142' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C           $PENV     IFEQ '1'                        ON-LINE
     C                     MOVEL'UPT2140' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C           $PENV     IFEQ '2'                        BATCH
     C                     MOVEL'UPT2110' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C                     ENDSR
     C*===============================================================*
     C*          R1000  ...SCREEN MAIN PROCESS
     C*===============================================================*
     C           R1000     BEGSR
     C*
     C* N99                SETON                     88
     C*
     C                     EXFMTDSPD1
     C                     MOVEA*ALL'0'   *IN,60
     C                     Z-ADD*ZERO     #LIN
     C                     Z-ADD*ZERO     #COL
     C*
     C           *IN03     IFEQ '1'
     C                     GOTO E1000
     C                     END
     C*
     C           *IN04     IFEQ '1'
     C                     EXSR R1E00                      .PROMPT
     C                     GOTO E1000
     C                     END
     C*
     C                     EXSR R1B00                      .CHECK      ?
     C*
     C           *IN99     IFEQ *ZERO
     C                     SETOF                     27
     C                     SETON                     28    .執行中
     C                     WRITEDSPD1
     C                     SETOF                     28
     C*
     C                     OUT  LDA
     C                     MOVEL'Y'       @PRT
     C                     END
     C*
     C           E1000     ENDSR
     C*===============================================================*
     C*          R1B00  ...CHECK DATA FROM SCREEN KEY-IN
     C*===============================================================*
     C           R1B00     BEGSR
     C*
     C*+<<DATA CHECK>>+++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     MOVE *BLANK    DHA02N
     C* CHK 公司別
     C           DHA02     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6099
     C                     END
     C   99                GOTO E1B00
     C*公司檔
     C           DHA02     IFNE *BLANK
     C                     CALL 'CKS#B0'
     C                     PARM DHA02     #B001I
     C           DHA02N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C           #B099O    IFNE 'Y'
     C                     MOVEL'UPT0030' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6099
     C                     END
     C                     END
     C   99                GOTO E1B00
     C*
     C* CHK 類別
     C           DHA01     IFNE *BLANK
     C           DHA01     IFNE '1'
     C           DHA01     ANDNE'2'
     C                     MOVEL'UPT0030' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6199
     C                     END
     C                     END
     C   99                GOTO E1B00
     C*
     C* SET 存在銀行帳號區間
     C           DHA16F    IFEQ *BLANK
     C                     MOVE *LOVAL    DHA16F
     C                     END
     C           DHA16T    IFEQ *BLANK
     C                     MOVE *HIVAL    DHA16T
     C                     END
     C*
     C* CHK 銀行帳號起迄
     C           DHA16F    IFGT DHA16T
     C                     MOVEL'UPT0042' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     626399
     C                     END
     C   99                GOTO E1B00
     C*
     C* CHK 帳戶種類
     C           DTYPE2    IFNE *BLANK
     C                     MOVEL'ACCTYPE' #A01
     C                     MOVE *BLANK    #A02
     C                     MOVELDTYPE2    #A02
     C                     MOVE *BLANK    DTYPEC
     C           KEY#A     CHAIN#A0                  40
     C           *IN40     IFEQ '1'
     C                     MOVEL'UPT2010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6599
     C                     END
     C                     END
     C   99                GOTO E1B00
|   C                     CALL 'P65'
|   C                     PARM #A03      P6501I 80
|   C                     PARM 14        P6502I  30
|   C           DTYPEC    PARM           P6511O 80
|   C                     PARM           P6512O 80
     C*
     C* SET 存在公司別區間
     C           DHA02     IFEQ *BLANK
     C                     MOVE *LOVAL    WHA02F
     C                     MOVE *HIVAL    WHA02T
     C                     ELSE
     C                     MOVELDHA02     WHA02F
     C                     MOVELDHA02     WHA02T
     C                     END
     C*
     C* SET 存在類別區間
     C           DHA01     IFEQ *BLANK
     C                     MOVE *LOVAL    WHA01F
     C                     MOVE *HIVAL    WHA01T
     C                     ELSE
     C                     MOVELDHA01     WHA01F
     C                     MOVELDHA01     WHA01T
     C                     END
     C*
     C* SET 存在帳戶種類區間
     C           DTYPE2    IFEQ *BLANK
     C                     MOVE *LOVAL    WTP2F
     C                     MOVE *HIVAL    WTP2T
     C                     ELSE
     C                     MOVELDTYPE2    WTP2F
     C                     MOVELDTYPE2    WTP2T
     C                     END
     C*
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* CHK FUN AUT
     C           $PENV     IFNE '2'
     C           $INQ      ANDNE'Y'
     C                     MOVEL'UPT2150' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     99
     C                     END
     C   99                GOTO E1B00
     C*
     CSR         E1B00     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
     C           R1E00     BEGSR
     C*
     C           #CSR      DIV  256       #LIN             *LINE
     C                     MVR            #COL             *COLUMN
     C*銀行帳號
     C           #LIN      IFEQ 10
     C           #COL      ANDGE36
     C           #COL      ANDLE41
     C                     CALL 'CKI1D0'
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM DHA02     P130I1  2
     C           DHA16F    PARM DHA16F    P130I2  6
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM           @RTNC   2
     C                     END
     C*
     C           #LIN      IFEQ 10
     C           #COL      ANDGE45
     C           #COL      ANDLE50
     C                     CALL 'CKI1D0'
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM DHA02     P130I1  2
     C           DHA16T    PARM DHA16T    P130I2  6
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM           @RTNC   2
     C                     END
     C*
     C*帳戶種類
     C           #LIN      IFEQ 14
     C           #COL      ANDGE36
     C           #COL      ANDLE36
     C*
     C                     MOVEL*BLANKS   S0101I 10
     C                     MOVEL*BLANKS   S0102I
     C*
     C                     CALL 'WDS012'
     C                     PARM 'ACCTYPE' S01I    8
     C           DTYPE2    PARM DTYPE2    S0102I 10
     C                     END
     C*印表機
     C           #LIN      IFEQ 20
     C           #COL      ANDGE36
     C           #COL      ANDLE37
     C                     CALL 'P64I'
     C           $PRTCD    PARM $PRTCD    P64II1  2
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
