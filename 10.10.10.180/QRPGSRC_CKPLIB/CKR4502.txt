     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : CKR450                                     *
     H*    PROGRAM NAME :票據兌現核對表                            *
     H*    AUTHOR       : A1152   ANGY                               *
     H*    CREATE DATE  : 81/10/01                                   *
     H*    UPDATE DATE  : 81/10/06                                   *
     H*    SYSTEM       :                                            *
     H*==============================================================*
     H* CODE | AUTHOR |  DATE  | DESCRIPTION
     H*--------------------------------------------------------------
     H* M001 |MICHELLE|07/04/05| OFOE擷取完整
     H*--------------------------------------------------------------
     H****************************************************************
     FCKHAPF  IP  E           K        DISK
     FPT#XPF  IF  E           K        DISK
     FCKHBLF01IF  E           K        DISK
     FCKHDLF01IF  E           K        DISK
     FCKHFLF04IF  E           K        DISK
     FCKR450P O   E             39     PRINTER
     E*---------------------------------------------------------------
     E                    PHA01C  1   2 10
     E*--------------------------------------------------------------
     I* KEY BREAK DEFINE
     IHA0
     I                                              HA15  L1
     I                                              HA16  L2
     I                                              HA02  L3
     I                                              HA01  L3
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I* LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      125 1270$CPY
     I                                      138 138 $INQ
     I                                      173 176 $COR
     I                                      209 2160$C8YMD
     I                                      201 2080$A8YMD
     I                                      219 219 $PENV
     I                                      220 229 $PRTNM
     I* PROGRAM USE DATA DEFINE ++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DHA02F
     I                                      505 506 DHA02T
     I                                      509 509 DHA01F
     I                                      510 510 DHA01T
     I                                      523 5300HA15F
     I                                      531 5380HA15T
     I                                      539 539 HA27F
     I                                      540 540 HA27T
     I                                      622 622 DHA59F
     I                                      623 623 DHA59T
     I            DS
     I                                        1  14 HA16
     I                                        9  14 HA161
     I/COPY QRPGSRC,PT#XDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *LIKE     DEFN #X01      D#X03
     C*
     C           *LIKE     DEFN HA02      #B001I
     C           *LIKE     DEFN HA02      #C001I
     C           *LIKE     DEFN HA04      #C002I
     C           *LIKE     DEFN HA02      #D001I
     C           *LIKE     DEFN HA04      #D002I
     C           *LIKE     DEFN HA02      #E001I
     C           *LIKE     DEFN HA13      #E002I
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYHD3    KLIST
     C                     KFLD           HA02
     C                     KFLD           HA161
     C           KEYHF     KLIST
     C                     KFLD           HF03
     C                     KFLD           HF02
     C           KEY#X3    KLIST                           .CHAIN #X0
     C                     KFLD           D#X03
     C                     KFLD           DHA02F
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR R1000
     C*+ LEVEL BREAK INIT PROCESS +++++++++++++++++++++++++++++++++++
     C   L3                EXSR R7300
     C   L2                EXSR R7200
     C   L1                EXSR R7100
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C*
     C                     EXSR R2000
     C*
     C*+ LEVEL BREAK PROCESS ++++++++++++++++++++++++++++++++++++++++
     CL1 10                EXSR R8000
     CL2 10                EXSR R8100
     CL3 10                EXSR R8900
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     CLRN10                EXSR R8999
     C*==============================================================*
     C*          R1000 ... INITIAL PROCESS
     C*==============================================================*
01===C           R1000     BEGSR
|    C*
|    C                     SETON                     1039
|    C                     IN   PTDA01
|    C*
|    C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
|    C                     CALL 'P31'
|    C                     PARM $A8YMD    P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM $PFMT     P3104I  1
|    C                     PARM $PTYPE    P3105I  1
|    C           PDATE     PARM           P3101O  80
|    C*
|    C*GET表頭公司別++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     MOVEL'CK'      D#X03
|    C           KEY#X3    CHAIN#X0                  40
|    C                     CALL 'P31'
|    C                     PARM HA15F     P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM #X101     P3104I  1
|    C                     PARM #X102     P3105I  1
|    C           PHA15F    PARM           P3101O  80
|    C*
|    C                     CALL 'P31'
|    C                     PARM HA15T     P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM #X101     P3104I  1
|    C                     PARM #X102     P3105I  1
|    C           PHA15T    PARM           P3101O  80
|    C*
| 01-C           DHA59F    IFEQ '1'
|   C                     MOVE '北'    PHA59T
| 01*C                     ELSE
| 02-C           DHA59F    IFEQ '2'
|   C                     MOVE '中'    PHA59T
| 02*C                     ELSE
| 03-C           DHA59F    IFEQ '3'
|   C                     MOVE '南'    PHA59T
| 03*C                     ELSE
|   C                     MOVE '全'    PHA59T
| 03-C                     END
| 02-C                     END
| 01-C                     END
| 04-C           HA27F     IFEQ *LOVAL
|   C                     MOVE '全'    PHA27
| 04*C                     ELSE
| 05-C           HA27F     IFEQ 'Y'
|   C                     MOVE '是'    PHA27
| 05*C                     ELSE
|   C                     MOVE '否'    PHA27
| 05-C                     END
| 04-C                     END
|    C*
|    C*CLEAR TOTAL FIELD
|    C                     Z-ADD*ZERO     PT11
|    C                     Z-ADD*ZERO     PT21
|    C                     Z-ADD*ZERO     PT31
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R2000 ... DETAIL
     C*==============================================================*
02===C           R2000     BEGSR
|    C*
|    C*+<<DATA SET>>+++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     MOVEL*BLANK    P#04
| 01-C           HA01      IFEQ '1'
|   C*客戶檔
|   C                     CALL 'CKS#C0'
|   C                     PARM HA02      #C001I
|   C                     PARM HA04      #C002I
|   C           P#04      PARM           #C003O 10
|   C                     PARM           #C004O 42
|   C                     PARM           #C005O 10
|   C                     PARM           #C098O 50
|   C                     PARM           #C099O  1
M001AC                     CALL 'GLS921'
M001AC                     PARM P#04      G1     10
M001AC           P#04      PARM           G2     10
| 01-C                     END
|    C*
| 02-C           HA01      IFEQ '2'
|   C*廠商檔
|   C           KEYHF     CHAINCKHFLF04             40
| 03-C           *IN40     IFEQ '0'
|   C                     MOVELHF77      #D003O
| 03*C                     ELSE
|   C                     CALL 'CKS#D0'
|   C                     PARM HA02      #D001I
|   C                     PARM HA04      #D002I
|   C                     PARM           #D003O 18
|   C                     PARM           #D004O 42
|   C                     PARM           #D005O 62
|   C                     PARM           #D098O 50
|   C                     PARM           #D099O  1
| 03-C                     END
|   C*
|   C                     CALL 'P65'
|   C                     PARM #D003O    P6501I 80
|   C                     PARM 10        P6502I  30
|   C           P#04      PARM           P6511O 80
|   C                     PARM           P6512O 80
M001AC                     CALL 'GLS921'
M001AC                     PARM P#04      G1     10
M001AC           P#04      PARM           G2     10
| 02-C                     END
     C*
|    C                     CALL 'P31'
|    C                     PARM HA07      P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM #X101     P3104I  1
|    C                     PARM #X102     P3105I  1
|    C           PHA07     PARM           P3101O  80
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
| 04-C           *IN39     IFEQ '1'
|   C                     WRITEPH1
|   C                     SETOF                     39
|   C* BREAK CONTROL FOR FIELD PRINTED +++++++++++++++++++++++++++++++
|   C                     SETON                     3132
|   C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
| 04-C                     END
|    C*
|    C                     WRITEPD1
|    C* BREAK CONTROL FOR FIELD PRINTED +++++++++++++++++++++++++++++++
|    C                     SETOF                     3132
|    C* ACCUMULATE TOTAL
|    C                     ADD  HA10      PT11
|    C                     ADD  HA10      PT31
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
02===C                     ENDSR
     C*==============================================================*
     C*          R7300 ... LEVEL BREAK 3 :INIT PROCESS
     C*==============================================================*
03===C           R7300     BEGSR
|    C*
|    C                     MOVEL*BLANK    PCONAM
|    C                     CALL 'CKS#B0'
|    C                     PARM HA02      #B001I
|    C                     PARM           #B002O 16
|    C           PCONAM    PARM           #B003O 34
|    C                     PARM           #B098O 50
|    C                     PARM           #B099O  1
|    C*
|    C                     MOVELHA02      PHA02
|    C*
| 01-C           HA01      IFEQ '1'
|   C                     MOVEL'應收'  P#A03
| 01*C                     ELSE
|   C                     MOVEL'應付'  P#A03
| 01-C                     END
|    C*
|    C*
|    C*
| 02-C           HA01      IFEQ '1'
|   C                     MOVELPHA01C,1  HA01C
| 02-C                     END
| 03-C           HA01      IFEQ '2'
|   C                     MOVELPHA01C,2  HA01C
| 03-C                     END
|    C*
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << IF THIS KEY BREAK SKIP PAGE THEN SETON >>------------------
|    C                     SETON                     39
|    C*---------------------------------------------------------------
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          R7200 ... LEVEL BREAK 2 :INIT PROCESS
     C*==============================================================*
04===C           R7200     BEGSR
|    C*
|    C           KEYHD3    CHAINHD0                  40
|    C     N40   HD03      CHAINHB0                  40
| 01-C           *IN40     IFEQ '0'
|   C                     MOVELHB02      PHB02
| 01*C                     ELSE
|   C                     MOVEL*BLANK    PHB02
| 01-C                     END
|    C*
|    C* BREAK CONTROL FOR FIELD PRINTED +++++++++++++++++++++++++++++++
|    C                     SETON                     31
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << IF THIS KEY BREAK SKIP PAGE THEN SETON >>------------------
|    C*---------------------------------------------------------------
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R7100 ... LEVEL BREAK 1 :INIT PROCESS
     C*==============================================================*
05===C           R7100     BEGSR
|    C*
|    C                     CALL 'P31'
|    C                     PARM HA15      P3101I  80
|    C                     PARM '2'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM #X101     P3104I  1
|    C                     PARM #X102     P3105I  1
|    C           PHA15     PARM           P3101O  80
|    C*
|    C*
|    C*
|    C* BREAK CONTROL FOR FIELD PRINTED +++++++++++++++++++++++++++++++
|    C                     SETON                     32
05===C                     ENDSR
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C* << IF THIS KEY BREAK SKIP PAGE THEN SETON >>------------------
     C*---------------------------------------------------------------
     C*
     C*==============================================================*
     C*          R8000 ... LEVEL BREAK 1
     C*==============================================================*
06===C           R8000     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << TOTAL LINE DATA PREPARE >>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     WRITEPT3
|    C*
|    C* << ACCUMULATE & CLEAR >>+++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD0         PT31
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R8100 ... LEVEL BREAK 2
     C*==============================================================*
07===C           R8100     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << TOTAL LINE DATA PREPARE >>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     WRITEPT1
|    C*
|    C* << ACCUMULATE & CLEAR >>+++++++++++++++++++++++++++++++++++++++
|    C                     ADD  PT11      PT21
|    C                     Z-ADD*ZERO     PT11
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R8900 ... LEVEL BREAK LR -- HAS DATA
     C*==============================================================*
08===C           R8900     BEGSR
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C* << TOTAL LINE DATA PREPARE >>
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     WRITEPT2
|    C*
|    C* << ACCUMULATE & CLEAR >>+++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD*ZERO     PT21
|    C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
|    C*
08===C                     ENDSR
     C*==============================================================*
     C*          R8999 ... LEVEL BREAK LR -- NO DATA
     C*==============================================================*
09===C           R8999     BEGSR
|    C*
|    C                     MOVEL*BLANK    PCONAM
|    C*公司檔
|    C                     CALL 'CKS#B0'
|    C                     PARM DHA02F    #B001I
|    C                     PARM           #B002O 16
|    C           PCONAM    PARM           #B003O 34
|    C                     PARM           #B098O 50
|    C                     PARM           #B099O  1
|    C*
|    C                     EXSR R1000
|    C*
|    C                     WRITEPH1
|    C*
|    C                     WRITEPE9
|    C*
09===C                     ENDSR
     C*==============================================================
**
客戶簡稱
廠商簡稱
