     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : CKA130                                     *
     H*    PROGRAM NAME :票據託收作業                              *
     H*    AUTHOR       : A1152   ANGY                               *
     H*    CREATE DATE  : 92/09/29                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :                                            *
     F****************************************************************
     FCKHALF02UF  E           K        DISK
     FCKHELF01O   E           K        DISK
     FCKHDLF01IF  E           K        DISK
     FCKHBLF01IF  E           K        DISK
     FPA#APF  IF  E           K        DISK
     FPT#XPF  IF  E           K        DISK
     FCKA130D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E*
     E*---------------------------------------------------------------*
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*DATA AREA
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I*LOCAL DATA AREA
     ILDA        UDS
     I                                      101 110 $USER
     I                                      209 2160$C8YMD
     I                                      201 2080$A8YMD
     I                                      152 161 $USERN
     I                                      173 176 $COR
     I                                      219 219 $ENV
     I                                      501 502 HA02F
     I                                      505 506 HA02T
     I                                      509 514 HA16F
     I                                      515 520 HA16T
     I                                      521 5280HA15F
     I                                      529 5360HA15T
     I                                      537 537 DHA25
     I                                      538 538 STS
     I                                      539 5460HA07F
     I                                      547 5540HA07T
     I*DEFINE HD0  KEY
     I            DS
     I                                        1  14 HA16
     I                                        9  14 HA161
     I/COPY QRPGSRC,PT#XDS01
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C           *NAMVAR   DEFN *LDA      LDA
     C           *LIKE     DEFN HA15      WHA15
     C           *LIKE     DEFN #A01      D#A05
     C           *LIKE     DEFN #A02      HA051
     C           *LIKE     DEFN #X01      D#X03
     C           *LIKE     DEFN HA06      HA06F
     C           *LIKE     DEFN HA06      HA06T
     C*
     C           *LIKE     DEFN HA02      #B001I
     C           *LIKE     DEFN HA02      #C001I
     C           *LIKE     DEFN HA04      #C002I
     C           *LIKE     DEFN HA02      #E001I
     C           *LIKE     DEFN HA13      #E002I
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYHA     KLIST                           *MAIN FILE
     C                     KFLD           DHA01   1
     C                     KFLD           DHA02
     C                     KFLD           HA06F
     C                     KFLD           HA07F
     C           KEYHA1    KLIST                           *READE KEY
     C                     KFLD           DHA01   1
     C                     KFLD           DHA02
     C           KEYHA2    KLIST                           *CHAIN HA0
     C                     KFLD           DHA01   1
     C                     KFLD           DHA02
     C                     KFLD           DHA061  80
     C                     KFLD           DHA071  80
     C                     KFLD           HA03
     C           KEY#A5    KLIST                           *GET #A03
     C                     KFLD           D#A05
     C                     KFLD           HA051
     C           KEY#X3    KLIST                           *CHAIN #X0
     C                     KFLD           D#X03
     C                     KFLD           DHA02
     C           KEYHD     KLIST                           *CHAIN HD0
     C                     KFLD           DHA02
     C                     KFLD           DHA16
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
     C           *IN03     DOWEQ'0'
     C           SCID      CASEQ'SC01'    R1000            *SCR1
     C           SCID      CASEQ'SC02'    R2000            *SCR2
     C                     END
     C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C                     IN   PTDA01
     C*
     C* SCREEN INIT VALUE SET *+++++++++++++++++++++++++++++++++++++++
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM $PFMT     P3104I  1
     C                     PARM $PTYPE    P3105I  1
     C           DDATE     PARM           P3101O  80
     C*
     C                     MOVE 'SC01'    SCID    4
     C* SC01 FIELD INITIALIZE *--------------------------------------*
     C                     MOVEL$COR      DHA02
     C*公司檔
     C                     CALL 'CKS#B0'
     C                     PARM DHA02     #B001I
     C           DHA02N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C*
     C                     MOVEL'696092'  DHA16
     C                     Z-ADD*ZERO     DHA06F
     C                     Z-ADD*ZERO     DHA06T
     C                     MOVEL'1'       DHA24
     C                     MOVEL'1'       DHA25
     C                     MOVEL'Y'       DHA27
     C                     MOVEL'1'       STS
     C*
     C                     MOVEL'CK'      D#X03
     C           KEY#X3    CHAIN#X0                  40
     C           *IN40     IFEQ '0'
     C                     CALL 'P31'
     C                     PARM $A8YMD    P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHA15     PARM           P3101O  80
     C                     END
     C*--------------------------------------------------------------*
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C* N99                SETON                     88
     C*
     C                     EXFMTDSPD1
     C                     MOVEA*ALL'0'   *IN,60
     C                     Z-ADD*ZERO     #LIN
     C                     Z-ADD*ZERO     #COL
     C*
     C           *IN03     IFEQ '1'                        *END
     C                     GOTO E1000
     C                     END
     C*
     C           *IN04     IFEQ '1'
     C                     EXSR R1E00                      .PROMPT
     C                     GOTO E1000
     C                     END
     C*
     C           *IN09     IFEQ '1'                        *PRINT
     C                     EXSR R1B00                      *SCR CHK
     C           *IN99     IFEQ '0'                        *SCR CHK OK
     C                     EXSR R1A00
     C                     GOTO E1000
     C                     END
     C                     END
     C*
     C                     EXSR R1B00                      *SCR CHK
     C*
     C           *IN99     IFEQ '0'                        *SCR CHK OK
     C                     MOVEL'SC02'    SCID
     C                     END
     C*
     C           E1000     ENDSR
     C*==============================================================*
     C*          R1A00 ....列印作業 (PF9 : CALL SUB-PROGRAM)
     C*==============================================================*
     C           R1A00     BEGSR
     C*<CHECK託收方式>
     C           DHA02     IFEQ *BLANK
     C                     MOVEL*LOVAL    HA02F
     C                     MOVEL*HIVAL    HA02T
     C                     ELSE
     C                     MOVELDHA02     HA02F
     C                     MOVELDHA02     HA02T
     C                     END
     C*
     C                     MOVEL*LOVAL    HA16F
     C                     MOVEL*HIVAL    HA16T
     C*
     C                     Z-ADD*ZERO     HA15F
     C                     Z-ADD99999999  HA15T
     C*
     C*<CALL SUB-PROGRAM>
     C                     MOVE '2'       $ENV
     C                     OUT  LDA
     C                     CALL 'CKR4702C'
     C*
     C                     MOVEL'UPT2140' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     99
     C*
     C           E1A00     ENDSR
     C*==============================================================*
     C*          R1B00 .... 'SC01' SCREEN CHECK
     C*==============================================================*
     C           R1B00     BEGSR
     C*
     C*<CHECK公司別>
     C                     MOVEL*BLANK    DHA02N
     C*
     C           DHA02     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6099
     C                     ELSE
     C*公司檔
     C                     CALL 'CKS#B0'
     C                     PARM DHA02     #B001I
     C           DHA02N    PARM           #B002O 16
     C                     PARM           #B003O 34
     C                     PARM           #B098O 50
     C                     PARM           #B099O  1
     C           #B099O    IFNE 'Y'
     C                     MOVEL'UPT0030' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6099
     C                     END
     C*
     C                     END
     C   99                GOTO E1B00
     C*<CHECK銀行帳號>
     C           DHA16     IFEQ *BLANK
     C                     MOVEL'UPT0010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6199
     C                     END
     C   99                GOTO E1B00
     C*
     C           KEYHD     CHAINHD0                  40
     C           *IN40     IFEQ '1'
     C                     MOVEL'UPT2010' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6199
     C                     END
     C   99                GOTO E1B00
     C*<CHECK作業日期>
     C           DHA15     IFEQ *ZERO
     C                     MOVEL'UPT0012' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6299
     C                     END
     C   99                GOTO E1B00
     C*
     C                     MOVEL'CK'      D#X03
     C           KEY#X3    CHAIN#X0                  40
     C                     MOVEL*BLANK    CHECK   1
     C                     CALL 'P30'
     C                     PARM DHA15     P3001I  80
     C                     PARM #X101     P3002I  1
     C                     PARM #X102     P3003I  1
     C           CHECK     PARM           P3011O  1
     C           CHECK     IFNE 'Y'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6299
     C                     END
     C   99                GOTO E1B00
     C*
     C                     CALL 'P31'
     C                     PARM DHA15     P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           WHA15     PARM           P3101O  80
     C*
     C*<CHECK收票日－起>
     C           DHA06F    IFEQ *ZERO
     C                     Z-ADD0         DHA06F
     C                     Z-ADD0         HA06F
     C                     ELSE
     C                     MOVEL*BLANK    CHECK   1
     C                     CALL 'P30'
     C                     PARM DHA06F    P3001I  80
     C                     PARM #X101     P3002I  1
     C                     PARM #X102     P3003I  1
     C           CHECK     PARM           P3011O  1
     C           CHECK     IFNE 'Y'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6399
     C                     END
     C   99                GOTO E1B00
     C*<DATE SCREEN-->FILE>
     C                     CALL 'P31'
     C                     PARM DHA06F    P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           HA06F     PARM           P3101O  80
     C                     END
     C*<CHECK收票日－迄>
     C           DHA06T    IFEQ *ZERO
     C*                    Z-ADD99999999  DHA06T
     C                     Z-ADD99999999  HA06T
     C                     ELSE
     C                     MOVEL*BLANK    CHECK   1
     C                     CALL 'P30'
     C                     PARM DHA06T    P3001I  80
     C                     PARM #X101     P3002I  1
     C                     PARM #X102     P3003I  1
     C           CHECK     PARM           P3011O  1
     C           CHECK     IFNE 'Y'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6499
     C                     END
     C   99                GOTO E1B00
     C*
     C                     CALL 'P31'
     C                     PARM DHA06T    P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           HA06T     PARM           P3101O  80
     C                     END
     C*<CHECK RANGE>
     C           HA06F     IFGT HA06T
     C                     MOVEL'UPT0041' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     636499
     C                     END
     C   99                GOTO E1B00
     C*
     C*<CHECK到期日－起>
     C           DHA07F    IFEQ *ZERO
     C                     Z-ADD0         DHA07F
     C                     Z-ADD0         HA07F
     C                     ELSE
     C                     MOVEL*BLANK    CHECK   1
     C                     CALL 'P30'
     C                     PARM DHA07F    P3001I  80
     C                     PARM #X101     P3002I  1
     C                     PARM #X102     P3003I  1
     C           CHECK     PARM           P3011O  1
     C           CHECK     IFNE 'Y'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6599
     C                     END
     C   99                GOTO E1B00
     C*
     C                     CALL 'P31'
     C                     PARM DHA07F    P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           HA07F     PARM           P3101O  80
     C                     END
     C*<CHECK到期日－迄>
     C           DHA07T    IFEQ *ZERO
     C*                    Z-ADD99999999  DHA07T
     C                     Z-ADD99999999  HA07T
     C                     ELSE
     C                     MOVEL*BLANK    CHECK   1
     C                     CALL 'P30'
     C                     PARM DHA07T    P3001I  80
     C                     PARM #X101     P3002I  1
     C                     PARM #X102     P3003I  1
     C           CHECK     PARM           P3011O  1
     C           CHECK     IFNE 'Y'
     C                     MOVEL'UPT0040' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     6699
     C                     END
     C   99                GOTO E1B00
     C*
     C                     CALL 'P31'
     C                     PARM DHA07T    P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           HA07T     PARM           P3101O  80
     C                     END
     C*<CHECK RANGE>
     C           HA07F     IFGT HA07T
     C                     MOVEL'UPT0041' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     SETON                     656699
     C                     END
     C   99                GOTO E1B00
     C*<CHECK票據區>
     C           DHA24     IFEQ *BLANK
     C                     MOVEL*LOVAL    DHA24F  1
     C                     MOVEL*HIVAL    DHA24T  1
     C                     ELSE
     C                     MOVELDHA24     DHA24F  1
     C                     MOVELDHA24     DHA24T  1
     C                     END
     C*<CHECK託收入帳>
     C           DHA27     IFEQ *BLANK
     C                     MOVEL*LOVAL    DHA27F  1
     C                     MOVEL*HIVAL    DHA27T  1
     C                     ELSE
     C                     MOVELDHA27     DHA27F  1
     C                     MOVELDHA27     DHA27T  1
     C                     END
     C*
     C           E1B00     ENDSR
     C*==============================================================*
     C*          R1E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
     C           R1E00     BEGSR
     C*
     C           #CSR      DIV  256       #LIN             *LINE
     C                     MVR            #COL             *COLUMN
     C*銀行帳號
     C           #LIN      IFEQ 6
     C           #COL      ANDGE36
     C           #COL      ANDLE41
     C                     CALL 'CKI1D0'
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM DHA02     P130I1  2
     C           DHA16     PARM DHA16     P130I2  6
     C*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     PARM           @RTNC   2
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           R2000     BEGSR
     C*
     C*--------------------------------------------------------------*
     C                     EXSR R2A00                      .SFL INIT
     C*
     C           SCID      DOWEQ'SC02'
     C*
     C                     WRITEDSPC1
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '1'                        *END
     C                     MOVEL'SC01'    SCID
     C                     GOTO E2000
     C                     END
     C*
     C           *IN12     IFEQ '1'                        *PREVIOUS
     C*RELEAS FILE-------------------------------------------------
     C           *LOVAL    SETLLHA0
     C*------------------------------------------------------------
     C                     MOVEL'SC01'    SCID
     C                     GOTO E2000
     C                     END
     C*
     C           *IN14     IFEQ '1'                        *END
     C                     EXSR R2C00                      *OPTION
     C                     MOVEL'SC01'    SCID
     C                     END
     C*
     C                     END
     C*
     C           E2000     ENDSR
     C*=============================================================*
     C*          R2A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
     C           R2A00     BEGSR
     C*
     C                     Z-ADD0         RRN
     C                     Z-ADD1         DRRN
     C*
     C                     SETON                     52    *SFLCLR
     C                     WRITESFLCR
     C                     SETOF                     52
     C*
     C                     SETON                     51    *SFLDSPCTL
     C                     SETOF                     50    *SFLDSP
     C*
     C                     EXSR R2A20                      *SFL HEAD
     C                     MOVEL*BLANK    DSLT
     C                     Z-ADD*ZERO     DSUM   100
     C*
     C                     MOVEL'1'       DHA01
     C           KEYHA     SETLLHA0
     C                     EXSR R2A10                      *ADD SFL DTA
     C*
     C           RRN       IFGT 0
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     99    *NO DTA
     C                     MOVEL'UPT2050' ERRID
     C                     MOVEL'PTMF'    ERRF
     C                     END
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R2A10 .... ADD SUBFILE DATA
     C*=============================================================*
     C           R2A10     BEGSR
     C*
     C           KEYHA1    READEHA0                      46
     C*
     C           *IN46     DOWEQ'0'
     C           HA06      ANDLEHA06T
     C                     EXSR R2A11
     C                     END
     C*                    SETON                       96
     C*
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R2A11 .... WRITE SUBFILE DATA - 託收OR貼現
     C*=============================================================*
     C           R2A11     BEGSR
     C*
     C*<<SUBFILE DATA VALUE SET>>
     C           HA17      IFEQ '1'
     C           HA07      ANDGEHA07F
     C           HA07      ANDLEHA07T
     C           HA24      ANDGEDHA24F
     C           HA24      ANDLEDHA24T
     C           HA27      ANDGEDHA27F
     C           HA27      ANDLEDHA27T
     C*
     C                     EXSR R2A13
     C                     ADD  1         RRN
     C                     WRITESFLSR
     C                     END
     C           KEYHA1    READEHA0                      46
     C*
     C                     ENDSR
     C*===============================================================*
     C*          R2A13     SUBFILE DATA VALUE SET
     C*================================================================
     C           R2A13     BEGSR
     C*<DATE FILE-->SCREEN>
     C                     MOVEL'CK'      D#X03
     C           KEY#X3    CHAIN#X0                  40
     C                     CALL 'P31'
     C                     PARM HA06      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHA06     PARM           P3101O  80
     C*
     C                     CALL 'P31'
     C                     PARM HA07      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHA07     PARM           P3101O  80
     C*
     C                     MOVEL*BLANKS   D#C03
     C*客戶檔
     C                     CALL 'CKS#C0'
     C                     PARM DHA02     #C001I
     C                     PARM HA04      #C002I
     C           D#C03     PARM           #C003O 10
     C                     PARM           #C004O 42
     C                     PARM           #C005O 10
     C                     PARM           #C098O 50
     C                     PARM           #C099O  1
     C*<GET #A03>
     C                     MOVEL'CKTYPE'  D#A05
     C                     MOVE *BLANK    HA051
     C                     MOVELHA05      HA051
     C                     MOVEL*BLANK    #A03D
     C           KEY#A5    CHAIN#A0                  40
     C           *IN40     IFEQ '0'
|   C                     MOVEL#A03      #A03D
| 02-C                     END
|    C*
     C*
     C                     CALL 'P31'
     C                     PARM HA11      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM #X101     P3104I  1
     C                     PARM #X102     P3105I  1
     C           DHA11     PARM           P3101O  80
     C*
     C                     ADD  HA10      DSUM
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R2A20 .... SFL HEADING (SFL CONTROL DATA PREPARE)
     C*=============================================================*
     C           R2A20     BEGSR
     C*
     C                     MOVELHD02      DHD02
     C                     MOVEL*BLANK    DHB03
     C           HD03      CHAINHB0                  40
     C  N40                MOVELHB03      DHB03
     C                     MOVEL'不託收'DOPTC
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R2C00 ....WRITE CKHELF01 OR UPDATE CKHALF02
     C*=============================================================*
     C           R2C00     BEGSR
     C*
     C                     Z-ADD0         WRRN    30
     C                     SETOF                     40
     C           *IN40     DOUEQ'1'
     C                     ADD  1         WRRN
     C           WRRN      CHAINSFLSR                40
     C           *IN40     IFEQ '0'
     C           DSLT      IFEQ *BLANK
     C                     EXSR R2CA0
     C                     END
     C                     END
     C                     END
     C*
     C*
     C                     ENDSR
     C*===============================================================
     C*          R2CA0.... WRITE & UPDATE
     C*===============================================================
     C           R2CA0     BEGSR
     C                     MOVEL'1'       HE01
     C                     MOVELDHA02     HE02
     C                     MOVELHA03      HE03
     C                     MOVELHD02      HE04
     C                     Z-ADDWHA15     HE05    80
     C*
     C                     MOVEL'2'       HE06
     C                     MOVEL*BLANK    HE07
     C                     MOVELHA17      HE08
     C                     TIME           HE97
     C                     Z-ADD$A8YMD    HE98
     C                     MOVEL$USER     HE99
     C                     WRITEHE0
     C*<DATE SCREEN-->FILE>
     C                     MOVEL'1'       DHA01
     C                     CALL 'P31'
     C                     PARM DHA06     P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           DHA061    PARM           P3101O  80
     C*
     C                     CALL 'P31'
     C                     PARM DHA07     P3101I  80
     C                     PARM #X101     P3102I  1
     C                     PARM #X102     P3103I  1
     C                     PARM '2'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           DHA071    PARM           P3101O  80
     C*
     C           KEYHA2    CHAINHA0                  40
     C           *IN40     IFEQ '0'
     C                     Z-ADD*ZERO     HB11
     C           HA08      CHAINHB0                  41
     C           WHA15     IFGT HA07
     C                     CALL 'P37'
     C                     PARM WHA15     P3701I  80
     C                     PARM '2'       P3702I  1
     C                     PARM '1'       P3703I  1
     C                     PARM HB11      P3704I  40
     C           HA11      PARM           P3711O  80
     C                     ELSE
     C                     CALL 'P37'
     C                     PARM HA07      P3701I  80
     C                     PARM '2'       P3702I  1
     C                     PARM '1'       P3703I  1
     C                     PARM HB11      P3704I  40
     C           HA11      PARM           P3711O  80
     C                     END
     C                     Z-ADDHE05      HA15
     C                     MOVELHE04      HA16
     C                     MOVELHE06      HA17
     C                     MOVELDHA25     HA25
     C                     TIME           HA97
     C                     Z-ADD$A8YMD    HA98
     C                     MOVEL$USER     HA99
     C                     UPDATHA0
     C*
     C                     END
     C*
     C                     ENDSR
