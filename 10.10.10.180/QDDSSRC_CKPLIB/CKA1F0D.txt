     A*%%TS  SD  19941114  152450  REPGMR      REL-V2R2M0  5738-PW1
     A*  92/09/29  14:29:57    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKA1F0                                     *
     A*    PROGRAM NAME :票據兌現作業                              *
     A*    AUTHOR       : A1062                                      *
     A*    CREATE DATE  : 92/09/29                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A*  92/09/29  14:19:51    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD1
     A*%%TS  SD  19930507  094427  TSPGMR      REL-V2R2M0  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA1F0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'應付票據票號確認'
     A                                      DSPATR(RI)
     A                                  6 24'公司別　:'
     A            DHF02     R        B  6 36REFFLD(HF02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DHF02N    R        O  6 39REFFLD(#B03 RERF)
     A                                  8 24'銀行帳戶:'
     A                                      DSPATR(HI)
     A            DHF16A         6A  B  8 36
     A                                      DSPATR(HI)
     A  88
     AO 61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DHF16     R        O  8 43REFFLD(HF16)
     A                                 10 24'列印日期:'
     A            DHF72F    R   -1   B 10 36REFFLD(HF72)
     A                                      EDTWRD('   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 46'-'
     A            DHF72T    R   -1   B 10 48REFFLD(HF72)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 12 24'列印序號:'
     A            DHF73F    R        B 12 36REFFLD(HF73)
     A                                      EDTCDE(4)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 12 41'-'
     A            DHF73T    R        B 12 43REFFLD(HF73)
     A                                      EDTCDE(4)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 27'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A          R SFLSR                     SFL
     A            RRN            4Y 0H
     A            HF03C     R        H      REFFLD(HF03)
     A            DHF03C    R        H      REFFLD(HF03)
     A            HF72      R        H
     A            DHF03A         3A  B  7  2
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A            DHF03B         7Y 0B  7  6
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A            DHF72     R   -1   O  7 14REFFLD(HF72)
     A                                      EDTWRD('   /  /  ')
     A            HF73      R        O  7 24EDTCDE(4)
     A            DHF06     R   -1   O  7 30REFFLD(HF06)
     A                                      EDTWRD('   /  /  ')
     A            HF71      R        O  7 40EDTCDE(4)
     A            DHF04         10A  O  7 46
     A            DHF07     R   -1   O  7 57REFFLD(HF07)
     A                                      EDTWRD('   /  /  ')
     A            HF10      R        O  7 67EDTCDE(2)
     A*
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0052)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF10(10)
     A                                      CF12(12)
     A                                      CF14(14)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'<CKA1F0.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            $C8YMD         8Y 0O  1 70EDTWRD('    /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'應付票據票號確認'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHF02     R        O  4 13REFFLD(HF02)
     A            DHF02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  4 35'帳戶號碼:'
     A            DHF16     R        O  4 47REFFLD(HF16)
     A            HC03      R        O  4 62
     A                                  6  2'票據號碼'
     A                                  6 15'列印日　序號'
     A                                  6 31'開票日　序號'
     A                                  6 46'廠商名稱　　到期日'
     A                                  6 69'票據金額'
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F12=回前畫面'
     A                                 23 44'PA1=下一頁'
     A                                 23 61'PA2=上一頁'
     A                                 24 10'F10=票號續編'
     A                                 24 27'F14=票號確認'
     A                                 24 67'【泛太資訊】'
