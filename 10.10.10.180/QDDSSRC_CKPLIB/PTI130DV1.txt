     A*%%TS  SD  19941110  154533  QSECOFR     REL-V2R3M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PTI130                                     *
     A*    PROGRAM NAME :公司基本資料全頁查詢                      *
     A*    AUTHOR       : A1149  MAY                                 *
     A*    CREATE DATE  : 1992.10.28                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :基本資料系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19941110  153242  QSECOFR     REL-V2R3M0  5738-PW1
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      VALUES('/' ' ')
     A            RRN            3Y 0O  8  4EDTCDE(1)
     A            #B01      R        O  8  8REFFLD(#B01)
     A            #B03      R        O  8 11REFFLD(#B03)
     A            #B02      R        O  8 28REFFLD(#B02)
     A            #B08      R        O  8 63REFFLD(#B08)
     A            #B06      R        O  8 79REFFLD(#B06)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19941110  154533  QSECOFR     REL-V2R3M0  5738-PW1
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PTI130.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 29'公司基本資料全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  3'S (=作業) : /-挑選'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A            D#B01     R        B  7  8REFFLD(#B01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A                                  6  4'NO'
     A                                      DSPATR(UL)
     A                                  6  7'公司'
     A                                      DSPATR(UL)
     A                                  6 14'公司簡稱'
     A                                      DSPATR(UL)
     A                                  6 39'公司全名'
     A                                      DSPATR(UL)
     A                                  6 64'公司電話'
     A                                      DSPATR(UL)
     A                                  6 75'型態'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 28'PF12=回前畫面'
     A                                 23 46'PA1 =下一頁'
     A                                 24 10'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
