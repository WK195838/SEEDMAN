     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR4E0                                     *
     A*    PROGRAM NAME :其他資金預測表                            *
     A*    AUTHOR       : A1149  HUANG-SHU.MEI                       *
     A*    CREATE DATE  : 1993.02.23                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKR4E0>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'其它資金預測表'
     A                                      DSPATR(RI)
     A                                  6 21'**列印範圍:'
     A                                  7 21'  公司別　:'
     A            DHA02     R        B  7 36REFFLD(HA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DHA02N    R        O  7 39REFFLD(#B03 RERF)
     A                                  8 21'  預測日期:'
     A            DHA11     R   -1   B  8 36REFFLD(HA11)
     A                                      EDTWRD('   /  /  ')
     A  88
     AO 62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 46'前'
     A                                  9 21'  預估帳號:'
     A                                      DSPATR(HI)
     A            DHA16F         6A  B  9 36
     A N99                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 43'-'
     A            DHA16T         6A  B  9 45
     A N99                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 10 21'  來源用途:'
     A                                      DSPATR(HI)
     A            DHA23     R        B 10 36REFFLD(HA23)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A                                 10 39'(空白表示全部)'
     A*
     A                                 12 21'**排序方式:'
     A            DSORT          1A  B 12 36VALUES('1' '2' '3')
     A                                 13 25'1.預測日期＋來源用途'
     A                                 14 25'2.預估帳號＋預測日期'
     A                                 15 25'3.來源用途＋預測日期'
     A                                 18 24'執行環境:'
     A            $PENV          1A  B 18 36VALUES('0' '1' '2')
     A                                 18 40'(0-查詢 1-即時 2-批次)'
     A                                 19 24'列印份數:'
     A            $CPY           3Y 0B 19 36EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20 24'列表機　:' DSPATR(HI)
     A            $PRTCD         2A  B 20 36DSPATR(HI)
     A  27                             21 25'*** 請準備　８０格報表紙 ***'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
