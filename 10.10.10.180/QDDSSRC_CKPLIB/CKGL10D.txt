     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKGL10D                                    *
     A*    PROGRAM NAME :收＼開票產生傳票                          *
     A*    AUTHOR       : A1139                                      *
     A*    CREATE DATE  : 84/04/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A                                      CA03(03)
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<CKGL10.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2 28'收票／開票資料產生傳票'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A*
     A                                  6 24'公司別　:'
     A            DHA02     R        B  6 36REFFLD(HA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            #B02      R        O    +2REFFLD(#B02 *LIBL/PTRF)
     A                                  8 24'票據性質:'
     A            DHA01     R        B  8 36REFFLD(HA01)
     A                                      VALUES('1' '2')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                    +3'(1.應收 2.應付)'
     A                                 10 24'票據類別:'
     A                                      DSPATR(HI)
     A            DHA05     R        B 10 36REFFLD(HA05)
     A                                      VALUES('1' '2' '3' '4' '5' '6' ' ')
     A N99                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                    +3'(空白表示ALL)'
     A                                 12 22'廠商／客戶:'
     A                                      DSPATR(HI)
     A            DHA04F    R        B 12 36REFFLD(HA04)
     A N99                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 12 47'∼'
     A            DHA04T    R        B 12 52REFFLD(HA04)
     A N99                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 14 24'票據號碼:'
     A            DHA03F    R        B 14 36REFFLD(HA03)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 14 47'∼'
     A            DHA03T    R        B 14 52REFFLD(HA03)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 16 24'到期日　:'
     A            DHA07F    R   -1   B 16 36REFFLD(HA07)
     A                                      EDTWRD('   /  /  ')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                 16 47'∼'
     A            DHA07T    R   -1   B 16 52REFFLD(HA07)
     A                                      EDTWRD('   /  /  ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A*
     A**
     A**                               19 24'列印份數:'
     A**          $CPY           3Y 0B 19 36EDTCDE(3)
     A**                                    CHECK(RB)
     A**                               20 24'列表機　:'
     A**                                    DSPATR(HI)
     A**          $PRTCD         2   B 20 36
     A**                                    DSPATR(HI)
     A**27                             21 30'***請準備８０行報表紙***'
     A**                                    DSPATR(RI)
     A**28                             21 30'報表處理中，請稍候'
     A**                                    DSPATR(RI BL)
     A
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  3'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
