     A*%%TS  SD  19941124  113427  REPGMR      REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PTI#D0                                     *
     A*    PROGRAM NAME : 廠商資料全頁查詢                         *
     A*    AUTHOR       : A1062      JEANNY                          *
     A*    CREATE DATE  : 82/02/22                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            4Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            MB01      R        O  8  5
     A            MB03      R        O  8 11
     A            DMB12         16   O  8 31
     A            MB13      R        O  8 49
     A            MB15      R        O  8 65
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0052)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PTI#D0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'廠商資料全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            D#D01     R        B  4 13REFFLD(#D01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DMB01          5   B  7  5DSPATR(HI)
     A                                      DSPATR(CS)
     A            #B03      R        O  4 16
     A                                  6  2'S 廠商代號  廠商簡稱       +
     A                                      聯絡人          電話一        -
     A                                      傳真號碼   '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3=回主畫面'
     A                                 23 27'F12=回前畫面'
     A                                 23 44'PA1=下一頁'
     A                                 23 59'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
