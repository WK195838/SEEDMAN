     A*%%TS  SD  19930210  094837  REPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CK999                                      *
     A*    PROGRAM NAME : 應收票據全頁查詢                         *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 1992.09.22                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            HA03      R        O  8  4REFFLD(HA03)
     A            DHA06          7Y 0O  8 15EDTWRD('   /  /  ')
     A            D#C03         10A  O  8 25
     A            DHB02     R        O  8 36REFFLD(HB02)
     A*           D#A03          6A  O  8 47
     A*           #A03D          4A  O  8 54
     A            DHA07          7Y 0O  8 47EDTWRD('   /  /  ')
     A            HA10           9  0O  8 57
     A                                      EDTCDE(2)
     A            HA55           9  0O  8 69
     A                                      EDTCDE(2)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19930210  094837  REPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0052)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<CK999 .1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 28'可沖帳應收票據全頁查詢'
     A                                      DSPATR(RI)
     A                                  6  2'S 票據號碼  收票日 客戶名稱 -
     A                                      銀行名稱 到期日  票據金額   -
     A                                      未沖金額'
     A                                      DSPATR(UL)
     A            DHA03     R        B  7  4REFFLD(HA03)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3=回主畫面'
     A                                 23 27'F12=回前畫面'
     A                                 23 44'PA1=下一頁'
     A                                 23 59'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
