     A*%%TS  SD  19930222  201829  REPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PTI#C0                                     *
     A*    PROGRAM NAME : 客戶資料全頁查詢                         *
     A*    AUTHOR       : A1062      JEANNY                          *
     A*    CREATE DATE  : 82/02/22                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PTRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19930222  201829  REPGMR      REL-V2R1M1  5738-PW1
     A            RRN            4Y 0H
     A            DOPT           1A  B  8  3DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            #C02      R        O  8  6
     A            #C03      R        O  8 18
     A            #C08      R        O  8 30
     A            #C09      R        O  8 46
     A            #C12      R        O  8 63
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19930222  201829  REPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0052)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<PTI#C0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'客戶資料全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            D#C01     R        B  4 13REFFLD(#C01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            D#C01N    R        O  4 16REFFLD(#B02 PTRF)
     A                                  6  3'S  客戶代號  客戶簡稱  聯絡+
     A                                      人一      電話一          傳+
     A                                      真號碼    '
     A                                      DSPATR(UL)
     A            D#C02     R        B  7  6REFFLD(#C02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3=回主畫面'
     A                                 23 27'F12=回前畫面'
     A                                 23 44'PA1=下一頁'
     A                                 23 59'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
