     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR430                                     *
     A*    PROGRAM NAME :財務動態日報表                            *
     A*    AUTHOR       : A1149    MAY                               *
     A*    CREATE DATE  : 1992.10.08                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<CKR430>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATEC         6Y 0O  1 71EDTCDE(Y)
     A                                  2 32'財務動態日報表'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A*
     A                                  6 28'公司別　:'
     A            DHA02     R        B  6 40REFFLD(HA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DHA02N    R        O  6 43REFFLD(#B03 RERF)
     A                                  8 28'資料日期:'
     A            DDATE          7  0B  8 40EDTWRD('   /  /  ')
     A  88
     AO 62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A*
     A                                 18 23'執行環境:'
     A            $PENV          1   B 18 35VALUES('0' '1' '2')
     A                                 18 38'(0-查詢 1-即時 2-批次)'
     A                                 19 23'列印份數:'
     A            $CPY           3Y 0B 19 35EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20 23'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 35DSPATR(HI)
     A  27                             21 30'請準備８０格報表紙'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
