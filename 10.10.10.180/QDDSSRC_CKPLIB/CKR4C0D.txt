     A*  92/10/06  17:45:06    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR4C01                                    *
     A*    PROGRAM NAME :簽收條／信封／掛號單                      *
     A*    AUTHOR       : A1150                                      *
     A*    CREATE DATE  : 81/10/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A*  92/10/06  17:45:06    TSPGMR      REL-R03M00  5728-PW1
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<CKR4C0>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2 29'簽收條／信封／掛號單'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  6 23'公司別　:'
     A            DHA02     R        B  6 35REFFLD(HA02 CKRF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DHA02N    R        O  6 38REFFLD(#B03 RERF)
     A                                  8 23'財務用途:'
     A                                      DSPATR(HI)
     A            DHA23          2A  B  8 35
     A  88
     AO 61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A            #A03D         10A  O  8 38
     A                                 10 23'開票日期:'
     A            HA06F          7Y 0B 10 35EDTWRD('   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 45'-'
     A            HA06T          7Y 0B 10 47EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 12 23'廠商代號:'
     A                                      DSPATR(HI)
     A            HA04F     R        B 12 35REFFLD(HA04 CKRF)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A                                 12 46'-'
     A            HA04T     R        B 12 48REFFLD(HA04 CKRF)
     A N99                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 14 23'報表種類:'
     A            DKIND          1   B 14 35VALUES('1' '2' '3')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 14 37'( 1簽收條 2信封 3掛號單)'
     A*
     A                                 18 23'執行環境 :'
     A            $PENV          1   B 18 36VALUES('0' '1' '2')
     A                                 18 39'(0-查詢 1-即時 2-批次)'
     A                                 19 23'列印份數 :'
     A            $CPY           3Y 0B 19 36EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20 23'列表機　 :'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 36DSPATR(HI)
     A  27                             21 30'請準備８０格報表紙'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A
     A*
     A*--------------------------------------------------------
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 27'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
