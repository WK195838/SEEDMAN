     A*%%TS  SD  19930210  202002  REPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKI230                                     *
     A*    PROGRAM NAME : 銀行往來查詢                             *
     A*    AUTHOR       : A1149    MAY                               *
     A*    CREATE DATE  : 1992.10.02                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 票據系統                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930210  202002  REPGMR      REL-V2R1M1  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKI230.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'銀行往來查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        B  4 13REFFLD(HA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'銀行帳號:'
     A                                      DSPATR(HI)
     A            DHA16          6A  B  5 13
     A  88
     AO 62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A            DHD02     R        O  5 20REFFLD(HD02)
     A                                  6  1'處理日期:'
     A            DHA15F    R   -1   B  6 13REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  6 23'-'
     A            DHA15T    R   -1   B  6 25REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            SFL1      R   -1   O  9  2REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A            SFL2           6   O  9 13
     A            SFL3      R        O  9 21REFFLD(HA03)
     A            SFL4           9  0O  9 33EDTCDE(2)
     A            SFL5           9  0O  9 46EDTCDE(2)
     A            SFL6          13  2O  9 59EDTCDE(K)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19930210  202002  REPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<CKI230.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'銀行往來查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        O  4 13REFFLD(HA02)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'銀行帳號:'
     A            DHA16          6A  O  5 13
     A            DHD02     R        O  5 20REFFLD(HD02)
     A                                  5 39'銀行名稱:'
     A            DHB03     R        O  5 51REFFLD(HB03)
     A                                  6  1'處理日期:'
     A            DHA15F    R   -1   O  6 13REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A                                  6 23'-'
     A            PHA15T    R   -1   O  6 25REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A                                  6 39'前日餘額:'
     A            DBLNF     R        O  6 51REFFLD(HD07)
     A                                      EDTCDE(K)
     A                                  8  2'處理日期 性質  票據號碼    -
     A                                      存入金額   提出金額         +
     A                                      帳戶餘額'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F12=回前畫面'
     A                                 23 44'PA1=下一頁'
     A                                 23 59'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
