     A*%%TS  SD  19811009  142829  TSPGMR      REL-V2R1M1  5738-PW1
     A*  92/09/21  14:47:59    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKA1C0                                     *
     A*    PROGRAM NAME : 銀行基本資料維護                         *
     A*    AUTHOR       : A1149 MAY                                  *
     A*    CREATE DATE  : 1992.09.15                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<CKA1C0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2 31'銀行基本資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'銀行代碼:'
     A            DHB01     R        B  4 13REFFLD(HB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  4 21'(拷貝:'
     A            DHB01C    R        B  4 30REFFLD(HB01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  4 38')'
     A                                  5  1'作業選擇:'
     A            DOPT           1Y 0B  5 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  5 15'( 1.新增'
     A                                  5 26'2.修改'
     A                                  5 35'4.刪除'
     A                                  5 44'5.查詢)'
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A*  92/09/21  14:47:59    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD2                     CSRLOC(#LIN HBOL)
     A*%%TS  SD  19811009  142829  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A            #LIN           3  0H
     A            HBOL           3  0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA1C0.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'銀行基本資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'銀行代碼:'
     A            DHB01     R        O  4 13REFFLD(HB01)
     A                                  4 21'(拷貝:'
     A            DHB01C    R        O  4 30REFFLD(HB01)
     A                                  4 38')'
     A                                  5  1'作業選擇:'
     A            DOPT           1Y 0O  5 13EDTCDE(4)
     A                                  5 15'( 1.新增'
     A                                  5 26'2.修改'
     A                                  5 35'4.刪除'
     A                                  5 44'5.查詢)'
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  1'銀行簡稱:'
     A            DHB02     R        B  7 13REFFLD(HB02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  1'銀行名稱:'
     A            DHB03     R        B  8 13REFFLD(HB03)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  1'地址一  :'
     A            DHB04     R        B  9 13REFFLD(HB04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  1'地址二  :'
     A            DHB05     R        B 10 13REFFLD(HB05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  1'電話號碼:'
     A            DHB06     R        B 11 13REFFLD(HB06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 45'傳真號碼:'
     A            DHB07     R        B 11 57REFFLD(HB07)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  1'聯絡人  :'
     A            DHB08     R        B 12 13REFFLD(HB08)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 45'往來銀行:'
     A            DHB09     R        B 12 57REFFLD(HB09)
     A                                      VALUES(' ' 'Y')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 59'( Y :往來)'
     A                                 14  1'地區別  :'
     A            DHB10     R        B 14 13REFFLD(HB10)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 15'( 1本地 2外地)'
     A                                 14 45'託收交易天數:'
     A            DHB11     R        B 14 61REFFLD(HB11)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  1'銀行類別:'
     A                                      DSPATR(ND)
     A*           DHB12     R        O 15 13REFFLD(HB12)
     A*N99                                  DSPATR(HI)
     A* 63                                  DSPATR(PC)
     A* 63                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A            D#A03         10A  O 15 15
     A                                 16  1'備　　註:'
     A            DHB13     R        B 16 13REFFLD(HB13)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 23 45'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
