     A*%%TS  SD  19950126  142919  REPGMR      REL-V2R2M0  5738-PW1
     A*  92/10/01  14:15:00    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKA170                                     *
     A*    PROGRAM NAME :帳戶存提轉作業                            *
     A*    AUTHOR       : A1150                                      *
     A*    CREATE DATE  : 92/09/25                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A*  92/10/01  14:15:00    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD1
     A*%%TS  SD  19930111  112325  REPGMR      REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<CKA170.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2 32'帳戶存提轉作業'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司別　:'
     A            DHA02     R        B  4 13REFFLD(HA02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DHA02N    R        O  4 16REFFLD(#B02 PTRF)
     A                                  5  1'往來日期:'
     A            DHA15     R   -1   B  5 13REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6  1'序　　號:'
     A            DHA31     R        B  6 13REFFLD(HA31)
     A                                      EDTCDE(4)
     A  63
     AO 62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7  1'性　　質:'
     A            DHA01     R        B  7 13REFFLD(HA01)
     A  88
     AO 64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  7 15'( 3.存入'
     A                                  7 26'4.提出'
     A                                  7 35'9.轉帳)'
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  8 15'( 1.新增'
     A                                  8 26'2.修改'
     A                                  8 35'4.刪除'
     A                                  8 44'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*  92/10/01  14:15:00    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD2
     A*%%TS  SD  19950126  142919  REPGMR      REL-V2R2M0  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA170.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'帳戶存提轉作業'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        O  4 13REFFLD(HA02)
     A            DHA02N    R        O  4 16REFFLD(#B02 PTRF)
     A                                  5  1'往來日期:'
     A            DHA15     R   -1   O  5 13REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A                                  6  1'序　　號:'
     A            DHA31     R        O  6 13REFFLD(HA31)
     A                                      EDTCDE(4)
     A                                  7  1'性　　質:'
     A            DHA01     R        O  7 13REFFLD(HA01)
     A                                  7 15'( 3.存入'
     A                                  7 26'4.提出'
     A                                  7 35'9.轉帳)'
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0O  8 13EDTCDE(4)
     A                                  8 15'( 1.新增'
     A                                  8 26'2.修改'
     A                                  8 35'4.刪除'
     A                                  8 44'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 10  1'提出銀行帳戶:'
     A                                      DSPATR(HI)
     A            DH16A          6A  B 10 17
     A N99                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            HD02A     R        O 10 24REFFLD(HD02)
     A                                 10 40'銀行名稱:'
     A            HD03A     R        O 10 52REFFLD(HD03)
     A            HB03A     R        O 10 60REFFLD(HB03)
     A                                 11  5'台幣金額:'
     A            DH10A     R        B 11 17REFFLD(HA10)
     A                                      EDTCDE(K)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 40'原幣金額:'
     A            DH34A     R        B 11 52REFFLD(HA34)
     A                                      EDTCDE(K)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 69'幣別:'
     A            HD06A     R        O 11 77REFFLD(HD06)
     A                                 12  5'來源用途:'
     A                                      DSPATR(HI)
     A            DH23A     R        B 12 17REFFLD(HA23)
     A N99                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DH23AC        10A  O 12 20
     A                                 12 40'貨款記號:'
     A            DH28A     R        B 12 52REFFLD(HA28)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      VALUES('Y' 'N')
     A                                 12 65'入帳記號:'
     A            DH27A     R        B 12 77REFFLD(HA26)
     A                                      VALUES('Y' 'N')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  5'備　　註:'
     A            DH32A     R        B 13 17REFFLD(HA32)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 65'對帳記號:'
     A            DH26A     R        B 13 77REFFLD(HA26)
     A                                      VALUES('Y' 'N')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  1'存入銀行帳戶:'
     A                                      DSPATR(HI)
     A            DH16B          6A  B 15 17
     A N99                                  DSPATR(HI)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            HD02B     R        O 15 24REFFLD(HD02)
     A                                 15 40'銀行名稱:'
     A            HD03B     R        O 15 52REFFLD(HD03)
     A            HB03B     R        O 15 60REFFLD(HB03)
     A                                 16  5'台幣金額:'
     A            DH10B     R        B 16 17REFFLD(HA10)
     A                                      EDTCDE(K)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 16 40'原幣金額:'
     A            DH34B     R        B 16 52REFFLD(HA34)
     A                                      EDTCDE(K)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 16 69'幣別:'
     A            HD06B     R        O 16 77REFFLD(HD06)
     A                                 17  5'來源用途:'
     A                                      DSPATR(HI)
     A            DH23B     R        B 17 17REFFLD(HA23)
     A N99                                  DSPATR(HI)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DH23BC        10A  O 17 20
     A                                 17 40'貨款記號:'
     A            DH28B     R        B 17 52REFFLD(HA28)
     A                                      VALUES('Y' 'N')
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 17 65'入帳記號:'
     A            DH27B     R        B 17 77REFFLD(HA26)
     A                                      VALUES('Y' 'N')
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 18  5'備　　註:'
     A            DH32B     R        B 18 17REFFLD(HA32)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 18 65'對帳記號:'
     A            DH26B     R        B 18 77REFFLD(HA26)
     A                                      VALUES('Y' 'N')
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 20  1'轉帳票據號碼:'
     A            DHA03     R        B 20 17REFFLD(HA03)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                 20 40'傳票號碼:'
     A            DHA51     R        O 20 52REFFLD(HA51)
     A*
     A*
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 23 45'F5 =參考資料'
     A                                 23 62'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
