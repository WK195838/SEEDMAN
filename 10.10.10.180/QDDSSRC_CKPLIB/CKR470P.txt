     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR470                                     *
     A*    PROGRAM NAME :票據託收作業表                      *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.10.05                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A                                      REF(CKRF)
     A          R PH1                       SKIPB(2)
     A            PCONAM    R              1REFFLD(#B02 RERF)
     A                                    87'日期:'
     A            PDATE          6  0     95EDTCDE(Y)
     A                                   106'頁次:'
     A                                   114PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'票據作業表－'
     A            PTYPE         10A       15
     A                                    87'時間:'
     A                                    97TIME
     A                                      EDTWRD('  :  :  ')
     A                                   108'<CKR470P>'
     A                                      SPACEA(2)
     A                                     1'公司別　:'
     A            HA02      R             13REFFLD(HA02)
     A                                    36'作業日期:'
     A            PHA15F         7  0     48EDTWRD('   /  /  ')
     A                                    58'-'
     A            PHA15T         7  0     60EDTWRD('   /  /  ')
     A                                      SPACEA(1)
     A                                     1'銀行名稱:'
     A            PHB02A        20        13
     A                                    36'銀行帳號:'
     A            HA16      R             48
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==========================='
     A                                      SPACEA(1)
     A                                     4'到期日　客戶名稱'
     A                                    23'銀行名稱　　　　　　銀行帳戶'
     A                                    59'票據號碼'
     A                                    70'票據種類'
     A                                    83'收票日'
     A                                    97'金額'
     A                                   105'作業蓋章'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==========================='
     A*
     A          R PD1                       SPACEB(1)
     A N80                                 1'------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       --------------------------'
     A N80                                  SPACEA(1)
     A  31        PHA07          7  0      2EDTWRD('   /  /  ')
     A  32        P#D03         10O       12
     A            PHB03B        20O       23
     A            HA09      R             44
     A            HA03      R             59
     A            #A03P         10O       70
     A            PHA06          7  0     81EDTWRD('   /  /  ')
     A            HA10      R             91EDTCDE(2)
     A*
     A          R PT1                       SPACEB(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==========================='
     A                                      SPACEA(1)
     A                                    66'合計張數:'
     A            PT11           6  0     85EDTCDE(2)
     A                                      SPACEA(1)
     A                                    66'合計金額:'
     A            PT12          11  0     78EDTCDE(2)
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==========================='
     A*
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
