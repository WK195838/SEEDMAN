     A*%%TS  SD  19931220  211223  MAY         REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR4H0                                     *
     A*    PROGRAM NAME :支票登記簿                                *
     A*    AUTHOR       : A1149   MAY                                *
     A*    CREATE DATE  : 1993.11.22                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19931220  211223  MAY         REL-V2R2M0  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<CKR4H0>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 71EDTCDE(Y)
     A                                  2 31' 　支票登記簿　'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4 23'公司別　:'
     A            DHF02     R        B  4 35REFFLD(HF02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DHF02N    R        O  5 39REFFLD(#B03 RERF)
     A                                  6 23'開票日　:'
     A            DHA06F         6Y 0B  6 35
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                      EDTCDE(Y)
     A                                  6 45'-'
     A            DHA06T         6Y 0B  6 47
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      EDTCDE(Y)
     A                                  8 23'列印日　:'
     A            DHF72          6Y 0B  8 35
     A                                      EDTCDE(Y)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 10 23'列印序號:'
     A            DHF73S    R        B 10 35REFFLD(HF73)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 10 40'-'
     A            DHF73E    R        B 10 42REFFLD(HF73)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 18 23'執行環境:'
     A            $PENV          1A  B 18 35VALUES('0' '1' '2')
     A                                 18 38'(0-查詢 1-即時 2-批次)'
     A                                 19 23'列印份數:'
     A            $CPY           3Y 0B 19 35EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20 23'列表機　:'
     A            $PRTCD         2A  B 20 35DSPATR(HI)
     A  27                             21 30'請準備８０格報表紙'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
