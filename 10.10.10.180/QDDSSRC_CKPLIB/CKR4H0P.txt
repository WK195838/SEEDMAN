     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR4H0                                     *
     A*    PROGRAM NAME :支票登記簿                                *
     A*    AUTHOR       : A1149   MAY                                *
     A*    CREATE DATE  : 1993.11.22                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A                                      REF(CKRF)
     A          R PH1                       SKIPB(2)
     A            P#B03     R              4REFFLD(#B02 RERF)
     A                                    79'日期:'
     A            PDATE          6  0     87EDTCDE(Y)
     A                                    96'頁次:'
     A                                   104PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     4'支票登記簿'
     A                                      CHRSIZ(2 2)
     A                                    79'時間:'
     A                                    87TIME
     A                                      EDTWRD('  :  :  ')
     A                                    98'<CKR4H0P>'
     A                                      SPACEA(2)
     A                                     1'公司別　:'
     A            DHF02     R             13REFFLD(HF02)
     A                                    20'開票日:'
     A            DHA06F         6  0     30EDTCDE(Y)
     A                                    41'-'
     A            DHA06T         6  0     43EDTCDE(Y)
     A                                   +10'列印日:'
     A            DHF72          6  0     +2EDTCDE(Y)
     A                                    +5'列印序號:'
     A            DHF73S    R             +2REFFLD(HF73)
     A                                      EDTCDE(Z)
     A                                    +2'-'
     A            DHF73E    R             +2REFFLD(HF73)
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ======================'
     A                                      SPACEA(1)
     A                                     1'序號　開票日'
     A                                    15'傳票號碼'
     A                                    28'到期日　請款人／名稱'
     A                                    58'支票號碼'
     A                                    74'金額'
     A                                    85'蓋　　　章'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ======================'
     A*
     A          R PD1                       SPACEB(1)
     A            HF73      R              1EDTCDE(Z)
     A  31        WHA06          7  0      5EDTWRD('   /  /  ')
     A            HA18      R             15REFFLD(HB02)
     A*           WHA16          6        25
     A            WHA07          7  0     26EDTWRD('   /  /  ')
     A            HA04      R             36
     A            HA04N         10        47
     A            HA03      R             58
     A            HA10      R             69EDTCDE(2)
     A*
     A                                      SPACEA(2)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ======================'
     A*
     A          R PE9                       SPACEB(1)
     A                                    19'無符合列印範圍之資料'
