     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR480                                     *
     A*    PROGRAM NAME :票據貼現作業表                            *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.10.05                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A                                      REF(CKRF)
     A          R PH1                       SKIPB(2)
     A            PCONAM    R              1REFFLD(#B02 RERF)
     A                                    77'日期:'
     A            PDATE          6  0     85EDTCDE(Y)
     A                                    96'頁次:'
     A                                   104PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'票據作業表－'
     A                                    15'貼現'
     A                                    77'時間:'
     A                                    87TIME
     A                                      EDTWRD('  :  :  ')
     A                                    98'<CKR480P>'
     A                                      SPACEA(2)
     A                                     1'公司別　:'
     A            HA02      R             13REFFLD(HA02)
     A                                    36'作業日期:'
     A            PHA15F         7  0     48EDTWRD('   /  /  ')
     A                                    58'-'
     A            PHA15T         7  0     60EDTWRD('   /  /  ')
     A                                      SPACEA(1)
     A                                     1'銀行名稱:'
     A            PHB02A        20        13
     A                                    36'銀行帳號:'
     A            HA16      R             48
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================='
     A                                      SPACEA(1)
     A                                     4'到期日　客戶名稱'
     A                                    23'銀行名稱　銀行帳戶'
     A                                    49'票據號碼'
     A                                    60'票據種類'
     A                                    73'收票日'
     A                                    87'金額'
     A                                    95'作業蓋章'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================='
     A*
     A          R PD1                       SPACEB(1)
     A N80                                 1'------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       -----------------'
     A N80                                  SPACEA(1)
     A  31        PHA07          7  0      2EDTWRD('   /  /  ')
     A  32        P#D03         10O       12
     A            PHB02B        10O       23
     A            HA09      R             34
     A            HA03      R             49
     A            #A03P         10O       60
     A            PHA06          7  0     71EDTWRD('   /  /  ')
     A            HA10      R             81EDTCDE(2)
     A*
     A          R PT1                       SPACEB(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================='
     A                                      SPACEA(1)
     A                                    66'合計張數:'
     A            PT11           6  0     85EDTCDE(2)
     A                                      SPACEA(1)
     A                                    66'合計金額:'
     A            PT12          11  0     78EDTCDE(2)
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================='
     A*
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
