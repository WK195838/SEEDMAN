     A*%%TS  SD  19950127  114306  GLPGMR      REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA760                                     *
     A*    PROGRAM NAME : 存款傳票登錄                             *
     A*    AUTHOR       : A1034  STEPHANIE                           *
     A*    CREATE DATE  : 1995.01.24                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA760.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'付款傳票登錄'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　　:'
     A            DAH01     R        B  4 16REFFLD(AH01 GLRF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            #B02      R        O  4 19REFFLD(#B02 PTRF)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  98        DMSG          78A  O 22  2DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A*%%TS  SD  19950123  135644  QSECOFR     REL-V2R2M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            HA15      R        H
     A            RRN            3Y 0H
     A            DOPT           1A  B  9  2VALUES(' ' '2' 'X')
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DHA15     R   -1   O  9  4REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A            HA31      R        O    +1EDTCDE(4)
     A            HD04      R        O    +1
     A            HB03      R        O    +1
     A            HA10      R        O    +1EDTCDE(J)
     A            DHA32         10O  O    +1
     A            DAH02     R        O    +1REFFLD(AH02 GLRF)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19950127  114306  GLPGMR      REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0110)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF13(13)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLA760.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'付款傳票登錄'
     A                                      DSPATR(RI)
     A                                  4  2'公司別　　:'
     A            DAH01     R        O  4 16REFFLD(AH01 GLRF)
     A            #B02      R        O  4 19REFFLD(#B02 PTRF)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  6  2'S (=作業) :'
     A                                  6 16'2-傳票登錄  X-放棄'
     A                                  8  2'S 處理日期　序　處理帳號  　　+
     A                                      　　　　　　　　　金額　　備註   -
     A                                       傳票號碼'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC2
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  98        DMSG          78A  O 22  2DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PF13=印表設定'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC0
     A                                      ASSUME
     A                                  2  2' '
     A          R DSPD9
     A*%%TS  SD  19921119  170150  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      CF04(04)
     A                                      OVERLAY
     A                                      PROTECT
     A                                      CLRL(*NO)
     A                                  7 30'                          '
     A                                      DSPATR(RI)
     A                                  8 30' '
     A                                      DSPATR(RI)
     A                                  8 32'<GLA760.3>            '
     A                                  8 55' '
     A                                      DSPATR(RI)
     A                                  9 30' '
     A                                      DSPATR(RI)
     A                                  9 32'                      '
     A                                  9 55' '
     A                                      DSPATR(RI)
     A                                 10 30' '
     A                                      DSPATR(RI)
     A                                 10 32'   列印份數:'
     A            $CPY           3Y 0B 10 47EDTCDE(3)
     A                                      CHECK(RB)
     A                                 10 51'   '
     A                                 10 55' '
     A                                      DSPATR(RI)
     A                                 11 30' '
     A                                      DSPATR(RI)
     A                                 11 32'   列表機　:'
     A            $PRTCD         2A  B 11 47DSPATR(HI)
     A                                 11 50'    '
     A                                 11 55' '
     A                                      DSPATR(RI)
     A                                 12 30' '
     A                                      DSPATR(RI)
     A                                 12 31'                        '
     A                                 12 32'                      '
     A                                 12 55' '
     A                                      DSPATR(RI)
     A                                 13 30' '
     A                                      DSPATR(RI)
     A                                 13 32'執行  PF4=欄位查詢'
     A                                 13 55' '
     A                                      DSPATR(RI)
     A                                 14 30'                          '
     A                                      DSPATR(RI)
