     A*%%TS  SD  19951005  113106  DERLERN     REL-V3R1M0  5763-PW1
     A*  92/10/09  09:48:57    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKA110                                     *
     A*    PROGRAM NAME :應收票據資料維護                          *
     A*    AUTHOR       : A1152      ANGY                            *
     A*    CREATE DATE  : 92/9/21                                    *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A*  92/09/26  14:09:44    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD1
     A*%%TS  SD  19930210  094250  REPGMR      REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF08(08)
     A                                      CF18(18)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA110.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'應收票據資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        B  4 13REFFLD(HA02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'票據號碼:'
     A            DHA03     R        B  5 13REFFLD(HA03)
     A  88
     AO 61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 24'(拷貝:'
     A            DHA02C    R        B  5 33REFFLD(HA02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DHA03C    R        B  5 36REFFLD(HA03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  5 47')'
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 15'( 1.新增'
     A                                  6 26'2.修改'
     A                                  6 35'4.刪除'
     A                                  6 44'5.查詢)'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F8 =沖帳作業'
     A                                 23 46'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*  92/10/09  09:48:57    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD2
     A*%%TS  SD  19951005  113106  DERLERN     REL-V3R1M0  5763-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA110.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'應收票據資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        O  4 13REFFLD(HA02)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'票據號碼:'
     A            DHA03     R        O  5 13REFFLD(HA03)
     A                                  5 24'(拷貝:'
     A            DHA02C    R        O  5 33REFFLD(HA02)
     A            DHA03C    R        O  5 36REFFLD(HA03)
     A                                  5 47')'
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0O  6 13EDTCDE(4)
     A                                  6 15'( 1.新增'
     A                                  6 26'2.修改'
     A                                  6 35'4.刪除'
     A                                  6 44'5.查詢)'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------'
     A                                  8  1'客戶代號:'
     A                                      DSPATR(HI)
     A            DHA04     R        B  8 13REFFLD(HA04)
     A N99                                  DSPATR(HI)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31
     AON38                                  DSPATR(UL)
     A N31
     AO 38                                  DSPATR(PR)
     A            D#C03     R        O  8 24REFFLD(#C03 PTRF)
     A                                  8 42'地區別:'
     A            DHA59          1A  B  8 52VALUES('1' '2' '3')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 54'( 1台北 2台中 3高雄)'
     A                                  9  1'票據類別:'
     A                                      DSPATR(HI)
     A            DHA05     R        B  9 13REFFLD(HA05)
     A N99                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DHA05C        10A  O  9 15
     A                                  9 26'( 1本票 2客票 3支票'
     A                                  9 52'4保証票 5商業本票)'
     A                                 10  1'收票日　:'
     A            DHA06     R   -1   B 10 13REFFLD(HA06)
     A                                      EDTWRD('   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 42'到期日　:'
     A            DHA07     R   -1   B 10 56REFFLD(HA07)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  1'付款銀行:'
     A                                      DSPATR(HI)
     A            DHA08     R        B 11 13REFFLD(HA08)
     A N99                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DHB03     R        O 11 21REFFLD(HB03)
     A                                 11 42'付款帳號:'
     A            DHA09     R        B 11 56REFFLD(HA09)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  1'票面金額:'
     A            DHA10     R        B 12 13REFFLD(HA10)
     A                                      EDTCDE(2)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 12 42'預兌日期:'
     A            DHA11     R   -1   B 12 56REFFLD(HA11)
     A                                      EDTWRD('   /  /  ')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  1'業務人員:'
     A                                      DSPATR(HI)
     A            DHA13     R        B 13 13REFFLD(HA13 CKRF)
     A N99                                  DSPATR(HI)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#E03     R        O 13 24REFFLD(#E03 PTRF)
     A            DHA14          4A  B 14 39DSPATR(PR)
     A                                      DSPATR(ND)
     A                                 13 42'票據區　:'
     A            DHA24          1A  B 13 56VALUES(' ' '1' '2')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 58'( 1本地 2外地)'
     A                                 14  1'來源用途:'
     A                                      DSPATR(HI)
     A            DHA23     R        B 14 13REFFLD(HA23)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DHA23C        10A  O 14 16
     A                                 15  1'票據狀況:'
     A            DHA17     R        O 15 13REFFLD(HA17)
     A            DHA17C         6A  O 15 15
     A                                 15 22'( 0作廢 1庫存 2託收'
     A                                 15 48'3貼現 4兌現'
     A                                 15 64'5退票 6抽票)'
     A                                 16  1'處理日期:'
     A            DHA15     R   -1   O 16 13REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A                                 16 42'處理帳號:'
     A                                      DSPATR(HI)
     A            DHA16          6A  B 16 56
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17  1'貨款記號:'
     A            DHA28     R        B 17 13REFFLD(HA28)
     A                                      VALUES('Y' 'N')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 15'( Y是 N否)'
     A                                 17 42'未沖金額:'
     A            DHA55     R        O 17 56REFFLD(HA55)
     A                                      EDTCDE(2)
     A                                 18  1'入帳類別:'
     A            DHA27     R        B 18 13REFFLD(HA27)
     A                                      VALUES('Y' 'N')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 15'( Y是 N否)'
     A                                 18 42'抽票補票:'
     A            DHA58          1A  B 18 56VALUES('Y' 'N')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 58'( Y是 N否)'
     A                                 19  1'對帳記號:'
     A            DHA26     R        B 19 13REFFLD(HA26)
     A                                      VALUES('Y' 'N')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19 15'( Y是 N未)'
     A                                 19 42'對應原票號:'
     A                                      DSPATR(HI)
     A            DHA57     R        B 19 56REFFLD(HA57)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 20  1'備　　註:'
     A            DHA32     R        B 20 13REFFLD(HA32)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A N31                             21  1'對現傳票:'
     A N31        DHA18     R        O 21 13REFFLD(HA18)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 23 45'F5 =參考資料'
     A                                 23 62'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
