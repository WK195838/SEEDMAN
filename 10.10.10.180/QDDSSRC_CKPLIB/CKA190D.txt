     A*%%TS  SD  19811009  142829  TSPGMR      REL-V2R1M1  5738-PW1
     A*  92/09/21  14:47:59    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKA190                                     *
     A*    PROGRAM NAME : 期它資金預測資料維護                     *
     A*    AUTHOR       : A1149  HUANG-SHU.MEI                       *
     A*    CREATE DATE  : 1993.02.22                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 票據系統                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<CKA190.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2 29'其它資金預測資料維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司別　:'
     A            DHA02     R        B  4 13REFFLD(HA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'資金性質:'
     A            DHA01     R        B  5 13REFFLD(HA01)
     A                                      VALUES('I' 'O')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  5 15'( I收入 O支出)'
     A                                  6  1'資金日期:'
     A            DHA11     R   -1   B  6 13REFFLD(HA11)
     A                                      EDTWRD('   /  /  ')
     A  88
     AO 63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  7  1'序　　號:'
     A            DHA41     R        B  7 13REFFLD(HA41)
     A                                      EDTCDE(4)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0B  8 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  8 15'( 1.新增'
     A                                  8 26'2.修改'
     A                                  8 35'4.刪除'
     A                                  8 44'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A*  92/09/21  14:47:59    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD2                     CSRLOC(#LIN #COL)
     A*%%TS  SD  19811009  142829  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3  0H
     A            #COL           3  0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA190.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 29'其它資金預測資料維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        O  4 13REFFLD(HA02)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'資金性質:'
     A            DHA01     R        O  5 13REFFLD(HA01)
     A                                  5 15'( I收入 O支出)'
     A                                  6  1'資金日期:'
     A            DHA11     R   -1   O  6 13REFFLD(HA11)
     A                                      EDTWRD('   /  /  ')
     A                                  7  1'序　　號:'
     A            DHA41     R        O  7 13REFFLD(HA41)
     A                                      EDTCDE(4)
     A                                  8  1'作業選擇:'
     A            DOPT           1Y 0O  8 13
     A                                      EDTCDE(4)
     A                                  8 15'( 1.新增'
     A                                  8 26'2.修改'
     A                                  8 35'4.刪除'
     A                                  8 44'5.查詢)'
     A                                  9  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 10  1'台幣金額:'
     A            DHA10     R        B 10 13REFFLD(HA10)
     A                                      EDTCDE(K)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  1'預估帳號:'
     A                                      DSPATR(HI)
     A            DHA16L         6   B 12 13
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A N99                                  DSPATR(HI)
     A            DHA16     R        O 12 20REFFLD(HA16)
     A            DHB03     R        O 12 35REFFLD(HB03)
     A                                 14  1'來源代碼:'
     A                                      DSPATR(HI)
     A            DHA23     R        B 14 13REFFLD(HA23)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A N99                                  DSPATR(HI)
     A            DHA23C        10A  O 14 16
     A                                 16  1'資金內容:'
     A            DHA42     R        B 16 13REFFLD(HA42)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F4 =欄位查詢'
     A                                 23 44'F5 =參考資料'
     A                                 23 61'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
