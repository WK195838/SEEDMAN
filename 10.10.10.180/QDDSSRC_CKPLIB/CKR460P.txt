     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR460                                     *
     A*    PROGRAM NAME :銀行資金預測表                            *
     A*    AUTHOR       : A1149    MAY                               *
     A*    CREATE DATE  : 1992.10.05                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 票據系統                                 *
     A****************************************************************
     A                                      REF(CKRF)
     A          R PH1                       SKIPB(2)
     A            PCONAM    R              1REFFLD(#B02 RERF)
     A                                    65'日期:'
     A            PDATE          6  0     73EDTCDE(Y)
     A                                    82'頁次:'
     A                                    90PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'銀行資金預測表'
     A                                    65'時間:'
     A                                    73TIME
     A                                      EDTWRD('  :  :  ')
     A                                    84'<CKR460P>'
     A                                      SPACEA(2)
     A                                     1'公司別:'
     A            HA02      R             11REFFLD(HA02)
     A                                    19'銀行帳號:'
     A            DHA16F         6        31
     A                                    38'-'
     A            DHA16T         6        40
     A                                    48'預測日期:'
     A            PHA11T    R   -1  0     60REFFLD(HA11)
     A                                      EDTWRD('   /  /  ')
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ====='
     A                                      SPACEA(1)
     A                                     2'銀行名稱'
     A                                    15'銀行帳號'
     A                                    28'預兌現日　票據號碼　應收票據金額+
     A                                       　應付票據金額'
     A                                    85'帳戶餘額'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ====='
     A*
     A          R PD1                       SPACEB(1)
     A            PD01      R              2REFFLD(HB02)
     A            PD02      R             13REFFLD(HD02)
     A            PD07          13  2     77EDTCDE(J)
     A          R PD2                       SPACEB(1)
     A  31        PD03      R   -1        28REFFLD(HA11)
     A                                      EDTWRD('   /  /  ')
     A            PD04      R             38REFFLD(HA03)
     A            PD05      R             50REFFLD(HA10)
     A                                      EDTCDE(1)
     A            PD06      R             64REFFLD(HA10)
     A                                      EDTCDE(1)
     A            PD08          13  2     77EDTCDE(J)
     A*
     A          R PT1                       SPACEB(1)
     A                                    28'帳號小計:'
     A            PT11          10  0     48EDTCDE(1)
     A            PT12          10  0     62EDTCDE(1)
     A                                      SPACEA(1)
     A*
     A          R PT2                       SPACEB(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ====='
     A                                      SPACEA(1)
     A                                     2'合　　計:'
     A            PT21          10  0     48EDTCDE(1)
     A            PT22          10  0     62EDTCDE(1)
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ====='
     A*
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
