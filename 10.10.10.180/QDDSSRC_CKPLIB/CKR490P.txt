     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR490                                     *
     A*    PROGRAM NAME :票齡分析表                                *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.10.06                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A                                      REF(CKR490F)
     A          R PH1                       SKIPB(2)
     A            PCONAM    R              1REFFLD(#B02 RERF)
     A                                    70'日期:'
     A            PDATE          6  0     78EDTCDE(Y)
     A                                    89'頁次:'
     A                                    97PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'票齡分析表'
     A                                    70'時間:'
     A                                    80TIME
     A                                      EDTWRD('  :  :  ')
     A                                    91'<CKR490P>'
     A                                      SPACEA(2)
     A                                     1'公司:'
     A            WF02      R              9REFFLD(WF02)
     A                                      SPACEA(1)
     A                                     1'類別:'
     A            PWF01N         6         9
     A                                    17'帳號:'
     A            DHA16F         6        25
     A                                    32'-'
     A            DHA16T         6        34
     A                                    44'含貼現票:'
     A            PTYPE1         1        56
     A                                    62'帳戶種類:'
     A            PTYPE2         6        74
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ============'
     A                                      SPACEA(1)
     A                                     2'帳號種類　銀行簡稱　銀行帳號'
     A                                    44'目前餘額'
     A                                    55'小於30天'
     A                                    70'31-60天'
     A                                    82'61-90天'
     A                                    91'大於90天'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ============'
     A*
     A          R PD1                       SPACEB(1)
     A  31        #A03P         10O        2
     A            PHB02         10O       13
     A            WF04      R             24
     A            WF07      R             42EDTCDE(K)
     A            WF08      R             55EDTCDE(2)
     A            WF09      R             67EDTCDE(2)
     A            WF10      R             79EDTCDE(2)
     A            WF11      R             91EDTCDE(2)
     A*
     A          R PT1                       SPACEB(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ============'
     A                                      SPACEA(1)
     A                                    13'合　　計:'
     A            PT11           9  0     55EDTCDE(2)
     A            PT12           9  0     67EDTCDE(2)
     A            PT13           9  0     79EDTCDE(2)
     A            PT14           9  0     91EDTCDE(2)
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ============'
     A*
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
