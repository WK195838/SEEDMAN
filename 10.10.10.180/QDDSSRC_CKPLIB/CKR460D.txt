     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR460                                     *
     A*    PROGRAM NAME :銀行資金預測表                            *
     A*    AUTHOR       : A1149    MAY                               *
     A*    CREATE DATE  : 1992.10.05                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 票據系統                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<CKR460>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2 32'銀行資金預測表'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A*
     A                                  6 24'公司別　:'
     A            DHA02F    R        B  6 36REFFLD(HA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DHA02N    R        O  6 39REFFLD(#B03 RERF)
     A                                  8 24'銀行帳號:'
     A                                      DSPATR(HI)
     A            DHA16F         6   B  8 36
     A  88
     AO 62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A                                  8 43'-'
     A            DHA16T         6   B  8 45
     A N99                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 24'預測日期:'
     A            DHA11T    R   -1   B 10 36REFFLD(HA11)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 10 46'迄'
     A*
     A                                 18 23'執行環境:'
     A            $PENV          1   B 18 35VALUES('0' '1' '2')
     A                                 18 38'(0-查詢 1-即時 2-批次)'
     A                                 19 23'列印份數:'
     A            $CPY           3Y 0B 19 35EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20 23'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 35DSPATR(HI)
     A  27                             21 30'請準備８０格報表紙'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI BL)
     A
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
