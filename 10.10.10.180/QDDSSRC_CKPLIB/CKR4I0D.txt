     A*%%TS  SD  19950210  142541  LILY        REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR4I01                                    *
     A*    PROGRAM NAME :列印票據作業－變動                        *
     A*    AUTHOR       : A1162                                      *
     A*    CREATE DATE  : 84/02/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19950210  142541  LILY        REL-V2R2M0  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF08(08)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKR4I0>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 34'列印票據作業'
     A                                      DSPATR(RI)
     A                                  5 23'公司別   :'
     A            DHF02     R        B  5 36REFFLD(HF02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DHF02N    R        O  5 39REFFLD(#B02 PTRF)
     A                                  7 23'銀行帳號 :'
     A                                      DSPATR(HI)
     A            DHF16          6A  B  7 36
     A N99                                  DSPATR(HI)
     A  88
     AO 62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 23'開票日期 :'
     A            DHF06F    R   -1   B  9 36REFFLD(HF06)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 46'-'
     A            DHF06T    R   -1   B  9 48REFFLD(HF06)
     A                                      EDTWRD('   /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 11 23'開票序號 :'
     A            DHF71F    R        B 11 36REFFLD(HF71)
     A                                      EDTCDE(4)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 11 41'-'
     A            DHF71T    R        B 11 43REFFLD(HF71)
     A                                      EDTCDE(4)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 13 23'票據種類 :'
     A            DHF76     R        B 13 36REFFLD(HF76)
     A                                      VALUES('1' '3')
     A                                 13 38'( 1本票 3支票)'
     A                                 15 23'列印方式 :'
     A            DTYPE          1A  B 15 36VALUES('1' '2')
     A                                 15 38'( 1首印 2重印)'
     A                                 18 23'執行環境 :'
     A            $PENV          1A  B 18 36VALUES('0' '1' '2')
     A                                 18 38'(0-查詢 1-即時 2-批次)'
     A                                 19 23'列印份數 :'
     A            $CPY           3Y 0O 19 36EDTCDE(3)
     A                                 20 23'列表機　 :'
     A            $PRTCD         2A  B 20 36
     A  27                             21 30'請準備票據專用報表'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 27'F4 =欄位查詢'
     A                                 23 45'F8 =啟動票據專屬印表機'
     A                                 24 67'【泛太資訊】'
