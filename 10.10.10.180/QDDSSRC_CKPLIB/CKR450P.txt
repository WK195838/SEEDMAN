     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR4502                                    *
     A*    PROGRAM NAME :票據兌現核對表                            *
     A*    AUTHOR       : A1152    ANGY                              *
     A*    CREATE DATE  : 81/10/01                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      REF(CKRF)
     A          R PH1                       SKIPB(2)
     A            PCONAM    R              1REFFLD(#B02 RERF)
     A                                    61'日期:'
     A            PDATE          6  0     69EDTCDE(Y)
     A                                    78'頁次:'
     A                                    86PAGNBR
     A                                      SPACEA(1)
     A                                     1'票據兌現核對表'
     A                                    61'時間:'
     A                                    69TIME
     A                                      EDTWRD('  :  :  ')
     A                                    80'<CKR450P>'
     A                                      SPACEA(2)
     A                                     1'公司別:'
     A            PHA02          6        11
     A                                    17'性質:'
     A            P#A03          6O       24
     A                                    31'地區別:'
     A            PHA59T         4O       40
     A                                    45'兌現日期:'
     A            PHA15F         7  0     57EDTWRD('   /  /  ')
     A                                    67'-'
     A            PHA15T         7  0     69EDTWRD('   /  /  ')
     A                                    80'入帳:'
     A            PHA27          4O       87
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       =============================='
     A                                      SPACEA(1)
     A                                     1'銀行帳號'
     A                                    18'銀行簡稱'
     A                                    32'兌現日期'
     A            HA01C         10        43
     A                                    55'票據號碼'
     A                                    70'到期日'
     A                                    81'兌現金額'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       =============================='
     A*
     A          R PD1                       SPACEB(1)
     A  31        HA16      R              2
     A  31        PHB02         12        18
     A  32        PHA15          7  0     32EDTWRD('   /  /  ')
     A            P#04          10        43
     A            HA03      R             55
     A            PHA07          7  0     68EDTWRD('   /  /  ')
     A            HA10      R             79EDTCDE(2)
     A*
     A          R PT1                       SPACEB(1)
     A                                    43'帳號小計----------------------->'
     A            PT11           9  0     79EDTCDE(2)
     A                                      SPACEA(1)
     A*
     A          R PT3                       SPACEB(1)
     A                                    43'日期小計----------------------->'
     A            PT31           9  0     79EDTCDE(2)
     A*
     A          R PT2
     A                                     1'==============================+
     A                                       ==============================+
     A                                       =============================='
     A                                      SPACEA(1)
     A                                     1'合計:'
     A            PT21          10  0     77EDTCDE(2)
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       =============================='
     A*
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
