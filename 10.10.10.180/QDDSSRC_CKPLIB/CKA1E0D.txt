     A*%%TS  SD  19941114  151303  REPGMR      REL-V2R2M0  5738-PW1
     A*  92/09/22  11:08:51    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKA1E0                                     *
     A*    PROGRAM NAME :應付票據開票登錄                          *
     A*    AUTHOR       : A1062                                      *
     A*    CREATE DATE  : 04/07/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A*  92/09/21  15:28:16    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD1
     A*%%TS  SD  19930506  170848  TSPGMR      REL-V2R2M0  5738-PW1
     A                                      CA03(03)
     A                                      CF18(18)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA1E0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'應付票據開票登錄'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHF02     R        B  4 13REFFLD(HF02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DHF02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'開票日期:'
     A            DHF06     R   -1   B  5 13REFFLD(HF06)
     A                                      EDTWRD('   /  /  ')
     A  88
     AO 61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 25'開票序號:'
     A            DHF71     R        B  5 37REFFLD(HF71)
     A                                      EDTCDE(4)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A*                                 5 44'(拷貝:'
     A            DHF02C    R        H      REFFLD(HF02)
     A* 63                                  DSPATR(PC)
     A* 63                                  DSPATR(RI)
     A            DHF06C    R   -1   H      REFFLD(HF06)
     A*                                     EDTWRD('   /  /  ')
     A* 64                                  DSPATR(PC)
     A* 64                                  DSPATR(RI)
     A            DHF71C    R        H      REFFLD(HF71)
     A*                                     EDTCDE(4)
     A* 63                                  DSPATR(PC)
     A* 63                                  DSPATR(RI)
     A*                                 5 71')'
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0B  6 13VALUES(1 2 4 5)
     A                                      EDTCDE(4)
     A                                  6 26'(1.新增 2.修改'
     A                                  6 45'4.刪除'
     A                                  6 55'5.查詢)'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------- '
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*  92/09/22  11:08:51    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD2
     A*%%TS  SD  19941114  151303  REPGMR      REL-V2R2M0  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF19(19)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA1E0.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'應付票據開票登錄'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHF02     R        O  4 13REFFLD(HF02)
     A            DHF02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'開票日期:'
     A            DHF06     R   -1   O  5 13REFFLD(HF06)
     A                                      EDTWRD('   /  /  ')
     A                                  5 25'開票序號:'
     A            DHF71     R        O  5 37REFFLD(HF71)
     A                                      EDTCDE(4)
     A                                  6  1'作業選擇:'
     A            DOPT           1Y 0O  6 13EDTCDE(4)
     A                                  6 26'(1.新增 2.修改'
     A                                  6 45'4.刪除'
     A                                  6 55'5.查詢)'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  1'廠商代號:'
     A                                      DSPATR(HI)
     A            DHF04     R        B  8 13REFFLD(HF04)
     A N99                                  DSPATR(HI)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DHF04C        42O  B  8 24
     A*                                 8 56'票據號碼:'
     A            DHF03         10A  O  8 69
     A                                  9  1'票據類別:'
     A                                      DSPATR(HI)
     A            DHF05     R        B  9 13REFFLD(HF05)
     A N99                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DHF05C        10O  O  9 15
     A                                  9 26'( 1本票 3支票 4保証票 5商+
     A                                      業本票)'
     A                                 10  1'開票日　:'
     A            DHF06B    R   -1   O 10 13REFFLD(HF06)
     A                                      EDTWRD('   /  /  ')
     A                                 10 39'到期日  :'
     A            DHF07     R   -1   B 10 51REFFLD(HF07)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  1'付款帳號:'
     A                                      DSPATR(HI)
     A            DHF09N         6A  B 11 13
     A N99                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            HD02      R        O 11 20REFFLD(HD02)
     A                                 11 39'付款銀行:'
     A            DHF08     R        O 11 51REFFLD(HF08)
     A            HB03N     R        O 11 59REFFLD(HB03)
     A                                 12  1'票面金額:'
     A            DHF10     R        B 12 13REFFLD(HF10)
     A                                      EDTCDE(2)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 12 39'預兌日期:'
     A            DHF11     R   -1   B 12 51REFFLD(HF11)
     A                                      EDTWRD('   /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  1'來源用途:'
     A                                      DSPATR(HI)
     A            DHF23     R        B 13 13REFFLD(HF23)
     A N99                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DHF23C        10O  O 13 16
     A                                 13 39'列印票類:'
     A            DHF76     R        B 13 51REFFLD(HF75)
     A                                      VALUES('1' '3')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 53'( 1本票 3支票)'
     A                                 14  1'票據狀況:'
     A            DHF17     R        O 14 13REFFLD(HF17)
     A            DHF17C         6O  O 14 15
     A                                 14 23'( 0作廢 2託收 4兌現)'
     A                                 15  1'處理日期:'
     A            DHF15     R   -1   O 15 13REFFLD(HF15)
     A                                      EDTWRD('   /  /  ')
     A                                 15 39'處理帳號:'
     A            DHF16     R        O 15 51REFFLD(HF16)
     A                                 16  1'列印日期:'
     A            DHF72     R   -1   O 16 13REFFLD(HF72)
     A                                      EDTWRD('   /  /  ')
     A                                 16 39'列印序號:'
     A            DHF73     R        O 16 51REFFLD(HF73)
     A                                      EDTCDE(3)
     A                                 17  1'加印抬頭:'
     A            DHF74     R        B 17 13REFFLD(HF74)
     A                                      VALUES('Y' 'N')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 15'( Y是 N否)'
     A                                 17 39'轉帳票　:'
     A            DHF75     R        O 17 51REFFLD(HF75)
     A                                 17 53'( Y是 N否)'
     A                                 18  1'貨款記號:'
     A            DHF28     R        B 18 13REFFLD(HF28)
     A                                      VALUES('Y' 'N')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 15'( Y是 N否)'
     A                                 18 39'入帳類別:'
     A            DHF27     R        B 18 51REFFLD(HF27)
     A                                      VALUES('Y' 'N')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 53'( Y是 N否)'
     A                                 19  1'備　　註:'
     A            DHF32     R        B 19 13REFFLD(HF32)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F4 =欄位查詢'
     A                                 23 44'F5 =參考資料'
     A                                 23 61'F12=回前畫面'
     A                                 24 11'F19=廠商查詢'
     A                                 24 67'【泛太資訊】'
