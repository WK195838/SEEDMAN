     A*%%TS  SD  19930210  203634  REPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKI1D0                                     *
     A*    PROGRAM NAME : 帳戶資料全頁查詢                         *
     A*    AUTHOR       : A1152    ANGY                              *
     A*    CREATE DATE  : 1992.09.17                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DOPT           1   B  8  3DSPATR(CS PC HI)
     A            HD04      R        O  8  7REFFLD(HD04)
     A            HD02      R        O  8 14REFFLD(HD02)
     A            HB03      R        O  8 29REFFLD(HB03)
     A            D#A03         10   O  8 50
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19930210  203634  REPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0052)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<CKI1D0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'帳戶資料全頁查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHD01     R        B  4 13REFFLD(HD01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DHD01N    R        O  4 16REFFLD(#B03 RERF)
     A                                  6  3'S   簡稱   銀行帳號   銀行名+
     A                                      稱           帳戶種類'
     A                                      DSPATR(UL)
     A            DHD04     R        B  7  7REFFLD(HD04)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3=回主畫面'
     A                                 23 27'F12=回前畫面'
     A                                 23 44'PA1=下一頁'
     A                                 23 59'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
