     A*%%TS  SD  19930501  151355  TSPGMR      REL-V2R2M0  5738-PW1
     A*  92/10/09  10:56:34    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKI210                                     *
     A*    PROGRAM NAME :應收票據資料查詢                          *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.09.29                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*  92/10/09  10:56:34    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD1
     A*%%TS  SD  19930210  174642  REPGMR      REL-V2R1M1  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKI210.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'應收票據資料查詢'
     A                                      DSPATR(RI)
     A                                  5 21'***查詢範圍:'
     A                                  6 24'公司別　:'
     A            DHA02     R        B  6 36REFFLD(HA02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DHA02N    R        O  6 39REFFLD(#B03 RERF)
     A                                  7 24'客戶代號:'
     A                                      DSPATR(HI)
     A            DHA04     R        B  7 36REFFLD(HA04)
     A  88
     AO 61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A            D#C03         10A  O  7 47
     A                                  8 24'收票日　:'
     A            DHA06F         7Y 0B  8 36EDTWRD('   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 46'-'
     A            DHA06T         7Y 0B  8 48EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 24'到期日　:'
     A            DHA07F         7Y 0B  9 36EDTWRD('   /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 46'-'
     A            DHA07T         7Y 0B  9 48EDTWRD('   /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 10 24'票據狀況:'
     A                                      DSPATR(HI)
     A            DHA17     R        B 10 36REFFLD(HA17)
     A                                      VALUES(' ' '1' '2' '3' '4' '5')
     A N99                                  DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 10 38'( 0作廢 1庫存 2託收'
     A                                 11 40'3貼現 4兌現 5退票)'
     A                                 12 24'地區別　:'
     A            DHA24     R        B 12 36REFFLD(HA24)
     A                                      VALUES(' ' '1' '2')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 12 38'( 1本地 2外地)'
     A                                 13 24'入帳類別:'
     A            DHA27     R        B 13 36REFFLD(HA27)
     A                                      VALUES(' ' 'Y' 'N')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                 13 38'( Y是   N否)'
     A                                 15 21'***排序方式:'
     A            DSORT          1A  B 15 36VALUES('1' '2')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                 16 25'1.收票日＋到期日＋票據號碼'
     A                                 17 25'2.到期日＋收票日＋票據號碼'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*  92/10/09  10:56:34    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPC2
     A*%%TS  SD  19930210  174642  REPGMR      REL-V2R1M1  5738-PW1
     A                                  1  2'<CKI210.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'應收票據資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        O  4 13REFFLD(HA02)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  4 39'收票日　:'
     A            DHA06F         7Y 0O  4 51EDTWRD('   /  /  ')
     A                                  4 61'-'
     A            PHA06T         7Y 0O  4 63EDTWRD('   /  /  ')
     A                                  5  1'客戶代號:'
     A            DHA04     R        O  5 13REFFLD(HA04)
     A            D#C03         10A  O  5 24
     A                                  5 39'到期日　:'
     A            DHA07F         7Y 0O  5 51EDTWRD('   /  /  ')
     A                                  5 61'-'
     A            PHA07T         7Y 0O  5 63EDTWRD('   /  /  ')
     A                                  6  1'票據狀況:'
     A            DHA17     R        O  6 13REFFLD(HA17)
     A            D#A031         6A  O  6 15
     A                                  6 39'地區別　:'
     A            DHA24     R        O  6 51REFFLD(HA24)
     A                                  6 53'( 1本地 2外地)'
     A                                  7 39'入帳類別:'
     A            DHA27     R        O  7 51REFFLD(HA27)
     A                                  7 53'( Y是   N否)'
     A                                  8  2'( KEY :'
     A            DKEY           8A  O  8 10
     A                                  8 19')'
     A                                  9  2'S  收票日 客戶名稱 銀行名稱 -
     A                                      票據號碼 票況　票類  到期日  -
     A                                      票據金額'
     A                                      DSPATR(UL)
     A*  92/10/09  10:56:34    TSPGMR      REL-R03M00  5728-PW1
     A          R SFLSR1                    SFL
     A*%%TS  SD  19921021  172629  TSPGMR      REL-V2R1M1  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4Y 0H
     A            DOPT           1A  B 10  2
     A            DHA06          7Y 0O 10  4EDTWRD('   /  /  ')
     A            D#C031        10   O 10 14
     A            DHB02     R        O 10 25REFFLD(HB02)
     A            DHA03     R        O 10 36REFFLD(HA03)
     A            S#A03          6A  O 10 47
     A            #A03S          4A  O 10 54
     A            DHA07          7Y 0O 10 59EDTWRD('   /  /  ')
     A            DHA10     R        O 10 69REFFLD(HA10)
     A                                      EDTCDE(2)
     A            HA01      R        H
     A            HA02      R        H
     A            HA16      R        H
     A            HA17      R        H
     A            HA11      R        H
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*  92/10/09  10:56:34    TSPGMR      REL-R03M00  5728-PW1
     A*
     A                                      SFLSIZ(0022)
     A                                      SFLPAG(0011)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A N53                             21 59'       '
     A  53                             21 59'合計:'
     A  53        DSUM          10Y 0O 21 67EDTCDE(2)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F12=回前畫面'
     A                                 23 44'PA1=下一頁'
     A                                 23 59'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
     A*  92/10/09  10:56:34    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD3
     A*%%TS  SD  19930501  151355  TSPGMR      REL-V2R2M0  5738-PW1
     A                                      CA03(03)
     A                                      CF12(12)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKI210.3>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'應收票據資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        O  4 13REFFLD(HA02)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'票據號碼:'
     A            DHA03     R        O  5 13REFFLD(HA03)
     A                                  6  1'作業功能:明細查詢'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------'
     A                                  8  1'客戶代號:'
     A            DHA042    R        O  8 13REFFLD(HA04)
     A            D#C032        10A  O  8 20
     A                                  9  1'票據類別:'
     A            DHA05     R        O  9 13REFFLD(HA05)
     A            #A03D1        10A  O  9 15
     A                                  9 26'( 1本票 2客票 3支票'
     A                                  9 52'4保証票 5商業本票)'
     A                                 10  1'收票日　:'
     A            DHA06          7Y 0O 10 13EDTWRD('   /  /  ')
     A                                 10 44'到期日　:'
     A            DHA07          7Y 0O 10 56EDTWRD('   /  /  ')
     A                                 11  1'付款銀行:'
     A            DHA08     R        O 11 13REFFLD(HA08)
     A            DHB031    R        O 11 21REFFLD(HB03)
     A                                 11 44'付款帳號:'
     A            DHA09     R        O 11 56REFFLD(HA09)
     A                                 12  1'票面金額:'
     A            DHA10     R        O 12 13REFFLD(HA10)
     A                                      EDTCDE(2)
     A                                 12 44'預兌日期:'
     A            DHA11     R   -1   O 12 56REFFLD(HA11)
     A                                      EDTWRD('   /  /  ')
     A                                 13  1'業務人員:'
     A            DHA13     R        O 13 13REFFLD(HA13)
     A            D#E03     R        O 13 24REFFLD(MC02 RERF)
     A            DHA14          4   O 13 39
     A                                 13 44'票據區　:'
     A            DHA242         1A  O 13 56
     A                                 13 58'( 1本地 2外地)'
     A                                 14  1'來源用途:'
     A            DHA23     R        O 14 13REFFLD(HA23)
     A            DHA23C        10A  O 14 16
     A                                 15  1'票據狀況:'
     A            DHA172    R        O 15 13REFFLD(HA17)
     A            D#A032         6A  O 15 15
     A                                 15 22'( 0作廢 1庫存 2託收'
     A                                 15 48'3貼現 4兌現'
     A                                 15 64'5退票 6抽票 )'
     A                                 16  1'處理日期:'
     A            DHA15     R   -1   O 16 13REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A                                 16 44'處理帳號:'
     A            DHA16     R        O 16 56REFFLD(HA16)
     A                                 17  1'貨款記號:'
     A            DHA28     R        O 17 13REFFLD(HA28)
     A                                 17 15'( Y是 N否)'
     A                                 17 44'未沖金額:'
     A            DHA55     R        O 17 56REFFLD(HA55)
     A                                      EDTCDE(2)
     A                                 18  1'入帳類別:'
     A            DHA27     R        O 18 13REFFLD(HA27)
     A                                 18 15'( Y是 N否)'
     A                                 18 44'抽票補票:'
     A            DHA58     R        O 18 56REFFLD(HA58)
     A                                 19  1'對帳記號:'
     A            DHA26     R        O 19 13REFFLD(HA26)
     A                                 19 15'( Y是 N未)'
     A                                 19 42'對應原票號:'
     A            DHA57     R        O 19 56REFFLD(HA57)
     A                                 20  1'備　　註:'
     A            DHA32     R        O 20 13REFFLD(HA32)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
