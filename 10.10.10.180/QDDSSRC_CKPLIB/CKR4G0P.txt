     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR4G0                                     *
     A*    PROGRAM NAME :登錄票據明細表                            *
     A*    AUTHOR       : A1062  JEANNY                              *
     A*    CREATE DATE  : 1993.04.10                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A                                      REF(CKRF)
     A          R PH1                       SKIPB(2)
     A            PCONAM        32A        1
     A                                    51'日期:'
     A            DDATE          6  0     59EDTWRD('  /  /  ')
     A                                    70'頁次:'
     A                                    78PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'登錄票據明細表'
     A                                    51'時間:'
     A                                    59TIME
     A                                      EDTWRD('  :  :  ')
     A                                    72'<CKR4G0P>'
     A                                      SPACEA(2)
     A                                     1'公司別　:'
     A            DHF02     R             13REFFLD(HF02)
     A                                    20'開票日:'
     A            DHF06F         7  0     30EDTWRD('   /  /  ')
     A                                    40'-'
     A            DHF06T         7  0     42EDTWRD('   /  /  ')
     A                                    53'開票序號:'
     A            DHF71F    R             65REFFLD(HF71) EDTCDE(4)
     A                                    70'-'
     A            DHF71T    R             72REFFLD(HF71) EDTCDE(4)
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     4'開票日'
     A                                    13'序號　票據號碼'
     A                                    30'廠商名稱'
     A                                    41'銀行簡稱'
     A                                    52'票類'
     A                                    61'到期日'
     A                                    71'票據金額'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ===================='
     A*
     A          R PD1                       SPACEB(1)
     A            HF06N     R   -1   O     4REFFLD(HF06)
     A                                      EDTWRD('   /  /  ')
     A            HF71      R        O    14EDTCDE(4)
     A            HF03      R        O    19
     A            PHF04     R        O    30REFFLD(#D03 PTRF)
     A            HB02      R        O    41
     A            HF05N          6A  O    52
     A            HF07N     R   -1   O    59REFFLD(HF07)
     A                                      EDTWRD('   /  /  ')
     A            HF10      R        O    69EDTCDE(2)
     A*
     A          R PT1                       SPACEB(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ===================='
     A                                      SPACEA(1)
     A            PT11          10  0O    67EDTCDE(2)
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ===================='
     A*
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
