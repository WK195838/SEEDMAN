     A*%%TS  SD  19930501  150630  TSPGMR      REL-V2R2M0  5738-PW1
     A*  92/09/30  09:10:17    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKA1A0                                     *
     A*    PROGRAM NAME :票據狀況更正                              *
     A*    AUTHOR       : A1149   ANGY                               *
     A*    CREATE DATE  : 1992.09.25                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A*==============================================================
     A* CODE | AUTHOR |  DATE  | DESCRIPTION
     A*--------------------------------------------------------------
     A* M001 |MICHELLE|03.04.15|票據不存在於收款沖帳檔時,不允許修正
     A*--------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A          R DSPD1
     A*  92/09/30  09:10:17    TSPGMR      REL-R03M00  5728-PW1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<CKA1A0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2 33'票據狀況更正'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司別　:'
     A            DHA02     R        B  4 13REFFLD(HA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'票據號碼:'
     A            DHA03     R        B  5 13REFFLD(HA03)
     A  88
     AO 62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6  1'票據性質:'
     A            DHA01     R        B  6 13REFFLD(HA01)
     A                                      VALUES('1' '2')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  6 15'( 1應收 2應付)'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F18=全頁查詢'
     A                                 24 67'【寰盛洋酒】'
     A          R DSPD2
     A*%%TS  SD  19930501  150630  TSPGMR      REL-V2R2M0  5738-PW1
     A                                      CA03(03)
     A                                      CF12(12)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA1A0.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'票據狀況更正'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        O  4 13REFFLD(HA02)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'票據號碼:'
     A            DHA03     R        O  5 13REFFLD(HA03)
     A                                  6  1'票據性質:'
     A            DHA01     R        O  6 13REFFLD(HA01)
     A                                  6 15'( 1應收 2應付)'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  1'客戶代號:'
     A            DHA04     R        O  8 13REFFLD(HA04)
     A            D#C03     R        O  8 24REFFLD(#C03 PTRF)
     A                                  9  1'票據類別:'
     A            DHA05     R        O  9 13REFFLD(HA05)
     A            DHA05C        10A  O  9 15
     A                                  9 26'( 1本票 2客票 3支票'
     A                                  9 52'4保証票 5商業本票)'
     A                                 10  1'收票日　:'
     A            DHA06     R   -1   O 10 13REFFLD(HA06)
     A                                      EDTWRD('   /  /  ')
     A                                 10 44'到期日　:'
     A            DHA07     R   -1   O 10 56REFFLD(HA07)
     A                                      EDTWRD('   /  /  ')
     A                                 11  1'付款銀行:'
     A            DHA08     R        O 11 13REFFLD(HA08)
     A            DHB03     R        O 11 21REFFLD(HB03)
     A                                 11 44'付款帳號:'
     A            DHA09     R        O 11 56REFFLD(HA09)
     A                                 12  1'票面金額:'
     A            DHA10     R        O 12 13REFFLD(HA10)
     A                                      EDTCDE(2)
     A                                 12 44'預計兌現:'
     A            DHA11     R   -1   O 12 56REFFLD(HA11)
     A                                      EDTWRD('   /  /  ')
     A                                 13  1'業務人員:'
     A            DHA13     R        O 13 13REFFLD(HA13)
     A            D#E03     R        O 13 24REFFLD(#E03 PTRF)
     A            DHA14          4   O 13 39
     A                                 13 44'票據區　:'
     A            DHA24          1A  O 13 56
     A                                 13 58'( 1本地 2外地)'
     A                                 14  1'來源用途:'
     A            DHA23     R        O 14 13REFFLD(HA23)
     A            DHA23C        10O  O 14 16
     A                                 15  1'票據狀況:'
     A            DHA17     R        B 15 13REFFLD(HA17)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DHA17C         6O  O 15 15
     A                                 15 22'( 0作廢 1庫存 2託收'
     A                                 15 48'3貼現 4兌現'
     A                                 15 64'5退票 6抽票 )'
     A                                 16  1'處理日期:'
     A            DHA15     R   -1   O 16 13REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A                                 16 44'處理帳號:'
     A            DHA16     R        O 16 56REFFLD(HA16)
     A                                 17  1'貨款記號:'
     A            DHA28     R        O 17 13REFFLD(HA28)
     A                                 17 15'( Y是 N否)'
     A                                 17 44'未沖金額:'
     A            DHA55     R        O 17 56REFFLD(HA55)
     A                                      EDTCDE(2)
     A                                 18  1'入帳類別:'
     A            DHA27     R        O 18 13REFFLD(HA27)
     A                                 18 15'( Y是 N否)'
     A                                 18 44'抽票補票:'
     A            DHA58     R        O 18 56REFFLD(HA58)
     A                                 19  1'對帳記號:'
     A            DHA26     R        O 19 13REFFLD(HA26)
     A                                 19 15'( Y是 N未)'
     A                                 19 42'對應原票號:'
     A                                      DSPATR(HI)
     A            DHA57     R        O 19 56REFFLD(HA57)
     A                                 20  1'備　　註:'
     A            DHA32     R        O 20 13REFFLD(HA32)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F12=回前畫面'
M001AA  80                             24  2'本票據不存在於收款檔! +
M001AA                                      不可修改.'
  "  A  80                                  DSPATR(HI)
     A                                 24 67'【寰盛洋酒】'
     A          R DSPD3
     A*%%TS  SD  19930217  094108  REPGMR      REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF12(12)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA1A0.3>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'票據狀況更正'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        O  4 13REFFLD(HA02)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'票據號碼:'
     A            DHA03     R        O  5 13REFFLD(HA03)
     A                                  6  1'票據性質:'
     A            DHA01     R        O  6 13REFFLD(HA01)
     A                                  6 15'( 1應收 2應付)'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  1'廠商代號:'
     A            DHA04     R        O  8 13REFFLD(HA04)
     A            D#D03     R        O  8 24REFFLD(MB03 RERF)
     A                                  9  1'票據類別:'
     A            DHA05     R        O  9 13REFFLD(HA05)
     A            DHA05C        10A  O  9 15
     A                                  9 26'( 1本票 3支票 4保証票 5商+
     A                                      業本票)'
     A                                 10  1'開票日　:'
     A            DHA06     R   -1   O 10 13REFFLD(HA06)
     A                                      EDTWRD('   /  /  ')
     A                                 10 39'到期日  :'
     A            DHA07     R   -1   O 10 51REFFLD(HA07)
     A                                      EDTWRD('   /  /  ')
     A                                 11  1'付款帳號:'
     A            DHA09N         6A  O 11 13
     A            HD02      R        O 11 20REFFLD(HD02)
     A                                 11 39'付款銀行:'
     A            DHA08     R        O 11 51REFFLD(HA08)
     A            HB03N     R        O 11 59REFFLD(HB03)
     A                                 12  1'票面金額:'
     A            DHA10     R        O 12 13REFFLD(HA10)
     A                                      EDTCDE(2)
     A                                 12 39'預兌日期:'
     A            DHA11     R   -1   O 12 51REFFLD(HA07)
     A                                      EDTWRD('   /  /  ')
     A                                 13  1'來源用途:'
     A            DHA23     R        O 13 13REFFLD(HA23)
     A            DHA23C        10O  O 13 16
     A                                 14  1'票據狀況:'
     A            DHA17     R        B 14 13REFFLD(HA17)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#Y03          6A  O 14 15
     A                                 14 22'( 0作廢 2託收 4兌現)'
     A                                 15  1'處理日期:'
     A            DHA15     R   -1   O 15 13REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A                                 15 39'處理帳號:'
     A            DHA16     R        O 15 51REFFLD(HA16)
     A                                 16  1'貨款記號:'
     A            DHA28     R        O 16 13REFFLD(HA28)
     A                                 16 15'( Y是 N否)'
     A                                 17  1'入帳類別:'
     A            DHA27     R        O 17 13REFFLD(HA27)
     A                                 17 15'( Y是 N否)'
     A                                 18  1'對帳記號:'
     A            DHA26     R        O 18 13REFFLD(HA26)
     A                                 18 15'( Y是 N未)'
     A                                 19  1'備　　註:'
     A            DHA32     R        O 19 13REFFLD(HA32)
     A*
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F12=回前畫面'
M001AA  80                             24  2'本票據不存在於收款檔! +
M001AA                                      不可修改.'
  "  A  80                                  DSPATR(HI)
     A                                 24 67'【寰盛洋酒】'
