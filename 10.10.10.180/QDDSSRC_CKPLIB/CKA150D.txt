     A*%%TS  SD  19951011  131548  DERLERN     REL-V3R1M0  5763-PW1
     A*  92/09/29  14:29:57    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKA150                                     *
     A*    PROGRAM NAME :票據兌現作業                              *
     A*    AUTHOR       : A1150                                      *
     A*    CREATE DATE  : 92/09/29                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A*  92/09/29  14:19:51    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD1
     A*%%TS  SD  19930210  112607  REPGMR      REL-V2R1M1  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF09(09)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA150.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'票據兌現作業'
     A                                      DSPATR(RI)
     A                                  4 24'公司別　:'
     A            DHA02     R        B  4 36REFFLD(HA02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DHA02N    R        O  4 39REFFLD(#B03 RERF)
     A                                  6 24'類　　別:'
     A            DHA01     R        B  6 36REFFLD(HA01)
     A  88
     AO 61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  6 38'( 1應收 2應付)'
     A                                  7 24'銀行帳號:'
     A                                      DSPATR(HI)
     A            DHA16          6A  B  7 36
     A N99                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 24'作業日期:'
     A            DHA15          7Y 0B  8 36EDTWRD('   /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 24'託收日期:'
     A            DHA15S         7Y 0B  9 36EDTWRD('   /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                  9 45'-'
     A            DHA15E         7Y 0B  9 47EDTWRD('   /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 10 24'預兌日期:'
     A            DHA11          7Y 0B 10 36EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 10 46'前'
     A                                 11 24'入帳類別:'
     A            DHA27     R        B 11 36REFFLD(HA27 CKRF)
     A                                      VALUES(' ' 'Y' 'N')
     A                                 11 38'( Y是 N否)'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 23 45'F9 =列印作業'
     A                                 24 67'【泛太資訊】'
     A*  92/09/29  14:19:51    TSPGMR      REL-R03M00  5728-PW1
     A          R SFLSR                     SFL
     A*%%TS  SD  19951011  131548  DERLERN     REL-V3R1M0  5763-PW1
     A            RRN            3Y 0H
     A            HA06      R        H      REFFLD(HA06)
     A            HA07      R        H      REFFLD(HA07)
     A            HA04      R        H      REFFLD(HA04)
     A            HA17      R        H      REFFLD(HA17)
M001AA            HA28      R        H      REFFLD(HA28)
M001AA            HA58      R        H      REFFLD(HA58)
     A            HD02      R        H      REFFLD(HD02)
     A            DOPT           1A  B  9  3DSPATR(CS)
     A                                      VALUES('X' ' ')
M001MA*                                     DSPATR(PC)
  "  A*                                     DSPATR(HI)
  "  A  80                                  DSPATR(PC)
  "  A  80                                  DSPATR(HI)
     A            HA15N          6Y 0O  9  5EDTCDE(Y)
     A            HA16N          6A  O  9 14
     A            HA04N         10A  O  9 23
     A            HA03          10A  O  9 36
     A            HA06N     R   -1   O  9 48REFFLD(HA06)
     A                                      EDTWRD('   /  /  ')
     A            HA07N     R   -1   O  9 59REFFLD(HA07)
     A                                      EDTWRD('   /  /  ')
     A            HA10      R        O  9 70REFFLD(HA10)
     A                                      EDTCDE(2)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*  92/09/29  14:29:57    TSPGMR      REL-R03M00  5728-PW1
     A                                      SFLSIZ(0052)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF14(14)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                 21 55'合計:'
     A            DSUM          10Y 0O 21 63EDTCDE(2)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3=回主畫面'
     A                                 23 25'F12=回前畫面'
     A                                 23 41'F14=確認'
     A                                 23 53'PA1=下一頁'
     A                                 23 67'PA2=上一頁'
M001AA  80                             24  2'票據'
  "  A  80                                  DSPATR(HI)
  "  A  80        HA03          10A       +1
  "  A  80                                  DSPATR(HI)
  "  A  80                                +1'不存在於收款檔!'
  "  A  80                                  DSPATR(HI)
M001AA  80                                +1'不可執行兌現.'
  "  A  80                                  DSPATR(HI)
     A                                 24 67'【泛太資訊】'
     A*  92/09/29  14:29:57    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPC1
     A*%%TS  SD  19951011  131548  DERLERN     REL-V3R1M0  5763-PW1
     A                                  1  2'<CKA150.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'票據兌現作業'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        O  4 13REFFLD(HA02)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  4 39'作業日期:'
     A            DHA15          7Y 0O  4 51EDTWRD('   /  /  ')
     A                                  5  1'銀行名稱:'
     A            HB03N1    R        O  5 13REFFLD(HB03)
     A                                  5 39'預兌日期:'
     A            PHA11          7Y 0O  5 51EDTWRD('   /  /  ')
     A                                  5 61'前'
     A                                  6  1'銀行帳號:'
     A            HD02N     R        O  6 13REFFLD(HD02)
     A                                  6 39'類　　別:'
     A            DHA01     R        O  6 51REFFLD(HA01)
     A                                  6 53'( 1應收 2應付)'
     A                                  7  2'X=不兌現'
     A                                  8  3'X'
     A                                  8  5'託收日'
     A                                  8 14'帳號'
     A                                  8 23'客戶／廠商'
     A                                  8 36'票據號碼'
     A                                  8 48'收開票日'
     A                                  8 59'到期日'
     A                                  8 70'票據金額'
