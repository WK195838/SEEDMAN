     A*%%TS  SD  19820420  152553  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR4G0                                     *
     A*    PROGRAM NAME :登錄票據明細表                            *
     A*    AUTHOR       : A1062  JEANNY                              *
     A*    CREATE DATE  : 1993.04.20                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19820420  152553  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKR4G0>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 32'登錄票據明細表'
     A                                      DSPATR(RI)
     A                                  5 24'公司別　:'
     A            DHF02     R        B  5 36REFFLD(HF02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DHF02N    R        O  5 39REFFLD(#B03 RERF)
     A                                  7 24'開票日期:'
     A            DHF06F         7Y 0B  7 36EDTWRD('   /  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 46'-'
     A            DHF06T         7Y 0B  7 48EDTWRD('   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 24'開票序號:'
     A            DHF71F    R    4Y 0B  9 36REFFLD(HF71) EDTCDE(4)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 41'-'
     A            DHF71T    R    4Y 0B  9 43REFFLD(HF71) EDTCDE(4)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 18 24'執行環境:'
     A            $PENV          1A  B 18 36VALUES('1' '2')
     A                                 18 39'(1-即時 2-批次)'
     A                                 19 24'列印份數:'
     A            $CPY           3Y 0B 19 36EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20 24'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 36
     A                                      DSPATR(HI)
     A  27                             21 30'請準備８０格報表紙'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 27'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
