     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR420                                     *
     A*    PROGRAM NAME :應付票據明細表－按銀行帳號                *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.10.05                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A                                      REF(CKRF)
     A          R PH1                       SKIPB(2)
     A            PCONAM    R              1REFFLD(#B02 RERF)
     A                                    77'日期:'
     A            PDATE          6  0     85EDTCDE(Y)
     A                                    96'頁次:'
     A                                   104PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'應付票據明細表－按銀行帳號'
     A                                    77'時間:'
     A                                    87TIME
     A                                      EDTWRD('  :  :  ')
     A                                    98'<CKR423P>'
     A                                      SPACEA(2)
     A                                     1'公司別　:'
     A            HA02      R             13REFFLD(HA02)
     A                                    40'收票日:'
     A            PHA06F         7  0     50EDTWRD('   /  /  ')
     A                                    60'-'
     A            PHA06T         7  0     62EDTWRD('   /  /  ')
     A                                    78'票據狀況:'
     A            P#A03          6        90
     A                                      SPACEA(1)
     A                                     1'客戶代號:'
     A            DHA04F    R             13REFFLD(HA04)
     A                                    24'-'
     A            DHA04T    R             26REFFLD(HA04)
     A                                    40'到期日:'
     A            PHA07F         7  0     50EDTWRD('   /  /  ')
     A                                    60'-'
     A            PHA07T         7  0     62EDTWRD('   /  /  ')
     A                                    78'票據種類:'
     A            PHA05C        10        90
     A                                      SPACEA(1)
     A                                     1'銀行帳號:'
     A            DHA16F         6        13
     A                                    20'-'
     A            DHA16T         6        22
     A                                    40'地區別:'
     A            PHA24A         6        50
     A                                    57'入帳類別:'
     A            PHA27C         4        69
     A                                    78'來源用途:'
     A            PHA23C        10        90
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==='
     A                                      SPACEA(1)
     A                                     1'銀行名稱'
     A                                    22'銀行帳號'
     A                                    39'開票日　客戶簡稱'
     A                                    58'票據號碼'
     A                                    71'到期日'
     A                                    80'票據金額　票類　票況　來源用途'
     A                                   112'地區'
     A                                   118'入帳'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==='
     A*
     A          R PD1                       SPACEB(1)
     A  31        PHB03         20O        1
     A  31        HA16      R             22
     A            PHA06          7  0     37EDTWRD('   /  /  ')
     A            P#D03         10O       47
     A            HA03      R             58
     A            PHA07          7  0     69EDTWRD('   /  /  ')
     A            HA10      R             79EDTCDE(2)
     A            PHA05          4O       91
     A            PHA17          6O       96
     A            PHA23         10O      102
     A            PHA24          4O      113
     A            PHA27          4O      119
     A*
     A          R PT1                       SPACEB(1)
     A                                     4'小計:'
     A            PT11          10  0     77EDTCDE(2)
     A                                      SPACEA(1)
     A                                     1' '
     A*
     A          R PT2                       SPACEB(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==='
     A                                      SPACEA(1)
     A                                     1'合計張數:'
     A            PT22           5  0     84EDTCDE(2)
     A                                      SPACEA(1)
     A                                     1'合計金額:'
     A            PT21          10  0     77EDTCDE(2)
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==='
     A*
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
