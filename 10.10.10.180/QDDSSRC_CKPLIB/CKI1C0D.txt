     A*%%TS  SD  19930210  214645  REPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKI1C0                                     *
     A*    PROGRAM NAME : 銀行資料全頁查詢                         *
     A*    AUTHOR       : A1149    MAY                               *
     A*    CREATE DATE  : 1992.09.15                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19930210  214645  REPGMR      REL-V2R1M1  5738-PW1
     A            RRN            3Y 0H
     A            DOPT           1A  B  6  6DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            HB01      R        O  6 10REFFLD(HB01)
     A            HB02      R        O  6 20REFFLD(HB02)
     A            HB03      R        O  6 33REFFLD(HB03)
     A            HB08      R        O  6 56REFFLD(HB08)
     A            HB11      R        O  6 71REFFLD(HB11)
     A            HB09      R        O  6 76REFFLD(HB09)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19930210  214645  REPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0070)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<CKI1C0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'銀行資料全頁查詢'
     A                                      DSPATR(RI)
     A                                  3 69'交易　往'
     A                                  4  6'S  銀行代號 銀行簡稱   銀行+
     A                                      名稱             聯絡人     +
     A                                      天數　來'
     A                                      DSPATR(UL)
     A            DHB01     R        B  5 10REFFLD(HB01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F12=回前畫面'
     A                                 23 44'PA1=下一頁'
     A                                 23 59'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
