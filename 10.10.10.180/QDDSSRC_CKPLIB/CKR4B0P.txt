     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR4B0                                     *
     A*    PROGRAM NAME :銀行調節表                                *
     A*    AUTHOR       : A1149   HUANG-SHU.MEI                      *
     A*    CREATE DATE  : 1993.02.24                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A                                      REF(CKRF)
     A          R PH1                       SKIPB(2)
     A            PCONAM    R              1REFFLD(#B02 RERF)
     A                                    77'日期:'
     A            PDATE          6  0     85EDTCDE(Y)
     A                                    94'頁次:'
     A                                   102PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'銀行調節表'
     A                                    77'時間:'
     A                                    85TIME
     A                                      EDTWRD('  :  :  ')
     A                                    96'<CKR4B0P>'
     A                                      SPACEA(2)
     A                                     1'公司別:'
     A            DHC01     R             11REFFLD(HC01)
     A                                    16'銀行名稱:'
     A            PHB03     R             28REFFLD(HB03)
     A                                    50'銀行帳號:'
     A            PHD02     R             62REFFLD(HD02)
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================='
     A                                      SPACEA(1)
     A                                     1'銀行　帳面__________________+
     A                                       收入類_____________________'
     A                                    61'__________________+
     A                                       支出類____________________'
     A                                      SPACEA(1)
     A                                     1'記錄　記錄'
     A                                    13'異動日期　客戶名稱　　票據號碼'
     A                                    50'異動金額'
     A                                    61'異動日期　廠商名稱　　票據號碼'
     A                                    98'異動金額'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================='
     A*
     A          R PD1                       SPACEB(1)
     A            PBANK          4         2
     A            PCOMP          4         8
     A            PIDATE    R   -1        13REFFLD(HC03)
     A                                      EDTWRD('   /  /  ')
     A            P#C           10O       23
     A            PICKNO    R             34REFFLD(HC05)
     A            PIAMT         11  2     45EDTCDE(K)
     A            PODATE    R   -1        61REFFLD(HC03)
     A                                      EDTWRD('   /  /  ')
     A            P#D           10O       71
     A            POCKNO    R             82REFFLD(HC05)
     A            POAMT         11  2     93EDTCDE(K)
     A*
     A          R PT1                       SPACEB(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================='
     A*
     A          R PE9                       SPACEB(1)
     A                                    16'無符合列印範圍之資料'
