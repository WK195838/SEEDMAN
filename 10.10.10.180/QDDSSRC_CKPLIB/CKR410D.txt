     A*%%TS  SD  19951205  165010  D910074     REL-V3R1M0  5763-PW1
     A*  92/10/09  11:01:50    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR410                                     *
     A*    PROGRAM NAME :應收票據明細表                            *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.10.02                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*  92/10/09  11:01:50    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD1
     A*%%TS  SD  19951205  163929  D910074     REL-V3R1M0  5763-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKR410>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'應收票據明細表'
     A                                      DSPATR(RI)
     A                                  4 15'列印範圍'
     A                                  5  5'**********************************-
     A                                      *'
     A                                  6  4'公司別　:'
     A            DHA02     R        B  6 16REFFLD(HA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DHA02N    R        O  6 19REFFLD(#B03 RERF)
     A                                  7  4'地　區  :'
     A            DHA59     R        B  7 16REFFLD(HA59)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A                                  7 18'(1-北 2-中 3南 " "-ALL)'
     A                                  8  4'客戶代號:'
     A                                      DSPATR(HI)
     A            DHA04F    R        B  8 16REFFLD(HA04)
     A  88
     AO 62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A                                  8 27'-'
     A            DHA04T    R        B  8 29REFFLD(HA04)
     A N99                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9  4'銀行帳號:'
     A                                      DSPATR(HI)
     A            DHA16F         6A  B  9 16
     A N99                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 27'-'
     A            DHA16T         6A  B  9 29
     A N99                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 10  4'收票日　:'
     A            DHA06F         7Y 0B 10 16EDTWRD('   /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 10 27'-'
     A            DHA06T         7Y 0B 10 29EDTWRD('   /  /  ')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 11  4'到期日　:'
     A            DHA07F         7Y 0B 11 16EDTWRD('   /  /  ')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                 11 27'-'
     A            DHA07T         7Y 0B 11 29EDTWRD('   /  /  ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                 12  4'票據狀況:'
     A                                      DSPATR(HI)
     A            DHA17     R        B 12 16REFFLD(HA17)
     A N99                                  DSPATR(HI)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                 12 19'(空白表示全部)'
     A                                 13  4'票據種類:'
     A                                      DSPATR(HI)
     A            DHA05     R        B 13 16REFFLD(HA05)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A                                 13 19'(空白表示全部)'
     A                                 14  4'來源用途:'
     A                                      DSPATR(HI)
     A            DHA23     R        B 14 16REFFLD(HA23)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A                                 14 19'(空白表示全部)'
     A                                 15  4'入帳類別:'
     A            DHA27     R        B 15 16REFFLD(HA27)
     A                                      VALUES(' ' 'Y' 'N')
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                 15 18'( Y是　 N否)'
     A                                 16  4'票據區　:'
     A            DHA24     R        B 16 16REFFLD(HA24)
     A                                      VALUES(' ' '1' '2')
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A                                 16 18'( 1本地 2外地)'
     A                                  4 52'排序方式'
     A                                  5 42'*******************************'
     A                                  6 42'1.收票日期＋到期日期＋票據號碼'
     A                                  8 42'2.到期日期＋收票日期＋票據號碼'
     A                                 10 42'3.銀行帳號＋票據號碼'
     A                                 12 42'4.客戶代號＋收票日期＋票據號碼'
     A                                 16 41'請輸入排序方式:'
     A            DSORT          1A  B 16 59VALUES('1' '2' '3' '4')
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A                                 18 24'執行環境:'
     A            $PENV          1A  B 18 36VALUES('0' '1' '2')
     A                                 18 40'(0-查詢 1-即時 2-批次)'
     A                                 19 24'列印份數:'
     A            $CPY           3Y 0B 19 36EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20 24'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 36DSPATR(HI)
     A  27                             21 25'*** 請準備　８０格報表紙 ***'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
