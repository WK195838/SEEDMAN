     A*%%TS  SD  19931221  105318  MAY         REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKR420                                     *
     A*    PROGRAM NAME :應付票據明細表                            *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.10.03                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19931221  105318  MAY         REL-V2R2M0  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<CKR420>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2 32'應付票據明細表'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  2 32'應付票據明細表'
     A                                      DSPATR(RI)
     A                                  5 15'列印範圍'
     A                                  6  5'**********************************-
     A                                      *'
     A                                  7  4'公司別　:'
     A            DHA02     R        B  7 16REFFLD(HA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DHA02N    R        O  7 19REFFLD(#B03 RERF)
     A                                  8  4'廠商代號:'
     A                                      DSPATR(HI)
     A            DHA04F    R        B  8 16REFFLD(HA04)
     A  88
     AO 62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A                                  8 27'-'
     A            DHA04T    R        B  8 29REFFLD(HA04)
     A N99                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9  4'銀行帳號:'
     A                                      DSPATR(HI)
     A            DHA16F         6A  B  9 16
     A N99                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 27'-'
     A            DHA16T         6A  B  9 29
     A N99                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 10  4'開票日　:'
     A            DHA06F         7Y 0B 10 16EDTWRD('   /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 10 27'-'
     A            DHA06T         7Y 0B 10 29EDTWRD('   /  /  ')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 11  4'到期日　:'
     A            DHA07F         7Y 0B 11 16EDTWRD('   /  /  ')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                 11 27'-'
     A            DHA07T         7Y 0B 11 29EDTWRD('   /  /  ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                 12  4'票據狀況:'
     A                                      DSPATR(HI)
     A            DHA17     R        B 12 16REFFLD(HA17)
     A N99                                  DSPATR(HI)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                 12 19'(空白表示全部)'
     A                                 13  4'票據種類:'
     A                                      DSPATR(HI)
     A            DHA05     R        B 13 16REFFLD(HA05)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A                                 13 19'(空白表示全部)'
     A                                 14  4'來源用途:'
     A                                      DSPATR(HI)
     A            DHA23     R        B 14 16REFFLD(HA23)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A                                 14 19'(空白表示全部)'
     A                                 15  4'入帳類別:'
     A            DHA27     R        B 15 16REFFLD(HA27)
     A                                      VALUES(' ' 'Y' 'N')
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                 15 18'( Y是　 N否)'
     A                                 16  4'地區別　:'
     A            DHA24     R        B 16 16REFFLD(HA24)
     A                                      VALUES(' ' '1' '2')
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A                                 16 18'( 1本地 2外地)'
     A                                  5 52'排序方式'
     A                                  6 42'*******************************'
     A                                  7 43'1.開票日期＋到期日期＋票據號碼'
     A                                 11 43'5.銀行帳號＋到期日期（印支票）'
     A                                  9 43'3.銀行帳號＋到期日＋票據號碼'
     A                                 10 43'4.廠商代號＋開票日期＋票據號碼'
     A                                 16 44'請輸入排序方式:'
     A            DSORT          1A  B 16 62VALUES('1' '2' '3' '4' '5')
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A                                 18 24'執行環境:'
     A            $PENV          1A  B 18 36VALUES('0' '1' '2')
     A                                 18 39'(0-查詢 1-即時 2-批次)'
     A                                 19 24'列印份數:'
     A            $CPY           3Y 0B 19 36EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20 24'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 20 36DSPATR(HI)
     A  27                             21 30'請準備８０格報表紙'
     A                                      DSPATR(RI)
     A  28                             21 30'報表處理中，請稍候'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A                                 13 47'列印序號:'
     A            DHF73S    R        B 13 59REFFLD(HF73)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13 64'-'
     A            DHF73E    R        B 13 66REFFLD(HF73)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 12 47'列印日　:'
     A            DHF72          7Y 0B 12 59EDTWRD('   /  /  ')
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A                                  8 43'2.到期日期＋開票日期＋票據號碼'
