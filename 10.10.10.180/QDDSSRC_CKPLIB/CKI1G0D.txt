     A*%%TS  SD  19921119  213929  TSPGMR      REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKI1G0                                     *
     A*    PROGRAM NAME : 會計科目全頁查詢                         *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.10.27                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19921119  213929  TSPGMR      REL-V2R1M1  5738-PW1
     A            DOPT           1A  B  8  2DSPATR(CS)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      VALUES('/' ' ')
     A            RRN            3Y 0O  8  4EDTCDE(4)
     A            HG01      R        O  8  9
     A            HG02      R        O  8 14
     A            AF03      R        O  8 23REFFLD(AF03 GLAFPF)
     A            HG03A          2   O  8 56
     A            HG04      R        O  8 70
     A*----------------------------------------------------------------
     A**                                    CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921119  213929  TSPGMR      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0130)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<CKI1G0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'會計科目設定查詢'
     A                                      DSPATR(RI)
     A                                  4  2'S (=作業) : /-挑選'
     A                                  6  2'S'
     A                                      DSPATR(UL)
     A                                  6  4' No公司　會計科目　科目中文名稱'
     A                                  6 56'票據代碼'
     A                                  6 70'彙總代碼'
     A                                      DSPATR(UL)
     A            DHG01     R        B  7  9REFFLD(HG01)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DHG02     R        B  7 14REFFLD(HG02)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3=回主畫面'
     A                                 23 26'PF12=回前畫面'
     A                                 23 43'PA1=下一頁'
     A                                 24 10'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
