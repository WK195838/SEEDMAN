     A*%%TS  SD  19930217  193943  REPGMR      REL-V2R1M1  5738-PW1
     A*  92/09/26  14:09:44    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKI220                                     *
     A*    PROGRAM NAME :應付票據資料查詢                          *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 1992.09.30                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*  92/09/26  14:09:44    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD1
     A*%%TS  SD  19930210  200814  REPGMR      REL-V2R1M1  5738-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKI220.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'應付票據資料查詢'
     A                                      DSPATR(RI)
     A                                  5 21'***查詢範圍:'
     A                                  6 24'公　　司'
     A            DHA02     R        B  6 36REFFLD(HA02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DHA02N    R        O  6 39REFFLD(#B03 RERF)
     A                                  7 24'廠商代號:'
     A                                      DSPATR(HI)
     A            DHA04     R        B  7 36REFFLD(HA04)
     A  88
     AO 61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A            D#D03         10A  O  7 47
     A                                  8 24'開票日　:'
     A            DHA06F         7Y 0B  8 36EDTWRD('   /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 46'-'
     A            DHA06T         7Y 0B  8 48EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 24'到期日　:'
     A            DHA07F         7Y 0B  9 36EDTWRD('   /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 46'-'
     A            DHA07T         7Y 0B  9 48EDTWRD('   /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 10 24'付款帳號:'
     A                                      DSPATR(HI)
     A            DHA09          6A  B 10 36
     A N99                                  DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 11 24'財務用途:'
     A                                      DSPATR(HI)
     A            DHA23     R        B 11 36REFFLD(HA23)
     A N99                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A            #A03D         10A  O 11 39
     A                                 13 21'***排序方式:'
     A            DSORT          1A  B 13 36VALUES('1' '2')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                 14 25'1.開票日＋到期日＋票據號碼'
     A                                 15 25'2.到期日＋開票日＋票據號碼'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A*  92/09/26  11:17:29    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPC2
     A*%%TS  SD  19930210  200814  REPGMR      REL-V2R1M1  5738-PW1
     A                                  1  2'<CKI220.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'應付票據資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        O  4 13REFFLD(HA02)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  4 39'開票日　:'
     A            DHA06F         7Y 0O  4 51EDTWRD('   /  /  ')
     A                                  4 61'-'
     A            PHA06T         7Y 0O  4 63EDTWRD('   /  /  ')
     A                                  5  1'廠商代號:'
     A            DHA04     R        O  5 13REFFLD(HA04)
     A            D#D03         10A  O  5 24
     A                                  5 39'到期日　:'
     A            DHA07F         7Y 0O  5 51EDTWRD('   /  /  ')
     A                                  5 61'-'
     A            PHA07T         7Y 0O  5 63EDTWRD('   /  /  ')
     A                                  6  1'付款帳號:'
     A            DHA091    R        O  6 13REFFLD(HA09)
     A            DHB04         10A  O  6 28
     A                                  6 39'財務用途:'
     A            DHA23     R        O  6 51REFFLD(HA23)
     A            #A03D         10A  O  6 54
     A                                  8  2'( KEY :'
     A            DKEY           8A  O  8 10
     A                                  8 19')'
     A                                  9  2'S  開票日 廠商名稱 銀行名稱 -
     A                                      票據號碼 票況　票類  到期日  -
     A                                      票據金額'
     A                                      DSPATR(UL)
     A*  92/09/26  11:17:29    TSPGMR      REL-R03M00  5728-PW1
     A          R SFLSR1                    SFL
     A*%%TS  SD  19921021  173509  TSPGMR      REL-V2R1M1  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4Y 0H
     A            HA01      R        H
     A            HA02      R        H
     A            HA16      R        H
     A            HA17      R        H
     A            HA11      R        H
     A            DOPT           1A  B 10  2
     A            DHA06          7Y 0O 10  4EDTWRD('   /  /  ')
     A            D#D031        10   O 10 14
     A            DHB02     R        O 10 25REFFLD(HB02)
     A            DHA03     R        O 10 36REFFLD(HA03)
     A            S#A03          6A  O 10 47
     A            #A03S          4A  O 10 54
     A            DHA07          7Y 0O 10 59EDTWRD('   /  /  ')
     A            DHA10     R        O 10 69REFFLD(HA10)
     A                                      EDTCDE(2)
     A            DHA32         42A  O 11  4
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*  92/09/26  11:20:31    TSPGMR      REL-R03M00  5728-PW1
     A*
     A                                      SFLSIZ(0022)
     A                                      SFLPAG(0005)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A                                      SFLDROP(CF02)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A**53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A N53                             21 59'       '
     A  53                             21 59'合計:'
     A  53        DSUM          10  0O 21 67
     A                                      EDTCDE(2)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23  9'F2=備註'
     A                                 23 19'F3=回主畫面'
     A                                 23 33'F12=回前畫面'
     A                                 23 48'PA1=下一頁'
     A                                 23 61'PA2=上一頁'
     A*                                24 67'【泛太資訊】'
     A*  92/09/26  14:09:44    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD3
     A*%%TS  SD  19930217  193943  REPGMR      REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF12(12)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKI220.3>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 31'應付票據資料查詢'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        O  4 13REFFLD(HA02)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'票據號碼:'
     A            DHA03     R        O  5 13REFFLD(HA03)
     A                                  6  1'作業功能:明細查詢'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------'
     A                                  8  1'廠商代號:'
     A            DHA042    R        O  8 13REFFLD(HA04)
     A            D#D032    R        O  8 24REFFLD(HF77 CKHFPF)
     A                                  9  1'票據類別:'
     A            DHA05     R        O  9 13REFFLD(HA05)
     A            #A03D1        10A  O  9 15
     A                                  9 26'( 1本票 3支票'
     A                                  9 44'4保証票 5商業本票)'
     A                                 10  1'開票日　:'
     A            DHA06          7Y 0O 10 13EDTWRD('   /  /  ')
     A                                 10 39'到期日　:'
     A            DHA07          7Y 0O 10 51EDTWRD('   /  /  ')
     A                                 11  1'付款帳號:'
     A            DHA092         6A  O 11 13
     A            DHA093    R        O 11 20REFFLD(HA09)
     A                                 11 39'付款銀行:'
     A            DHA08     R        O 11 51REFFLD(HA08)
     A            DHB031    R        O 11 59REFFLD(HB03)
     A                                 12  1'票面金額:'
     A            DHA10     R        O 12 13REFFLD(HA10)
     A                                      EDTCDE(2)
     A                                 12 39'預兌日期:'
     A            DHA11          7Y 0O 12 51EDTWRD('   /  /  ')
     A                                 13  1'財務用途:'
     A            DHA232    R        O 13 13REFFLD(HA23)
     A            #A03D2        10A  O 13 16
     A                                 14  1'票據狀況:'
     A            DHA17     R        O 14 13REFFLD(HA17)
     A            D#A032         8A  O 14 15
     A                                 14 24'( 0作廢 2託收 4兌現)'
     A                                 15  1'處理日期:'
     A            DHA15     R   -1   O 15 13REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A                                 15 39'處理帳號:'
     A            DHA16     R        O 15 51REFFLD(HA16)
     A                                 16  1'貨款記號:'
     A            DHA28     R        O 16 13REFFLD(HA28)
     A                                 16 15'( Y是 N否)'
     A                                 17  1'入帳類別:'
     A            DHA27     R        O 17 13REFFLD(HA27)
     A                                 17 15'( Y是 N否)'
     A                                 18  1'對帳記號:'
     A            DHA26     R        O 18 13REFFLD(HA26)
     A                                 18 15'( Y是 N未)'
     A                                 19  1'備　　註:'
     A            DHA32     R        O 19 13REFFLD(HA32)
     A*
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
