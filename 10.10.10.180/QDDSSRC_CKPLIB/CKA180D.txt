     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKA180                                     *
     A*    PROGRAM NAME :銀行對帳單維護                            *
     A*    AUTHOR       : A1149  HUANG-SHU.MEI                       *
     A*    CREATE DATE  : 1993.02.22                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 票據系統                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA180.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2 32'銀行對帳單維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司別　:'
     A            DHC01     R        B  4 13REFFLD(HC01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DHC01N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'銀行帳號:'
     A                                      DSPATR(HI)
     A            DHC02          6A  B  5 13
     A  88
     AO 62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N99                                  DSPATR(HI)
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F4 =欄位查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            DHA26     R        B  8  4REFFLD(HA26)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            DHA15     R   -1   O  8  9REFFLD(HA15)
     A                                      EDTWRD('   /  /  ')
     A            DHA01C         6O  O  8 21
     A            DHA03     R        O  8 30REFFLD(HA03)
     A            DHA10O    R        O  8 42REFFLD(HA10)
     A                                      EDTCDE(K)
     A            DHA10I    R        O  8 55REFFLD(HA10)
     A                                      EDTCDE(K)
     A            DHA07     R   -1   O  8 68REFFLD(HA07)
     A                                      EDTWRD('   /  /  ')
     A            HA31      R        H      REFFLD(HA31)
     A            HA01      R        H      REFFLD(HA01)
     A            HA02      R        H      REFFLD(HA02)
     A            HA03      R        H      REFFLD(HA03)
     A            HA15      R        H      REFFLD(HA15)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<CKA180.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2 32'銀行對帳單維護'
     A                                      DSPATR(RI)
     A            $USERN        10A  O  2  2
     A                                  4  1'公司別　:'
     A            DHC01     R        O  4 13REFFLD(HC01)
     A            DHC01N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'銀行帳號:'
     A            DHC02          6A  O  5 13
     A            DHC02F    R        O  5 20REFFLD(HC02)
     A                                  5 38'銀行名稱:'
     A            DHC02N        20O  O  5 50
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  1'對帳　　往來日期　交易類別 票-
     A                                      據號碼    支出金額　　存入金額-
     A                                      　　到期日期   '
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F6 =輸入資料'
     A                                 23 44'F12=回前畫面'
     A                                 24 10'PA1=下一頁'
     A                                 24 27'PA2=上一頁'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  19930222  105106  REPGMR      REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      CF12(12)
     A                                  1  2'<CKA180.3>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 32'銀行對帳單維護'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHC01     R        O  4 13REFFLD(HC01)
     A            DHC01N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'銀行帳號:'
     A            DHC02          6A  O  5 13
     A            DHC02F    R        O  5 20REFFLD(HC02)
     A                                  5 38'銀行名稱:'
     A            DHC02N        20O  O  5 50
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  1'往來日期:'
     A            DHC03     R   -1   B  7 13REFFLD(HC03)
     A                                      EDTWRD('   /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  9  1'交易類別:'
     A            DHC08     R        B  9 13REFFLD(HC08)
     A                                      VALUES('1' '2' '3' '4' '5' '6')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  9 15'( 1收票 2付票 3存入'
     A                                  9 41'4提出 5轉存 6轉提)'
     A                                 11  1'票據號碼:'
     A            DHC05     R        B 11 13REFFLD(HC05)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 13  1'金　　額:'
     A            DHC06     R        B 13 13REFFLD(HC06)
     A                                      EDTCDE(K)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
