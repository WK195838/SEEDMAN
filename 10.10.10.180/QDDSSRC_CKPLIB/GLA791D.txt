     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : GLA791                                     *
     A*    PROGRAM NAME : 收票傳票登錄                             *
     A*    AUTHOR       : A1034  STEPHANIE                           *
     A*    CREATE DATE  : 1995.01.23                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A*
     A            DOPT           1A  B 10  2VALUES(' ' '1' '2' '4' '5')
     A                                      DSPATR(PC)
     A  75                                  DSPATR(RI)
     A            RRN            3Y 0H
     A            DAH031    R        O 10  4REFFLD(AH03)
     A                                      EDTCDE(4)
     A            DAH041    R        O 10  8REFFLD(AH04)
     A            DAH042    R        O 10 17REFFLD(AH04)
     A            DAF031        18A  O 10 26
     A            DAH121    R        O 10 46REFFLD(AH12)
     A                                      EDTCDE(2)
     A            DAH131    R        O 10 64REFFLD(AH13)
     A                                      EDTCDE(2)
     A            DAH161    R        O 11 25REFFLD(AH16)
     A            DAH111         1   O 11 78
     A            DNO       R        H      REFFLD(RRN *SRC)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0005)
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      CF09(09)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      SFLDROP(CF02)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 PTMF)
     A**
     A**          DRRN           3S 0H      SFLRCDNBR(CURSOR)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<GLA791.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'收票傳票登錄'
     A                                      DSPATR(RI)
     A                                  3  2'公司別　　:'
     A            DAH01     R        O  3 16REFFLD(AH01)
     A            #B02      R        O  3 19REFFLD(#B02 PTRF)
     A                                  3 40'客戶名稱:'
     A            D#C03         10A  O    +1
     A                                  4  2'票據號碼　:'
     A            DHA03     R        O  4 16REFFLD(HA03 CKRF)
     A                                  4 40'票據類別:'
     A            DHA05D         6A  O    +1
     A                                  5  2'傳票日期　:'
     A            DAH10     R   -1   O  5 16REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A                                  5 40'到期日　:'
     A            DHA07     R   -1   O    +1REFFLD(HA07 CKRF)
     A                                      EDTWRD('   /  /  ')
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  2'S (=作業) :'
     A                                  7 17'1-新增'
     A                                    +2'2-修改'
     A                                    +2'4-刪除'
     A                                    +2'5-查詢'
     A                                  9  2'S  NO借方／貸方科目　　科目名+
     A                                      稱／摘　要　　　　　借方金+
     A                                      額　　　　　貸方金額  '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A*  91/05/24  18:28:48    Q790601     REL-R03M00  5728-PW1
     A                                 20  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                 21 37'合計:'
     A            DDTOT         13Y 2O 21 46EDTCDE(1)
     A            DCTOT         13Y 2O 21 64EDTCDE(1)
     A*
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  98        DMSG          78A  O 22  2DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF2 =明細顯示'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF6 =分錄製作完畢'
     A                                 24 10'PF9 =製作列印'
     A                                    +2'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*================================================================
     A          R DSPD3
     A                                      CSRLOC(#LIN #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                  1  2'<GLA791.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'收票傳票登錄'
     A                                      DSPATR(RI)
     A                                  3  2'公司別　　:'
     A            DAH01     R        O  3 16REFFLD(AH01)
     A            #B02      R        O  3 19REFFLD(#B02 PTRF)
     A                                  4  2'票據號碼　:'
     A            DHA03     R        O  4 16REFFLD(HA03 CKRF)
     A                                  5  2'傳票日期　:'
     A            DAH10     R   -1   O  5 16REFFLD(AH10)
     A                                      EDTWRD('   /  /  ')
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  7  4'序號　　:'
     A            DAH03     R        O  7 16REFFLD(AH03)
     A                                      EDTCDE(4)
     A            DFUN          14      7 60DSPATR(RI)
     A                                  8  4'科目　　:'
     A                                      DSPATR(HI)
     A            DAH04     R        B  8 16REFFLD(AH04)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A N99                                  DSPATR(HI)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A            DAF03     R        O  8 26REFFLD(AF03)
     A                                  9  4'借方金額:'
     A            DAH12     R        B  9 16REFFLD(AH12)
     A                                      EDTCDE(2)
     A                                      DSPATR(CS)
     A                                      CHECK(RB)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                 10  4'貸方金額:'
     A            DAH13     R        B 10 16REFFLD(AH13)
     A                                      EDTCDE(2)
     A                                      DSPATR(CS)
     A                                      CHECK(RB)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                 10 58'分攤碼　:'
     A            DAH11          1   B 10 70
     A                                      VALUES('Y' 'N')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                 10 72'(Y/N)'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A*----------------------------------------------------------------
     A          R DSPC3
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF4 =欄位查詢'
     A                                    +2'PF5 =參考資料'
     A                                 24 10'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD4
     A                                      CSRLOC(#LIN #COL)
     A                                      CA03(03)
     A  31                                  CF04(04)
     A  31                                  CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PROTECT
     A  37                                  FRCDTA
     A  37                                  LOCK
     A            #LIN           3  0H
     A            #COL           3  0H
     A                                 12  5'1.'
     A N55                                  DSPATR(HI)
     A            DAA031    R        O 12  8REFFLD(AA03)
     A N55                                  DSPATR(HI)
     A                                 12 29':'
     A N55                                  DSPATR(HI)
     A            DAH05     R        B 12 31REFFLD(AH05)
     A N99N55                               DSPATR(HI)
     A  60                                  DSPATR(ND)
     A  60
     AON31                                  DSPATR(PR)
     A  80                                  DSPATR(PC RI)
     A            DAH051        20A  O 12 53
     A                                 13  5'2.'
     A N56                                  DSPATR(HI)
     A            DAA032    R        O 13  8REFFLD(AA03)
     A N56                                  DSPATR(HI)
     A                                 13 29':'
     A N56                                  DSPATR(HI)
     A            DAH06     R        B 13 31REFFLD(AH06)
     A N99N56                               DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61
     AON31                                  DSPATR(PR)
     A  81                                  DSPATR(PC RI)
     A            DAH061        20A  O 13 53
     A                                 14  5'3.'
     A N57                                  DSPATR(HI)
     A            DAA033    R        O 14  8REFFLD(AA03)
     A N57                                  DSPATR(HI)
     A                                 14 29':'
     A N57                                  DSPATR(HI)
     A            DAH07     R        B 14 31REFFLD(AH07)
     A N99N57                               DSPATR(HI)
     A  62                                  DSPATR(ND)
     A  62
     AON31                                  DSPATR(PR)
     A  82                                  DSPATR(PC RI)
     A            DAH071        20A  O 14 53
     A                                 15  5'4.'
     A N58                                  DSPATR(HI)
     A            DAA034    R        O 15  8REFFLD(AA03)
     A N58                                  DSPATR(HI)
     A                                 15 29':'
     A N58                                  DSPATR(HI)
     A            DAH08     R        B 15 31REFFLD(AH08)
     A N99N58                               DSPATR(HI)
     A  63                                  DSPATR(ND)
     A  63
     AON31                                  DSPATR(PR)
     A  83                                  DSPATR(PC RI)
     A            DAH081        20A  O 15 53
     A                                 16  5'5.'
     A            DAA035    R        O 16  8REFFLD(AA03)
     A                                 16 29':'
     A            DAH09     R   -1   B 16 31REFFLD(AH09)
     A                                      EDTWRD('   /  /  ')
     A  64                                  DSPATR(ND)
     A  64
     AON31                                  DSPATR(PR)
     A  84                                  DSPATR(PC RI)
     A            DAH091        20A  O 16 53
     A                                 17  5'6.'
     A            DAA036    R        O 17  8REFFLD(AA03)
     A                                 17 29':'
     A            DAH14     R        B 17 31REFFLD(AH14)
     A                                      EDTCDE(2)
     A  65                                  DSPATR(ND)
     A  65
     AON31                                  DSPATR(PR)
     A  85                                  DSPATR(PC RI)
     A            DAH141        20A  O 17 53
     A                                 18  5'7.'
     A N59                                  DSPATR(HI)
     A            DAA037    R        O 18  8REFFLD(AA03)
     A N59                                  DSPATR(HI)
     A                                 18 29':'
     A N59                                  DSPATR(HI)
     A            DAH15     R        B 18 31REFFLD(AH15)
     A N99N59                               DSPATR(HI)
     A  66                                  DSPATR(ND)
     A  66
     AON31                                  DSPATR(PR)
     A  86                                  DSPATR(PC RI)
     A            DAH151        20A  O 18 53
     A                                 19  8'資金代碼'
     A                                      DSPATR(HI)
     A                                 19 29':'
     A                                      DSPATR(HI)
     A            DAH22     R        B 19 31REFFLD(AH22)
     A N99                                  DSPATR(HI)
     A N31                                  DSPATR(PR)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A            DAD03     R        O 19 41REFFLD(AD03)
     A                                 20  8'沖帳傳票號碼'
     A                                      DSPATR(HI)
     A                                 20 29':'
     A                                      DSPATR(HI)
     A            DAH24     R        B 20 31REFFLD(AH24)
     A N99                                  DSPATR(HI)
     A N31                                  DSPATR(PR)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A                                 21  8'摘要'
     A                                 21 29':'
     A            DAH16     R        B 21 31REFFLD(AH16)
     A N31                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R DSPC4
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                    +2'PF3 =回主畫面'
     A                                    +2'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
     A*================================================================
     A          R SFLSRD                    SFL
     A            RRND           3Y 0   9  2
     A            DAH12D    R        H      REFFLD(AH12)
     A            DAH13D    R        H      REFFLD(AH13)
     A            DAH04D    R        H      REFFLD(AH04)
     A            DAH05D    R        H      REFFLD(AH05)
     A            DAH05A    R        H      REFFLD(AH05A)
     A            DAH06D    R        H      REFFLD(AH06)
     A            DAH06A    R        H      REFFLD(AH06A)
     A            DAH07D    R        H      REFFLD(AH07)
     A            DAH07A    R        H      REFFLD(AH07A)
     A            DAH08D    R        H      REFFLD(AH08)
     A            DAH08A    R        H      REFFLD(AH08A)
     A            DAH09D    R        H      REFFLD(AH09)
     A            DAH09A    R        H      REFFLD(AH09A)
     A            DAH11D         1   H
     A            DAH14D    R        H      REFFLD(AH14)
     A            DAH14A    R        H      REFFLD(AH14A)
     A            DAH15D    R        H      REFFLD(AH15)
     A            DAH15A    R        H      REFFLD(AH15A)
     A            DAH16D    R        H      REFFLD(AH16)
     A            DAH16H    R        H      REFFLD(AH16)
     A            DAH22D    R        H      REFFLD(AH22)
     A            DAH24D    R        H      REFFLD(AH24)
     A            DLTCOD         1   H
     A*----------------------------------------------------------------
     A          R SFLCRD                    SFLCTL(SFLSRD)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A*================================================================
     A          R SFLSRC                    SFL
     A            RRNC           3Y 0   9  2
     A            DAH12D    R        H      REFFLD(AH12)
     A            DAH13D    R        H      REFFLD(AH13)
     A            DAH04D    R        H      REFFLD(AH04)
     A            DAH05D    R        H      REFFLD(AH05)
     A            DAH05A    R        H      REFFLD(AH05A)
     A            DAH06D    R        H      REFFLD(AH06)
     A            DAH06A    R        H      REFFLD(AH06A)
     A            DAH07D    R        H      REFFLD(AH07)
     A            DAH07A    R        H      REFFLD(AH07A)
     A            DAH08D    R        H      REFFLD(AH08)
     A            DAH08A    R        H      REFFLD(AH08A)
     A            DAH09D    R        H      REFFLD(AH09)
     A            DAH09A    R        H      REFFLD(AH09A)
     A            DAH11D         1   H
     A            DAH14D    R        H      REFFLD(AH14)
     A            DAH14A    R        H      REFFLD(AH14A)
     A            DAH15D    R        H      REFFLD(AH15)
     A            DAH15A    R        H      REFFLD(AH15A)
     A            DAH16D    R        H      REFFLD(AH16)
     A            DAH16H    R        H      REFFLD(AH16)
     A            DAH22D    R        H      REFFLD(AH22)
     A            DAH24D    R        H      REFFLD(AH24)
     A            DLTCOD         1   H
     A*----------------------------------------------------------------
     A          R SFLCRC                    SFLCTL(SFLSRC)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
