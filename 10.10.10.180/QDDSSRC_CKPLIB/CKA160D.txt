     A*%%TS  SD  19930501  151212  TSPGMR      REL-V2R2M0  5738-PW1
     A*  92/09/30  09:10:17    TSPGMR      REL-R03M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKA160                                     *
     A*    PROGRAM NAME :票據退票作業                              *
     A*    AUTHOR       : A1149   MAY                                *
     A*    CREATE DATE  : 1992.09.25                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :票據系統                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      PRINT
     A*  92/09/30  09:10:17    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD1
     A*%%TS  SD  19930210  165937  REPGMR      REL-V2R1M1  5738-PW1
     A                                      CA03(03)
     A                                      CF18(18)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA160.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'票據退票作業'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        B  4 13REFFLD(HA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'票據號碼:'
     A            DHA03     R        B  5 13REFFLD(HA03)
     A  88
     AO 62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6  1'作業功能:退票'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A                                 23 28'F18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*  92/09/30  09:10:17    TSPGMR      REL-R03M00  5728-PW1
     A          R DSPD2
     A*%%TS  SD  19930501  151212  TSPGMR      REL-V2R2M0  5738-PW1
     A                                      CA03(03)
     A                                      CF12(12)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<CKA160.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 33'票據退票作業'
     A                                      DSPATR(RI)
     A                                  4  1'公司別　:'
     A            DHA02     R        O  4 13REFFLD(HA02)
     A            DHA02N    R        O  4 16REFFLD(#B03 RERF)
     A                                  5  1'票據號碼:'
     A            DHA03     R        O  5 13REFFLD(HA03)
     A                                  6  1'作業功能:退票'
     A                                  7  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                  8  1'客戶代號:'
     A            DHA04     R        O  8 13REFFLD(HA04)
     A            D#C03     R        O  8 24REFFLD(#C03 PTRF)
     A                                  9  1'票據類別:'
     A            DHA05     R        O  9 13REFFLD(HA05)
     A            DHA05C        10A  O  9 15
     A                                  9 26'( 1本票 2客票 3支票'
     A                                  9 52'4保証票 5商業本票)'
     A                                 10  1'收票日　:'
     A            DHA06     R   -1   O 10 13REFFLD(HA06)
     A                                      EDTWRD('   /  /  ')
     A                                 10 44'到期日　:'
     A            DHA07     R   -1   O 10 56REFFLD(HA07)
     A                                      EDTWRD('   /  /  ')
     A                                 11  1'付款銀行:'
     A            DHA08     R        O 11 13REFFLD(HA08)
     A            DHB03     R        O 11 21REFFLD(HB03)
     A                                 11 44'付款帳號:'
     A            DHA09     R        O 11 56REFFLD(HA09)
     A                                 12  1'票面金額:'
     A            DHA10     R        O 12 13REFFLD(HA10)
     A                                      EDTCDE(2)
     A                                 12 44'預計兌現:'
     A            DHA11     R   -1   O 12 56REFFLD(HA11)
     A                                      EDTWRD('   /  /  ')
     A                                 13  1'業務人員:'
     A            DHA13     R        O 13 13REFFLD(HA13)
     A            D#E03     R        O 13 24REFFLD(#E03 PTRF)
     A            DHA14          4   O 13 39
     A                                 13 44'票據區　:'
     A            DHA24          1A  O 13 56
     A                                 13 58'( 1本地 2外地)'
     A                                 14  1'來源用途:'
     A            DHA23     R        O 14 13REFFLD(HA23)
     A            DHA23C        10A  O 14 16
     A                                 15  1'票據狀況:'
     A            DHA17     R        O 15 13REFFLD(HA17)
     A            DHA17C         6A  O 15 15
     A                                 15 22'( 0作廢 1庫存 2託收'
     A                                 15 48'3貼現 4兌現'
     A                                 15 64'5退票)'
     A                                 16  1'處理日期:'
     A            DHA15     R   -1   B 16 13REFFLD(HA15)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                      EDTWRD('   /  /  ')
     A                                 16 44'處理帳號:'
     A            DHA16     R        O 16 56REFFLD(HA16)
     A                                 17  1'貨款記號:'
     A            DHA28     R        O 17 13REFFLD(HA28)
     A                                 17 15'( Y是 N否)'
     A                                 18  1'入帳類別:'
     A            DHA27     R        O 18 13REFFLD(HA27)
     A                                 18 15'( Y是 N否)'
     A                                 19  1'對帳記號:'
     A            DHA26     R        O 19 13REFFLD(HA26)
     A                                 19 15'( Y是 N未)'
     A                                 20  1'備　　註:'
     A            DHA32     R        O 20 13REFFLD(HA32)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 27'F12=回前畫面'
     A                                 24 67'【泛太資訊】'
