     A*%%TS  SD  19921027  134637  PANTECH     REL-V2R1M1  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : CKGL101D                                   *
     A*    PROGRAM NAME : 收／開票產生傳票                         *
     A*    AUTHOR       : A1139                                      *
     A*    CREATE DATE  : 84/04/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CKRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            DOPT           1   B 10  2DSPATR(HI)
     A                                      VALUES('5' '6' ' ')
     A  31                                  DSPATR(UL CS PC)
     A N31                                  DSPATR(PR)
     A            RRN            3Y 0O 10  4EDTCDE(4)
     A            HA03      R        O 10  9REFFLD(HA03)
     A            DHA05N         4   O 10 20
     A            HA04      R        O 10 25REFFLD(HA04)
     A            DHA04N    R        O 10 36REFFLD(#S04 *LIBL/LYRF)
     A            DHA06     R   -1   O 10 50EDTWRD('   /  /  ')
     A                                      REFFLD(HA06)
     A            DHA07     R   -1   O 10 60EDTWRD('   /  /  ')
     A                                      REFFLD(HA07)
     A            HA51      R        O 10 70
     A*----------------------------------------------------------------
     A**                                    CSRLOC(#LIN #COL)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921027  134637  PANTECH     REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A**                                    ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<CKGL10.2>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 28'收票／開票資料產生傳票'
     A                                      DSPATR(RI)
     A                                  4  2'公司別  :'
     A            DHA02     R        O  4 14REFFLD(HA02)
     A            #B02      R        O    +2REFFLD(#B02 PT#BPF)
     A                                  5  2'票據性質:'
     A            DHA01N        10   O  5 14
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A                                  7  2'S (=作業) : 5-票據資料查詢'
     A                                  7 35'6-產生傳票'
     A                                  9  2'S'
     A                                      DSPATR(UL)
     A                                  9  5'NO 票據號碼 票類　廠商／客戶 -
     A                                                 收／開票日　到期日 +
     A                                       傳票號碼'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
