     A*%%TS  SD  19961206  142824  GOPGMR      REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF   -ID   : GLR2A0D                                    *
     A*    PROGRAM-ID   : GLR2A0                                     *
     A*    PROGRAM NAME : 分類帳                                   *
     A*    AUTHOR       :  A1149    MAY                              *
     A*    CREATE DATE  :  1992.11.11                                *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       : 泛太總帳                                 *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GLRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19961206  142824  GOPGMR      REL-V3R1M0  5763-PW1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<GLR2A0.1>'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DDATE          6Y 0O  1 69EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 37'分類帳'
     A                                      DSPATR(RI)
     A                                  5 28'公司別　:'
     A            DAH01     R        B  5 40REFFLD(AH01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  7 28'選擇內容:'
     A            DOPT           1A  B  7 40VALUES('1' '2')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 45'1.總分類帳－日計表'
     A                                  8 45'2.明細分類帳'
     A                                  9 28'會計科目:'
     A            DAH04S    R        B  9 40REFFLD(AH04)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 49'-'
     A            DAH04E    R        B  9 51REFFLD(AH04)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 10 28'會計年度:'
     A                                      DSPATR(HI)
     A            DYEAR          3Y 0B 10 40EDTCDE(4)
     A N99                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 11 28'會計期間:'
     A                                      DSPATR(HI)
     A            DYY            2Y 0B 11 40EDTCDE(4)
     A N99                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 11 49'-'
     A            DYMD           7Y 0B 11 51EDTWRD('   /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 13 26'含追溯分攤:'
     A            DFLD           1   B 13 40VALUES('Y' 'N')
     A                                 13 45'(Y/N)'
     A                                 15 28'執行環境:'
     A            $PENV          1A  B 15 40VALUES('0' '1' '2')
     A                                 15 45'(0-查詢 1-即時 2-批次)'
     A                                 16 28'列印份數:'
     A            $CPY           3Y 0B 16 40EDTCDE(3)
     A                                      CHECK(RB)
     A                                 17 28'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTCD         2A  B 17 40DSPATR(HI)
     A  27                             21 28'***請準備　８０行報表紙***'
     A                                      DSPATR(RI)
     A  28                             21 28'***報表處理中，請稍候***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF3 =回主畫面'
     A                                 23 27'PF4 =欄位查詢'
     A                                 23 44'PF14=處理作業'
     A                                 24 67'【泛太資訊】'
